[Info]
Version=2.43
CheckChange=75f824a3
PLCFileName=Mill450.awl
// ***********************************************************************
// *                                                                     *
// *                  SPS-Programm fuer ConceptMill450                   *
// *                                                                     *
// ***********************************************************************
//
//
// Bei Verwendung der USB-SPS Platine für Robotik muss folgender Eintrag im MSD-File stehen:
//    [USBSPS1]
//    Enabled=1
//    LangIDError=1204299
//    NoAutoReset=1;  Alarm bei fehlender Robotik-USBSPS muss mit Reset gelöscht werden
//    Der HEX-Schalter ist auf position "2" zu stellen.
//
// V2.00 31.03.2008 LEW  Prototyp-Version (Basis: F5P_103)
// V2.01 08.04.2008 LEW  abgesteckte 4. Achse parkend schalten (FC 101)
//                       autom. Referenzieren geaendert (FC 59)
// V2.02 14.04.2008 LEW  RFG der Achsen bei Aux-Off zwingend wegnehmen (T35)
//                       FM6073 setzt DB100.DBX35.6 zurueck  > Mag. neu ref.
//       16.04.2008 LEW  Spaenefoerderer eingebunden
//       17.04.2008 LEW  Magazinpos. remanent, Magazin autom. referenzieren
// V2.03 25.04.2008 LEW  Alarmnummern geaendert wegen Anzeige in WinNC
//                       Grenze SBH in der PLC auf 1mm reduziert
// V2.04 14.05.2008 LEW  alle Safety-Alarme mit Reset quittierbar
//                       Test der Abschaltpfade eingebaut
// V2.05 20.05.2008 LEW  Bremse WZ-Arm bei Not-Aus immer mit Tasten loesen
//                       FC170: Positionsueberw. bei Aux-Off ausblenden
//                       FM6063 bei Not-Aus Test nicht ausgeben (nach Hochlauf)
//                       SS Einrichten: VS-Override auf 100% begrenzt, sonst SG-Alarm
//                       Sicherheitstechnik: Testbits eingebaut, Ueberw. verbessert
//                       Statussignale Sicherheitsmodul V5 eingebunden
//                       Magazinplatz an eigene Nahtstelle uebergeben
//                       Magazinkoecher schwenken: Taste Spindel-Stop & Mag.+ / Mag.-
//                       LED's der Spindeloverridetasten ansteuern
// V2.06 26.05.2008 LEW  Magazinkoecher schwenkt bei Aux-On nur ein wenn Arm in Grst.
//                       Spindel immer in Regelung, ausser SS-Hand+ZT
//                       Freifahren WZ-Arm bei Spannen/Loesen anhalten
// V2.07 28.05.2008 LEW  Feindiagnose fuer FM6073 und FM6079 (DB100.DBB99)
//                       Magazin: Ueberwachungszeiten 'WZ in Pos.' verlaengert
//                       Magazin: dyn. Positionsueberw. zeitverzoegert
// V2.08 29.05.2008 LEW  WZ-Spanner auch durch Schrittkette loesen, nicht nur Eingang
//                       Tuerautomatik pneumatisch fuer CM450 neu
//                       Ueberwachungszeit Magazin Start erhoeht
// V2.09 03.06.2008 LEW  FM6063 aus 'NC-Stop WZW Alarm' entfernt
//                       SPM 1: Taste verlegt, Fusstasten eingebunden
//                       Tuerverriegelung bei Tuerautom. waehrend Bewegung ansteuern
//                       Ansteuerung Tuerautomatik verbessert (Schutzleiste)
// V2.10 05.06.2008 LEW  KM-Baustein der CT450 eingebunden (Spuelpumpe)
//                       FM6039 Tuerueberwachung verzoegert (Sp. von Hand drehen)
//       09.06.2008      KM-Pistole eingebunden (entweder KM1 oder KM2)
//                       FC 91 "Spannmittel 2" geloescht
//                       SPM 1 auf einfachwirkend umstellbar, nur 1 DS (PLC-Bit)
// V2.11...V2.14         reserviert als Testversionen
// V2.15 19.06.2008 LEW  Pocket up/down verzoegert und Timer in Schrittketten verwendet
//                       man. Funkt. WZW und Magazin gegenseitig verriegelt
//                       PLC-Bit zum Ausblenden 'Werkzeug durchgespannt' (DL)
//                       KM-Pistole nur bei Aux-On aktivieren
// V2.16 29.07.2008 LEW  Robotikinterface Festo angepasst (ueber ACC-EA als Sonder und USBSPS2)
//                       neue MD's fuer Abfrage Beladeposition X/Y Robotik
//                       Not-Aus Test ueber PLC-Bit HEX[1].7 abwaehlbar
//                       DNC-Signale eingebaut
//                       Tuerautomatik Ueberwachung verbessert (Al. wenn lange auf Taste)
//                       Tuerautom. Ventil Tuer auf erst ansteuern wenn Tuer entriegelt ist
//                       Blasluft der Fraesspindel kann mit M48 angesteuert werden (M9=aus)
// V2.17 10.09.2008 LEW  falsche Drehrichtung beim WZW-Arm wird erkannt (Al.6079)
//                       FC51: 'falsche Betriebsart' nur ausgeben wenn ein Bef. blockiert wird
//                       Taste WZ-Spanner: bei WZW-Alarm auch bei geschl. Tuer
//                       Ueberwachungszeit Tuerautom. Testung auf 750ms erhoeht
//       23.09.2008      Abschaltung Schwenkzylinder WZ-Magazin ueber 5/3-Wege Ventil
//                       man. Taktung WZ-Magazin verbessert (Flankenauswertung der Tasten)
//                       bei FM6102 SBD Stoerung Drehzahlueberwachung auch X/Y eingebunden
//       25.09.2008      falsch eingesetztes WZ wird erkannt (BM7029), Grenze in MD
// V9.01 25.11.2008 LEW  Ansteuerung Oelnebelabscheider (Festo)
//                       Tuer bei M30/M32 nicht freigeben
// V2.18 28.11.2008 LEW  Eingangssignale WZ-Wechsler gefiltert, nur gefilterte Signale abfragen
//                       Rueckpositionieren durch ACC nach WZW vorbereitet (ab ACC 2.07)
// V2.19 11.12.2008 LEW  Rueckpositionieren durch ACC korrigiert (FC75, FC78)
//                       Schwimmerschalter Minimalmengenschmierung eingebunden
//                       frei programmierbare Ausgaenge (M80...M87)
// V2.20 30.03.3009 LEW  Spindelwerkzeug auch mit T99 entladen (fuer Fanuc21)
//                       Richtungssignale SBD mit IPO-Fahrbef. verriegelt
//                       A4.1: IPO-Fahrbef. verh. Schalten wenn Tuer offen und JOG
// V2.21 18.05.2009 LEW  'VSH manuell' mit Reset zuruecksetzen
//                       A4.1: Verriegelung wirkt nur in JOG/INC
// V2.22 20.10.2009 LEW  E9.5 Stoerung SS-Kuehlgeraet auswerten (FM6048, NC-Start Sperre)
//                       Hochdruckpumpe mit Schwimmerschalter eingebunden (E9.6)
//                       Kuehlmittelfilter mit Druckschalter eingebunden (E3.7)
//                       Robotik ueber USB: Adressen korrigiert (lt. Ronny)
//       10.11.2009 LEW  Nahtstellenbits fuer Interpolationsarten korrigiert (Fehler in Doku)
//                       Grenze Werkzeug durchgespannt auf USER_DATA_INT[13] gelegt
//       30.11.2009 LEW  Kühlmittelstand Minimum sperrt die KM-Pumpen
//                       BM7041/7041 stoppen die Spindel mit den Achsen
// V2.23 20.01.2009 LEW  Freigabe Fagor F8055
// V2.24 08.11.2010 SCF  NC-START verhindern wenn der WZW-Schwenkarm nicht in Grundstellung ist
// V2.25 27.05.2011 LEW  neuer Werkzeugwechsler DETA (über Bit aktivierbar)
//                       Spindel nicht in Sicherheitstechnik, bei offener Tür Kl.663 wegschalten (A10.6)
// V2.26-29 kommt noch
//
// V2.30 13.06.2012 SCF  Schlüsselschalter Handling ein/aus deaktiviert bei neuem Robotikinterface mit USB-SPS
// V2.31 26.06.2012 SCF  Hauptantrieb ausschalten während A 10.4 Leistung Schwenkarm eingeschaltet ist.
// V2.32 27.06.2012 SCF  AC Reglerfreigabe DB1.DBX 1460.3 auch wegnehmen während A 10.4 Leistung Schwenkarm eingeschaltet ist.
// V2.33 04.12.2012 SCF  UCAM 4.5. Achse eingebaut
// V2.34 01.03.2013 SCF  Beim WZW auch die X-Achse wegfahren wenn in USER_DATA_HEX[8] BIT 3 gesetzt ist und in
//                       USER_DATA_FLOAT[5] die X-Achsposition in m eingetragen ist.
// V2.35 13.05.2013 SCF  Rundachse Klemmung lösen erst nach Genauhalt der X,Y und Z-Achse.
// V2.36 14.05.2013 hinterbr   SBL und M01 funktionierte bei Sinumerik 840d nicht
// V2.37 24.06.2013 SCF  Robotik ergänzt, M3,4,5 Rückmelden.
// V2.39 30.07.2013 SCF  UCAM Schwenkachse Endlagenüberwachung geändert
// V2.42 17.12.2013 SCF  UCAM Schwenkachse nach Rückmeldung Klemmung offen 1,5s Wartezeit für Reglerfreigabe eingebaut
// ***********************************************************************
// Unterschiede zu Step7:
//  - FP und FN sind VKE-Begrenzend und weisen die Flanke dem beim
//    Befehl angegebenen Operanden zu
//  - es koennen keine DB's definiert werden (DB100 wird vom Besy angelegt)
//  - es gibt keine lokalen Variablen in Bausteinen
//  - PLC-Alarme werden von der Oberflaeche nur angezeigt solange die
//    entspr. Bits anstehen. Quittierung muss PLC verwalten!!
//  - Vorsicht bei langen U-O-U Verkettungen: VKE stimmt sporad. nicht
//    (entweder Klammern oder mit Hilfsmerker arbeiten)
//  - keinen Doppelpunkt in Netzwerkueberschriften (TITLE =)
// ************************************************************************
//  DB20.DBX 833.0  RAFI Tastatur X2.1/2 Schlüsselschalter auf Sonderbetrieb
//  DB20.DBX 833.2  RAFI Tastatur X2.3/4 SCHLÜSSELSCHALTER ENDLAGENÜBERWACHUNG DEAKTIVIEREN
//  DB20.DBX 833.3  RAFI Tastatur X3.1/2 CYCLE START Taste
//  DB20.DBX 833.4  RAFI Tastatur X4.1/2 Einzelstücktaste
//  DB20.DBX 833.6  RAFI Tastatur X5.1/2 Spannmitteltaste
//  DB20.DBX 834.0  RAFI Tastatur X6.1/2 Zustimmtaste Maschinenständer
FUNCTION FC 71 : VOID
NAME: UCAM Dreh-Schwenkeinheit
BEGIN
;;DB10.DBX 100.2 ; ACC_MSD 100=4  (mill450B.MSD) Mit 4.5. UCAM Rundachse
//  DB1.DBB 1646  Einstellung des Wahlschalters – USBSPS3
//  DB1.DBW 1650  Eingänge X4 – USBSPS3
//  DB1.DBW 1652  Eingänge X5 – USBSPS3
//   Ausgaenge nur lesen:
//  DB1.DBW 1666  Zustand Ausgänge X2 – USBSPS3
//  DB1.DBW 1668  Zustand Ausgänge X3 – USBSPS3
//   Ausgaenge schreiben:
//  DB1.DBW 1678  Ausgänge X2 – USBSPS3
//  DB1.DBW 1680  Ausgänge X3 – USBSPS3
//
NETWORK
TITLE = Überprüfen auf gültige USBSPS-Hexschaltereinstellung
U   M 126.3         // Einstellung des Wahlschalters – USBSPS3
FP  M 126.4         // FM Einstellung des Wahlschalters – USBSPS3
U   M 126.3         // Einstellung des Wahlschalters – USBSPS3
SPB M7100
L   DB1.DBB 1646    // Einstellung des Wahlschalters – USBSPS3
L   3
==I;                // VERGLEICH AUF GLEICH
S   M 126.3         // Einstellung des Wahlschalters – USBSPS3
M7100:NOP 0;
NETWORK
TITLE = ÜBERWACHUNG ACHSANTRIEBE X,Y SCHARF
U   DB1.DBX 1680.7  // X3:8  Ausgang Reglerfreigabe Drehantrieb
U   DB1.DBX 1680.5  // X3:6  Ausgang Reglerfreigabe Schwenkantrieb
U   M 136.0;        // AUX-ON
U   M 202.2;        // Tuer zu und verriegelt
UN  DB100.DBX 169.7 // Teststop laeuft
L   S5T#3S;         // 3 SEKUNDEN
SE  T 2;            // ACHSÜBERWACHUNG SCHARF
NETWORK
TITLE = Alarm 6105 Antrieb Schwenkachse B-Achse-6 nicht bereit
U   T 2;            // ACHSÜBERWACHUNG SCHARF
UN  DB1.DBX 1652.7  // X5:8  Eingang Betriebsbereit Schwenkachse B-Achse-6
L   S5T#100MS;      // 2X0,1S
SE  T 3;
U   T 3;
S   DB2.DBX 13.1;   // 6105 Antrieb Schwenkachse B-Achse-6 nicht bereit
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 13.1;   // 6105 Antrieb Schwenkachse B-Achse-6 nicht bereit
NETWORK
TITLE = Alarm 6031  Antrieb Drehachse A-Achse-7 nicht bereit
U   T 2;            // ACHSÜBERWACHUNG SCHARF
UN  DB1.DBX 1653.0  // X5:9  Eingang Betriebsbereit Drehachse A-Achse-7
L   S5T#100MS;         // 2X0,1S
SE  T 4;
U   T 4;
S   DB2.DBX 3.7;    // 6031  Antrieb Drehachse A-Achse-7 nicht bereit
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 3.7;    // 6031  Antrieb Drehachse A-Achse-7 nicht bereit
NETWORK  1
TITLE = Schwenkachse Drehüberwachung
U   M 126.4         // FM Einstellung des Wahlschalters – USBSPS3
UN  E 16.6          // Referenzpunktschalter Schwenkachse
=   M 125.0         // Schwenkachse steht beim Einschalten auf dem Referenzpunktschalter
O   E 16.6          // Referenzpunktschalter Schwenkachse
FN  M 125.2;        // FN ENDSCHALTER MINUS Schwenkachse BNGESPROCHEN
U   M 126.4         // FM Einstellung des Wahlschalters – USBSPS3
UN  DB1.DBX 1652.0  // X5:1  Eingang Schwenkachse Überwachungsbero
=   M 126.0         // Schwenkachse steht beim Einschalten auf dem Überwachungsbero
O   DB1.DBX 1652.0  // X5:1  Eingang Schwenkachse Überwachungsbero
FN  M 126.1;        // FN ENDSCHALTER PLUS Schwenkachse BNGESPROCHEN
NETWORK
TITLE =  Handrad und Taster Schwenkachse nur in PLUS Richtung
O   M 125.2;        // FN ENDSCHALTER MINUS Schwenkachse BNGESPROCHEN
O   M 125.0         // Schwenkachse steht beim Einschalten auf dem Referenzpunktschalter
UN  DB20.DBX 327.2; // BETRIEBSART REFERENZ
SPBN M7101
=   DB20.DBX 4.5;   // Achsrichtung anfordern
=   DB20.DBX 886.6  // Schwenkachse 6 nur in PLUS
=   DB1.DBX 1440.0  // NC-RESET AUSLÖSEN
M7101: NOP 0;
NETWORK
TITLE =  Handrad und Taster Schwenkachse nur in MINUS Richtung
O   M 126.1;        // FN ENDSCHALTER PLUS Schwenkachse BNGESPROCHEN
O   M 126.0         // Schwenkachse steht beim Einschalten auf dem Überwachungsbero
UN  DB20.DBX 327.2; // BETRIEBSART REFERENZ
SPBN M7102
=   DB20.DBX 4.5;   // Achsrichtung anfordern
=   DB20.DBX 887.6; // Schwenkachse 6 nur in MINUS
=   DB1.DBX 1440.0  // NC-RESET AUSLÖSEN
M7102: NOP 0;
NETWORK
TITLE =  Richtungsbeschränkungen für Schwenkachse wieder aufheben
O   E 16.6          // Referenzpunktschalter Schwenkachse
FP  M 127.0;        // FP ENDSCHALTER MINUS Schwenkachse BNGESPROCHEN
O   DB1.DBX 1652.0  // X5:1  Eingang Schwenkachse Überwachungsbero
FP  M 127.1;        // FP ENDSCHALTER PLUS Schwenkachse BNGESPROCHEN
O   M 127.0;        // FP ENDSCHALTER MINUS Schwenkachse BNGESPROCHEN
O   M 127.1;        // FP ENDSCHALTER PLUS Schwenkachse BNGESPROCHEN
SPBN M7103
=   DB20.DBX 4.5;   // Achsrichtung anfordern
CLR
=   DB20.DBX 886.6  // Schwenkachse 6 nur in PLUS
=   DB20.DBX 887.6; // Schwenkachse 6 nur in MINUS
M7103: NOP 0;
NETWORK
TITLE =  JOG aus bei offener Tür
U   M 50.0
R   DB1.DBX 1734.6      // Deaktivierung absolute Achse 6 (A-Rundachse)
R   DB1.DBX 1734.7      // Deaktivierung absolute Achse 7 (A-Rundachse)
R   M 50.0
O   DB20.DBX  11.2   // JOG in pos. Richtung für Kanalachse A
O   DB20.DBX  11.3   // JOG in pos. Richtung für Kanalachse B
O   DB20.DBX  13.2   // JOG in neg. Richtung für Kanalachse A
O   DB20.DBX  13.3   // JOG in neg. Richtung für Kanalachse B
U(
O   M 50.1          // FN Taste AUX-ON
ON  DB1.DBX 1653.1  // Zustimmtaste und Schlüsselschalter
U   E 2.2;          // 1-Signal wenn die Tür offen ist
)
SPBN   M7104
R   DB20.DBX  11.2   // JOG in pos. Richtung für Kanalachse A
R   DB20.DBX  11.3   // JOG in pos. Richtung für Kanalachse B
R   DB20.DBX  13.2   // JOG in neg. Richtung für Kanalachse A
R   DB20.DBX  13.3   // JOG in neg. Richtung für Kanalachse B
=   DB20.DBX  1.2
=   DB1.DBX 1734.6      // Deaktivierung absolute Achse 6 (A-Rundachse)
=   DB1.DBX 1734.7      // Deaktivierung absolute Achse 7 (A-Rundachse)
S   M 50.0
M7104:  NOP 0
NETWORK  1
TITLE = FREIGABE DIFFERENZIALLEITUNGSTREIBER AUF ACC-MOTHERBOARD
//  Gilt für alle drei Schrittmotormodule:
SET
S   A 3.7;          // FREIGABE DIFFERENZIALLEITUNGSTREIBER TAKTE FÜR SM
S   A 3.2;          // ENABLE SM MODUL C
NETWORK
TITLE = VORSCHUBSCHALTER AUF 0%
L   DB20.DBW 358;   // FEED-OVERRIDE-ISTWERT
L   W#16#0000;      // LADE 0
==I;                // VERGLEICH AUF GLEICH
=   M 127.3;        // VORSCHUBSCHALTER AUF 0%
NETWORK
TITLE = FREIGABE REFERENZIEREN
U   M 202.2;        // Tuer zu und verriegelt
U   DB20.DBX 340.4; // Achsfreigabe Kanal 0
U   M 126.6;        // Rundachsen  Betriebsbereit
UN  M 127.3;        // VORSCHUBSCHALTER AUF 0%
=   M 127.2;        // REF ACHSEN FREIGABE
UN  M 127.2;        // REF ACHSEN FREIGABE
=   M 127.4;        // Keine REF ACHSEN FREIGABE
NETWORK  2
TITLE = A,B-ACHSE REFERENZIEREN
U   DB1.DBX 134.2;  // REFERENZPUNKT Z-ACHSE AKTIV
FP  M 126.7;        // FM REFERENZPUNKT Z-ACHSE AKTIV
U   M 126.7;        // FM REFERENZPUNKT Z-ACHSE AKTIV
U   M 90.0;         // autom. Ref. aktiv
SPBN   M7105;
=   DB20.DBX 9.2;   // REFERENZIEREN VON KANALACHSE A
=   DB20.DBX 9.3;   // REFERENZIEREN VON KANALACHSE B
=   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
M7105:     NOP 0;
NETWORK
TITLE = Reglerfreigabe Drehantrieb
UN  DB2.DBX 0.0;    // ALARM NOT-AUS
U   M 126.3         // Einstellung des Wahlschalters – USBSPS3
U   DB1.DBX 1680.3  // X3:4  Ausgang Drehachse Klemmung öffnen
U   DB1.DBX 1652.5  // X5:6  Eingang Drehachse nicht geklemmt
L   S5TIME#1500MS;
SE  T 11
U   T 11
S   DB1.DBX 1680.7  // X3:8  Ausgang Reglerfreigabe Drehantrieb
UN  DB1.DBX 1680.3  // X3:4  Ausgang Drehachse Klemmung öffnen
U   DB1.DBX 1652.4  // X5:5  Eingang Drehachse geklemmt
O   DB2.DBX 0.0;    // ALARM NOT-AUS
R   DB1.DBX 1680.7  // X3:8  Ausgang Reglerfreigabe Drehantrieb
NETWORK
TITLE = Reglerfreigabe Schwenkantrieb
UN  DB2.DBX 0.0;    // ALARM NOT-AUS
U   M 126.3         // Einstellung des Wahlschalters – USBSPS3
U   DB1.DBX 1680.2  // X3:3  Ausgang Schwenkachse Klemmung öffnen
U   DB1.DBX 1652.2  // X5:3  Eingang Schwenkachse nicht geklemmt
L   S5TIME#1500MS;
SE  T 12
U   T 12
S   DB1.DBX 1680.5  // X3:6  Ausgang Reglerfreigabe Schwenkantrieb
UN  DB1.DBX 1680.2  // X3:3  Ausgang Schwenkachse Klemmung öffnen
U   DB1.DBX 1652.1  // X5:2  Eingang Schwenkachse geklemmt
O   DB2.DBX 0.0;    // ALARM NOT-AUS
R   DB1.DBX 1680.5  // X3:6  Ausgang Reglerfreigabe Schwenkantrieb
NETWORK
TITLE = REGLERFREIGABE Schwenkachse B ABSOLUTE ACHSE 6
U   DB1.DBX 1652.7  // X5:8  Eingang Betriebsbereit Schwenkachse B-Achse-6
FP  M 113.2         // FM Drehachse B-Achse  SERVO-READY
U   M 113.2         // FM Drehachse B-Achse  SERVO-READY
S   M 113.3         // HM REGLERFREIGABE ABSOLUTE ACHSE 6
U   DB1.DBX 1680.5  // X3:6  Ausgang Reglerfreigabe Schwenkantrieb
FN  M 113.4         // FM B-ACHSE Reglerfreigabe
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   M 113.4         // FM A-ACHSE Reglerfreigabe
R   M 113.3         // HM REGLERFREIGABE ABSOLUTE ACHSE 6
;       U   M 113.3         // HM REGLERFREIGABE ABSOLUTE ACHSE 6
U   DB1.DBX 1652.7  // X5:8  Eingang Betriebsbereit Schwenkachse B-Achse-6
=   DB1.DBX 1460.6  // ACC-REGLERFREIGABE Schwenkachse B ABSOLUTE ACHSE 6
NETWORK
TITLE = ACC-REGLERFREIGABE Drehachse A ABSOLUTE ACHSE 7
U   DB1.DBX 1653.0  // X5:9  Eingang Betriebsbereit Drehachse A-Achse-7
FP  M 112.2         // FM Drehachse A-Achse  SERVO-READY
U   M 112.2         // FM Drehachse A-Achse  SERVO-READY
S   M 112.3         // HM REGLERFREIGABE ABSOLUTE ACHSE 7
U   DB1.DBX 1680.7  // X3:8  Ausgang Reglerfreigabe Drehantrieb
FN  M 112.4         // FM A-ACHSE Reglerfreigabe
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   M 112.4         // FM A-ACHSE Reglerfreigabe
R   M 112.3         // HM REGLERFREIGABE ABSOLUTE ACHSE 7
;       U   M 112.3         // HM REGLERFREIGABE ABSOLUTE ACHSE 7
U   DB1.DBX 1653.0  // X5:9  Eingang Betriebsbereit Drehachse A-Achse-7
=   DB1.DBX 1460.7  // ACC-REGLERFREIGABE Drehachse A ABSOLUTE ACHSE 7
NETWORK
TITLE = ACHSEN BETRIEBSBEREIT
U(
UN  DB1.DBX 1680.2  // X3:3  Ausgang Schwenkachse Klemmung öffnen
U   DB1.DBX 1652.1  // X5:2  Eingang Schwenkachse geklemmt
O   DB1.DBX 1652.7  // X5:8  Eingang Betriebsbereit Schwenkachse B-Achse-6
)
U(
UN  DB1.DBX 1680.3  // X3:4  Ausgang Drehachse Klemmung öffnen
U   DB1.DBX 1652.4  // X5:5  Eingang Drehachse geklemmt
O   DB1.DBX 1653.0  // X5:9  Eingang Betriebsbereit Drehachse A-Achse-7
)
=   M 126.6;        // Rundachsen  Betriebsbereit
NETWORK
TITLE = Impulsfreigabe Dreh-Schwenkantrieb
UN  DB2.DBX 0.0;    // ALARM NOT-AUS
=   A 10.5          // Impulsfreigabe Dreh-Schwenkantrieb
NETWORK
TITLE = Kopplung Dreh-Schwenkachsen einschalten
U   M 126.6;        // Rundachsen  Betriebsbereit
S   DB1.DBX 1680.4  // X3:5  Ausgang Kopplung aktiv Schwenkantrieb
S   DB1.DBX 1680.6  // X3:7  Ausgang Kopplung aktiv Drehantrieb
NETWORK
TITLE = Kopplung Dreh-Schwenkachsen ausschalten
O   DB2.DBX 3.7;    // 6031 Antrieb Drehachse A-Achse-7 nicht bereit
O   DB2.DBX 13.1;   // 6105 Antrieb Schwenkachse B-Achse-6 nicht bereit
O   DB2.DBX 3.1;    // 6025 Stoerung E/R-Modul
O   DB2.DBX 0.0;    // 6000  NOT-AUS
=   M 119.5         // HM Kopplung Dreh-Schwenkachsen ausschalten
R   DB1.DBX 1680.4  // X3:5  Ausgang Kopplung aktiv Schwenkantrieb
R   DB1.DBX 1680.6  // X3:7  Ausgang Kopplung aktiv Drehantrieb
O   DB1.DBX 134.6;  // B-Schwenkachse 6 ( 11 ) ist referenziert
O   DB1.DBX 134.7;  // A-Rundachse 7 ( 10 ) ist referenziert
U   M 119.5         // HM Kopplung Dreh-Schwenkachsen ausschalten
=   DB20.DBX 348.4  // Referenzpunkt der Rundachsen deaktivieren (A,B, C Achse)
NETWORK
TITLE = RUNDACHSE KLEMMUNG LÖSEN
ON  DB20.DBX 843.2; // A ACHSE IPO BEFEHL AKTIV
ON  DB20.DBX 843.3  // B ACHSE IPO BEFEHL AKTIV
UN  DB1.DBX 1358.0  // Achse 0 in Bewegung (X)
UN  DB1.DBX 1358.1  // Achse 1 in Bewegung (Y)
UN  DB1.DBX 1358.2  // Achse 2 in Bewegung (Z)
UN  DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
O   DB20.DBX 193.3; // M11 DYN.KLEMMUNG LÖSEN
U   M 136.0;        // AUX-ON
S   DB1.DBX 1680.2  // X3:3  Ausgang Schwenkachse Klemmung öffnen
S   DB1.DBX 1680.3  // X3:4  Ausgang Drehachse Klemmung öffnen
=   DB20.DBX 257.3; // M11 RÜCKMELDEN
U   DB20.DBX 193.3; // M11 DYN.KLEMMUNG LÖSEN
S   M 119.3         // HM M11 DYN.KLEMMUNG LÖSEN
NETWORK
TITLE = EINLESEFREIGABE WÄHREND DER KLEMMPHASE WEGNEHMEN
U   DB20.DBX 257.3; // M11 RÜCKMELDEN
UN  M 119.3         // HM M11 DYN.KLEMMUNG LÖSEN
U   DB20.DBX 340.3; // EINLESEFREIGABE
S   M 119.6         // EINLESEFREIGABE WEGNEHMEN (VON RUNDACHSE)
U   DB20.DBX 193.2; // M10 DYN.KLEMMUNG EIN
R   M 119.3         // HM M11 DYN.KLEMMUNG LÖSEN
UN  M 119.3         // HM M11 DYN.KLEMMUNG LÖSEN
U   DB20.DBX 843.2  // A ACHSE IPO BEFEHL AKTIV
U   DB20.DBX 843.3  // B ACHSE IPO BEFEHL AKTIV
U   DB20.DBX 546.6  // Genauhalt fein für Achse 6
U   DB20.DBX 546.7  // Genauhalt fein für Achse 7
L   S5TIME#200MS;
SE  T 18;           // Genauhalt fein für Achse 6/7
U   T 18;           // Genauhalt fein für Achse 6/7
UN  DB1.DBX 1460.6  // ACC-REGLERFREIGABE Schwenkachse B ABSOLUTE ACHSE 6
UN  DB1.DBX 1460.7  // ACC-REGLERFREIGABE Drehachse A ABSOLUTE ACHSE 7
R   M 119.6         // EINLESEFREIGABE WEGNEHMEN (VON RUNDACHSE)
U   T 18;           // Genauhalt fein für Achse 6/7
FP  M 119.0         // Genauhalt fein für Achse 6/7
U   DB1.DBX 134.6;  // B-Schwenkachse 6 ( 11 ) ist referenziert
U   DB1.DBX 134.7;  // A-Rundachse 7 ( 10 ) ist referenziert
FP  M 119.7         // FM Achse 10/11 ist referenziert
NETWORK
TITLE = RUNDACHSE EINLESEFREIGABE
U   M 119.6         // EINLESEFREIGABE WEGNEHMEN (VON RUNDACHSE)
UN  DB20.DBX 862.0  // Transmit oder Tracyl aktiv
=   M 9.2;          // EINLESEFREIGABE WEGNEHMEN (VON 4.5.RUNDACHSE)
NETWORK
TITLE = RUNDACHSE KLEMMEN
O   M 119.7         // FM Achse 10/11 ist referenziert
O   M 119.0         // Genauhalt fein für Achse 6/7
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
ON  M 136.0;        // AUX-ON
R   DB1.DBX 1680.2  // X3:3  Ausgang Schwenkachse Klemmung öffnen
R   DB1.DBX 1680.3  // X3:4  Ausgang Drehachse Klemmung öffnen
=   DB20.DBX 257.2; // M10 RÜCKMELDEN
R   M 119.3         // HM M11 DYN.KLEMMUNG LÖSEN
NETWORK
TITLE = ALARM 6107 Drehachse Klemmung Endlagenfehler
O(
U   DB1.DBX 1680.3  // X3:4  Ausgang Drehachse Klemmung öffnen
U   DB1.DBX 1652.4  // X5:5  Eingang Drehachse geklemmt
)
O(
UN  DB1.DBX 1680.3  // X3:4  Ausgang Drehachse Klemmung öffnen
U   DB1.DBX 1652.5  // X5:6  Eingang Drehachse nicht geklemmt
)
O(
U   DB1.DBX 1680.3  // X3:4  Ausgang Drehachse Klemmung öffnen
UN  DB1.DBX 1652.5  // X5:6  Eingang Drehachse nicht geklemmt
)
O(
UN  DB1.DBX 1680.3  // X3:4  Ausgang Drehachse Klemmung öffnen
UN  DB1.DBX 1652.4  // X5:5  Eingang Drehachse geklemmt
)
UN  DB2.DBX 5.5     // FM6045 "Druckueberwachung Luftversorgung"
U   M 136.0;        // AUX-ON
L   S5T#500MS
SE  T 6
U   T 6
S   DB2.DBX 13.3;   // ALARM 6107 Drehachse Klemmung Endlagenfehler
U   M 138.0;        // Fl. Reset-Taste
R   DB2.DBX 13.3;   // ALARM 6107 Drehachse Klemmung Endlagenfehler
NETWORK
TITLE = ALARM 6108 Schwenkachse Klemmung Endlagenfehler
O(
U   DB1.DBX 1680.2  // X3:3  Ausgang Schwenkachse Klemmung öffnen
U   DB1.DBX 1652.1  // X5:2  Eingang Schwenkachse geklemmt
)
O(
UN  DB1.DBX 1680.2  // X3:3  Ausgang Schwenkachse Klemmung öffnen
U   DB1.DBX 1652.2  // X5:3  Eingang Schwenkachse nicht geklemmt
)
O(
U   DB1.DBX 1680.2  // X3:3  Ausgang Schwenkachse Klemmung öffnen
UN  DB1.DBX 1652.2  // X5:3  Eingang Schwenkachse nicht geklemmt
)
O(
UN  DB1.DBX 1680.2  // X3:3  Ausgang Schwenkachse Klemmung öffnen
UN  DB1.DBX 1652.1  // X5:2  Eingang Schwenkachse geklemmt
)
UN  DB2.DBX 5.5     // FM6045 "Druckueberwachung Luftversorgung"
U   M 136.0;        // AUX-ON
L   S5T#500MS
SE  T 7
U   T 7
S   DB2.DBX 13.4;   // ALARM 6108 Schwenkachse Klemmung Endlagenfehler
U   M 138.0;        // Fl. Reset-Taste
R   DB2.DBX 13.4;   // ALARM 6108 Schwenkachse Klemmung Endlagenfehler
O   DB2.DBX 13.3;   // ALARM 6107 Drehachse Klemmung Endlagenfehler
O   DB2.DBX 13.4;   // ALARM 6108 Schwenkachse Klemmung Endlagenfehler
FP  M 49.0
O   M 49.0
SPBN M7106
=   DB1.DBX 1440.0  // NC-RESET AUSLÖSEN
M7106: NOP 0;
END_FUNCTION
ORGANIZATION_BLOCK OB 1
TITLE=ZYK_OB
NAME: ZYK_OB
BEGIN
NETWORK
TITLE = E-Zuweisung ACC
UC    FC 11;              // Analogwerte lesen
UN    E 3.0;              // Drehz. nicht 0 Sicherheitsmodul
=     M 202.0;            // n=0 Sicherheitsmodul
U     E 7.0;              // Referenzpunkt Werkzeugwechsler
U     M 26.0;             // HM E7.0
S     M 7.0;              // Referenzpunkt Werkzeugwechsler gef.
UN    E 7.0;              // Referenzpunkt Werkzeugwechsler
UN    M 26.0;             // HM E7.0
R     M 7.0;              // Referenzpunkt Werkzeugwechsler gef.
U     E 7.0;              // Referenzpunkt Werkzeugwechsler
=     M 26.0;             // HM E7.0
U     E 7.1;              // Werkzeug loesen
U     M 26.1;             // HM E7.1
S     M 7.1;              // Werkzeug loesen gef.
UN    E 7.1;              // Werkzeug loesen
UN    M 26.1;             // HM E7.1
R     M 7.1;              // Werkzeug loesen gef.
U     E 7.1;              // Werkzeug loesen
=     M 26.1;             // HM E7.1
U     E 7.2;              // Werkzeug spannen
U     M 26.2;             // HM E7.2
S     M 7.2;              // Werkzeug spannen gef.
UN    E 7.2;              // Werkzeug spannen
UN    M 26.2;             // HM E7.2
R     M 7.2;              // Werkzeug spannen gef.
U     E 7.2;              // Werkzeug spannen
=     M 26.2;             // HM E7.2
U     E 7.3;              // Pocket down
UN    E 7.4;              // Pocket up
L     S5T#100MS;
SE    T 55;               // Pocket down verz.
UN    E 7.3;              // Pocket down
U     E 7.4;              // Pocket up
L     S5T#100MS;
SE    T 56;               // Pocket up verz.
U     E 7.6;              // Referenzpunkt Magazin
U     M 26.6;             // HM E7.6
S     M 27.6;             // Referenzpunkt Magazin gef.
UN    E 7.6;              // Referenzpunkt Magazin
UN    M 26.6;             // HM E7.6
R     M 27.6;             // Referenzpunkt Magazin gef.
U     E 7.6;              // Referenzpunkt Magazin
=     M 26.6;             // HM E7.6
NETWORK
TITLE = Werkzegwechsler Signalbildung
// V2.25: NW neu
U     DB10.DBX 51.4;      // USER_DATA_HEX[11]: WZW DETA
SPB   _DTA;
// Werkzeugmagazin Pragati:
U     M 7.0;
=     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
U     M 7.1;
=     M 27.1;             // Werkzeug loesen gef.
U     M 7.2;
=     M 27.2;             // Werkzeug spannen gef.
U     E 7.5;              // WZ-Halter in Position
=     M 7.5;
U     M 110.1;            // "1"
R     M 7.7;              // HM Motor Arm aus
SPA   _EWZ;
// Werkzeugmagazin DETA:
_DTA: UN    M 7.0;              // Referenzpunkt Werkzeugwechsler
U     M 7.1;              // tool clipping point
=     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
U     M 7.1;              // tool clipping point
FN    M 8.0;              // Fl. clipping point
U     M 7.2;              // Cam box brake sensor
UN    M 8.1;              // HM Teil 2
UN    M 8.2;              // HM Teil 3
S     M 27.1;             // Werkzeug lösen gef.
U     M 27.1;             // Werkzeug lösen gef.
U     M 8.0;              // Fl. clipping point
O     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
R     M 27.1;             // Werkzeug lösen gef.
S     M 8.1;              // HM Teil 2
U     M 8.1;              // HM Teil 2
U     M 7.2;              // Cam box brake sensor
S     M 8.2;              // HM Teil 3
U     M 8.1;              // HM Teil 2
U     M 8.2;              // HM Teil 3
U     M 8.0;              // Fl. clipping point
O     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
R     M 8.1;              // HM Teil 2
U     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
R     M 8.2;              // HM Teil 3
U     M 8.1;              // HM Teil 2
U     M 8.2;              // HM Teil 3
UN    M 7.2;              // Cam box brake sensor
=     M 27.2;             // Werkzeug spannen gef.
U     E 7.5;              // WZ-Halter in Position
=     M 7.5;
U     M 27.1;             // Werkzeug lösen gef.
UN    M 7.2;              // Cam box brake sensor
O     ;
U     M 27.2;             // Werkzeug spannen gef.
UN    M 7.2;              // Cam box brake sensor
=     M 7.7;              // HM Motor Arm aus
_EWZ: NOP   0;
NETWORK
TITLE = Zuweisungen fuer PC-Steuerung
U     DB1.DBX 1370.0;     // 1.SPS-SCHLEIFENDURCHLAUF
=     M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF
UN    M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF
SPB   M001;
// remanente Bereiche aus SE/Daten laden
L     DB15.DBW 20;        // Maschinenkonfig. in SE-Daten
T     MW 5;               // akt. Maschinenkonfiguration
L     DB15.DBB 34;        // SE-Daten: akt. Magazinplatz
T     DB100.DBB 29;       // Mag. Istpos. HW
L     DB15.DBB 35;        // SE-Daten: akt. Spindelwerkzeug CM450
T     DB100.DBB 31;       // WZ-Nummer in Spindel
L     DB100.DBB 29;       // Mag. Istpos. HW
L     0;
<>I   ;
S     DB100.DBX 35.6;     // WZW synchronisiert
M001: NOP   0;
// Achskommandos von externen Quellen ruecksetzen (ausser INC!)
;      U     M 110.1;            // "1"
;      UN    DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
;      R     DB20.DBX 1.2;       // JOG-Kommando
// Zuweisung der Tasten auf Merkerschnittstelle
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M022;
U     DB20.DBX 826.0;     // SKIP
=     M 100.7;
U     DB20.DBX 826.1;     // DRY
=     M 100.6;
U     DB20.DBX 826.2;     // A-
=     M 100.4;
U     DB20.DBX 826.3;     // Z+
=     M 100.3;
U     DB20.DBX 826.4;     // Y+
=     M 100.2;
U     DB20.DBX 826.5;     //
=     M 100.5;
U     DB20.DBX 826.6;     //
=     M 101.5;
U     DB20.DBX 826.7;     //
=     M 102.5;
U     DB20.DBX 827.0;     // Pinole
=     M 105.6;
U     DB20.DBX 827.1;     // WZW
=     M 105.5;
U     DB20.DBX 827.2;     //
=     M 105.4;
U     DB20.DBX 827.3;     // 1x
=     M 101.7;
U     DB20.DBX 827.4;     // OPT
=     M 101.6;
U     DB20.DBX 827.5;     // X-
=     M 101.4;
U     DB20.DBX 827.6;     // Eilgang
=     M 101.3;
U     DB20.DBX 827.7;     // X+
=     M 101.2;
U     DB20.DBX 828.0;     //
=     M 101.1;
U     DB20.DBX 828.1;     //
=     M 101.0;
U     DB20.DBX 828.2;     //
=     M 105.3;
U     DB20.DBX 828.3;     // Mag. -1
=     M 105.2;
U     DB20.DBX 828.4;     //
=     M 105.1;
U     DB20.DBX 828.5;     // Mag. +1
=     M 105.0;
O     DB20.DBX 828.6;     // Reset-Taste MSTT
O     DB1.DBX 1440.0;     // NC Reset Kommando
=     M 102.7;
U     DB20.DBX 828.7;     // SBL
=     M 102.6;
U     DB20.DBX 829.0;     // Y-
=     M 102.4;
U     DB20.DBX 829.1;     // Z-
=     M 102.3;
U     DB20.DBX 829.2;     // A+
=     M 102.2;
U     DB20.DBX 829.6;     //
=     M 106.6;
U     DB20.DBX 829.7;     //
=     M 106.5;
U     DB20.DBX 830.0;     // SPM 1 auf/zu
=     M 106.4;
U     DB20.DBX 830.1;     //
=     M 102.1;
U     DB20.DBX 830.2;     // PRT
=     M 102.0;
U     DB20.DBX 830.3;     //
=     M 104.5;
U     DB20.DBX 830.4;     // REF
=     M 104.3;
U     DB20.DBX 830.5;     //
=     M 104.0;
U     DB20.DBX 830.6;     // %-
=     M 106.7;
U     DB20.DBX 830.7;     // 100%
=     M 106.3;
U     DB20.DBX 831.0;     // %+
=     M 107.5;
U     DB20.DBX 831.1;     // KM
=     M 106.2;
U     DB20.DBX 831.2;     // SPF
=     M 106.1;
U     DB20.DBX 831.3;     //
=     M 106.0;
U     DB20.DBX 831.4;     // NC-Stop
=     M 104.7;
U     DB20.DBX 831.5;     // NC-Start
=     M 104.6;
U     DB20.DBX 831.6;     // VS-Halt
=     M 104.4;
U     DB20.DBX 832.0;     // VS-Start
=     M 104.2;
U     DB20.DBX 832.1;     // HA-Stop
=     M 104.1;
U     DB20.DBX 832.3;     // HA Start
=     M 107.7;
U     DB20.DBX 832.4;     // AUX OFF
=     M 107.6;
U     DB20.DBX 832.6;     // AUX-ON
=     M 107.4;            // Taste AUX-ON
U     M 107.4;            // Taste AUX-ON
FN    M 50.1              // FN Taste AUX-ON
U     DB20.DBX 833.3;     // ext. NC-Start Taste
=     M 201.5;            // Taste NC-Start extern
U     E 9.4;              // Zustimmtaste Handrad
U     DB10.DBX 50.1;      // USER_DATA_HEX[10]: Handrad vorhanden
O     DB20.DBX 834.0;     // Zustimmtaste Bedienpult
=     M 202.7;
M022: NOP   0;
NETWORK
TITLE =MSTT auf defekte Tasten pruefen
O(    ;
L     DB20.DBW 826;
L     0;
<>I   ;
)     ;
O(    ;
L     DB20.DBW 828;
L     0;
<>I   ;
)     ;
O(    ;
L     DB20.DBW 830;
L     0;
<>I   ;
)     ;
O     DB20.DBX 832.0;
O     DB20.DBX 832.1;
O     DB20.DBX 832.2;
O     DB20.DBX 832.3;
O     DB20.DBX 832.4;
O     DB20.DBX 832.5;
U     DB20.DBX 832.6;     // AUX-On Taste
UN    M 136.0;            // AUX-ON
=     DB2.DBX 2.0;        // FM6016 Tastatur defekt
NETWORK
TITLE =H-Aenderung um einen PLC-Zyklus verlaengern
O     DB20.DBX 182.1;     // H-Aenderung Spindel 0
O     M 111.1;            // HM H-Aend. verz.
=     M 111.0;            // H-Aenderung verl.
U     DB20.DBX 182.1;     // H-Aenderung Spindel 0
=     M 111.1;            // HM H-Aend. verz.
NETWORK
TITLE =Progr. laeuft wie Siemens
U     DB20.DBX 324.0;     // Progr. laeuft K0
UN    DB20.DBX 324.1;     // Progr. stop K0
UN    DB20.DBX 325.1;     // Stop durch Single K0
UN    M 111.5;            // M00/M01 aktiv
=     M 111.7;            // Programm laeuft
=     DB20.DBX 851.5;     // LED NC-Start Taste
NETWORK
TITLE =Progr. unterbrochen wie Siemens
O     DB20.DBX 324.1;     // Progr. stop K0
O     DB20.DBX 325.1;     // Stop durch Single K0
O     M 111.5;            // M00/M01 aktiv
U     DB20.DBX 324.0;     // Progr. laeuft K0
=     M 111.6;            // Programm unterbrochen
=     DB20.DBX 851.4;     // LED NC-Stop Taste
NETWORK
TITLE =M00/M01 aktiv nachbilden
U     DB20.DBX 192.1;     // M01 dyn.
U     DB20.DBX 324.5;     // Opt. Stop aktiv
O     DB20.DBX 192.0;     // M00 dyn.
S     M 111.5;            // M00/M01 aktiv
UN    DB20.DBX 324.1;     // Progr. stop K0
UN    DB20.DBX 324.0;     // Progr. laeuft K0
O     M 102.7;            // Reset-Taste
ON    M 136.0;            // AUX-ON
O     DB20.DBX 0.2;       // NC-Start Anforderung von Oberfläche
R     M 111.5;            // M00/M01 aktiv
NETWORK
TITLE =autom. Offsetabgleich in allen Achsen
U     M 155.2;            // BA REF
U     M 137.7;            // Antriebe ein
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
U     DB20.DBX 833.2;     // SS Dateneingabe
U     M 104.6;            // Taste NC-Start
U     M 104.2;            // Taste VS-Start
U     DB3.DBX 1.1;        // BM7009 Referenzpunkt nicht erreicht
UN    M 136.4;            // Summe Fahrbefehl
S     M 70.0;             // autom. Abgleich aktiv
U     DB1.DBX 1468.0;     // Abgleich beendet, Daten gesichert
S     M 70.7;             // autom. Abgleich beendet
O     M 102.7;            // Taste Reset
ON    M 136.0;            // AUX-ON
R     M 70.0;             // autom. Abgleich aktiv
R     M 70.7;             // autom. Abgleich beendet
U     M 70.0;             // autom. Abgleich aktiv
FP    M 70.1;             // Fl. Rundachse ein Offsetabgleich
U     M 70.0;             // autom. Abgleich aktiv
U     M 110.4;            // Spindel im Rundachsbetrieb
L     S5T#10S;            // Zeitwert ev. in MSD ablegen ?
SI    T 0;                // Dauer autom. Abgleich
U     T 0;
=     DB1.DBX 1466.0;     // Offsetabgleich durchfuehren
=     M 70.6;             // Abgleich durchfuehren AC
U     M 70.0;             // autom. Abgleich aktiv
UN    M 70.7;             // autom. Abgleich beendet
=     DB3.DBX 8.6;        // BM7070 Offsetabgleich aktiv
U     M 70.7;             // autom. Abgleich beendet
=     DB3.DBX 8.7;        // BM7071 Offsetabgleich beendet, Daten gesichert
NETWORK
TITLE =Hauptspindelleistung an Oberflaeche
L     DB1.DBW 1472;       // ACIF Analogkanal 0
T     DB1.DBW 1508;       // Leistung abs. Achse 3
NETWORK
TITLE =Zyklisches Anwenderprogramm
UC    FC 101;
NETWORK
TITLE =Zeitgesteuertes Anwenderprogramm
UN    DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
CC    FC 100;
NETWORK
TITLE =eingeschwenktes WZ an Anzeige melden
L     DB100.DBB 31;       // Werkzeug in Spindel
T     DB20.DBW 356;       // akt. WZ fuer Anzeige
NETWORK
TITLE =Magazinplatz an die Anzeige melden
L     DB100.DBB 29;       // Mag. Istpos. HW
T     DB20.DBW 956;       // Magazinplatz fuer Anzeige
NETWORK
TITLE = Kommandoanforderung REF generieren
L     DB20.DBW 8;         // REF-Anforderung fuer alle Achsen
L     0;
<>I   ;
FP    DB20.DBX 1.1;       // Anforderung REF
ON    M 137.7;            // Antriebe ein
O     M 127.4;            // Keine REF ACHSEN FREIGABE
R     DB20.DBX 1.1;       // Anforderung REF
NETWORK
TITLE = JOG-ACHSEN/STARTVERR.
U   DB20.DBX 1.2;   // FC JOG-ACHSEN
UN  DB20.DBX 340.4; // Achsfreigabe Kanal 0
;       O   M 149.1         // NEGATIV FLANKENMERKER AUS FREIGABE VON SICHERHEITSEINRICHTUNGEN
SPBN  M1051
=   DB20.DBX 1.2;   // JOG-ANFORDERUNG
L   DW#16#0000_0000;// LADE 0
T   DB20.DBD 10;    // JOG IN ALLEN ACHSEN AUS
M1051: NOP 0
NETWORK
TITLE = INC-ACHSEN/STARTVERR.
U   DB20.DBX 1.3;   // FC INC ANFORDERUNG
U   DB20.DBX 340.4; // Achsfreigabe Kanal 0
=   DB20.DBX 1.3;   // FC INC ANFORDERUNG
NETWORK
TITLE = Funktionen fuer Handrad
O     DB20.DBX 326.5;     // BA INC1 von Kern
O     DB20.DBX 326.6;     // BA INC10 von Kern
O     DB20.DBX 326.7;     // BA INC100 von Kern
O     DB20.DBX 327.0;     // BA INC1000 von Kern
=     M 97.6;             // Handrad-BA
U(    ;
O(    ;
U     M 97.6;             // Handrad-BA
UN    M 97.7;             // HM Handrad aktiv
)     ;
O     M 97.0;             // Fl. Anwahl INC1
O     M 97.1;             // Fl. Anwahl INC10
O     M 97.2;             // Fl. Anwahl INC100
O     M 97.3;             // Fl. Anwahl INC1000
)     ;
U     DB10.DBX 50.1;      // USER_DATA_HEX[10]: Handrad vorhanden
=     DB1.DBX 1440.2;     // Handrad Anwahl
O     M 138.0;            // Fl. Reset
ON    M 97.6;             // Handrad-BA
U     M 97.7;             // HM Handrad aktiv
=     DB1.DBX 1440.3;     // Handrad Abwahl
U     DB1.DBX 1440.2;     // Handrad Anwahl
S     M 97.7;             // HM Handrad aktiv
U     DB1.DBX 1440.3;     // Handrad Abwahl
R     M 97.7;             // HM Handrad aktiv
// INC-Schrittweite bei BA-Wechsel mitfuehren
U     DB20.DBX 326.5;     // BA INC1 von Kern
FP    M 97.0;             // Fl. Anwahl INC1
UN    M 97.0;
SPB   INC1;
L     +1.0E-06;
T     DB20.DBD 16;        // INC Schrittweite
INC1: NOP   0;
U     DB20.DBX 326.6;     // BA INC10 von Kern
FP    M 97.1;             // Fl. Anwahl INC10
UN    M 97.1;
SPB   INC2;
L     +1.0E-05;
T     DB20.DBD 16;        // INC Schrittweite
INC2: NOP   0;
U     DB20.DBX 326.7;     // BA INC100 von Kern
FP    M 97.2;             // Fl. Anwahl INC100
UN    M 97.2;
SPB   INC3;
L     +1.0E-04;
T     DB20.DBD 16;        // INC Schrittweite
INC3: NOP   0;
U     DB20.DBX 327.0;     // BA INC1000 von Kern
FP    M 97.3;             // Fl. Anwahl INC1000
UN    M 97.3;
SPB   INC4;
L     +1.0E-03;
T     DB20.DBD 16;        // INC Schrittweite
INC4: NOP   0;
NETWORK
TITLE = Kommandoanforderung JOG generieren
;    // das JOG-Kommando wird verzoegert, weil die Tasten und das von
;    // der Oberflaeche generierte INC-Kommando nicht immer im selben
;    // PLC Durchlauf kommen
;      L     DB20.DBD 10;        // JOG+ und JOG- fuer alle Achsen
;      L     DB100.DBD 810;      // Schattenspeicher JOG-Kommandos
;      <>D   ;                   // bei jeder Aenderung...
;      S     DB100.DBX 9.0;      // HM JOG aktiv
;
;    // bei TNC426 nur wenn wirklich JOG angewaehlt ist
;      ON    DB1.DBX 1403.2;     // TNC426
;      O     DB20.DBX 288.0;     // BA JOG
;      =     DB100.DBX 9.2;      // HM JOG Kommando freigeben
;
;      U     DB100.DBX 9.2;      // HM JOG Kommando freigeben
;      L     S5T#100MS;
;      SE    T 1;                // Verzoegerung JOG
;      U     T 1;
;      =     DB20.DBX 1.2;       // JOG-Kommando
;      U     DB100.DBX 9.0;      // HM JOG aktiv
;
;      O     DB20.DBX 1.2;       // JOG-Kommando
;      O     DB20.DBX 1.3;       // INC-Kommando
;      O     DB100.DBX 9.1;      // INC JOG Verriegelung
;      R     DB100.DBX 9.0;      // HM JOG aktiv
;
;    // Schattenspeicher fuer Flankenerkennung
;      L     DB20.DBD 10;        // JOG+ und JOG- fuer alle Achsen
;      T     DB100.DBD 810;      // Schattenspeicher JOG-Kommandos
;
;    // Verriegelung INC gegen JOG: weil sonst bei Betaetigung
;    // von zwei Tasten und loslassen einer Taste ein Jog-Kommando
;    // generiert wuerde.
;      U     DB20.DBX 1.3;       // INC-Kommando
;      S     DB100.DBX 9.1;      // INC JOG Verriegelung
;      L     DB20.DBD 10;        // JOG+ und JOG- fuer alle Achsen
;      L     0;                  // keine Taste mehr betaetigt?
;      ==D   ;
;      L     S5T#150MS;
;      SE    T 7;                // Verzoegerung JOG
;      U     T 7;
;      U     DB100.DBX 9.1;      // INC JOG Verriegelung
;      R     DB100.DBX 9.1;      // INC JOG Verriegelung
NETWORK
TITLE =M-Code Rueckmeldung
;      L     DB20.DBD 192;
;      T     DB20.DBD 256;
;      L     DB20.DBD 196;
;      T     DB20.DBD 260;
;      L     DB20.DBD 200;
;      T     DB20.DBD 264;
;      L     DB20.DBD 204;
;      T     DB20.DBD 268;
;      L     DB20.DBD 208;
;      T     DB20.DBD 272;
;      L     DB20.DBD 212;
;      T     DB20.DBD 276;
;      L     DB20.DBD 216;
;      T     DB20.DBD 280;
;      L     DB20.DBD 220;
;      T     DB20.DBD 284;
NETWORK
TITLE =LED's an Tastatur bei Shutdown loeschen
U     DB1.DBX 1440.1;     // Shutdown angefordert
S     M 0.3;              // Shutdown aktiv
UN    M 0.3;              // Shutdown aktiv
SPB   _NSD;
L     0;
T     DB20.DBD 846;
T     DB20.DBD 850;
T     DB20.DBD 854;
T     DB20.DBD 858;
_NSD: NOP   0;
END_ORGANIZATION_BLOCK
//**************************************************************************
// DB100 wird vom PLC-Besy mit fester Laenge (1000 Bytes) angelegt!!!
// Diese Definition ist nur wegen Import in S7 notwendig
// Es koennen dzt. keine anderen DB's definiert werden!!!
DATA_BLOCK DB 100       // zweiter Merkerbereich
STRUCT
daten : ARRAY  [1 .. 1000 ] OF BYTE ;
END_STRUCT ;
BEGIN
// keine Vorbelegungen - DB100 ist nicht remanent!!
END_DATA_BLOCK
//**************************************************************************
FUNCTION FC 101 : VOID
TITLE=EMCO_SPRUNG
NAME: EMCO_SPR
BEGIN
NETWORK
TITLE =SET
O     M 110.1;
ON    M 110.1;
S     M 110.1;            // "1"
R     M 110.0;            // "0"
NETWORK
TITLE =PLC-Stueckzaehler (abhaengig von Oberflaeche)
UC    FC 2;            // PLC-Stueckzaehler
NETWORK
TITLE =Sicherheitstechnik/Sonderbetrieb
UC    FC 51;           // Sicherheitstechnik/Sonderbetriebsart
UC    FC 170;           // Achsueberwachungen
UC    FC 12;           // Daten Sicherheitsmodul lesen
NETWORK
TITLE =Betriebsarten/Konfiguration
UC    FC 52;           // Betriebsarten/Konfiguration
NETWORK
TITLE =PLC-MD Aufbereiten
UC    FC 53;
NETWORK
TITLE =Kuehlmittel
UC    FC 50;           // Kuehlmittel
U     M 110.1;            // "1"
=     DB20.DBX 382.3;     // KM vorhanden
=     DB20.DBX 382.4;     // Ausblasvorrichtung vorhanden
NETWORK
TITLE =4 frei programmierbare Ausgaenge
UC    FC 57;           // freie M-Funktionen
NETWORK
TITLE =Referenzieren/Freifahren
UN    DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
CC    FC 59;           // Referenzieren/Freifahren
NETWORK
TITLE =Achsen
UC    FC 60;           // Achsen
NETWORK
TITLE =Tuerautomatik
U     DB10.DBX 41.1;      // USER_DATA_HEX[1]: Tuerautomatik angewaehlt
=     DB1.DBX 1418.0;     // autom. Tuer vorhanden
CC    FC 61;           // Bst. Tuerautomatik
NETWORK
TITLE =Erkennung Teilapparat, A-Achse parkend
U     E 9.1;              // Erkennung RTA
U     DB10.DBX 42.0;      // USER_DATA_HEX[2] RTA angew.
UN    DB10.DBX 100.2      // ACC_MSD B100=4  (mill450B.MSD) 4.5. UCAM Rundachse aktiv
UN    DB1.DBX 1402.2;     // Steuerungskennung EMCOTRONIC TM02
UN    DB1.DBX 1402.5;     // Steuerungskennung Heidenhain TNC355
S     M 121.0;            // RTA angeschlossen
S     DB1.DBX 1534.0;     // $A_IN[1]: RTA aktiv
ON    E 9.1;              // Erkennung RTA
ON    DB10.DBX 42.0;      // USER_DATA_HEX[2] RTA angew.
O     DB10.DBX 100.2      // ACC_MSD B100=4  (mill450B.MSD) 4.5. UCAM Rundachse aktiv
R     M 121.0;            // RTA angeschlossen
R     DB1.DBX 1534.0;     // $A_IN[1]: RTA aktiv
U     M 121.0;            // RTA angeschlossen
=     DB20.DBX 382.5;     // Teilapparat vorhanden
UN    M 121.0;            // RTA angeschlossen
=     A 9.7;              // Abwahl 4. Achse (parkend)
NETWORK
TITLE =Teilapparat
U     DB10.DBX 42.0;      // USER_DATA_HEX[2] RTA angew.
UN    DB10.DBX 100.2      // ACC_MSD B100=4  (mill450B.MSD) 4.5. UCAM Rundachse aktiv
CC    FC 65;           // Teilapparat
NETWORK
TITLE =Spindel
UC    FC 70;           // Spindel
NETWORK
TITLE =UCAM Dreh-Schwenkeinheit
U     DB10.DBX 100.2     // ACC_MSD B100=4  (mill450B.MSD) 4.5. UCAM Rundachse aktiv
UN    DB10.DBX 42.0;     // USER_DATA_HEX[2] RTA angew.
U     DB1.DBX 1652.3     // Eingang X5:4  UCAM Schwenkeinheit angeschlossen
=     DB1.DBX 1681.0     // Ausgang X3:9 am Regler parkende Achse abschalten 4.und 5. Achse
CC    FC 71;             // UCAM Dreh-Schwenkeinheit
NETWORK
TITLE =Aufruf Werkzeugwechselbausteine
UC    FC 58;           // WZW-Freifahrlogik, Ueberwachungen
UC    FC 76;           // progr. WZW: Verwaltung und Ueberwachung
UC    FC 75;           // Werkzeugwechsel Ablauf
UC    FC 77;           // WZ-Magazin
UC    FC 78;           // WZ-Schwenkarm
NETWORK
TITLE =Hilfsantriebe
UC    FC 80;           // Hilfsantriebe
NETWORK
TITLE =Schmiermittel
UC    FC 81;           // Schmiermittel
NETWORK
TITLE =Spaenefoerderer
UC    FC 54;           // Spaenefoerderer
NETWORK
TITLE =Robotik-Interface Festo
U    DB1.DBX 1426.1;     // Robotik-Interface installiert (SW)
U(
O    DB10.DBX 48.2;      // USER_DATA_HEX[8]=4  USB Robotik-Interface
O    DB10.DBX 48.1;      // USER_DATA_HEX[8]=2  ACC Robotik-Interface
)
=    M 191.0;            // Robotik aktiv
CALL  FC 109;             // Festo Robotik-Interface
NETWORK
TITLE =Warnlampe
UC    FC 82;           // Warnlampe 2fach
NETWORK
TITLE =NC-Start Dauerlauf
UC    FC 83;           // NC-Start Dauerlauf
NETWORK
TITLE =NC-Start/NC-Stop
UC    FC 84;           // NC Start-Stop
NETWORK
TITLE =NC-Daten lesen/schreiben, PI-Dienste
UC    FC 55;           // NC-Daten lesen/schreiben, PI-Dienste
NETWORK
TITLE =FM,BM nach Einschalten initialisieren
// nicht bei PC-Maschine
NETWORK
TITLE =Spannmittel 1
UC    FC 88;
NETWORK
TITLE =PLC=>NC Transfer
UC    FC 85;           // PLC=>NC
END_FUNCTION
//**************************************************************************
FUNCTION FC 50 : VOID
TITLE=KUEHLMITTEL
NAME: KM
BEGIN
NETWORK
TITLE = Flanke Taste Kuehlmittel
U     M 106.2;            // Taste Kuehlmittel
FP    M 162.5;            // Flanke Taste KM
NETWORK
TITLE = Kuehlmittel 1 programmiert
O     DB20.DBX 193.0;     // M08 dyn.
O     DB20.DBX 302.6;     // DNC KM ein
O     DB20.DBX 201.6;     // M78 dyn.
O(    ;
U     DB20.DBX 194.2;     // M18 dyn.
UN    DB10.DBX 42.7;      // USER_DATA_HEX[2]: KM 3 Hochdruck
)     ;
O(    ;
U     DB20.DBX 201.5;     // M77 dyn.
UN    DB10.DBX 58.3;      // USER_DATA_HEX[18] Minimalschmierung
)     ;
S     M 162.1;            // KM1 programmiert
R     M 162.4;            // alle KM aus
O(    ;
U     DB20.DBX 194.2;     // M18 dyn.
U     DB10.DBX 42.7;      // USER_DATA_HEX[2]: KM 3 Hochdruck
)     ;
O(    ;
U     DB20.DBX 201.5;     // M77 dyn.
U     DB10.DBX 58.3;      // USER_DATA_HEX[18] Minimalschmierung
)     ;
O     M 162.4;            // alle KM aus
R     M 162.1;            // KM1 programmiert
NETWORK
TITLE = Kuehlmittel 2 programmiert (Spuelung)
O     DB20.DBX 192.7;     // M07 dyn.
O     DB20.DBX 200.7;     // M71 dyn.
S     M 162.2;            // KM2 programmiert
O     DB20.DBX 201.0;     // M72 dyn.
O     M 162.4;            // alle KM aus
ON    DB10.DBX 42.6;      // USER_DATA_HEX[2]: KM 2 Spuelung Arbeitsraum
ON    M 136.0;            // AUX-ON
R     M 162.2;            // KM2 programmiert
NETWORK
TITLE = Kuehlmittel 3 programmiert (Hochdruck)
O     DB20.DBX 194.2;     // M18 dyn.
O     DB20.DBX 201.6;     // M78 dyn.
S     M 161.1;            // KM3 programmiert
O     DB20.DBX 193.0;     // M08 dyn.
O     DB20.DBX 201.5;     // M77 dyn.
O     M 162.4;            // alle KM aus
ON    DB10.DBX 42.7;      // USER_DATA_HEX[2]: KM 3 Hochdruck
R     M 161.1;            // KM3 programmiert
NETWORK
TITLE = Minimalschmierung programmiert
U     DB20.DBX 201.5;     // M77 dyn.
S     M 162.3;            // Minimalschmierung progr.
O     DB20.DBX 193.0;     // M08 dyn.
O     DB20.DBX 194.2;     // M18 dyn.
O     DB20.DBX 201.6;     // M78 dyn.
O     M 162.4;            // alle KM aus
ON    DB10.DBX 58.3;      // USER_DATA_HEX[18] Minimalschmierung
R     M 162.3;            // Minimalschmierung progr.
NETWORK
TITLE = Blasluft Fraesspindel programmiert
O     DB20.DBX 198.0;     // M48 dyn.
O     DB20.DBX 303.0;     // DNC ausblasen ein
S     M 161.6;            // Blasluft ein
O     M 162.4;            // alle KM aus
O     DB20.DBX 302.7;     // DNC ausblasen aus
R     M 161.6;            // Blasluft ein
U     M 161.6;            // Blasluft ein
=     DB20.DBX 310.7;     // Rueckmeldung Ausblasen aktiv
NETWORK
TITLE = alle Kuehlmittel ausschalten
O     M 102.7;            // Reset Taste
O     DB20.DBX 192.2;     // M02 dyn.
O     DB20.DBX 193.1;     // M09 dyn.
O     DB20.DBX 195.6;     // M30 dyn.
O     DB20.DBX 302.5;     // DNC KM aus
ON    M 136.0;            // AUX-ON
=     M 162.4;            // alle KM aus
R     M 162.1;            // KM1 programmiert
NETWORK
TITLE = KM unterbrochen mit Taste
O(    ;
U     M 162.5;            // Fl. Taste KM
U     M 162.7;            // KM unterbrochen mit Taste
)     ;
O(    ;
UN    M 162.1;            // KM1 programmiert
UN    M 162.2;            // KM2 programmiert
UN    M 161.1;            // KM3 programmiert
UN    M 162.3;            // Minimalschmierung progr.
)     ;
R     M 162.7;            // KM unterbrochen mit Taste
SPB   M061;
U     M 162.5;            // Fl. Taste KM
U     M 162.0;            // KM Freigabe
UN    M 162.7;            // KM unterbrochen mit Taste
S     M 162.7;            // KM unterbrochen mit Taste
M061: NOP   0;
NETWORK
TITLE = KM ein mit Taste
O(    ;
U     M 106.2;            // Taste KM
UN    M 202.2;            // Tuer zu, verr.
)     ;
O(    ;
U     M 162.5;            // Fl. Taste KM
UN    M 162.6;            // KM ein mit Taste
)     ;
UN    M 162.1;            // KM1 programmiert
UN    M 162.2;            // KM2 programmiert
UN    M 161.1;            // KM3 programmiert
UN    M 162.3;            // Minimalschmierung progr.
U     M 136.0;            // AUX-ON
S     M 162.6;            // KM ein mit Taste
SPB   M071;
U     M 162.5;            // Fl. Taste KM
U     M 162.6;            // KM ein mit Taste
O     M 162.1;            // KM1 programmiert
O     M 162.2;            // KM2 programmiert
O     M 161.1;            // KM3 programmiert
O     M 162.3;            // Minimalschmierung progr.
O     M 162.4;            // alle KM aus
ON    M 202.2;            // Tuer zu, verr.
R     M 162.6;            // KM ein mit Taste
M071: NOP   0;
NETWORK
TITLE = Kuehlmittel Freigabe
// V2.22 Pumpen aus bei Kühlmittelstand Minimum
U(    ;
O     M 202.2;            // Tuer zu, verr.
O     M 106.2;            // Taste Kuehlmittel
)     ;
UN    M 111.5;            // M00/M01 aktiv
UN    DB20.DBX 324.2;     // DRY aktiv
UN    M 162.7;            // KM unterbrochen mit Taste
U(    ;
ON    M 111.6;            // Programm unterbrochen
ON    M 155.1;            // BA AUTO
)     ;
U(    ;
ON    M 146.3;            // VSH/EFG WZW schwenkt
O     DB10.DBX 40.7;      // USER_DATA_HEX[0] KM bei WZW nicht wegschalten
)     ;
UN    M 161.0;            // Sperre KM Minimum
U     E 4.6;              // Therm. Stoerung sofort
=     M 162.0;            // KM Freigabe
NETWORK
TITLE = Leistung Kuehlmittelpumpe 1
U(    ;
O     M 162.1;            // KM1 programmiert
O     M 162.6;            // KM ein mit Taste
)     ;
U     M 162.0;            // KM Freigabe
O     ;
U     M 161.7;            // KM Pistole ein
U     DB10.DBX 58.0;      // USER_DATA_HEX[18]: KM Pistole auf KM1
=     A 4.2;              // Leistung KM1
NETWORK
TITLE = Leistung Kuehlmittelpumpe 2 (Spuelung)
U     M 162.2;            // KM2 programmiert
U     M 162.0;            // KM Freigabe
U     DB10.DBX 42.6;      // USER_DATA_HEX[2] KM 2 Spuelung Arbeitsraum
O     ;
U     M 161.7;            // KM Pistole ein
U     DB10.DBX 58.1;      // USER_DATA_HEX[18]: KM Pistole auf KM2
=     A 4.3;              // Leistung KM2
NETWORK
TITLE = Leistung Kuehlmittelpumpe 3 (Hochdruck)
U     M 161.1;            // KM3 programmiert
U     M 162.0;            // KM Freigabe
U     DB10.DBX 42.7;      // USER_DATA_HEX[2] KM 3 Hochdruck
=     A 5.3;              // Leistung KM3
NETWORK
TITLE = MV Minimalschmierung
UN    DB10.DBX 58.3;      // USER_DATA_HEX[18] Minimalschmierung
SPB   M111;
U     M 162.3;            // Minimalschmierung progr.
U     M 162.0;            // KM Freigabe
=     A 4.4;              // MV Minimalschmierung
M111: NOP   0;
NETWORK
TITLE = Schwimmerschalter Kuehlmittel
// V2.19: Funktion aktiviert
// V2.22: Funktion erweitert
U(    ;
U     E 9.6;              // Schwimmerschalter Minimalschmierung
UN    DB10.DBX 57.3;      // USER_DATA_HEX[17]: inv. SS Minimalschmierung
O     ;
UN    E 9.6;              // Schwimmerschalter Minimalschmierung
U     DB10.DBX 57.3;      // USER_DATA_HEX[17]: inv. SS Minimalschmierung
)     ;
U     M 110.7;            // Blinktakt
U     DB10.DBX 58.3;      // USER_DATA_HEX[18] Minimalschmierung
=     DB3.DBX 3.7;        // BM7231 Minimalschmiersystem leer
UN    E 9.6;              // Schwimmerschalter Kuehlmittel
U     DB10.DBX 57.5;      // USER_DATA_HEX[17]: SS Kühlmittel vorh.
L     S5T#1S;
SE    T 46;
U     T 46;
S     DB3.DBX 5.1;        // BM7041 Kuehlmittelstand pruefen
O     DB20.DBX 544.2;     // Eilgangbewegung aktiv
O     DB10.DBX 58.5;      // USER_DATA_HEX[18]: BM7041 VSH sofort
U     DB3.DBX 5.1;        // BM7041 Kuehlmittelstand pruefen
U     M 155.1;            // BA AUTO
S     M 147.1;            // VSH BM7041
U(    ;
O     M 104.2;            // Taste VS-Start
O     M 107.7;            // Taste Spindel start
)     ;
U     E 9.6;              // Schwimmerschalter Kuehlmittel
O     ;
U     M 102.7;            // Reset-Taste
UN    DB20.DBX 324.0;     // Programm laeuft
R     M 147.1;            // VSH BM7041
R     DB3.DBX 5.1;        // BM7041 Kuehlmittelstand pruefen
U     M 147.1;            // VSH BM7041
U     DB20.DBX 324.0;     // Programm laeuft
O     ;
U     DB3.DBX 5.1;        // BM7041 Kuehlmittelstand pruefen
U(    ;
ON    DB20.DBX 324.0;     // Programm laeuft
ON    M 155.1;            // BA AUTO
)     ;
=     M 161.0;            // Sperre KM Minimum
NETWORK
TITLE = BM7040 Kuehlmittelfilter verschmutzt
// V2.22: Netzwerk neu
UN    E 3.7;              // DS Kuehlmittelfilter
U     DB10.DBX 57.4;      // USER_DATA_HEX[17]: DS KM-Filter vorh.
L     S5T#1S;
SE    T 47;
U     T 47;
S     DB3.DBX 5.0;        // BM7040 Kuehlmittelfilter verschmutzt
O     DB20.DBX 544.2;     // Eilgangbewegung aktiv
O     DB10.DBX 58.4;      // USER_DATA_HEX[18]: BM7040 VSH sofort
U     DB3.DBX 5.0;        // BM7040 Kuehlmittelfilter verschmutzt
U     M 155.1;            // BA AUTO
S     M 147.2;            // VSH BM7040
U(    ;
O     M 104.2;            // Taste VS-Start)
O     M 107.7;            // Taste Spindel start
)     ;
U     E 3.7;              // DS Kuehlmittelfilter
O     ;
U     M 102.7;            // Reset-Taste
UN    DB20.DBX 324.0;     // Programm laeuft
R     M 147.2;            // VSH BM7040
R     DB3.DBX 5.0;        // BM7040 Kuehlmittelfilter verschmutzt
NETWORK
TITLE = LED KM-Taste
U     M 162.7;            // KM unterbrochen mit Taste
U     M 110.7;            // Blinktakt
O     A 4.2;              // Leistung KM1
O     A 5.3;              // Leistung KM3
O     A 4.4;              // MV Minimalschmierung
=     DB20.DBX 851.1;     // LED KM-Taste
O     A 4.2;              // Leistung KM1
O     A 5.3;              // Leistung KM3
O     A 4.4;              // MV Minimalschmierung
=     DB20.DBX 310.6;     // Rueckmeldung KM spritzt
O     M 162.7;            // KM unterbrochen mit Taste
O     DB20.DBX 310.6;     // Rueckmeldung KM spritzt
=     DB20.DBX 310.5;     // Rueckmeldung KM ein
NETWORK
TITLE = MV KM Pistole
O     DB10.DBX 58.0;      // USER_DATA_HEX[18]: KM Pistole auf KM1
O     DB10.DBX 58.1;      // USER_DATA_HEX[18]: KM Pistole auf KM2
U     E 9.7;              // SS KM Pistole
UN    M 202.2;            // Tuer zu, verr.
U     M 136.0;            // Aux-On
=     M 161.7;            // KM Pistole ein
NETWORK
TITLE = MV KM Pistole
U     A 4.2;              // Leistung KM1
UN    M 161.7;            // KM Pistole ein
U     DB10.DBX 58.0;      // USER_DATA_HEX[18]: KM Pistole auf KM1
O     ;
U     A 4.3;              // Leistung KM2
UN    M 161.7;            // KM Pistole ein
U     DB10.DBX 58.1;      // USER_DATA_HEX[18]: KM Pistole auf KM2
=     A 9.3;              // MV KM Pistole
END_FUNCTION
//**************************************************************************
FUNCTION FC 51 : VOID
TITLE=SICHERHEITSTECHNIK
NAME: SITECH
BEGIN
NETWORK
TITLE = Sonderbetriebsart
U(    ;
U     DB20.DBX 324.7;     // SBL aktiv
U     DB20.DBX 324.2;     // DRY aktiv
ON    M 155.1;            // BA AUTO
)     ;
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
=     M 137.1;            // Einrichtebetriebsart
=     DB20.DBX 340.2;     // Sonderbetrieb an AC
=     DB1.DBX 1390.6;     // Rueckmeldung Sonderbetrieb
U     M 137.1;            // Einrichtebetriebsart
UN    M 202.2;            // Tuer zu und verriegelt
=     M 210.7;            // Sonderbetriebsart
NETWORK
TITLE = Verr. Start Einrichtbetrieb
U     M 155.1;            // BA Auto
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
UN    DB100.DBX 35.2;     // man. WZW aktiv
=     M 145.5;            // Verr. Start Einrichtbetrieb
NETWORK
TITLE = Verr. Start Tuer
ON    T 42;               // Antriebe ein verzoegert
O     DB2.DBX 8.0;        // FM6064 Sicherheitsleiste Tuerautom.
=     M 145.2;            // Start Verr. Tuer
NETWORK
TITLE = Tuertest nach Einschalten
U     M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF
S     M 138.4;            // Tuertest angefordert
U     E 2.2;              // Tuer offen
UN    M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF
R     M 138.4;            // Tuertest angefordert
UN    E 2.2;              // Tuer offen
U(    ;
O     DB100.DBX 174.0;    // SBD Signal Tuer zu
ON    DB100.DBX 174.4;    // SBD kein Alarm
)     ;
UN    M 138.4;            // Tuertest angefordert
=     M 202.2;            // Tuer zu und verriegelt
NETWORK
TITLE = BM7018 Tuer offen
O     M 202.2;            // Tuer zu und verriegelt
O     M 138.0;            // Fl. Reset Taste
R     DB3.DBX 2.2;        // BM7018 Tuer offen
UN    M 202.2;            // Tuer zu und verriegelt
U     M 136.0;            // AUX-ON
U(    ;
UN    DB1.DBX 1402.1;     // Fanuc 0TB
UN    DB1.DBX 1403.0;     // Fanuc 21TB
O     M 137.2;            // Start Befehle
)     ;
S     DB3.DBX 2.2;        // BM7018 Tuer offen
U     E 6.5;              // Tuerautom. Tuer offen
U     DB10.DBX 41.1;      // USER_DATA_HEX[1]: Tuerautomatik angewaehlt
O     ;
UN    DB10.DBX 41.1;      // USER_DATA_HEX[1]: Tuerautomatik angewaehlt
UN    M 202.2;            // Tuer zu und verriegelt
=     DB1.DBX 1390.0;     // Rueckmeldung Tuer offen
U     M 202.2;            // Tuer zu und verriegelt
UN    DB100.DBX 136.7;    // Anforderung Teststop
=     DB1.DBX 1390.1;     // Rueckmeldung Tuer zu
NETWORK
TITLE =VSH/EFG Antriebe nicht bereit
U     M 111.7;            // Programm laeuft
UN    DB20.DBX 324.3;     // Satzsuchl. aktiv
UN    T 42;               // Antriebe ein verzoegert
=     M 147.4;            // VSH/EFG Antriebe nicht bereit
NETWORK
TITLE = FM6039 Tuerueberwachung
U     M 202.2;            // Tuer zu und verriegelt
L     S5T#1S;             // Abfallverzoegerung Ausgabemodule
SA    T 27;               // Antriebe ein verz.
O     M 111.7;            // Programm laeuft
O     M 136.6;            // Spindel laeuft
UN    T 27;               // Antriebe ein verz.
U     E 2.4;              // Not Aus
U     M 136.0;            // AUX-ON
L     S5T#150MS;
SE    T 28;
U     T 28;
S     DB2.DBX 4.7;        // FM6039 Tuerueberwachung
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 4.7;        // FM6039
NETWORK
TITLE = Flanke Schluesselsch. Einrichtbetrieb
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
FP    M 140.4;            // Flanke SS Einrichten
NETWORK
TITLE = Flanke Umschalten Sonderbetriebsart
U     M 140.4;            // Flanke SS Einrichten
U(    ;
O     M 111.7;            // Programm laeuft
O     M 136.6;            // Spindel laeuft
)     ;
U     M 136.0;            // AUX-ON
=     M 140.5;            // Flanke Umschalten Sonder-BA
NETWORK
TITLE = BM7026 Falsche Betriebsart
U     M 137.2;            // Start Befehle
U     M 145.5;            // Verriegel. Start Einrichtebetrieb
O     M 164.2;            // HA Start Befehl
O     M 140.5;            // Fl. Umschalten SBA
S     DB3.DBX 3.2;        // BM 7026 Falsche Betriebsart
NETWORK
TITLE = Meldung "Falsche Betriebsart" ruecks.
ON    DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
O     M 102.7;            // Taste Reset
R     DB3.DBX 3.2;        // BM7026 Falsche Betriebsart
NETWORK
TITLE = Start Befehle
U     M 148.2;            // NC Start gesamt
UN    M 155.5;            // BA Jog
O     M 164.5;            // HA-start manuell ??---
O     M 164.2;            // HA-start Befehl ??---
=     M 137.2;            // Start Befehle
NETWORK
TITLE = FM6025 Stoerung E/R-Modul
U     M 136.0;            // AUX-ON
UN    DB100.DBX 169.0;    // Teststop aktiv
L     S5T#3S;
SE    T 41;               // Ueberwachungszeit BB Antriebe
U     T 41;               // Ueberwachungszeit BB Antriebe
UN    E 4.3;              // BB E/R-Modul
L     S5T#500MS;
SE    T 5;                // Verzoegerung Stoerung A
U     T 5;                // Verzoegerung Stoerung A
S     DB2.DBX 3.1;        // FM6025 Stoerung E/R-Modul
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 3.1;        // FM6025 Stoerung E/R-Modul
NETWORK
TITLE = FM6026 Stoerung AS-Signale
// V2.25: Ueberw. AS-Signal HA-Steller
O(    ;
UN    A 9.0;              // Lstg. AUX-ON
UN    E 2.3;              // AS-Kette
O     ;
U     M 136.0;            // AUX-ON
U     E 2.3;              // AS-Kette
)     ;
O(    ;
UN    A 10.6              // Impulsfreigabe Hauptantrieb
UN    E 16.3;             // AS-Signal HA-Steller
U     DB10.DBX 50.4       // USER_DATA_HEX[10]: IFG Spindel mit Tür auf wegsch.
O     ;
U     A 10.6              // Impulsfreigabe Hauptantrieb
U     E 16.3;             // AS-Signal HA-Steller
U     DB10.DBX 50.4       // USER_DATA_HEX[10]: IFG Spindel mit Tür auf wegsch.
)     ;
L     S5T#200MS;          // Ueberwachungszeit AS-Signale
SE    T 43;
U     T 43;
=     M 138.5;            // Ueberwachung AS-Signale
S     DB2.DBX 3.2;        // FM6026 Stoerung AS-Signale
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 3.2;
NETWORK
TITLE = FM6027 Stoerung AUX-ON Schuetz
U(    ;
UN    E 2.0;              // Rueckmeldung Netzschuetz E/R
UN    A 9.0;              // Lstg. AUX-ON
O     ;
U     E 2.0;              // Rueckmeldung Netzschuetz E/R
U     A 9.0;              // Lstg. AUX-ON
)     ;
L     S5T#200MS;
SE    T 40;               // Ueberwachungszeit K11
U     T 40;
S     DB2.DBX 3.3;        // FM6027 Stoerung AUX-ON Schuetz
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 3.3;
NETWORK
TITLE = SW-Freigabe Antriebe
U(    ;
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
U     M 202.7;            // Zustimmtaste
O     M 202.2;            // Tuer zu und verriegelt
)     ;
U     M 136.0;            // AUX-ON
U     E 4.3;              // BB E/R-Modul
=     M 137.7;            // Antriebe ein
L     S5T#1S;
SA    T 26;               // Ausschaltverz. Antriebe
U     T 26;               // Ausschaltverz. Antriebe
UN    DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
=     A 5.7;              // FG Antriebe
NETWORK
TITLE = Not-Aus Test
UN    DB100.DBX 169.3;    // Test Schritt 3
=     A 3.5;              // Not-Aus Test
NETWORK
TITLE = Antriebe ein verzoegert
U     M 137.7;            // Antriebe ein
U     M 202.2;            // Tuer zu und verriegelt
L     S5T#300MS;
SE    T 42;               // Verzoegerungszeit Antriebe ein
NETWORK
TITLE =Spindel laeuft unverzoegert
UN    DB1.DBX 1356.3;     // Spindel steht
U     A 0.3;              // RFG Spindel 611U
//      UN    DB21.DBX 33.7;    // PRT aktiv
=     M 136.6;            // Spindel laeuft
NETWORK
TITLE = Spindel laeuft verzoegert
UN    DB1.DBX 1356.3;     // Spindel steht
U     A 0.3;              // RFG Spindel 611U
U     M 202.2;            // Tuer zu und verriegelt
//      UN    DB21.DBX 33.7;    // PRT aktiv
L     S5T#1S;
SA    T 102;              // Verzoegerung f. M136.7
U     T 102;
=     M 136.7;            // Spindel laeuft verzoegert
NETWORK
TITLE = n=0 an Sicherheitsplatine
// V9.01: Tuer bei M30 M32 nicht freigeben
UN    M 136.7;            // Spindel laeuft verz
UN    M 111.7;            // Programm laeuft
UN    M 146.3;            // VSH/EFG WZW
UN    M 174.7;            // M32 gespeichert
UN    DB100.DBX 35.2;     // man. WZW aktiv
=     M 210.6;            // n=0 an Sicherheitsplatine
NETWORK
TITLE = Lampe Zustimmtaste
U     M 210.6;            // n=0 an Sicherheitsplatine
U     M 202.2;            // Tuer zu und verriegelt
O     ;
U     M 138.4;            // Tuertest angefordert
U     M 110.7;            // Blinktakt
=     A 6.4;              // Lampe Zustimmtaste
NETWORK
TITLE = Maschinentuer entriegeln
// V2.20: IPO-Fahrbef. eigebunden, verh. Schalten wenn Tuer offen und JOG
// V2.21: Verriegelung wirkt nur in JOG/INC
O     M 173.0;            // Tuer entriegeln Tuerautomatik
O     DB20.DBX 834.0;     // Zustimmtaste Bedienpult
U     M 210.6;            // n=0 an Sicherheitsplatine
U(    ;
U     DB20.DBX 842.0;     // Kanal 0 kein IPO-Befehl X
U     DB20.DBX 842.1;     // Kanal 0 kein IPO-Befehl Y
U     DB20.DBX 842.2;     // Kanal 0 kein IPO-Befehl Z
O     ;
UN    M 155.5;            // BA JOG
)     ;
=     A 4.1;              // Tuer entriegeln
NETWORK
TITLE =FM6042 Stoerung Drehzahlueberwachung
// Ueberwachung ist im FC170 realisiert (FM6102)
// neue Funktion mit USB-Sicherheitsmodul
END_FUNCTION
//**************************************************************************
FUNCTION FC 52 : VOID
TITLE=BA_KONF
NAME: BA_KONF
BEGIN
NETWORK
TITLE = BA Anwahl von Robotik-Interface
U     M 191.6;            // Rob. BA Ref. anwaehlen
U     M 191.0;            // Robotik aktiv
S     DB20.DBX 289.2;     // BA REF
U     M 191.7;            // Rob. BA Auto anwaehlen
U     M 191.0;            // Robotik aktiv
S     DB20.DBX 288.4;     // BA AUTO
NETWORK
TITLE =dyn. BA Anwahl abspeichern
// BA JOG beim Einschalten vorbesetzen (EDIT bei 840D oder Simulator)
U     M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF
S     DB100.DBX500.0;     // Bit fuer BA JOG
L     DB20.DBW 288;       // dyn. BA-Anwahl
L     W#16#0;
==I   ;
SPB   M011;
L     DB20.DBW 288;
T     DB100.DBW 500;      // BA-Anwahl gespeichert
M011: NOP   0;
NETWORK
TITLE =BA Ref. nach einschalten automatisch vorgeben
U(    ;
O     M 137.3;            // Referenzpunkt nicht erreicht
O     DB100.DBX 35.7;     // WZW Fehler
)     ;
UN    M 70.0;             // autom. Offsetabgleich aktiv
U     M 107.4;            // AUX-ON Taste
UN    M 90.0;             // autom. Ref. aktiv
ON    M 137.7;            // Antriebe ein
O(    ;
UN    M 137.3;            // Referenzpunkt nicht erreicht
UN    DB100.DBX 35.7;     // WZW Fehler
)     ;
SPB   M021;
// Anwahl BA REF an Nahtstelle legen
L     W#16#0400;          // Bit BA REF setzen
T     DB20.DBW 288;       // Nahtstelle fuer BA-Anwahl
SPA   M022;
M021: NOP   0;
// Anwahl abgespeicherte BA an Nahtstelle anlegen
L     DB100.DBW 500;      // BA-Anwahl gespeichert
T     DB20.DBW 288;       // Nahtstelle fuer BA-Anwahl
M022: NOP   0;
NETWORK
TITLE =BA-Wechsel in NC anfordern falls notwendig
L     DB20.DBW 326;       // stat. Kanalzustand BA
L     DB20.DBW 288;       // Nahtstelle fuer BA-Anwahl
<>I   ;
=     DB20.DBX 0.0;       // BA-Wechselanforderung NC
NETWORK
TITLE =BA Auto aktiv
U     DB20.DBX 326.4;     // BA AUTO von Kern
=     M 155.1;
NETWORK
TITLE =BA MDA aktiv
U     DB20.DBX 326.2;     // BA MDA von Kern
=     M 155.4;
NETWORK
TITLE =BA REF aktiv
U     DB20.DBX 327.2;     // BA REF von Kern
=     M 155.2;
NETWORK
TITLE =BA JOG aktiv
O     DB20.DBX 326.0;      // BA JOG von Kern
O     DB20.DBX 326.5;      // BA INC1 von Kern
O     DB20.DBX 326.6;      // BA INC10 von Kern
O     DB20.DBX 326.7;      // BA INC100 von Kern
O     DB20.DBX 327.0;      // BA INC1000 von Kern
O     DB20.DBX 327.1;      // BA INC10000 von Kern
O     DB20.DBX 327.3;      // BA INC VAR von Kern
=     M 155.5;
NETWORK
TITLE = M90 und H0 M99  SPM links Handspannfutter
U(    ;
L     DB20.DBW 186;
L     0;
==I   ;
)     ;
U     DB20.DBX 204.3;     // M99 dyn.
U     M 111.0;            // H-Aenderung verl.
O     DB20.DBX 203.2;     // M90 dyn.
R     M 5.0;
R     M 5.1;
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
NETWORK
TITLE = M91 und H1 M99  SPM links Futter (Zug)
U(    ;
L     DB20.DBW 186;
L     1;
==I   ;
)     ;
U     DB20.DBX 204.3;     // M99 dyn.
U     M 111.0;            // H-Aenderung verl.
O     DB20.DBX 203.3;     // M91 dyn.
S     M 5.0;
R     M 5.1;
R     M 5.2;              // SPM Endlagenueberw. aus
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
NETWORK
TITLE = M92 und H2 M99  SPM links Spannzange (Druck)
U(    ;
L     DB20.DBW 186;
L     2;
==I   ;
)     ;
U     DB20.DBX 204.3;     // M99 dyn.
U     M 111.0;            // H-Aenderung verl.
O     DB20.DBX 203.4;     // M92 dyn.
R     M 5.0;
S     M 5.1;
R     M 5.2;              // SPM Endlagenueberw. aus
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
NETWORK
TITLE =Handschraubstock links
U(    ;
L     DB20.DBW 186;       // H-Wort (word)
L     50;
==I   ;
)     ;
U     DB20.DBX 204.3;     // M99 dyn.
U     M 111.0;            // H-Aenderung verl.
R     M 5.6;
R     M 5.7;
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
NETWORK
TITLE =Maschinenschraubstock links Aussenspannung
U(    ;
L     DB20.DBW 186;       // H-Wort (word)
L     51;
==I   ;
)     ;
U     DB20.DBX 204.3;     // M99 dyn.
U     M 111.0;            // H-Aenderung verl.
S     M 5.6;
R     M 5.7;
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
NETWORK
TITLE =Maschinenschraubstock links Innenspannung
U(    ;
L     DB20.DBW 186;       // H-Wort (word)
L     52;
==I   ;
)     ;
U     DB20.DBX 204.3;     // M99 dyn.
U     M 111.0;            // H-Aenderung verl.
R     M 5.6;
S     M 5.7;
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
NETWORK
TITLE = M94 und H4 M99 Anwahl Neustart mit M30 M32
U(    ;
L     DB20.DBW 186;
L     4;
==I   ;
)     ;
U     DB20.DBX 204.3;     // M99 dyn.
U     M 111.0;            // H-Aenderung verl.
O     DB20.DBX 203.6;     // M94 dyn.
S     M 5.3;
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
NETWORK
TITLE = M95 und H5 M99 Abwahl Neustart mit M30 M32
U(    ;
L     DB20.DBW 186;
L     5;
==I   ;
)     ;
U     DB20.DBX 204.3;     // M99 dyn.
U     M 111.0;            // H-Aenderung verl.
O     DB20.DBX 203.7;     // M95 dyn.
R     M 5.3;
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
NETWORK
TITLE = Konfiguration sofort in Settingdaten speichern
L     MW 5;               // akt. Maschinenkonfiguration
T     DB15.DBW 20;        // Maschinenkonfig. in SE-Daten
NETWORK
TITLE =BM7043 Sollstueckzahl erreicht
U(    ;
L     DB20.DBW 186;       // H-Wort (word)
L     0;
==I   ;
)     ;
U     DB20.DBX 204.0;     // M96
U     M 111.0;            // H-Aenderung verl.
S     DB3.DBX 5.3;        // BM7043 Sollstueckzahl erreicht
END_FUNCTION
//**************************************************************************
FUNCTION FC 53 : VOID
TITLE=PLC_MD
NAME: PLC_MD
BEGIN
END_FUNCTION
//**************************************************************************
FUNCTION FC 54 : VOID
TITLE =Spaenefoerderer
NAME: SPF
BEGIN
NETWORK
TITLE = Abbruch
UN    DB10.DBX 46.0;              // USER_DATA_HEX[6]: SPF vorhanden
BEB   ;
U     M 110.1;                    // "1"
=     DB1.DBX 1418.1;             // SPF vorhanden
NETWORK
TITLE = negative FL. Taste Spaenefoerderer
U     M 106.1;                    // Taste Spaenefoerderer
FN    M 182.1;                    // neg. Flanke
NETWORK
TITLE = Pausenzeitaenderung mit M59 Hxx
ON    DB20.DBX 199.3;             // M59 dyn.
ON    M 111.0;                    // H-Aenderung verl.
SPB   M031;
L     DB20.DBW 186;               // H-Wort
T     DB10.DBW 6;                 // USER_DATA_INT[3]: SPF Pausenzeit
M031: NOP   0;
NETWORK
TITLE = Vergleich Einschaltzeit mit Zaehlerstand
L     DB100.DBW 82;               // aktueller Zaehlerstand Einschaltzeit
L     DB10.DBW 4;                 // USER_DATA_INT[2]: SPF Laufzeit
>=I   ;
=     M 182.2;                    // HM Laufzeit beendet
NETWORK
TITLE = Signal SPF bereit auswerten
ON    DB10.DBX 46.1;              // USER_DATA_HEX[6]: SPF Reparatursch.
O     E 4.2;                      // SPF Ueberlast
=     M 182.7;                    // SPF bereit
NETWORK
TITLE = Einschaltzeitzaehler
U     M 182.0;                    // SPF ein
U     A 10.0;                     // SPF vorwaerts
UN    A 10.1;                     // SPF zurueck
U     M 110.7;                    // 1Hz
ZV    Z 23;                       // Einschaltzeit
U     M 182.1;                    // neg Fl. Taste
UN    T 45;                       // verzoegerte Taste
O     M 182.2;                    // HM Laufzeit beendet
ON    M 136.0;                    // AUX-ON
ON    M 182.7;                    // SPF bereit
R     Z 23;
L     Z 23;
T     DB100.DBW 82;               // aktueller Einschaltzaehlerstand
NETWORK
TITLE = Vergleich Pausenzeit mit Zaehlerwert
O(    ;
L     DB100.DBW 84;               // aktueller Pausenzeitzaehlerwert
L     DB10.DBW 6;                 // USER_DATA_INT[3]: Pausenzeit SPF
>=D   ;
)     ;
O     ;
U     M 182.1;                    // neg. Fl Taste
UN    T 45;                       // verz. Taste
S     M 182.0;                    // SPF ein
=     M 182.3;                    // HM Pausenzeitzaehler
NETWORK
TITLE = Pausenzeitzaehler
U(    ;
L     DB100.DBW 84;               // aktueller Pausenzeitzaehlerwert
L     L#65530;
<D    ;
)     ;
U     M 111.7;                    // Prg. laeuft K1
UN    M 182.0;                    // SPF ein
UN    A 10.1;                     // SPF zurueck
U     M 110.7;                    // 1Hz
FP    M 182.4;                    // HM Flanke
U     M 182.4;                    // HM Flanke
SPBN  M081;
L     DB100.DBW 84;               // aktueller Pausenzeitzaehlerwert
L     1;
+I    ;
T     DB100.DBW 84;               // aktueller Pausenzeitzaehlerwert
M081: NOP   0;
O     M 182.3;                    // HM Pausenzeitzaehler
ON    M 136.0;                    // AUX-ON
SPBN  M082;
L     0;
T     DB100.DBW 84;               // aktueller Pausenzeitzaehlerwert
M082: NOP   0;
NETWORK
TITLE = SPF ruecksetzen
O     M 182.2;                    // HM Laufzeit beendet
O     T 45;                       // Zeit SPF rueckwaerts
ON    M 136.0;                    // AUX-ON
ON    M 182.7;                    // SPF bereit
R     M 182.0;                    // SPF ein
NETWORK
TITLE = SPF vorwaerts
U     M 182.0;                    // SPF ein
UN    M 106.1;                    // Taste SPF
U     E 4.7;                      // Therm. Stoerungen Stop nach M30
UN    A 10.1;                     // SPF rueckwaerts
U     M 136.0;                    // AUX-ON
U     M 202.2;                    // Tuer zu und verr.
=     A 10.0;                     // SPF vorwaerts
NETWORK
TITLE = Verz.Zeit SPF rueckwaerts
U     M 106.1;                    // Taste SPF
L     S5T#1S;                     // 1s
SE    T 45;                       // Zeit SPF rueckwaerts
NETWORK
TITLE = SPF rueckwaerts
U     T 45;                       // Zeit SPF rueckwaerts
U     E 4.7;                      // Therm. Stoerungen Stop nach M30
U     M 136.0;                    // AUX-ON
U     M 202.2;                    // Tuer zu und verr.
UN    A 10.0;                     // SPF vorwaerts
=     A 10.1;                     // SPF rueckwaerts
NETWORK
TITLE = LED Spaeneforderer
U     A 10.1;                     // SPF rueckwaerts
U     M 110.7;                    // Blinktakt
O     A 10.0;                     // SPF vorwaerts
=     DB20.DBX 851.2;             // LED SPF Taste
NETWORK
TITLE = Zeitueberw. SPF Stoerung (Schalter, Ueberlast)
END_FUNCTION
//**************************************************************************
FUNCTION FC 55 : VOID
TITLE=NC_DAT_RW
NAME: NC_D_RW
BEGIN
END_FUNCTION
//**************************************************************************
FUNCTION FC 57 : VOID
TITLE=FREI_M
NAME: FREI_M
BEGIN
NETWORK
TITLE = Ruecksetzbedingungen
O     M 173.7;                // Fl. M02/M30
O     M 102.7;                // Reset Taste
=     M 80.7;                 // Ausg. ruecksetzen
NETWORK
TITLE = Ausgang 1
UN    DB10.DBX 53.0;          // USER_DATA_HEX[13]: progr. Ausgang 1 aktiv
SPB   _NA1;
U     DB20.DBX 202.0;         // M80 dyn.
S     A 8.0;                  // progr. Ausgang 1
U     E 3.1;                  // Quittiereing.1
FP    M 80.0;                 // Fl. Eing.1
O(    ;
U     M 80.7;                 // Ausg. ruecksetzen
U     DB10.DBX 53.4;          // USER_DATA_HEX[13]: Ausg.1 mit Reset loeschen
)     ;
O(    ;
U     M 80.0;                 // Fl. Eing.1
U     DB10.DBX 54.4;          // USER_DATA_HEX[14]: Ausg.1 mit Eing. loeschen
)     ;
O     DB20.DBX 202.1;         // M81 dyn.
R     A 8.0;                  // progr. Ausgang 1
_NA1: NOP   0;
NETWORK
TITLE = Ausgang 2
UN    DB10.DBX 53.1;          // USER_DATA_HEX[13]: progr. Ausgang 2 aktiv
SPB   _NA2;
U     DB20.DBX 202.2;         // M82 dyn.
S     A 8.1;                  // progr. Ausgang 2
U     E 3.2;                  // Quittiereing.2
FP    M 80.1;                 // Fl. Eing.2
O(    ;
U     M 80.7;                 // Ausg. ruecksetzen
U     DB10.DBX 53.5;          // USER_DATA_HEX[13]: Ausg.2 mit Reset loeschen
)     ;
O(    ;
U     M 80.1;                 // Fl. Eing.2
U     DB10.DBX 54.5;          // USER_DATA_HEX[14]: Ausg.2 mit Eing. loeschen
)     ;
O     DB20.DBX 202.3;         // M83 dyn.
R     A 8.1;                  // progr. Ausgang 2
_NA2: NOP   0;
NETWORK
TITLE = Ausgang 3
UN    DB10.DBX 53.2;          // USER_DATA_HEX[13]: progr. Ausgang 3 aktiv
SPB   _NA3;
U     DB20.DBX 202.4;         // M84 dyn.
S     A 8.2;                  // progr. Ausgang 3
U     E 3.3;                  // Quittiereing.3
FP    M 80.2;                 // Fl. Eing.3
O(    ;
U     M 80.7;                 // Ausg. ruecksetzen
U     DB10.DBX 53.6;          // USER_DATA_HEX[13]: Ausg.3 mit Reset loeschen
)     ;
O(    ;
U     M 80.2;                 // Fl. Eing.3
U     DB10.DBX 54.6;          // USER_DATA_HEX[14]: Ausg.3 mit Eing. loeschen
)     ;
O     DB20.DBX 202.5;         // M85 dyn.
R     A 8.2;                  // progr. Ausgang 3
_NA3: NOP   0;
NETWORK
TITLE = Ausgang 4
UN    DB10.DBX 53.3;          // USER_DATA_HEX[13]: progr. Ausgang 4 aktiv
SPB   _NA4;
U     DB20.DBX 202.6;         // M86 dyn.
S     A 8.3;                  // progr. Ausgang 4
U     E 3.4;                  // Quittiereing.4
FP    M 80.3;                 // Fl. Eing.4
O(    ;
U     M 80.7;                 // Ausg. ruecksetzen
U     DB10.DBX 53.7;          // USER_DATA_HEX[13]: Ausg.4 mit Reset loeschen
)     ;
O(    ;
U     M 80.3;                 // Fl. Eing.4
U     DB10.DBX 54.7;          // USER_DATA_HEX[14]: Ausg.4 mit Eing. loeschen
)     ;
O     DB20.DBX 202.7;         // M87 dyn.
R     A 8.3;                  // progr. Ausgang 4
_NA4: NOP   0;
NETWORK
TITLE = VSH/EFG progr. Ausgaenge
O(    ;
U     A 8.0;                  // progr. Ausgang 1
UN    E 3.1;                  // Quittiereing.1
U     M 111.7;                // Programm laeuft
U     DB10.DBX 54.0;          // USER_DATA_HEX[14]: VSH/EFG bis Quittiereing.1
)     ;
O(    ;
U     A 8.1;                  // progr. Ausgang 2
UN    E 3.2;                  // Quittiereing.2
U     M 111.7;                // Programm laeuft
U     DB10.DBX 54.1;          // USER_DATA_HEX[14]: VSH/EFG bis Quittiereing.2
)     ;
O(    ;
U     A 8.2;                  // progr. Ausgang 3
UN    E 3.3;                  // Quittiereing.3
U     M 111.7;                // Programm laeuft
U     DB10.DBX 54.2;          // USER_DATA_HEX[14]: VSH/EFG bis Quittiereing.3
)     ;
O(    ;
U     A 8.3;                  // progr. Ausgang 4
UN    E 3.4;                  // Quittiereing.4
U     M 111.7;                // Programm laeuft
U     DB10.DBX 54.3;          // USER_DATA_HEX[14]: VSH/EFG bis Quittiereing.4
)     ;
=     M 147.0;                // VSH/EFG progr. Ausgaenge
END_FUNCTION
//**************************************************************************
FUNCTION FC 58 : VOID
TITLE=FREIFAHREN
NAME: FREIF
BEGIN
NETWORK
TITLE = WZW Arm freifahren
U     M 105.5;            // WZW-Taste
U     M 107.4;            // Taste Aux On
UN    M 155.1;            // BA AUTO
U     M 202.2;            // Tuer zu und verriegelt
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
UN    M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
=     DB100.DBX 37.1;     // WZW Arm freifahren
NETWORK
TITLE = Ansteuerung WZW Arm freifahren
O     T 76;               // WZ entspannt verz.
O     T 77;               // WZ gespannt verz.
U     DB100.DBX 37.1;     // WZW Arm freifahren
=     DB100.DBX 37.0;     // WZW freifahren aktiv
NETWORK
TITLE = WZW Z-Position ueberwachen
U(    ;
L     DB10.DBD 64;        // USER_DATA_FLOAT[0]: Wechselpos. Z
L     +0.1E-003;          // 0.1mm
-R    ;
L     DB1.DBD 8;          // Istpos. Z
<R    ;
)     ;
U(    ;
L     DB10.DBD 64;        // USER_DATA_FLOAT[0]: Wechselpos. Z
L     +0.1E-003;          // 0.1mm
+R    ;
L     DB1.DBD 8;          // Istpos. Z
>R    ;
)     ;
=     DB100.DBX 39.1;     // WZW Z-Position ok
NETWORK
TITLE = WZW Y-Position ueberwachen
U(    ;
L     DB10.DBD 68;        // USER_DATA_FLOAT[0]: Wechselpos. Y
L     +0.1E-003;          // 0.1mm
-R    ;
L     DB1.DBD 4;          // Istpos. Y
<R    ;
)     ;
U(    ;
L     DB10.DBD 68;        // USER_DATA_FLOAT[0]: Wechselpos. Y
L     +0.1E-003;          // 0.1mm
+R    ;
L     DB1.DBD 4;          // Istpos. Y
>R    ;
)     ;
=     DB100.DBX 39.2;     // WZW Y-Position ok
NETWORK
TITLE = WZW C-Position ueberwachen
U(    ;
L     DB10.DBD 60;        // USER_DATA_FLOAT[0]: Wechselpos. C
L     +5.0E-001;          // 0.5 Grad
-R    ;
L     +1.0E+003;
/R    ;
L     DB1.DBD 12;         // Istpos. C
<R    ;
)     ;
U(    ;
L     DB10.DBD 60;        // USER_DATA_FLOAT[0]: Wechselpos. C
L     +5.0E-001;          // 0.5 Grad
+R    ;
L     +1.0E+003;
/R    ;
L     DB1.DBD 12;         // Istpos. C
>R    ;
)     ;
U     M 110.4;            // Spindel im Rundachsbetrieb
=     DB100.DBX 39.3;     // WZW C-Position ok
NETWORK
TITLE = Wechselpos. ok
U     DB100.DBX 39.1;     // WZW Z-Position ok
U     DB100.DBX 39.2;     // WZW Y-Position ok
U     DB100.DBX 39.3;     // WZW C-Position ok
=     DB100.DBX 39.0;     // Wechselposition ok
NETWORK
TITLE = NC-Stop WZW Alarm
O     DB2.DBX 9.1;        // FM6073 WZW Positionsueberwachung
O     DB2.DBX 9.7;        // FM6079 WZW Beroueberwachung
=     M 143.0;            // NC-Stop WZW-Alarm
END_FUNCTION
//**************************************************************************
FUNCTION FC 59 : VOID
TITLE=REFERENZIEREN
NAME: REF
BEGIN
NETWORK
TITLE = Flanke Taste Ref.
O(    ;
U     M 104.6;            // NC-Start Taste
UN    M 104.2;            // Taste VS-Start
U     M 155.2;            // BA Ref
)     ;
O(    ;
U     DB1.DBX 1366.0;     // DNC aktiv
U     DB20.DBX 1.1;       // Ref.-Anforderung
)     ;
O     M 104.3;            // REF-Taste
O     M 190.2;            // Rob. Ref. Start
FP    M 91.0;             // Fl. Taste REF
U     M 100.3;            // Taste Z+
FP    M 92.0;             // Fl. Taste Z+
U     M 101.2;            // Taste X+
FP    M 92.1;             // Fl. Taste X+
U     M 100.2;            // Taste Y+
FP    M 92.2;             // Fl. Taste Y+
U     M 102.2;            // Taste A+
FP    M 92.3;             // Fl. Taste A+
NETWORK
TITLE = autom. Ref. starten
U     M 155.2;            // BA REF
U     M 137.7;            // Antriebe ein
U     M 91.0;             // Fl. Taste REF
UN    M 70.0;             // autom. Offsetabgleich aktiv
UN    DB100.DBX 35.7;     // WZW Fehler
S     M 90.0;             // autom. Ref. aktiv
NETWORK
TITLE = autom. Ref. ruecksetzen
O     M 102.7;            // Reset Taste
ON    M 137.7;            // Antriebe ein
O     M 91.7;             // Fl. autom Ref. fertig
ON    M 155.2;            // BA REF
;      R     M 90.0;             // autom. Ref. aktiv
SPBN  _REF;
L     0;
T     MB 90;              // Schrittkette Ref.
_REF: NOP   0;
NETWORK
TITLE = Z-Achse autom. referenzieren
U     M 90.0;             // autom. Ref. aktiv
U     M 91.0;             // Fl. Taste REF
S     M 90.1;             // Ref. Z aktiv
=     M 93.0;             // Fl. Ref. Z autom.
NETWORK
TITLE = Referenzpunkt Z erreicht
U     DB1.DBX 134.2;      // Achse 2 ist ref.
FP    M 91.1;             // Fl. Ref-Z erreicht
NETWORK
TITLE = X/Y autom. referenzieren
U     M 90.0;             // autom. Ref. aktiv
U     M 90.1;             // Ref. Z aktiv
U     M 91.1;             // Fl. Ref-Z erreicht
S     M 90.2;             // Ref. X/Y aktiv
R     M 90.1;             // Ref. Z aktiv
=     M 93.1;             // Fl. Ref. X autom.
=     M 93.2;             // Fl. Ref. Y autom.
NETWORK
TITLE = Referenzpunkt Y erreicht
U     DB1.DBX 134.0;      // Achse 0 ist ref.
U     DB1.DBX 134.1;      // Achse 1 ist ref.
FP    M 91.2;             // Fl. Ref-X/Y erreicht
NETWORK
TITLE = A-Achse autom. referenzieren
U     M 90.0;             // autom. Ref. aktiv
U     M 90.2;             // Ref. X/Y aktiv
U     M 91.2;             // Fl. Ref-X/Y erreicht
S     M 90.3;             // Ref. A aktiv
R     M 90.2;             // Ref. X/Y aktiv
=     M 93.3;             // Fl. Ref. A autom.
NETWORK
TITLE = alle Referenzpunkte erreicht - autom. referenzieren fertig
O(
U     M 91.2;             // Fl. Ref-X/Y erreicht
UN    M 121.0;            // RTA angeschlossen
UN    DB10.DBX 100.2      // ACC_MSD B100=4  (mill450B.MSD) 4.5. UCAM Rundachse aktiv
)
O(
U     DB1.DBX 134.4;      // Achse 4 ist ref.
UN    DB10.DBX 100.2      // ACC_MSD B100=4  (mill450B.MSD) 4.5. UCAM Rundachse aktiv
)
O(
U     DB10.DBX 100.2      // ACC_MSD B100=4  (mill450B.MSD) 4.5. UCAM Rundachse aktiv
U     DB1.DBX 134.6;      // B-Schwenkachse 6 ( 11 ) ist referenziert
U     DB1.DBX 134.7;      // A-Rundachse 7 ( 10 ) ist referenziert
)
R     M 90.3;             // Ref. A aktiv
FP    M 91.7;             // Fl. autom. Ref. fertig
NETWORK
TITLE = Freigabe Referenzpunktfahren XY-Achse
ON    M 155.2;            // BA REF
ON    DB1.DBX 134.2;      // Achse 2 ist ref.
R     M 90.7;             // Freigabe Ref. XY
U     M 91.1;             // Fl. Ref-Z erreicht
S     M 90.7;             // Freigabe Ref. XY
NETWORK
TITLE = Ref. Kommando Z-Achse
U     M 92.0;             // Fl. Taste Z+
UN    M 90.0;             // autom. Ref. aktiv
UN    DB100.DBX 35.7;     // WZW Fehler
U     M 155.2;            // BA REF
O     M 93.0;             // Fl. Ref. Z autom.
=     DB20.DBX 8.2;       // Kanalachse Z referenzieren
NETWORK
TITLE = Ref. Kommando X-Achse
U     M 90.7;             // Freigabe Ref. XY
U     M 92.1;             // Fl. Taste X+
UN    M 90.0;             // autom. Ref. aktiv
U     M 155.2;            // BA REF
O     M 93.1;             // Fl. Ref. X autom.
=     DB20.DBX 8.0;       // Kanalachse X referenzieren
NETWORK
TITLE = Ref. Kommando Y-Achse
U     M 90.7;             // Freigabe Ref. XY
U     M 92.2;             // Fl. Taste Y+
UN    M 90.0;             // autom. Ref. aktiv
U     M 155.2;            // BA REF
O     M 93.2;             // Fl. Ref. Y autom.
=     DB20.DBX 8.1;       // Kanalachse Y referenzieren
NETWORK
TITLE = Ref. Kommando A-Achse
U(    ;
U     M 90.7;             // Freigabe Ref. XY
U     M 92.3;             // Fl. Taste A+
UN    M 90.0;             // autom. Ref. aktiv
U     M 155.2;            // BA REF
O     M 93.3;             // Fl. Ref. A autom.
)     ;
U     M 121.0;            // RTA angeschlossen
UN    DB10.DBX 100.2      // ACC_MSD B100=4  (mill450B.MSD) 4.5. UCAM Rundachse aktiv
SPBN  M5901;
=     DB20.DBX 9.2;       // Kanalachse A referenzieren
M5901: NOP   0;
NETWORK
TITLE = BM7010 autom. Ref. aktiv
U     M 90.0;             // autom. Ref. aktiv
=     DB3.DBX 1.2;        // BM7010 autom. Ref. aktiv
END_FUNCTION
//**************************************************************************
FUNCTION FC 60 : VOID
TITLE=ACHSEN
NAME: ACHSEN
BEGIN
NETWORK
TITLE =  JOG aus bei offener Tür
O   DB20.DBX  10.0   // JOG in pos. Richtung für Kanalachse X
O   DB20.DBX  10.1   // JOG in pos. Richtung für Kanalachse Y
O   DB20.DBX  10.2   // JOG in pos. Richtung für Kanalachse Z
O   DB20.DBX  12.0   // JOG in neg. Richtung für Kanalachse X
O   DB20.DBX  12.1   // JOG in neg. Richtung für Kanalachse Y
O   DB20.DBX  12.2   // JOG in neg. Richtung für Kanalachse Z
U(
O   M 50.1          // FN Taste AUX-ON
ON  DB20.DBX 833.0; // Schluesselschalter Hand/Auto
ON  DB20.DBX 834.0; // Zustimmtaste Bedienpult
U   E 2.2;          // 1-Signal wenn die Tür offen ist
)
SPBN   M6001
=   DB1.DBX 1440.0  // NC-RESET AUSLÖSEN
R   DB20.DBX  10.0   // JOG in pos. Richtung für Kanalachse X
R   DB20.DBX  10.1   // JOG in pos. Richtung für Kanalachse Y
R   DB20.DBX  10.2   // JOG in pos. Richtung für Kanalachse Z
R   DB20.DBX  12.0   // JOG in neg. Richtung für Kanalachse X
R   DB20.DBX  12.1   // JOG in neg. Richtung für Kanalachse Y
R   DB20.DBX  12.2   // JOG in neg. Richtung für Kanalachse Z
;      =   DB20.DBX  1.2
;      =   DB1.DBX 1734.6      // Deaktivierung absolute Achse 6 (A-Rundachse)
;      =   DB1.DBX 1734.7      // Deaktivierung absolute Achse 7 (A-Rundachse)
;      S   M 50.0
M6001:  NOP 0
NETWORK
TITLE =Gewindeschneiden aktiv
U     DB20.DBX 544.5;     // Interpolationsart Gewinde aktiv
=     M 115.0;            // G33/G331/G332 aktiv
NETWORK
TITLE = Vorschubhalt manuell setzen
U     M 104.4;            // Taste Vorschub halt
U(    ;
O     M 111.7;            // Programm laeuft
O     M 111.6;            // Programm unterbrochen
)     ;
UN    M 115.0;            // G33/G331/G332 aktiv
U(    ;
O     M 136.4;            // Summe Fahrbefehl
O     M 111.6;            // Programm unterbrochen
)     ;
S     DB3.DBX 0.4;        // BM7004 "Vorschubhalt manuell"
NETWORK
TITLE = Vorschubhalt mauell ruecksetzen
// V2.21: VSH manuell mit Reset-Taste zuruecksetzen
U     M 104.2;            // Taste Vorschub start
UN    M 147.3;            // VSH/EFG Hauptantrieb manuell
UN    M 172.2;            // AWZ Unterbrechung
ON    M 155.1;            // BA AUTO
O     M 102.7;            // Reset-Taste
R     DB3.DBX 0.4;        // BM7004 "Vorschubhalt manuell"
U     DB3.DBX 0.4;        // BM7004 "Vorschubhalt manuell"
U     M 110.7;            // Blinktakt
=     DB20.DBX 852.0;     // LED Vorschub-Start Taste
NETWORK
TITLE = RFG Achse 1
U(    ;
UN    DB20.DBX 546.0;     // Genauhalt fein Achse 1
U     A 0.0;              // RFG Achse 1
U     T 35;               // Aux-On verz.
O     M 136.0;            // AUX-ON
)     ;
UN    DB100.DBX 169.0;    // Teststop aktiv
=     A 0.0;              // RFG Achse X 611U
=     DB1.DBX 1460.0;     // RFG NC-Achse 0
NETWORK
TITLE = X+ konv.
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M051;
ON    M 137.3;            // Referenzpunkt nicht erreicht
O     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
U     M 101.2;            // Taste X+
U     A 0.0;              // RFG Achse 1
U     M 137.7;            // Antriebe ein
U     M 155.5;            // BA JOG/INC
;      =     DB20.DBX 10.0;      // X+ konv.
M051: NOP   0;
NETWORK
TITLE = X- konv.
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M061;
ON    M 137.3;            // Referenzpunkt nicht erreicht
O     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
U     M 101.4;            // Taste X-
U     A 0.0;              // RFG Achse 1
U     M 137.7;            // Antriebe ein
U     M 155.5;            // BA JOG/INC
;      =     DB20.DBX 12.0;      // X- konv
M061: NOP   0;
NETWORK
TITLE = FM6028"Stoerung X-Achse"
U     T 41;               // Ueberwachungszeit BB Antriebe
UN    E 5.3;              // Bb X-Achse
S     DB2.DBX 3.4;        // FM6028
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 3.4;
NETWORK
TITLE =RFG ACHSE 2
U(    ;
UN    DB20.DBX 546.1;     // Genauhaltfein Achse 2
U     A 0.1;              // RFG Y-Achse 611U
U     T 35;               // Aux-On verz.
O     M 136.0;            // AUX-ON
)     ;
UN    DB100.DBX 169.0;    // Teststop aktiv
=     A 0.1;              // RFG Achse Y 611U
=     DB1.DBX 1460.1;     // RFG NC-Achse 1
NETWORK
TITLE = Y+ konv.
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M091;
ON    M 137.3;            // Referenzpunkt nicht erreicht
O     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
U     M 100.2;            // Taste Y+
UN    M 146.3;            // VSH/EFG WZW schwenkt
U     A 0.1;              // RFG Achse 2
U     M 137.7;            // Antriebe ein
U     M 155.5;            // BA JOG/INC
;      =     DB20.DBX 10.1;      // Y+ konv.
M091: NOP   0;
NETWORK
TITLE = Y- konv.
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M101;
ON    M 137.3;            // Referenzpunkt nicht erreicht
O     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
U     M 102.4;            // Taste Y-
UN    M 146.3;            // VSH/EFG WZW schwenkt
U     A 0.1;              // RFG Achse 2
U     M 137.7;            // Antriebe ein
U     M 155.5;            // BA JOG/INC
;      =     DB20.DBX 12.1;      // Y- konv.
M101: NOP   0;
NETWORK
TITLE = FM6030"Stoerung Y-Achse"
U     T 41;               // Ueberwachungszeit BB Antriebe
UN    E 5.4;              // Bb Y-Achse
S     DB2.DBX 3.6;        // FM6030
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 3.6;
NETWORK
TITLE =Bremse Z-Achse
// steht deshalb vor der Zuweisung der NC-RFG, weil die NC bei einem
// schwerwiegendem Achsfehler die NC-RFG selbst wegnimmt!
U     M 136.0;            // AUX-ON
U     DB1.DBX 1460.2;     // RFG NC-Achse 2
U     E 5.5;              // BB Z-Modul 611U
UN    DB100.DBX 169.0;    // Teststop aktiv
UN    DB100.DBX 170.7;    // SI Summenalarm
=     A 9.1;              // Motorbremse Z-Achse
=     A 9.2;              // Spindelbremse Z-Achse
NETWORK
TITLE = RFG Achse 3 an NCK
U(    ;
UN    DB20.DBX 546.2;     // Genauhalt fein Achse 3
U     A 0.2;              // Lageregelung Achse 3
U     T 35;               // Aux-On verz.
O     M 136.0;            // AUX-ON
)     ;
UN    DB100.DBX 169.0;    // Teststop aktiv
=     DB1.DBX 1460.2;     // RFG NC-Achse 2
=     A 0.2;              // RFG Z-Achse 611U
NETWORK
TITLE = Z+ konv.
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M141;
ON    M 137.3;            // Referenzpunkt nicht erreicht
O     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
U     M 100.3;            // Taste Z+
UN    M 146.3;            // VSH/EFG WZW schwenkt
UN    DB3.DBX 2.7;        // BM7023 Magazinieren aktiv
U     A 0.2;              // RFG Achse 3
U     M 137.7;            // Antriebe ein
U     M 155.5;            // BA JOG/INC
;      =     DB20.DBX 10.2;      // Z+ konv.
M141: NOP   0;
NETWORK
TITLE = Z- konv.
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M151;
ON    M 137.3;            // Referenzpunkt nicht erreicht
O     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
U     M 102.3;            // Taste Z-
UN    M 146.3;            // VSH/EFG WZW schwenkt
UN    DB3.DBX 2.7;        // BM7023 Magazinieren aktiv
U     A 0.2;              // RFG Achse 3
U     M 137.7;            // Antriebe ein
U     M 155.5;            // BA JOG/INC
;      =     DB20.DBX 12.2;      // Z- konv
M151: NOP   0;
NETWORK
TITLE = FM6029"Stoerung Z-Achse"
U     T 41;               // Ueberwachungszeit BB Antriebe
UN    E 5.5;              // Bb Z-Achse
S     DB2.DBX 3.5;        // FM6029
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 3.5;
NETWORK
TITLE = Eilgang Ueberlagerung
U     M 101.3;            // Taste Eilgang
U     T 42;               // Antriebe ein verzoegert
UN    DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
=     DB20.DBX 824.0;     // Eilgangueberlagerung Kanal 0
NETWORK
TITLE = Summe Fahrbefehl
ON    DB20.DBX 842.0;     // X-Achse IPO Befehl
ON    DB20.DBX 842.1;     // Y-Achse IPO Befehl
ON    DB20.DBX 842.2;     // Z-Achse IPO Befehl
O     M 120.1;            // Fahrbefehl A-Achse
=     M 136.4;            // Summe Fahrbefehl
NETWORK
TITLE = BM7009 "Referenzpunkt nicht erreicht"
U(    ;
O(    ;
UN    M 121.4;            // HM RTA ref. erreicht
U     M 121.0;            // RTA angeschlossen
)     ;
O(    ;
ON    DB1.DBX 134.6;      // Kanal Achse 11(B) Schwenkachse Referenzpunkt aktiv
ON    DB1.DBX 134.7;      // Kanal Achse 10(A) Rotationsachse Referenzpunkt aktiv
U     DB10.DBX 100.2      // ACC_MSD B100=4  (mill450B.MSD) 4.5. UCAM Rundachse aktiv
)     ;
ON    DB1.DBX 134.0;      // X: Ref. err.
ON    DB1.DBX 134.1;      // Y: Ref. err.
ON    DB1.DBX 134.2;      // Z: Ref. err.
)     ;
U     M 136.0;            // AUX-ON
=     M 137.3;            // Referenzpunkt nicht erreicht
U     M 137.3;            // Referenzpunkt nicht erreicht
UN    M 70.0;             // autom. Offsetabgleich aktiv
UN    DB3.DBX 1.2;        // BM7010 autom. Ref. aktiv
UN    DB3.DBX 1.6;        // BM7014 WZW abgebrochen - Ref-Taste
UN    DB3.DBX 2.2;        // BM7018 Tuer offen
=     DB3.DBX 1.1;        // BM7009 Referenzpunkt nicht erreicht
NETWORK
TITLE = LED REF-Taste ansteuern
U     DB3.DBX 1.1;        // BM7009 Referenzpunkt nicht erreicht
U     M 155.2;            // BA REF
U     M 110.7;            // Blinktakt
O     DB3.DBX 1.2;        // BM7010 autom. Ref. aktiv
=     DB20.DBX 850.4;     // LED REF-Taste
END_FUNCTION
//**************************************************************************
FUNCTION FC 61 : VOID
TITLE=TUERAUTOMATIK
NAME: TA
BEGIN
NETWORK
TITLE =Zeitverzoegerung Tuer zu
U     M 202.2;            // Tuer zu und verriegelt
L     S5T#500MS;
SE    T 64;               // Tuer zu verz.
NETWORK
TITLE =Flanke Tuer zu
U     T 64;
FP    M 173.4;            // Fl. Tuer zu
NETWORK
TITLE =NC-Start nach Tuer zu
U(    ;
UN    M 5.4;              // Handling aktiv
U     M 173.4;            // Fl. Tuer zu
O     ;
U     M 5.4;              // Handling aktiv
U     M 180.6;            // ???
)     ;
U     M 202.2;            // Tuer zu und verriegelt
U     M 155.1;            // BA Auto
UN    M 137.1;            // Einrichte-BA
U(    ;
O     M 111.5;            // M00/M01 aktiv
ON    M 111.6;            // Programm unterbrochen
)     ;
UN    M 191.0;            // Robotik aktiv
UN    DB1.DBX 1366.0;     // DNC aktiv
UN    DB2.DBX 8.0;        // FM6064 Sicherheitsleiste Tuerautom.
=     M 148.5;            // NC-Start Tuer zu
NETWORK
TITLE =Flanke M0/M1
U     DB20.DBX 192.1;     // M01 dyn.
U     DB20.DBX 324.5;     // Opt. Stop aktiv
O     DB20.DBX 192.0;     // M00 dyn.
S     M 173.2;            // Fl. M00/M01
U     M 173.2;            // Fl. M00/M01
U     M 210.6;            // n=0 an Sicherheitsplatine
L     S5T#100MS;
SE    T 65;
O     T 65;
ON    M 136.0;            // AUX-ON
R     M 173.2;            // Fl. M00/M01
NETWORK
TITLE =Flanke M2/M30
O     DB20.DBX 192.2;     // M02 dyn.
O     DB20.DBX 195.6;     // M30 dyn.
S     M 173.7;            // Fl. M02/M30
U     M 173.7;            // Fl. M02/M30
U     M 210.6;            // n=0 an Sicherheitsplatine
L     S5T#100MS;
SE    T 66;
O     T 66;
ON    M 136.0;            // AUX-ON
R     M 173.7;            // Fl. M02/M30
NETWORK
TITLE =Tuer auf
O(    ;
U     M 173.2;            // Fl. M00/M01
U     M 202.2;            // Tuer zu und verriegelt
U     M 155.1;            // BA AUTO
U     M 210.6;            // n=0 an Sicherheitsplatine
)     ;
O(    ;
U     M 173.7;            // Fl. M02/M30
U     M 202.2;            // Tuer zu und verriegelt
U     M 155.1;            // BA AUTO
U     M 155.7;            // 1x aktiv
U     M 210.6;            // n=0 an Sicherheitsplatine
)     ;
O(    ;
U     M 190.7;            // Tuer auf Robotik
U     M 191.0;            // Robotik aktiv
U     M 210.6;            // n=0 an Sicherheitsplatine
)     ;
O(    ;
U     DB1.DBX 1382.0;     // DNC Tuer auf
U     M 210.6;            // n=0 an Sicherheitsplatine
)     ;
O(    ;
U     DB20.DBX 834.0;     // Zustimmtaste Bedienpult
U     M 210.6;            // n=0 an Sicherheitsplatine
)     ;
S     M 173.5;            // Tuer auf gesp.
O     E 6.5;              // Tuer offen
O     M 201.5;            // Taste NC-Start extern
ON    E 6.4;              // Schaltleiste ok
O     M 102.7;            // Reset-Taste
ON    M 136.0;            // AUX-ON
O     DB2.DBX 8.0;        // FM6064 Sicherheitsleiste Tuerautom.
R     M 173.5;            // Tuer auf gesp.
NETWORK
TITLE =Tuer zu
O(    ;
U     M 190.6;            // Tuer zu Robotik
U     M 191.0;            // Robotik aktiv
)     ;
O(    ;
UN    M 202.2;            // Tuer zu und verriegelt
U     DB100.DBX 752.7;    // IB-Bit Tuerautomatik
U     M 155.1;            // BA AUTO
U     E 6.5;              // Tuer offen
)     ;
O     M 201.5;            // Taste NC-Start extern
O     DB1.DBX 1382.1;     // DNC Tuer zu
S     M 173.6;            // Tuer zu gesp.
U     M 173.6;            // Tuer zu gesp.
L     S5T#200MS;
SE    T 67;               // mind. Impuls Tuer zu
O(    ;
UN    E 6.4;              // Schaltleiste ok
UN    E 6.5;              // Tuer offen
UN    M 202.2;            // Tuer zu und verriegelt
)     ;
O(    ;
U     M 102.7;            // Reset-Taste
UN    M 202.2;            // Tuer zu und verriegelt
)     ;
O(    ;
U     T 64;               // Tuer zu verz.
U     T 67;               // mind. Impuls Tuer zu
)     ;
O     M 173.5;            // Tuer auf gesp.
ON    M 136.0;            // AUX-ON
O     DB2.DBX 8.0;        // FM6064 Sicherheitsleiste Tuerautom.
R     M 173.6;            // Tuer zu gesp.
NETWORK
TITLE =Ventil Tuer auf
O     M 173.5;            // Tuer auf gesp.
O     M 180.0;            // ???
=     M 173.0;            // Tuer entriegeln Tuerautomatik
U     M 173.0;            // Tuer entriegeln Tuerautomatik
U     A 4.1;              // Tuer entriegeln
UN    M 202.2;            // Tuer zu und verriegelt
=     A 6.7;              // MV Tuer oeffnen
NETWORK
TITLE =Ventil Tuer zu
U     M 173.6;            // Tuer zu gesp.
=     A 4.0;              // MV Tuer schliessen
NETWORK
TITLE =Tuerautom. Hauptventil
ON    E 6.5;              // Tuer offen
O     A 4.0;              // MV Tuer schliessen
L     S5T#500MS;
SA    T 63;               // HM Hauptventil verz.
U     T 63;               // HM Hauptventil verz.
U     M 136.0;            // AUX-ON
=     A 6.6;              // Tuerautomatik Hauptventil
NETWORK
TITLE =Tuerautomatik Testung
//Der Druckschalter wird auch in FM6045 ueberwacht (wenn kein Druck kommt)
O(    ;
U     A 6.6;              // Tuerautomatik Hauptventil
U     E 6.5;              // Tuer offen
UN    E 6.3;              // DS Tuer Ueberwachung
)     ;
O(    ;
UN    A 6.6;              // Tuerautomatik Hauptventil
U     E 6.3;              // DS Tuer Ueberwachung
)     ;
L     S5T#750MS;
SE    T 61;               // Ueberw. Tuerautom. Testung
NETWORK
TITLE =Tuerautomatik Ueberwachung Bewegung
O(    ;
U     A 4.0;              // MV Tuer schliessen
UN    M 202.2;            // Tuer zu und verriegelt
)     ;
O(    ;
U     A 6.7;              // MV Tuer oeffnen
UN    E 6.5;              // Tuer offen
)     ;
L     S5T#5S;
SE    T 62;               // Ueberw. Tuerautom. Bewegung
NETWORK
TITLE =FM6064 Sicherheitsleiste Tuerautom. hat angesprochen
U(    ;
O     DB20.DBX 834.0;     // Zustimmtaste Bedienpult
O     M 201.5;            // Taste NC-Start extern
)     ;
UN    E 6.4;              // Schaltleiste ok
U     A 6.6;              // Tuerautomatik Hauptventil
UN    E 6.5;              // Tuer offen
U     M 136.0;            // Aux-On
O     T 61;               // Ueberw. Tuerautom. Testung
O     T 62;               // Ueberw. Tuerautom. Bewegung
S     DB2.DBX 8.0;        // FM6064 Sicherheitsleiste Tuerautom.
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 8.0;        // FM6064 Sicherheitsleiste Tuerautom.
END_FUNCTION
//**************************************************************************
FUNCTION FC 65 : VOID
TITLE=RTA_1
NAME: RTA_1
BEGIN
NETWORK
TITLE =4. Achse (Teilapparat) referenziert
U     DB1.DBX 134.4;      // Achse 4 ist referenziert
=     M 121.4;            // HM RTA ref. erreicht
NETWORK
TITLE = Fahrbefehl 4. Achse TANI125
ON    DB20.DBX 843.2;     // IPO Befehl A-Achse Kanal 0
ON    DB25.DBX 843.2;     // IPO Befehl A-Achse kanal 1
O     M 70.0;             // autom. Abgleich aktiv
O(    ;
U     M 102.2;            // Taste A+
U     M 137.7;            // Antriebe ein
U     DB20.DBX 326.0;     // NC-BA JOG
)     ;
O(    ;
U     M 100.4;            // Taste A-
U     M 137.7;            // Antriebe ein
U     DB20.DBX 326.0;     // NC-BA JOG
)     ;
=     M 120.1;            // Fahrbef. A-Achse
NETWORK
TITLE =4. Achse (Teilapparat) Vorschub Halt ????
//    U     M 121.5;
//    =     DB35.DBX 4.3;         // VSH/Spindelhalt
NETWORK
TITLE = Klemmung geloest verz.
UN    E 9.0;              // RTA verriegelt
L     S5T#250MS;
SE    T 116;              // Klemmung geloest verz.
NETWORK
TITLE = Klemmung aktiv verz.
U     E 9.0;              // RTA verriegelt
L     S5T#250MS;
SE    T 117;              // Klemmung aktiv verz.
NETWORK
TITLE =Klemmung aus in INC/REF
U     M 155.5;            // BA JOG
UN    DB20.DBX 326.0;     // NC-BA JOG
O     ;
U     M 155.2;            // BA REF
U     M 137.7;            // Antriebe ein
O     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
=     M 120.6;            // Klemmung aus INC/REF
NETWORK
TITLE = Klemmung (M10/M11)
O     DB20.DBX 193.3;     // M11
S     M 120.7;            // M11 aktiv
UN    M 111.7;            // Programm laeuft
UN    M 111.6;            // Programm unterbrochen
O     DB20.DBX 193.2;     // M10
R     M 120.7;            // M11 aktiv
NETWORK
TITLE =Ventil Klemmung
UN    M 136.0;            // AUX-ON
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
U     M 107.6;            // Taste Aux-Off
U(    ;
O     M 100.4;            // Taste A-
O     M 102.2;            // Taste A+
)     ;
NOT   ;
UN    M 120.1;            // Fahrbef. A-Achse
UN    M 120.6;            // Klemmung aus INC/REF
UN    M 120.7;            // M11 aktiv
UN    DB20.DBX 862.0;     // Transformation aktiv
U     M 121.0;            // RTA angeschlossen
L     S5T#500MS;          // Positionierzeit (GenF fkt. nicht)
SE    T 110;              // Fahrbef. A verz. (bis GenF)
=     M 120.5;            // HM Klemmung ein
U     T 110;              // Fahrbef. A verz. (bis GenF)
=     A 4.5;              // RTA entriegeln
NETWORK
TITLE =Druckschalter Klemmung RTA fehlt
U(    ;
U     M 120.5;            // HM Klemmung ein
UN    E 9.0;              // RTA verriegelt
O     ;
UN    M 120.5;            // HM Klemmung ein
U     E 9.0;              // RTA verriegelt
)     ;
U     M 136.0;            // AUX-ON
U     M 121.0;            // RTA angeschlossen
L     S5T#2S;
SE    T 100;
NETWORK
TITLE =BM7027 Druckschalter Klemmung RTA fehlt
U     T 100;
=     DB3.DBX 3.3;        // BM7027 "DS TA-Klemmung fehlt"
NETWORK
TITLE =HM VSH/EFG Klemmung Teilapparat
U     M 120.1;            // Fahrbef. A-Achse
UN    DB1.DBX 1460.4;     // RFG NC-Achse 4
O     ;
U     M 120.5;            // HM Klemmung ein
UN    T 117;              // Klemmung aktiv verz.
O     ;
UN    M 120.5;            // HM Klemmung ein
UN    T 116;              // Klemmung geloest verz.
=     M 146.2;            // VSH/EFG RTA1
NETWORK
TITLE = HM RFG 4. Achse
U     M 120.5;            // HM Klemmung ein
UN    T 117;              // Klemmung aktiv verz.
U     M 120.2;            // HM RFG Achse 5
O     T 116;              // Klemmung geloest verz.
=     M 120.2;            // HM RFG Achse 5
NETWORK
TITLE = RFG 4. Achse TANI125
U     M 136.0;            // AUX-ON
U     M 120.2;            // HM RFG
U     M 121.0;            // RTA angeschlossen
UN    DB100.DBX 169.0;    // Teststop aktiv
=     A 0.4;              // RFG A 611U
=     DB1.DBX 1460.4;     // RFG NC-Achse 4
NETWORK
TITLE = A+ konv.
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M051;
ON    M 137.3;            // Referenzpunkt nicht erreicht
O     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
U     M 102.2;            // Taste A+
U     M 137.7;            // Antriebe ein
U     M 155.5;            // BA JOG/INC
U     A 0.4;              // RFG A 611U
=     DB20.DBX 11.2;      // A+ konv.
M051: NOP   0;
NETWORK
TITLE = A- konv.
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M061;
ON    M 137.3;            // Referenzpunkt nicht erreicht
O     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
U     M 100.4;            // Taste A-
U     M 137.7;            // Antriebe ein
U     M 155.5;            // BA JOG/INC
U     A 0.4;              // RFG A 611U
=     DB20.DBX 13.2;      // A- konv
M061: NOP   0;
NETWORK
TITLE = FM6031 Stoerung A-Achse
U     A 0.4;              // RFG A 611U
UN    E 5.7;              // BB Achse A 611U
L     S5T#3S;
SE    T 115;              // Verzoegerung Stoerung A
U     T 115;              // Verzoegerung Stoerung A
S     DB2.DBX 3.7;        // FM6031
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 3.7;
NETWORK
TITLE = 2.SW-Endschalter X- Y- bei RTA aktivieren
U     M 121.0;            // RTA angeschlossen
=     DB20.DBX 343.1;     // X PLC low limit
=     DB20.DBX 343.3;     // Y PLC low limit
END_FUNCTION
//**************************************************************************
FUNCTION FC 70 : VOID
TITLE=SPINDEL
NAME: SPINDEL
BEGIN
NETWORK
TITLE =Spindeloverride aus Tasten
U     M 106.3;            // Taste 100%
FP    DB100.DBX  127.1;
// Spindeloverride 100% vorgeben
UN    DB100.DBX 127.1;    // Fl. Taste 100%
UN    M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF
SPB   ovkl;
L     100;                // 100%
T     DB100.DBW  128;
SPA   oven;
ovkl: U     M 106.7;            // Taste -%
FP    DB100.DBX 127.3;
// Spindeloverride -5% (bis. min. 50%)
UN    DB100.DBX 127.3;    // Fl. Taste -%
SPB   ovgr;
L     DB100.DBW 128;
L     50;                 // 50%
==I   ;
SPB   oven;
L     DB100.DBW 128;
L     5;                  // 5%
-I    ;
T     DB100.DBW 128;
SPA   oven;
ovgr: U     M 107.5;            // Taste % groesser
FP    DB100.DBX 127.5;
// Spindeloverride +5% (bis max. 120%)
UN    DB100.DBX 127.5;    // Fl. Taste +%
SPB   oven;
L     DB100.DBW 128;
L     120;                // 120%
==I   ;
SPB   oven;
L     DB100.DBW 128;
L     5;                  // 5%
+I    ;
T     DB100.DBW 128;
oven: NOP   0;
NETWORK
TITLE = LED's Spindeloverride
L     DB20.DBW 360;       // Sp.-Override ist
L     1000;               // 100%
<I   ;
=     DB20.DBX 850.6;     // LED %-
L     DB20.DBW 360;       // Sp.-Override ist
L     1000;               // 100%
==I   ;
=     DB20.DBX 850.7;     // LED 100%
L     DB20.DBW 360;       // Sp.-Override ist
L     1000;               // 100%
>I   ;
=     DB20.DBX 851.0;     // LED %+
NETWORK
TITLE = Startverriegelungen
L     MB 145;             // Startverriegelungen
L     0;
==I   ;
=     M 137.0;            // keine Startverriegelungen
NETWORK
TITLE =M3
U     DB20.DBX 192.3;     // M3 dyn.
U     M 137.0;            // keine Startverriegelungen
O     M 68.0;             // TM02 Spindel RL
=     M 166.3;            // M3
S     M 166.0;            // Spindel ein
S     DB20.DBX 340.0;     // Spindel ein CW (Masterspindel)
R     DB20.DBX 340.1;     // Spindel ein CCW (Masterspindel)
=     DB20.DBX 256.3;     // M3 RÜCKMELDEN
NETWORK
TITLE =M4
U     DB20.DBX 192.4;     // M4 dyn.
U     M 137.0;            // keine Startverriegelungen
O     M 68.1;             // TM02 Spindel LL
=     M 166.4;            // M4
S     M 166.0;            // Spindel ein
R     DB20.DBX 340.0;     // Spindel ein CW (Masterspindel)
S     DB20.DBX 340.1;     // Spindel ein CCW (Masterspindel)
=     DB20.DBX 256.4;     // M4 RÜCKMELDEN
NETWORK
TITLE =M5
O     DB20.DBX 192.5;     // M5 dyn.
O     M 68.2;             // TM02 Spindel aus
=     M 166.5;            // M5
NETWORK
TITLE =M5
O     M 166.5;            // HM M5
O     M 102.7;            // Reset-Taste
ON    M 136.0;            // AUX-ON
O     DB20.DBX 192.2;     // M02 dyn.
O     DB20.DBX 195.6;     // M30 dyn.
O     M 165.2;            // C-Achse ein
R     M 166.0;            // Spindel ein
R     DB20.DBX 340.0;     // Spindel ein CW (Masterspindel)
R     DB20.DBX 340.1;     // Spindel ein CCW (Masterspindel)
=     DB20.DBX 256.5;     // M5 RÜCKMELDEN
NETWORK
TITLE = Rundachsensteuerung fuer Offsetabgleich
O     M 70.1;             // Fl. Rundachse ein Offsetabgleich
UN    M 110.4;            // Spindel im Rundachsbetrieb
S     M 165.2;            // C-Achse ein
U(    ;
O     DB20.DBX 192.2;     // M2 dyn.
O     DB20.DBX 192.3;     // M3 dyn.
O     DB20.DBX 192.4;     // M4 dyn.
O     DB20.DBX 195.6;     // M30 dyn.
O     M 138.0;            // Fl. Reset-Taste
)     ;
U     M 110.4;            // Spindel im Rundachsbetrieb
S     M 165.3;            // C-Achse aus
L     MB 165;             // Zustandsmerker Positionierung
L     0;
<>I   ;
=     M 167.1;            // VSH/EFG Spindelpositionierung
// Spindel in Positionierbetrieb schalten
U     M 165.2;            // C-Achse ein
U     DB1.DBX 1356.3;     // Spindel steht
S     M 165.4;            // C-Achse ein laeuft
R     M 165.2;
SPBN  ORI1;
S     DB25.DBX 3.5;       // Spindelumschaltung
L     0;
T     DB25.DBB 822;       // Spindelnummer
L     1;
T     DB25.DBB 823;       // Rundachsbetrieb
S     DB25.DBX 340.3;     // Einlesefreigabe K1
S     DB25.DBX 340.4;     // Achsfreigabe K1
ORI1: NOP   0;
U     M 165.4;            // C-Achse ein laeuft
U     M 110.4;            // Spindel im Rundachsbetrieb
R     M 165.4;
// Spindel in Spindelbetrieb zurueckschalten
U     M 165.3;            // C-Achse aus
L     S5T#300MS;
SE    T 10;               // Verzoegerung C-Achse aus
U     T 10;
U     M 165.3;            // C-Achse aus
R     M 165.3;
SPBN  ORI3;
S     DB25.DBX 3.5;       // Spindelumschaltung
L     0;
T     DB25.DBB 822;       // Spindelnummer
L     0;
T     DB25.DBB 823;       // Spindelbetrieb
ORI3: NOP   0;
NORI: NOP   0;
NETWORK
TITLE = Drehzahlbegrenzung Einrichtebetrieb
O(    ;
UN    DB20.DBX 182.2;     // neues S fuer Spindel 0
UN    M 107.7;            // Taste Spindel start
)     ;
O     M 202.2;            // Tuer zu und verriegelt
ON    DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
SPB   M152;
L     DB20.DBD 188;       // akt S-Wert Spindel 0
L     +1.0E+01;           // entspr. M3 S600 U/min (720 bei 120%)
>R    ;
SPBN  M151;
S     DB20.DBX 2.1;       // NC-Kommando Drehzahl setzen
L     0;                  // Spindel Nr. 0
T     DB20.DBW 594;       // Spindelnr. fuer Drehzahlkommando
L     +1.0E+01;           // entspr. 600 U/min
T     DB20.DBD 160;       // Daten fuer Drehzahlkommando
M151: NOP   0;
L     DB20.DBD 188;       // akt S-Wert Spindel 0
L     -1.0E+01;           // entspr. M4 S600 U/min (720 bei 120%)
<R    ;
SPBN  M152;
S     DB20.DBX 2.1;       // NC-Kommando Drehzahl setzen
L     0;                  // Spindel Nr. 0
T     DB20.DBW 594;       // Spindelnr. fuer Drehzahlkommando
L     -1.0E+01;           // entspr. 600 U/min
T     DB20.DBD 160;       // Daten fuer Drehzahlkommando
M152: NOP   0;
NETWORK
TITLE =Rundachsbetrieb der Spindel
L     DB1.DBB 1447;       // Achstyp fuer Spindel
L     3;                  // Kennung Rundachse
==I   ;
=     M 110.4;            // Spindel im Rundachsbetrieb
// Parametersatzumschaltung HA-Steller C-Achse/Gewindebohren
U     M 110.4;            // Spindel im Rundachsbetrieb
=     A 3.4;              // Parametersatz 1 611U (volles Moment)
NETWORK
TITLE = BM7022 "Spindelhalt manuell"
U(    ;
ON    M 111.7;            // Programm laeuft
ON    M 136.4;            // Summe Fahrbefehl
O     DB3.DBX 0.4;        // BM7004 "VSH manuell"
)     ;
UN    M 110.4;            // Spindel im Rundachsbetrieb
U     A 0.3;              // RFG Spindel 611U
U     M 166.0;            // Spindel ein
U     M 104.1;            // Taste Spindel stop
UN    M 68.0;             // TM02 Spindel RL
UN    M 68.1;             // TM02 Spindel LL
S     DB3.DBX 2.6;        // BM7022 "Spindelhalt manuell"
U     M 107.7;            // Taste Spindel start
U     DB3.DBX 2.6;        // BM7022
U     M 137.0;            // keine Start Verriegelungen
O     M 102.7;            // Taste Reset
R     DB3.DBX 2.6;        // BM7022
U     DB3.DBX 2.6;        // BM7022 "Spindelhalt manuell"
U     M 110.7;            // Blinktakt
=     DB20.DBX 852.3;     // LED Spindel-Start Taste
NETWORK
TITLE = Man. Spindel Start/Stop fuer EMCOTRONIC
// V2.23: Fagor F8055 eingebunden
UN    DB1.DBX 1402.2;     // Steuerungskennung TM02
UN    DB1.DBX 1403.3;     // Fagor F8055
SPB   NOET;
U     M 107.7;            // Taste Spindel start
UN    M 104.1;            // Taste Spindel stop
U     M 137.7;            // Antriebe ein
U     M 155.5;            // BA JOG
L     S5T#1S;
SE    T 53;               // Zeit RL/LL
=     M 68.3;             // HM TM02 Spindel ein
U     M 68.3;             // HM TM02 Spindel ein
UN    T 53;               // Zeit RL/LL
S     M 68.0;             // TM02 Spindel RL
R     M 68.1;             // TM02 Spindel LL
U     M 68.3;             // HM TM02 Spindel ein
U     T 53;               // Zeit RL/LL
R     M 68.0;             // TM02 Spindel RL
S     M 68.1;             // TM02 Spindel LL
U(    ;
O     M 104.1;            // Taste Spindel stop
O     M 102.7;            // Reset Taste
ON    M 137.7;            // Antriebe ein
ON    M 155.5;            // BA JOG
)     ;
U(    ;
O     M 68.0;             // TM02 Spindel RL
O     M 68.1;             // TM02 Spindel LL
)     ;
R     M 68.0;             // TM02 Spindel RL
R     M 68.1;             // TM02 Spindel LL
=     M 68.2;             // TM02 Spindel aus
NOET: NOP   0;
NETWORK
TITLE = VSH/EFG Spindel
U     DB3.DBX 2.6;        // BM7022
U(    ;
O     M 111.7;            // Programm laeuft
O     M 111.6;            // Programm unterbrochen
)     ;
O     M 167.1;            // VSH/EFG Spindelpositionierung
=     M 147.3;            // EFG/VSH Spindelstop manuell
NETWORK
TITLE = Spindelhalt BM
L     DB3.DBB 9;
L     0;
<>I   ;
=     M 167.2;            // Spindelhalt BM
NETWORK
TITLE = IFG Spindel (Kl.663)
// V2.25 NW neu: IFG Spindel mit Tür auf wegschalten
U     M 202.2;            // Tuer zu und verriegelt
U     DB10.DBX 50.4       // USER_DATA_HEX[10]: IFG Spindel mit Tür auf wegsch.
=     A 10.6              // Impulsfreigabe Hauptantrieb
NETWORK
TITLE = RFG Spindel
// V2.22 Spindel mit VSH BM7040/7041 anhalten
// V2.25 IFG Spindel mit Tür auf wegschalten
U(    ;
O(    ;
U     M 166.0;            // Spindel ein
UN    DB20.DBX 324.2;     // DRY aktiv
)     ;
O     M 110.4;            // Spindel im Rundachsbetrieb
O     M 165.2;            // C-Achse ein
UN    M 167.2;            // Spindelhalt BM
UN    DB3.DBX 2.6;        // BM7022 Spindelhalt man.
UN    M 111.5;            // M00/M01 aktiv
UN    M 147.1;            // VSH BM7041
UN    M 147.2;            // VSH BM7040
U     M 137.7;            // Antriebe ein
)     ;
NOT   ;
=     A 3.3;              // 611U Drehzahlfestsollwert 1 (0U/min)
U(    ;
U     DB1.DBX 1460.3;     // NC-RFG Achse 3
UN    DB1.DBX 1356.3;     // Spindel steht NC
O     M 136.0;            // Aux-On
)     ;
UN    DB100.DBX 169.0;    // Teststop aktiv
U     E 5.6;              // BB Spindel 611U
U(    ;
O     M 202.2;            // Tuer zu und verriegelt
ON    DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
ON    M 202.7;            // Zustimmtaste
)     ;
U     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
=     DB1.DBX 1460.3;     // NC-RFG Achse 3
U     DB1.DBX 1460.3;     // NC-RFG Achse 3
U(    ;
ON    DB10.DBX 50.4;      // USER_DATA_HEX[10]: IFG Spindel mit Tür auf wegsch.
O     A 10.6;             // Impulsfreigabe Hauptantrieb
)     ;
UN    A 10.4;             // Leistung Schwenkarm
=     A 0.3;              // RFG Spindel 611U
NETWORK
TITLE = Servo-Ready Filter
U   E 5.6;              // Betriebsbereit Spindel 611U
L   S5T#200MS           // 200 ms
SA  T 8
O   E 5.6;              // BB Spindel 611U
O   T 8
=   M 36.3              // Betriebsbereit Spindel 611U gefiltert
NETWORK
TITLE = FM6032 Stoerung Hauptantrieb
U     T 41;               // Ueberwachungszeit BB Antriebe
UN    M 36.3              // Betriebsbereit Spindel 611U gefiltert
S     DB2.DBX 4.0;        // FM6032
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 4.0;
NETWORK
TITLE = EFG/VSH Spindel im Sollbereich ausblenden
U     E 5.1;              // N=Nsoll von 611U
L     S5T#100MS;
SE    T 52;
U     T 52;
FP    M 166.2;            // HM Flanke
U     M 166.2;
R     M 166.7;
NETWORK
TITLE =HM M3M4M5
O     M 166.3;            // M3
O     M 166.4;            // M4
O     M 166.5;            // M5
=     M 166.1;            // HM M3/M4/M5
NETWORK
TITLE =setzen
O     M 166.1;            // HM M3/M4/M5
O     DB3.DBX 2.6;        // BM7022 "Spindelhalt manuell"
O     M 111.5;            // M00/M01 aktiv
S     M 166.7;            // EFG/VSH Spindel im Sollbereich ausbl.
NETWORK
TITLE =HM Spindelbetrieb
U     M 166.1;            // HM
S     M 166.6;            // HM Spindelbetrieb
NETWORK
TITLE = HM Spindelbetrieb
O     M 102.7;            // Taste Reset
O     M 110.4;            // Spindel im Rundachsbetrieb
R     M 166.6;
NETWORK
TITLE = EFG/VSH Spindel nicht im Sollbereich
UN    E 5.1;              // N=Nsoll von 611U
U     M 166.6;            // HM Spindelbetrieb
U     M 111.7;            // Programm laeuft
UN    M 166.7;            // Spindel im Sollbereich ausbl.
UN    DB20.DBX 324.2;     // DRY aktiv
//      UN    DB21.DBX 33.7;      // PRT
U(    ;
O     DB20.DBX 544.0;     // Geradenbewegung
O     DB20.DBX 544.3;     // Kreisbewegung
O     DB20.DBX 544.4;     // Helix
O     DB20.DBX 544.5;     // Gewinde
)     ;
=     DB3.DBX 0.5;        // BM7005 "Spindel nicht im Sollbereich"
END_FUNCTION
//**************************************************************************
FUNCTION FC 75 : VOID
TITLE=WZW_CM450
NAME: WZW_CM
BEGIN
NETWORK
TITLE = Start autom. Werkzeugwechsel
// V2.19: Achspositionen zum repositionieren im ACC speichern
// V2.20: T99=T0 Spindelwerkzeug entladen (fuer Fanuc21)
U(    ;
L     DB20.DBW 184;       // T-Wort
L     99;
<>I   ;
)     ;
SPB   nt99;
L     0;
T     DB20.DBW 184;       // T-Wort
nt99: NOP   0;
U(    ;
L     DB10.DBW 20;        // USER_DATA_INT[10]: Anzahl Magazinplaetze
L     DB20.DBW 184;       // T-Wort
>=I   ;
)     ;
U(    ;
L     DB20.DBW 184;       // T-Wort
L     DB100.DBB 31;       // Werkzeug in Spindel
<>I   ;
)     ;
U     M 130.0;            // Werkzeugwechsel-Anstoss
U     M 202.2;            // Tuer zu und verriegelt
UN    DB3.DBX 1.1;        // BM7009 Referenzpunkt nicht erreicht
S     DB100.DBX 40.0;     // autom. WZW aktiv
=     M 130.4;            // WZW gestartet
U     M 130.4;            // WZW gestartet
U     M 111.7;            // Programm laeuft
S     DB20.DBX 348.6;     // Achspositionen in ACC speichern
=     DB20.DBX 960.0;     // X-Position speichern
=     DB20.DBX 960.1;     // Y-Position speichern
O     DB100.DBX 40.7;     // autom. WZW beendet
O     M 102.7;            // Reset Taste
ON    M 137.7;            // Antriebe ein
R     DB100.DBX 40.0;     // autom. WZW aktiv
R     DB100.DBX 40.7;     // autom. WZW beendet
NETWORK
TITLE = ist ein Werkzeug in der Spindel?
U(    ;
L     DB100.DBB 31;       // Werkzeug in Spindel
L     0;
>I    ;
)     ;
UN    DB100.DBX 35.5;     // Werkzeug durchgespannt
U     DB100.DBX 41.7;     // WZW in Grst.
U     DB100.DBX 40.0;     // autom. WZW aktiv
S     DB100.DBX 40.1;     // Mag. fuer Spindelwerkzeug
UN    DB100.DBX 40.1;     // Mag. fuer Spindelwerkzeug
U     DB100.DBX 41.7;     // WZW in Grst.
U     DB100.DBX 40.0;     // autom. WZW aktiv
S     DB100.DBX 41.1;     // Magazin fuer Sollwerkzeug
NETWORK
TITLE = Magazin fur Spindelwerkzeug positionieren
U     DB100.DBX 40.1;     // Mag. fuer Spindelwerkzeug
UN    DB100.DBX 30.0;     // Anf. Magazin positionieren
UN    DB100.DBX 30.7;     // Magazin Position erreicht
S     DB100.DBX 40.2;     // warten auf Magazin
S     DB100.DBX 30.0;     // Anf. Magazin positionieren
=     DB100.DBX 40.5;     // autom. WZW Schritt weiter
SPBN  M031;
L     DB100.DBB 31;       // Werkzeug in Spindel
T     DB100.DBB 32;       // Sollpos. Magazin
M031: NOP   0;
O     DB100.DBX 40.5;     // autom. WZW Schritt weiter
ON    DB100.DBX 40.0;     // autom. WZW aktiv
R     DB100.DBX 40.1;     // Mag. fuer Spindelwerkzeug
NETWORK
TITLE = warten bis Magazin fertig
U     DB100.DBX 30.0;     // Anf. Magazin positionieren
U     DB100.DBX 30.7;     // Magazin Position erreicht
U     DB100.DBX 40.2;     // warten auf Magazin
S     DB100.DBX 40.3;     // Werkzeug auswechseln
R     DB100.DBX 30.0;     // Anf. Magazin positionieren
=     DB100.DBX 40.5;     // autom. WZW Schritt weiter
O     DB100.DBX 40.5;     // autom. WZW Schritt weiter
ON    DB100.DBX 40.0;     // autom. WZW aktiv
R     DB100.DBX 40.2;     // warten auf Magazin
NETWORK
TITLE = Spindelwerkzeug ins Magazin
U     DB100.DBX 40.3;     // Werkzeug auswechseln
U     DB100.DBX 40.0;     // autom. WZW aktiv
UN    DB100.DBX 47.0;     // Schwenkarm Start
UN    DB100.DBX 47.7;     // Werkzeugwechsel fertig
S     DB100.DBX 47.0;     // Schwenkarm Start
=     DB100.DBX 40.5;     // autom. WZW Schritt weiter
U(    ;
L     DB20.DBW 184;       // T-Wort
L     0;                  // Spindel leeren?
==I   ;
)     ;
U     DB100.DBX 40.3;     // Werkzeug auswechseln
U     DB100.DBX 40.5;     // autom. WZW Schritt weiter
S     DB100.DBX 41.4;     // warten auf Werkzeugwechsel 2
U     DB100.DBX 40.3;     // Werkzeug auswechseln
U     DB100.DBX 40.5;     // autom. WZW Schritt weiter
UN    DB100.DBX 41.4;     // warten auf Werkzeugwechsel 2
S     DB100.DBX 40.4;     // Warten auf Werkzeugwechsel
O     DB100.DBX 40.5;     // autom. WZW Schritt weiter
ON    DB100.DBX 40.0;     // autom. WZW aktiv
R     DB100.DBX 40.3;     // Werkzeug auswechseln
NETWORK
TITLE = warten bis WZW fertig
U     DB100.DBX 47.0;     // Schwenkarm Start
U     DB100.DBX 47.7;     // Werkzeugwechsel fertig
U     DB100.DBX 40.4;     // warten auf Werkzeugwechsel
S     DB100.DBX 41.1;     // Magazin fuer Sollwerkzeug
R     DB100.DBX 47.0;     // Schwenkarm Start
=     DB100.DBX 40.5;     // autom. WZW Schritt weiter
O     DB100.DBX 40.5;     // autom. WZW Schritt weiter
ON    DB100.DBX 40.0;     // autom. WZW aktiv
R     DB100.DBX 40.4;     // warten auf Werkzeugwechsel
NETWORK
TITLE = Magazin fur Sollwerkzeug positionieren
U     DB100.DBX 41.1;     // Mag. fuer Sollwerkzeug
UN    DB100.DBX 30.0;     // Anf. Magazin positionieren
UN    DB100.DBX 30.7;     // Magazin Position erreicht
S     DB100.DBX 41.2;     // warten auf Magazin 2
S     DB100.DBX 30.0;     // Anf. Magazin positionieren
=     DB100.DBX 40.5;     // autom. WZW Schritt weiter
SPBN  M071;
L     DB20.DBW 184;       // T-Wort
T     DB100.DBB 32;       // Sollpos. Magazin
M071: NOP   0;
O     DB100.DBX 40.5;     // autom. WZW Schritt weiter
ON    DB100.DBX 40.0;     // autom. WZW aktiv
R     DB100.DBX 41.1;     // Mag. fuer Sollwerkzeug
NETWORK
TITLE = warten bis Magazin fertig
U     DB100.DBX 30.0;     // Anf. Magazin positionieren
U     DB100.DBX 30.7;     // Magazin Position erreicht
U     DB100.DBX 41.2;     // warten auf Magazin 2
S     DB100.DBX 41.3;     // Werkzeug einwechseln
R     DB100.DBX 30.0;     // Anf. Magazin positionieren
=     DB100.DBX 40.5;     // autom. WZW Schritt weiter
O     DB100.DBX 40.5;     // autom. WZW Schritt weiter
ON    DB100.DBX 40.0;     // autom. WZW aktiv
R     DB100.DBX 41.2;     // warten auf Magazin 2
NETWORK
TITLE = Spindelwerkzeug in Magazin
U     DB100.DBX 41.3;     // Werkzeug einwechseln
U     DB100.DBX 40.0;     // autom. WZW aktiv
UN    DB100.DBX 47.0;     // Schwenkarm Start
UN    DB100.DBX 47.7;     // Werkzeugwechsel fertig
S     DB100.DBX 41.4;     // warten auf Werkzeugwechsel 2
S     DB100.DBX 47.0;     // Schwenkarm Start
=     DB100.DBX 40.5;     // autom. WZW Schritt weiter
O     DB100.DBX 40.5;     // autom. WZW Schritt weiter
ON    DB100.DBX 40.0;     // autom. WZW aktiv
R     DB100.DBX 41.3;     // Werkzeug einwechseln
NETWORK
TITLE = warten bis WZW fertig
U     DB100.DBX 47.0;     // Schwenkarm Start
U     DB100.DBX 47.7;     // Werkzeugwechsel fertig
U     DB100.DBX 41.4;     // warten auf Werkzeugwechsel 2
S     DB100.DBX 41.5;     // in Spindelbetrieb schalten
R     DB100.DBX 47.0;     // Schwenkarm Start
=     DB100.DBX 40.5;     // autom. WZW Schritt weiter
SPBN  M101;
// ???????????? testen
U     M 110.1;            // "1"
S     DB25.DBX 1.4;       // NC-Satz abschicken
S     DB25.DBX 549.3;     // Positionierbetrieb auf Spindel schalten
M101: NOP   0;
O     DB100.DBX 40.5;     // autom. WZW Schritt weiter
ON    DB100.DBX 40.0;     // autom. WZW aktiv
R     DB100.DBX 41.4;     // warten auf Werkzeugwechsel 2
NETWORK
TITLE = Spindel in Spindelbetrieb schalten
U     DB100.DBX 41.5;     // in Spindelbetrieb schalten
UN    M 110.4;            // Spindel Rundachsbetrieb
S     DB100.DBX 40.7;     // autom. WZW beendet
=     DB100.DBX 40.5;     // autom. WZW Schritt weiter
O     DB100.DBX 40.5;     // autom. WZW Schritt weiter
ON    DB100.DBX 40.0;     // autom. WZW aktiv
R     DB100.DBX 41.5;     // in Spindelbetrieb schalten
NETWORK
TITLE = autom. Werkzeugwechsel in Grst.
U(    ;
L     DB100.DBB 40;       // Schrittkette WZW LSB
L     0;
==I   ;
)     ;
UN    DB100.DBX 41.1;     // Magazin fuer Sollwerkzeug
UN    DB100.DBX 41.2;     // warten auf Magazin 2
UN    DB100.DBX 41.3;     // Werkzeug einwechseln
UN    DB100.DBX 41.4;     // warten auf Werkzeugwechsel 2
UN    DB100.DBX 41.5;     // in Spindelbetrieb schalten
=     DB100.DBX 41.7;     // WZW in Grst.
NETWORK
TITLE = VSH/EFG Werkzeugwechsel
UN    DB100.DBX 41.7;     // WZW in Grst.
=     M 146.3             // VSH/EFG WZW schwenkt
END_FUNCTION
//**************************************************************************
FUNCTION FC 76 : VOID
TITLE=WZW_UEBERW
NAME: WZW_UEW
BEGIN
NETWORK
TITLE =WZW mit M06 starten
U     DB20.DBX 192.6;     // M06 dyn.
U     DB10.DBX 40.6;      // USER_DATA_HEX[0]: WZW mit M06
O     ;
U     DB20.DBX 182.0;     // T-Aend.
UN    DB10.DBX 40.6;      // USER_DATA_HEX[0]: WZW mit M06
=     M 130.0;            // Werkzeugwechsel-Anstoss
NETWORK
TITLE =BM7013 Falsche Werkzeugposition!
// Ueberwachung ist nur aktiv, wenn mit M06 geschwenkt wird!!
L     DB100.DBB 31;       // WZ-Nummer in Spindel
L     DB20.DBW 184;       // T-Wort
<>I   ;
=     DB100.DBX 900.0;    // #pos_err;
U     M 155.1;            // BA AUTO
U     DB100.DBX 900.0;    // #pos_err;
U     M 111.7;            // Programm laeuft
//      UN    DB21.DBX 33.7;    // PRT aktiv K1
UN    M 146.3;            // VSH/EFG WZW aktiv
U     DB10.DBX 40.6;      // USER_DATA_HEX[0]: WZW mit M06
=     DB100.DBX 900.1;    // #hm_7013;
// absolutes VSH geben (mit AUX-ON ausblenden)
U     DB100.DBX 900.1;    // #hm_7013;
UN    M 107.4;            // AUX-ON Taste
=     DB100.DBX 22.0;     // VSH X/Y/Z
// Meldung 7013 ausgeben, wenn VSH wirkt
U     DB100.DBX 900.1;    // #hm_7013;
U     M 136.4;            // Summe Fahrbefehl
=     DB3.DBX 1.5;        // BM7013 Falsche Werkzeugposition
END_FUNCTION
//**************************************************************************
FUNCTION FC 77 : VOID
TITLE=Magazin
NAME: MAG_CM
BEGIN
// Die neue Sollposition des Magazines wird in DB100.DBB 32
// uebergeben und muss eine gueltige Position sein.
// Positionierung starten: DB100.DBX 30.0
// Zielposition erreicht:  DB100.DBX 30.7
NETWORK
TITLE = Werkzeug ein-/ausschwenken
U(    ;
O(    ;
U     M 105.2;            // Mag.-Taste -1
U     M 104.1;            // Taste Spindel stop
U     DB100.DBX 35.6;     // WZW synchronisiert
UN    DB100.DBX 47.0;     // Schwenkarm Start
U     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
)     ;
O(    ;
U     DB100.DBX 30.0;     // Anf. Magazin positionieren
UN    DB100.DBX 30.7;     // Magazin Position erreicht
)     ;
O     DB100.DBX 30.1;     // Start Magazin ref.
O     DB100.DBX 48.1;     // Werkzeughalter einschwenken
)     ;
U     M 136.0;            // Aux-On
O     ;
U     M 136.1;            // Fl. Aux-On
U     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
S     A 7.6;              // Werkzeughalter Magazin
R     A 7.7;              // Werkzeughalter Wechselpos.
O(    ;
U     M 105.0;            // Mag.-Taste +1
U     M 104.1;            // Taste Spindel stop
U     DB100.DBX 35.6;     // WZW synchronisiert
UN    DB100.DBX 47.0;     // Schwenkarm Start
U     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
)     ;
O     DB100.DBX 47.2;     // WZ ausschwenken
U     M 136.0;            // Aux-On
U     M 7.5;              // Werkzeughalter in Position
S     A 7.7;              // Werkzeughalter Wechselpos.
R     A 7.6;              // Werkzeughalter Magazin
UN    M 136.0;            // Aux-On
R     A 7.7;              // Werkzeughalter Wechselpos.
R     A 7.6;              // Werkzeughalter Magazin
NETWORK
TITLE = Positionszaeler Magazin
// FC126 muss vor dem Referenzieren aufgerufen werden!
UC    FC126;
L     DB100.DBB 29;       // Mag. Istpos. HW
T     DB15.DBB 34;        // SE-Daten: akt. Magazinplatz
NETWORK
TITLE = Magazin referenzieren
U     M 27.6;             // Referenzpunkt Magazin gef.
U     T 56;               // Pocket up verz.
U     M 7.5;              // Werkzeughalter in Position
U(    ;
O     DB100.DBX 30.1;     // Start Magazin ref.
O     M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF
)     ;
S     DB100.DBX 35.6;     // WZW synchronisiert
R     DB100.DBX 30.1;     // Start Magazin ref.
SPBN  M021;
L     1;
T     DB100.DBB 29;       // Mag. Istpos. HW
T     DB100.DBB 32;       // Magazin Sollposition
M021: NOP   0;
NETWORK
TITLE = Magazin manuell takten
O     M 105.0;            // Mag.-Taste +1
O     M 105.2;            // Mag.-Taste -1
FP    M 131.0;            // HM Fl. Mag.-Taste
U     M 131.0;            // HM Fl. Mag.-Taste
UN    M 104.1;            // Taste Spindel stop
UN    DB100.DBX 30.0;     // Anf. Magazin positionieren
UN    DB100.DBX 47.0;     // Schwenkarm Start
=     M 131.1;            // Fl. Mag.-Taste
O(    ;
U     M 155.5;            // BA JOG
U     M 137.7;            // Antriebe ein
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
U     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
U     M 131.1;            // Fl. Mag.-Taste
)     ;
O(    ;
U     M 155.2;            // BA REF
UN    DB100.DBX 35.6;     // WZW synchronisiert
U     M 137.7;            // Antriebe ein
U     M 91.0;             // Fl. Taste REF
)     ;
U     M 202.2;            // Tuer zu und verriegelt
U     DB1.DBX 1356.3;     // Spindelstillstand NC
S     M 130.1;            // Mag. manuell
S     DB100.DBX 30.0;     // Anf. Magazin positionieren
U     M 131.1;            // Fl. Mag.-Taste
U     M 105.0;            // Mag.-Taste +1
U     M 130.1;            // Mag. manuell
S     DB20.DBX 848.5;     // LED Taste Mag. +1
SPBN  M031;
L     DB100.DBB 29;       // Magazin Istpos. HW
L     1;
+I    ;
T     DB100.DBB 32;       // Magazin Sollposition
L     DB100.DBB 32;       // Magazin Sollposition
L     DB10.DBW 20;        // USER_DATA_INT[10]: Anzahl Magazinplaetze
<=I   ;
SPB   M031;
L     1;
T     DB100.DBB 32;       // Magazin Sollposition
M031: NOP   0;
U     M 131.1;            // Fl. Mag.-Taste
U     M 105.2;            // Mag.-Taste -1
U     M 130.1;            // Mag. manuell
UN    DB20.DBX 848.5;     // LED Taste Mag. +1
S     DB20.DBX 848.3;     // LED Taste Mag. -1
SPBN  M032;
L     DB100.DBB 29;       // Magazin Istpos. HW
L     1;
-I    ;
T     DB100.DBB 32;       // Magazin Sollposition
// Magazin Ispos. ist 0 wenn nicht referenziert ist
L     DB100.DBB 29;       // Magazin Istpos. HW
L     1;
>I    ;
SPB   M032;
L     DB10.DBW 20;        // USER_DATA_INT[10]: Anzahl Magazinplaetze
T     DB100.DBB 32;       // Magazin Sollposition
M032: NOP   0;
U     M 130.1;            // Mag. manuell
U     DB100.DBX 30.7;     // Magazin Position err.
ON    M 137.7;            // Antriebe ein
R     M 130.1;            // Mag. manuell
R     DB100.DBX 30.0;     // Anf. Magazin positionieren
R     DB20.DBX 848.5;     // LED Taste Mag. +1
R     DB20.DBX 848.3;     // LED Taste Mag. -1
NETWORK
TITLE = Start Magazin positionieren
U     DB100.DBX 30.0;     // Anf. Magazin positionieren
UN    DB100.DBX 35.6;     // WZW synchronisiert
S     DB100.DBX 30.1;     // Start Magazin ref.
U     DB100.DBX 30.0;     // Anf. Magazin positionieren
U     DB100.DBX 35.6;     // WZW synchronisiert
UN    DB100.DBX 30.3;     // Magazin aktiv
S     DB100.DBX 35.0;     // WZW Anforderung
S     DB100.DBX 30.3;     // Magazin aktiv
CC    FC 121;             // RILO
NETWORK
TITLE = Richtung aus RILO ableiten
UN    DB100.DBX 35.0;     // WZW Anforderung
SPB   M041;
L     DB100.DBW 33;       // Plaetze zum Ziel
L     0;
>I    ;
S     DB100.DBX 30.4;     // HM Magazin LL
R     DB100.DBX 35.0;     // WZW Anforderung
L     DB100.DBW 33;       // Plaetze zum Ziel
L     0;
<I    ;
S     DB100.DBX 30.5;     // HM Magazin RL
R     DB100.DBX 35.0;     // WZW Anforderung
M041: NOP   0;
NETWORK
TITLE = Zielpos. erreicht, WZ-Halter im Magazin
U(    ;
L     DB100.DBB 29;       // Magazin Istpos. HW
L     DB100.DBB 32;       // Magazin Sollposition
==I   ;
)     ;
U     T 56;               // Pocket up verz.
U     DB100.DBX 30.3;     // Magazin aktiv
R     DB100.DBX 30.3;     // Magazin aktiv
R     DB100.DBX 30.4;     // HM Magazin LL
R     DB100.DBX 30.5;     // HM Magazin RL
=     DB100.DBX 30.6;     // HM Pos. err.
S     DB100.DBX 30.7;     // Magazin Position erreicht
NETWORK
TITLE = Schrittkette beendet, Abbruch
UN    DB100.DBX 30.0;     // Anf. Magazin positionieren
UN    DB100.DBX 30.6;     // HM Pos. err.
R     DB100.DBX 30.3;     // Magazin aktiv
R     DB100.DBX 30.4;     // HM Magazin LL
R     DB100.DBX 30.5;     // HM Magazin RL
R     DB100.DBX 30.7;     // Magazin Position erreicht
NETWORK
TITLE = Magazin RL
U(    ;
O     DB100.DBX 30.1;     // Start Magazin ref.
O     DB100.DBX 30.4;     // HM Magazin RL
)     ;
U     T 56;               // Pocket up verz.
U     M 137.7;            // Antriebe ein
UN    DB2.DBX 9.1;        // FM6073
UN    A 10.2;             // Magazin RL
=     A 10.3;             // Magazin LL
NETWORK
TITLE = Magazin LL
U     DB100.DBX 30.5;     // HM Magazin LL
U     T 56;               // Pocket up verz.
U     M 137.7;            // Antriebe ein
UN    DB2.DBX 9.1;        // FM6073
UN    A 10.3;             // Magazin LL
=     A 10.2;             // Magazin RL
NETWORK
TITLE = FM6073 WZW Positionsueberwachung
U(    ;
L     DB100.DBB 29;       // Magazin Istpos. HW
L     1;
==I   ;
)     ;
U     M 7.5;              // Werkzeughalter in Position
UN    M 27.6;             // Referenzpunkt Magazin gef.
U     DB100.DBX 35.6;     // WZW synchronisiert
L     S5T#50MS;
SE    T 93;               // Ueberw. Pos.==1
U     T 93;               // Ueberw. Pos.==1
S     DB2.DBX 9.1;        // FM6073
S     DB100.DBX 99.0;     // Diag. FC77 Pos==1
U(    ;
L     DB100.DBB 29;       // Magazin Istpos. HW
L     1;
<>I   ;
)     ;
U     M 7.5;              // Werkzeughalter in Position
U     M 27.6;             // Referenzpunkt Magazin gef.
U     DB100.DBX 35.6;     // WZW synchronisiert
L     S5T#50MS;
SE    T 94;               // Ueberw. Pos.<>1
U     T 94;               // Ueberw. Pos.<>1
S     DB2.DBX 9.1;        // FM6073
S     DB100.DBX 99.1;     // Diag. FC77 Pos<>1
UN    M 7.5;              // Werkzeughalter in Position
L     S5T#750MS;
SE    T 90;               // Ueberw. Strobe=0
U     T 90;               // Ueberw. Strobe=0
S     DB100.DBX 99.2;     // Diag. FC77 Strobe=0
O     A 10.2;             // Magazin RL
O     A 10.3;             // Magazin LL
U     M 7.5;              // Werkzeughalter in Position
L     S5T#1S;
SE    T 91;               // Ueberw. Strobe=1
U     T 91;               // Ueberw. Strobe=1
S     DB100.DBX 99.3;     // Diag. FC77 Strobe=1
O     A 10.2;             // Magazin RL
O     A 10.3;             // Magazin LL
U     DB100.DBX 30.1;     // Start Magazin ref.
L     S5T#10S;
SE    T 92;               // Ueberw. Referenzieren
O     T 90;               // Ueberw. Strobe=0
O     T 91;               // Ueberw. Strobe=1
O     T 92;               // Ueberw. Referenzieren
S     DB2.DBX 9.1;        // FM6073
U     DB2.DBX 9.1;        // FM6073
R     DB100.DBX 35.6;     // WZW synchronisiert
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
UN    M 155.1;            // BA AUTO
U     M 107.4;            // AUX-ON Taste
R     DB2.DBX 9.1;        // FM6073
R     DB100.DBX 99.0;     // Diag. FC77 Pos==1
R     DB100.DBX 99.1;     // Diag. FC77 Pos<>1
R     DB100.DBX 99.2;     // Diag. FC77 Strobe=0
R     DB100.DBX 99.3;     // Diag. FC77 Strobe=1
END_FUNCTION
//**************************************************************************
FUNCTION FC 78 : VOID
TITLE=WZW_ARM
NAME: ARM_CM
BEGIN
NETWORK
TITLE = WZW manuell
U     M 105.5;            // WZW-Taste
FP    M 131.2;            // Fl. WZW-Taste
U     M 155.5;            // BA JOG
U     M 202.2;            // Tuer zu und verriegelt
U     DB1.DBX 1356.3;     // Spindelstillstand NC
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
UN    DB3.DBX 1.1;        // BM7009 Referenzpunkt nicht erreicht
UN    DB100.DBX 35.1;     // Werkzeug falsch gespannt
UN    DB100.DBX 30.0;     // Anf. Magazin positionieren
U     M 131.2;            // Fl. WZW-Taste
U     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
UN    M 27.1;             // Werkzeug loesen gef.
UN    M 27.2;             // Werkzeug spannen gef.
U     M 7.5;              // WZ-Halter in Position
S     M 130.2;            // WZW manuell
S     DB100.DBX 47.0;     // Schwenkarm Start
U     M 130.2;            // WZW manuell
U     DB100.DBX 47.7;     // Werkzeugwechsel fertig
O     M 102.7;            // Reset Taste
ON    M 137.7;            // Antriebe ein
R     M 130.2;            // WZW manuell
R     DB100.DBX 47.0;     // Schwenkarm Start
U     M 130.2;            // WZW manuell
=     DB20.DBX 847.1;     // LED WZW-Taste
=     DB100.DBX 35.2;     // man. WZW aktiv
NETWORK
TITLE = Schwenkarm Start, Wechselpos. anfahren
U     DB100.DBX 47.0;     // Schwenkarm Start
U     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
UN    M 27.1;             // Werkzeug loesen gef.
UN    M 27.2;             // Werkzeug spannen gef.
UN    DB100.DBX 47.1;     // Schwenkarm aktiv
S     DB100.DBX 47.1;     // Schwenkarm aktiv
S     DB100.DBX 47.2;     // WZ ausschwenken
S     DB100.DBX 48.5;     // Positionieren Teil 1
// Spindel in Rundachsbetrieb schalten
UN    DB100.DBX 48.5;     // Positionieren Teil 1
SPB   M7801
L     0;
T     DB25.DBB 822;       // Spindelnummer
L     1;
T     DB25.DBB 823;       // Rundachsbetrieb
U     DB100.DBX 48.5;     // Positionieren Teil 1
U     DB1.DBX 1356.3;     // Spindel steht
S     DB25.DBX 3.5;       // Spindelumschaltung
S     DB25.DBX 340.3;     // Einlesefreigabe K1
S     DB25.DBX 340.4;     // Achsfreigabe K1
M7801: NOP   0;
NETWORK
TITLE = Auswertung ob bei Werkzeugwechsel auch X-Achse wegfahren muß
U     DB10.DBX 48.3;      // USER_DATA_HEX[8]: Beim WZW zuerst Z-Achse dann X- und Y-Achse wegfahren
SPBN  M7802
L     DB10.DBD 80;        // USER_DATA_FLOAT[5]: Wechselpos. X [m]
L     0
<>D   ;
=     M 48.0              // Auch in X wegfahren
M7802: NOP   0;
NETWORK
TITLE = Wechselposition C, Z, anfahren
U     DB100.DBX 48.5;     // Positionieren Teil 1
U     M 110.4;            // Spindel im Rundachsbetrieb
S     DB100.DBX 48.6;     // Positionieren Teil 2
R     DB100.DBX 48.5;     // Positionieren Teil 1
SPBN  M7803
U     M 110.1;            // "1"
S     M 48.1              // HM Wechselposition nur C, Z, anfahren
S     DB25.DBX 1.4;       // NC-Satz abschicken
S     DB25.DBX 548.5;     // Genauhaltmodus fein setzen
S     DB25.DBX 548.7;     // G0
S     DB25.DBX 20.2;      // Positionieranforderung Kanalachse Z
S     DB25.DBX 21.4;      // Positionieranforderung Kanalachse C
L     DB10.DBD 64;        // USER_DATA_FLOAT[1]: Wechselpos. Z [m]
T     DB25.DBD 30;        // Zielposition Kanalachse Z
L     DB10.DBD 60;        // USER_DATA_FLOAT[0]: Wechselpos. C [Grad]
T     DB25.DBD 70;        // Positionswert Kanalachse C
U     DB10.DBX 48.3;      // USER_DATA_HEX[8]: Beim WZW zuerst Z-Achse dann X- und Y-Achse wegfahren
SPB   M7803
R     M 48.1              // HM Wechselposition nur C, Z, anfahren                 //
S     DB25.DBX 20.1;      // Positionieranforderung Kanalachse Y
L     DB10.DBD 68;        // USER_DATA_FLOAT[2]: Wechselpos. Y [m]
T     DB25.DBD 26;        // Zielposition Kanalachse Y
M7803: NOP   0;
TITLE = Wechselposition X, Y, anfahren
U     DB100.DBX 48.6;     // Positionieren Teil 2
U     M 110.4;            // Spindel im Rundachsbetrieb
U     DB10.DBX 48.3;      // USER_DATA_HEX[8]: Beim WZW zuerst Z-Achse dann X- und Y-Achse wegfahren
U     M 48.1              // HM Wechselposition C, Z, anfahren                 //
U     DB25.DBX 332.0;     // NC-Satz fertig
S     M 48.2              // HM Wechselposition X, Y, anfahren
R     M 48.1              // HM Wechselposition C, Z, anfahren
R     DB25.DBX 332.0;     // NC-Satz fertig
SPBN  M7804;
U     M 110.1;            // "1"
S     DB25.DBX 1.4;       // NC-Satz abschicken
S     DB25.DBX 548.5;     // Genauhaltmodus fein setzen
S     DB25.DBX 548.7;     // G0
S     DB25.DBX 20.1;      // Positionieranforderung Kanalachse Y
L     DB10.DBD 68;        // USER_DATA_FLOAT[2]: Wechselpos. Y [m]
T     DB25.DBD 26;        // Zielposition Kanalachse Y
U     M 48.0              // Auch in X wegfahren
SPBN  M7804;
S     DB25.DBX 20.0;      // Positionieranforderung Kanalachse X
L     DB10.DBD 80;        // USER_DATA_FLOAT[5]: Wechselpos. X [m]
T     DB25.DBD 22;        // Zielposition Kanalachse X
M7804: NOP   0;
NETWORK
TITLE = Wechselposition erreicht
U(
U     M 48.2              // HM Wechselposition X, Y, anfahren
U     DB10.DBX 48.3;      // USER_DATA_HEX[8]: Beim WZW zuerst Z-Achse dann X- und Y-Achse wegfahren
ON    DB10.DBX 48.3;      // USER_DATA_HEX[8]: Beim WZW zuerst Z-Achse dann X- und Y-Achse wegfahren
)
U     DB100.DBX 48.6;     // Positionieren Teil 2
U     DB25.DBX 332.0;     // NC-Satz fertig
S     DB100.DBX 48.7;     // Positionierung fertig
=     DB100.DBX 48.3;     // HM Schritt weiter
O     DB100.DBX 48.3;     // HM Schritt weiter
ON    DB100.DBX 47.1;     // Schwenkarm aktiv
R     DB100.DBX 48.6;     // Positionieren Teil 2
R     DB25.DBX 332.0;     // NC-Satz fertig
R     M 48.2              // HM Wechselposition X, Y, anfahren
R     M 48.1              // HM Wechselposition C, Z, anfahren
NETWORK
TITLE = VS-Override abfragen
L     DB20.DBW 152;       // VS-Override Kanal 0
L     20;                 // 2%
>I    ;
=     M 130.7;            // Fortschaltmerker
NETWORK
TITLE = Schwenken Schritt 1
U     DB100.DBX 47.2;     // WZ ausschwenken
U     T 55;               // Pocket down verz.
U     DB100.DBX 48.7;     // Positionierung fertig
U     DB100.DBX 39.0;     // Wechselposition ok
U     M 130.7;            // Fortschaltmerker
S     DB100.DBX 47.3;     // Schwenken Schritt 1
=     DB100.DBX 48.3;     // HM Schritt weiter
O     DB100.DBX 48.3;     // HM Schritt weiter
ON    DB100.DBX 47.1;     // Schwenkarm aktiv
R     DB100.DBX 47.2;     // WZ ausschwenken
R     DB100.DBX 48.7;     // Positionierung fertig
NETWORK
TITLE = Signal WZ loesen erkannt
U     DB100.DBX 47.3;     // Schwenken Schritt 1
U     M 27.1;             // Werkzeug loesen gef.
UN    M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
UN    M 27.2;             // Werkzeug spannen gef.
S     DB100.DBX 49.0;     // Signal WZ loesen erkannt
NETWORK
TITLE = Werkzeug loesen
U     DB100.DBX 47.3;     // Schwenken Schritt 1
U     DB100.DBX 49.0;     // Signal WZ loesen erkannt
UN    M 27.1;             // Werkzeug loesen gef.
=     DB100.DBX 49.4;     // Schwenken 1 fertig
U     DB100.DBX 49.4;     // Schwenken 1 fertig
U     M 130.7;            // Fortschaltmerker
S     DB100.DBX 47.4;     // Werkzeug loesen
=     DB100.DBX 48.3;     // HM Schritt weiter
O     DB100.DBX 48.3;     // HM Schritt weiter
ON    DB100.DBX 47.1;     // Schwenkarm aktiv
R     DB100.DBX 47.3;     // Schwenken Schritt 1
R     DB100.DBX 49.0;     // Signal WZ loesen erkannt
NETWORK
TITLE = Schwenken Schritt 2
U     DB100.DBX 47.4;     // Werkzeug loesen
U     T 76;               // WZ entspannt verz.
U     M 130.7;            // Fortschaltmerker
S     DB100.DBX 47.5;     // Schwenken Schritt 2
=     DB100.DBX 48.3;     // HM Schritt weiter
O     DB100.DBX 48.3;     // HM Schritt weiter
ON    DB100.DBX 47.1;     // Schwenkarm aktiv
R     DB100.DBX 47.4;     // Werkzeug loesen
NETWORK
TITLE = Signal WZ spannen erkannt
U     DB100.DBX 47.5;     // Schwenken Schritt 2
U     M 27.2;             // Werkzeug spannen gef.
UN    M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
UN    M 27.1;             // Werkzeug loesen gef.
S     DB100.DBX 49.1;     // Signal WZ spannen erkannt
NETWORK
TITLE = Werkzeug spannen
U     DB100.DBX 47.5;     // Schwenken Schritt 2
U     DB100.DBX 49.1;     // Signal WZ spannen erkannt
UN    M 27.2;             // Werkzeug spannen gef.
=     DB100.DBX 49.5;     // Schwenken 2 fertig
U     DB100.DBX 49.5;     // Schwenken 2 fertig
U     M 130.7;            // Fortschaltmerker
S     DB100.DBX 47.6;     // Werkzeug spannen
=     DB100.DBX 48.3;     // HM Schritt weiter
O     DB100.DBX 48.3;     // HM Schritt weiter
ON    DB100.DBX 47.1;     // Schwenkarm aktiv
R     DB100.DBX 47.5;     // Schwenken Schritt 2
R     DB100.DBX 49.1;     // Signal WZ spannen erkannt
NETWORK
TITLE = Arm in Grundstellung
U     DB100.DBX 47.6;     // Werkzeug spannen
U     T 77;               // WZ gespannt verz.
U     M 130.7;            // Fortschaltmerker
S     DB100.DBX 48.0;     // Arm in Grundstellung
R     DB100.DBX 47.6;     // Werkzeug spannen
SPBN  M7805
U     M 110.1;            // "1"
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
L     DB100.DBB 29;       // Mag. Istpos. HW
T     DB100.DBB 31;       // WZ-Nummer in Spindel
T     DB15.DBB 35;        // SE-Daten: akt. Spindelwerkzeug CM450
UN    DB100.DBX 35.5;     // Werkzeug durchgespannt
SPB   M7805
L     0;                  // kein Werkzeug
T     DB100.DBB 31;       // WZ-Nummer in Spindel
T     DB15.DBB 35;        // SE-Daten: akt. Spindelwerkzeug CM450
M7805: NOP   0;
NETWORK
TITLE = Werkzughalter einschwenken, Wechsel beendet
U     DB100.DBX 48.0;     // Arm in Grundstellung
U     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
UN    M 27.1;             // Werkzeug loesen gef.
UN    M 27.2;             // Werkzeug spannen gef.
S     DB100.DBX 48.1;     // Werkzeughalter einschwenken
S     DB100.DBX 47.7;     // Werkzeugwechsel fertig
=     DB100.DBX 48.3;     // HM Schritt weiter
O     DB100.DBX 48.3;     // HM Schritt weiter
ON    DB100.DBX 47.1;     // Schwenkarm aktiv
R     DB100.DBX 48.0;     // Arm in Grundstellung
NETWORK
TITLE = Schrittkette zuruecksetzen
ON    DB100.DBX 47.0;     // Schwenkarm Start
O     M 102.7;            // Reset Taste
ON    M 137.7;            // Antriebe ein
R     DB100.DBX 47.1;     // Schwenkarm aktiv
R     DB100.DBX 48.1;     // Werkzeughalter einschwenken
R     DB100.DBX 47.7;     // Werkzeugwechsel fertig
NETWORK
TITLE = Leistung Schwenkarm
// V2.25: NEUES Signal 'HM Motor Arm aus' für DETA-Wechsler
U(    ;
O(    ;
U     DB100.DBX 47.3;     // Schwenken Schritt 1
UN    DB100.DBX 49.4;     // Schwenken 1 fertig
UN    M 7.7;              // HM Motor Arm aus
)     ;
O(    ;
U     DB100.DBX 47.5;     // Schwenken Schritt 2
UN    DB100.DBX 49.5;     // Schwenken 2 fertig
UN    M 7.7;              // HM Motor Arm aus
)     ;
O(    ;
U     DB100.DBX 48.0;     // Arm in Grundstellung
UN    M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
)     ;
O(    ;
U     DB100.DBX 37.0;     // WZW freifahren aktiv
UN    M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
U(    ;
U     A 7.0;              // MV Werkzeugklemmung Spindel
U     T 76;               // WZ entspannt verz.
O     ;
UN    A 7.0;              // MV Werkzeugklemmung Spindel
U     T 77;               // WZ gespannt verz.
)     ;
)     ;
)     ;
U     M 137.7;            // Antriebe ein
UN    DB2.DBX 9.7;        // FM6079
=     A 10.4;             // Leistung Schwenkarm
NETWORK
TITLE = Bremse bei Not-Aus dauernd loesen (Service)
U     M 105.5;            // WZW-Taste
UN    M 155.1;            // BA AUTO
UN    M 202.2;            // Tuer zu und verriegelt
U     DB1.DBX 1356.3;     // Spindelstillstand NC
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
S     DB100.DBX 49.7;     // Bremse dauernd loesen (Service)
U     E 2.4;              // Not-Aus
R     DB100.DBX 49.7;     // Bremse dauernd loesen (Service)
NETWORK
TITLE = Bremse Schwenkarm
U     M 105.5;            // WZW-Taste
UN    M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
UN    M 155.1;            // BA AUTO
UN    M 202.2;            // Tuer zu und verriegelt
U     DB1.DBX 1356.3;     // Spindelstillstand NC
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
O     DB100.DBX 49.7;     // Bremse dauernd loesen (Service)
O     A 10.4;             // Leistung Schwenkarm
=     A 7.3;              // Bremse WZW Schwenkarm
NETWORK
TITLE = Werkzeugspannung
O(    ;
U     M 27.1;             // Werkzeug loesen gef.
U     A 10.4;             // Leistung Schwenkarm
)     ;
O     DB100.DBX 47.4;     // Werkzeug loesen
O     DB100.DBX 47.5;     // Schwenken Schritt 2
S     DB100.DBX 35.4;     // HM Werkzeugspannung
O(    ;
UN    M 27.2;             // Werkzeug spannen gef.
U     DB100.DBX 35.3;     // HM Fl. E7.2
)     ;
O     DB100.DBX 47.6;     // Werkzeug spannen
O     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
R     DB100.DBX 35.4;     // HM Werkzeugspannung
U     M 27.2;             // Werkzeug spannen gef.
=     DB100.DBX 35.3;     // HM Fl. E7.2
NETWORK
TITLE = MV Werkzeugspannung
O(    ;
U     DB20.DBX 833.6;     // Taste Werkzeugspanner
UN    M 155.1;            // BA AUTO
U(    ;
ON    M 202.2;            // Tuer zu und verriegelt
O     DB100.DBX 49.2;     // Abbruch mit Alarm
)     ;
U     DB1.DBX 1356.3;     // Spindelstillstand NC
)     ;
O(    ;
U     DB100.DBX 35.4;     // HM Werkzeugspannung
UN    M 27.1;             // Werkzeug loesen gef.
)     ;
U     M 136.0;            // AUX-ON
=     A 7.0;              // MV Werkzeugklemmung Spindel
NETWORK
TITLE = Werkzeug entspannt
U(    ;
L     DB100.DBW 172;      // Analogwert Werkzeugspanner
L     200;                // Grenze geloest
<I    ;
)     ;
U     A 7.0;              // MV Werkzeugklemmung Spindel
L     S5T#500MS;
SE    T 76;               // WZ entspannt verz.
NETWORK
TITLE = Werkzeug gespannt
U(    ;
L     DB100.DBW 172;      // Analogwert Werkzeugspanner
L     220;                // Grenze gespannt
>I    ;
)     ;
UN    A 7.0;              // MV Werkzeugklemmung Spindel
L     S5T#500MS;
SE    T 77;               // WZ gespannt verz.
;
;  NETWORK
;  TITLE = Dyn NC Interface Werkzeugspindel belegt
;
;       O   DB100.DBX 40.0;     // autom. WZW aktiv
;       ON  DB1.DBX 1356.3;     // Spindel steht
;    SPB    M5902
;       U   T 77;               // WZ gespannt verz.
;       UN  DB100.DBX 35.5;     // Werkzeug durchgespannt
;       UN  DB100.DBX 35.1;     // Werkzeug falsch gespannt
;       =   DB20.DBX 344.0      // dyn NC Interface Werkzeugspindel belegt
;M5902: NOP 0
;
NETWORK
TITLE = Werkzeug durchgespannt
// V2.22: Grenzwert auf MD gelegt, Wert: 320..400
U(    ;
L     DB10.DBW 26;        // USER_DATA_INT[13]: Grenze durchgespannt
L     320;
>=I   ;
)     ;
U(    ;
L     DB10.DBW 26;        // USER_DATA_INT[13]: Grenze durchgespannt
L     400;
<=I   ;
)     ;
SPB   dgok;
L     320;
T     DB10.DBW 26;        // USER_DATA_INT[13]: Grenze durchgespannt
dgok: U(    ;
L     DB100.DBW 172;      // Analogwert Werkzeugspanner
L     DB10.DBW 26;        // USER_DATA_INT[13]: Grenze durchgespannt
>I    ;
)     ;
UN    DB10.DBX 52.1;      // USER_DATA_HEX[12]: DL kein WZ durchgespannt
=     DB100.DBX 35.5;     // Werkzeug durchgespannt
NETWORK
TITLE = Werkzeug falsch gespannt
U(    ;
L     DB100.DBW 172;      // Analogwert Werkzeugspanner
L     DB10.DBW 24;        // USER_DATA_INT[12]: Grenze WZ falsch gespannt
<I    ;
)     ;
U     T 77;               // WZ gespannt verz.
=     DB100.DBX 35.1;     // Werkzeug falsch gespannt
=     M 145.6;            // Startverr. WZ falsch gespannt
U     DB100.DBX 35.1;     // Werkzeug falsch gespannt
U     M 110.7;            // Blinktakt
=     DB3.DBX 3.5;        // BM7029 Spindelpo. ok?
NETWORK
TITLE = MV Spindel ausblasen
U     DB100.DBX 47.5;     // Schwenken Schritt 2
UN    DB100.DBX 49.5;     // Schwenken 2 fertig
U     M 136.0;            // AUX-ON
O     M 161.6;            // Blasluft ein
=     A 7.1;              // MV Spindel ausblasen
NETWORK
TITLE = VSH WZW nicht in Grundstellung
U     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
UN    M 27.1;             // Werkzeug loesen gef.
UN    M 27.2;             // Werkzeug spannen gef.
=     DB100.DBX 36.0;     // Schwenkarm Grst.
UN    DB100.DBX 36.0;     // Schwenkarm Grst.
U(    ;
ON    DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
ON    M 107.4;            // Aux-On Taste
ON    M 155.5;            // BA JOG
)     ;
=     M 143.5;            // VSH WZW keine Grst.
NETWORK
TITLE = FM6063 Schwenkarm nicht in Grundstellung
UN    DB100.DBX 36.0;     // Schwenkarm Grst.
UN    DB100.DBX 47.0;     // Schwenkarm Start
UN    M 138.6;            // Not-Aus Test angefordert
=     DB2.DBX 7.7;        // FM6063 Schwenkarm nicht in Grundstellung
NETWORK
TITLE = FM6079 WZW Beroueberwachung
// V2.25: Überwachungen erweitert (DETA-Wechsler)
O     A 10.4;             // Leistung Schwenkarm
O     M 7.7;              // HM Motor Arm aus
L     S5T#2S;
SE    T 95;               // Ueberw. Schwenkarm ein
U     T 95;               // Ueberw. Schwenkarm ein
S     DB100.DBX 99.4;     // Diag. FC78 Zeitueberw.
U     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
U     M 27.1;             // Werkzeug loesen gef.
O     ;
U     DB100.DBX 99.5;     // Diag. FC78 Bero 1
U     DB2.DBX 9.7;        // FM6079
=     DB100.DBX 99.5;     // Diag. FC78 Bero 1
U     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
U     M 27.2;             // Werkzeug spannen gef.
O     ;
U     DB100.DBX 99.6;     // Diag. FC78 Bero 2
U     DB2.DBX 9.7;        // FM6079
=     DB100.DBX 99.6;     // Diag. FC78 Bero 2
U     M 27.1;             // Werkzeug loesen gef.
U     M 27.2;             // Werkzeug spannen gef.
O     ;
U     DB100.DBX 99.7;     // Diag. FC78 Bero 3
U     DB2.DBX 9.7;        // FM6079
=     DB100.DBX 99.7;     // Diag. FC78 Bero 3
O     DB100.DBX 99.5;     // Diag. FC78 Bero 1
O     DB100.DBX 99.6;     // Diag. FC78 Bero 2
O     DB100.DBX 99.7;     // Diag. FC78 Bero 3
L     S5T#200MS;
SE    T 96;               // FM6079 ausblenden
U(    ;
O     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
ON    M 27.1;             // Werkzeug loesen gef.
)     ;
U     DB100.DBX 47.3;     // Schwenken Schritt 1
UN    DB100.DBX 49.4;     // Schwenken 1 fertig
L     S5T#250MS;
SE    T 97;               // Ueberw. Signale nach Start
O(    ;
U     DB100.DBX 47.3;     // Schwenken Schritt 1
U     M 27.2;             // Werkzeug spannen gef.
)     ;
O     T 95;               // Ueberw. Schwenkarm ein
O     T 96;               // FM6079 ausblenden
O     T 97;               // Ueberw. Signale nach Start
S     DB2.DBX 9.7;        // FM6079
S     DB100.DBX 49.2;     // Abbruch mit Alarm
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 9.7;        // FM6079
R     DB100.DBX 99.4;     // Diag. FC78 Zeitueberw.
U     M 27.0;             // Referenzpunkt Werkzeugwechsler gef.
R     DB100.DBX 49.2;     // Abbruch mit Alarm
END_FUNCTION
//**************************************************************************
FUNCTION FC 80 : VOID
TITLE=HILFSANTRIEBE
NAME: AUX
BEGIN
NETWORK
TITLE = Aux Off Meldungen
L     DB2.DBD 2;          // Aux Off Fehlermeldungen
L     L#0;
==D   ;
=     M 177.2;            // HM Aux Off Alarme
NETWORK
TITLE = Verzoegerung Aux On
U     M 107.4;            // Taste Aux On
UN    M 107.6;            // Taste Aux Off
L     S5T#600MS;
SE    T 44;
NETWORK
TITLE = Auswertung fataler SPS Fehler
L     DB2.DBB 0;          // fatale SPS Fehler
L     L#0;
==I   ;
=     M 177.3;            // HM SPS fatale Fehler
NETWORK
TITLE = Aux On
U     M 190.0;            // Rob. Aux-On
U     M 191.0;            // Robotik aktiv
O     DB1.DBX 1382.4;     // DNC Aux-On
O     T 44;
S     M 136.0;            // AUX-ON
NETWORK
TITLE = Verzoegerung autom. Aux Off
UN    M 107.4;            // Taste Aux on
U     M 155.1;            // BA Auto
U     M 137.7;            // Antriebe ein
UN    M 111.7;            // Programm laeuft
UN    M 136.7;            // Spindel laeuft verzoegert
UN    M 136.4;            // Summe Fahrbefehl
UN    M 111.5;            // M00/M01 aktiv
UN    DB10.DBX 40.4;      // USER_DATA_HEX[0] kein autom. Aux Off
L     S5T#600S;
SE    T 24;
NETWORK
TITLE = Aux Off
O(    ;
U     T 24;               // Verz. autom. AUX-OFF
U     M 136.0;            // AUX-ON
)     ;
O(    ;
U     M 107.6;            // AUX-OFF Taste
UN    M 111.7;            // Programm laeuft
UN    M 136.4;            // Summe Fahrbefehl
UN    M 136.7;            // Spindel laeuft verz.
)     ;
O     DB1.DBX 1370.1;     // Fataler NC-Fehler steht an
O     DB1.DBX 1382.3;     // DNC Aux-Off
O     DB2.DBX 1.0;        // FM6008 fehlender CAN-Teilnehmer
ON    E 2.4;              // Not-Aus Taste
ON    M 177.2;            // HM AUX OFF Alarme
ON    M 177.3;            // HM fatale SPS Fehler
O     DB100.DBX 170.7;    // SI Summenalarm
O     DB1.DBX 1740.0      // Schleppfehlerüberwachung ACC
O     DB1.DBX 1740.1      // Schleppfehlerüberwachung ACC
O     DB1.DBX 1740.2      // Schleppfehlerüberwachung ACC
=     M 136.2;            // HM Aux-Off
R     M 136.0;            // AUX-ON
NETWORK
TITLE = Deaktivierung der stehenden Achsen
UN  M 136.0;            // AUX-ON
=   DB1.DBX 1734.0      // Deaktivierung absolute Achse X
=   DB1.DBX 1734.1      // Deaktivierung absolute Achse Y
=   DB1.DBX 1734.2      // Deaktivierung absolute Achse Z
UN  M 136.0;            // AUX-ON
U   DB10.DBX 42.0;      // USER_DATA_HEX[2] RTA angew.
=   DB1.DBX 1734.4      // Deaktivierung absolute Achse 4 (A-Rundachse)
NETWORK
TITLE = Aux-On abschaltverzoegert
U     M 136.0;            // AUX-ON
L     S5T#250MS;
SA    T 35;               // Aux-On verz.
NETWORK
TITLE = Flanke Aux On
U     M 136.0;            // AUX-ON
UN    A 9.0;              // Lstg. AUX-ON
=     M 136.1;            // Flanke Aux On
NETWORK
TITLE = Leistung Hilfsbetriebe
U(    ;
ON    DB1.DBX 1356.3;     // Spindelstillstand NC
ON    E 5.0;              // HA n<N0 611U
)     ;
U     A 9.0;              // Lstg. AUX-ON
O     T 35;               // Aux-On verz.
=     A 4.6;              // Sperrluft Spindel
U     A 4.6;              // Sperrluft Spindel
UN    DB100.DBX 169.1;    // Test Schritt 1
=     A 9.0;              // Lstg. AUX-ON
NETWORK
TITLE = Shutdown der Oberflaeche verhindern
U     M 136.0;            // AUX-ON
UN    DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
R     DB1.DBX 1440.1;     // Shutdown verhindern
NETWORK
TITLE =Wert wandeln von Ganzzahl in BCD
L     DB10.DBW 14;        // USER_DATA_INT[7]
ITB   ;
T     DB100.DBW 900;      // #Schmierwort;
NETWORK
TITLE =Zeitbasis 1s dazu und in Schmierwort laden
L     DB100.DBW 900;      // #Schmierwort;
L     2#10000000000000;
OW    ;
T     DB100.DBW 900;      // #Schmierwort;
NETWORK
TITLE =Zeitverzoegerung HAWE Hydraulik
U     E 9.4;              // DS Hydraulik
L     DB100.DBW 900;      // #Schmierwort;
SA    T 89;               // Zeit Hydraulik
NETWORK
TITLE =Leistung HAWE Hydraulik
U     T 89;               // Zeit Hydraulik
U     E 4.6;              // Therm. Stoerung sofort
=     M 211.3;            // Leistung HAWE Hydraulik
NETWORK
TITLE =Dualzahl in BCD wandeln
L     DB10.DBW 16;        // USER_DATA_INT[8]
ITB   ;
T     DB100.DBW 902;      // #Schmierwort_2;
NETWORK
TITLE =Zeitbasis 1s dazu und in Schmierwort_2 laden
L     DB100.DBW 902;      // #Schmierwort_2;
L     2#10000000000000;
OW    ;
T     DB100.DBW 902;      // #Schmierwort_2;
NETWORK
TITLE =Zeitueberwachung Hydraulik
//      U     M 211.3;            // Leistung HAWE Hydraulik
//      U     E 9.4;              // DS Hydraulik
//      L     DB100.DBW902;       // #Schmierwort_2;
//      SE    T 37;               // Zeitueberwachung Hydraulik
NETWORK
TITLE = FM6044 "Stoerung Hydraulik"
U     T 37;               // Zeitueberwachung Hydraulik
S     DB2.DBX 5.4;        // FM6044"Stoerung Hydraulik"
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 5.4;
NETWORK
TITLE = BM7008 "Aux Off"
UN    M 136.0;            // AUX-ON
=     DB3.DBX 1.0;        // BM7008 "Aux Off"
U     M 136.0;            // AUX-ON
=     DB1.DBX 1390.2;     // glob. Rueckmeldung Aux-On
UN    M 136.0;            // AUX-ON
UN    M 136.2;            // HM Aux-Off
U     M 110.7;            // Blinktakt
O     M 136.0;            // AUX-ON
=     DB20.DBX 852.6;     // LED Aux-On Taste
NETWORK
TITLE = FM6017 "Thermische Stoerungen"
U(    ;
ON    M 136.4;            // Summe Fahrbefehle
ON    M 146.3;            // EFG/VSH WZW schwenkt
)     ;
U(    ;
UN    M 111.7;            // Programm laeuft
UN    E 4.7;              // Therm. Stoerung Stop nach Programmende
ON    E 4.6;              // Therm. Stoerung sofort
)     ;
S     DB2.DBX 2.1;        // FM6017 Therm. Stoerungen
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 2.1;        // FM6017 Therm. Stoerungen
NETWORK
TITLE =FM6045 "Druckueberwachung Luftversorgung"
O(    ;
U     M 136.0;            // AUX-ON
UN    T 34;               // DS Luft verz.
U     DB10.DBX 43.0;      // USER_DATA_HEX[3]: DS Pneumatik vorhanden
)     ;
O(    ;
U     A 6.6;              // Tuerautom. Hauptventil
U     E 6.4;              // Tuerautom. Schaltleiste ok
UN    E 6.3;              // Tuerautom. DS Tuer
U     DB10.DBX 41.1;      // USER_DATA_HEX[1]: Tuerautomatik angewaehlt
)     ;
L     S5T#500MS;
SE    T 33;
U     T 33;
S     DB2.DBX 5.5;        // FM6045 "Druckueberwachung Luftversorgung"
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 5.5;        // FM6045
NETWORK
TITLE =Druckueberw. Pneumatik Zeitbasis 1s
L     DB10.DBW 18;        // USER_DATA_INT[9]
ITB   ;
T     DB100.DBW 902;      // #Schmierwort_2;
L     2#10000000000000;   // Zeitbasis 1s
OW    ;
T     DB100.DBW 902;      // #Schmierwort_2;
NETWORK
TITLE =DS Luft verzoegert
U     E 4.0;              // DS Luftversorgung
U     DB10.DBX 43.0;      // USER_DATA_HEX[3]: DS Pneumatik vorhanden
L     DB100.DBW 902;      // #Schmierwort_2;
SA    T 34;               // DS Luft verz.
NETWORK
TITLE =Absaugung Festo
// V9.01: Funktion neu
L     DB10.DBW 30;        // USER_DATA_INT[15]: Nachlaufzeit Absaugung
L     0;
==I   ;
SPB   no_1;
L     DB10.DBW 30;        // USER_DATA_INT[15]: Nachlaufzeit Absaugung
ITB   ;
T     DB100.DBW 902;      // #Schmierwort_2;
L     2#10000000000000;   // Zeitbasis 1s
OW    ;
T     DB100.DBW 902;      // #Schmierwort_2;
U     M 111.7;            // Programm laeuft
U     M 155.1;            // BA AUTO
L     DB100.DBW 902;      // #Schmierwort_2;
SA    T 36;               // Nachlaufzeit Absaugung
U     T 36;               // Nachlaufzeit Absaugung
U     M 136.0;            // AUX-ON
=     A 6.2;              // Lstg. Absaugung
no_1: NOP   0;
NETWORK
TITLE =FM6048 Stoerung SS-Kuehleraet
// V2.22: Netzwerk neu
UN    E 9.5;              // Stoerung SS-Kuehleraet
L     S5T#5S;
SE    T 101;
U     T 101;
=     DB2.DBX 6.0;        // FM6048 Stoerung Schaltschrankkuehlung
=     M 145.4;            // Startsperre Stoerung SS-Kuehlung
END_FUNCTION
//**************************************************************************
FUNCTION FC 81 : VOID
TITLE=SCHMIERUNG
NAME: SM
BEGIN
NETWORK
TITLE = Schmierzeit ueberwachen
U(    ;
L     DB100.DBW 64;       // Zaehlerwert Schmiermittel
L     DB10.DBW 2;         // USER_DATA_INT[1] Pausenzeit SM
>=I   ;
)     ;
UN    M 163.0;            // SM ein
=     M 163.1;            // Schmierzeit erreicht
NETWORK
TITLE = Schmierweg Linearachse erfassen
U     DB1.DBX 1422.0;     // Wegimpuls Achse 0
ZV    Z 0;                // Meterzaehler Achse 0
U     DB1.DBX 1422.1;     // Wegimpuls Achse 1
ZV    Z 1;                // Meterzaehler Achse 1
U     DB1.DBX 1422.2;     // Wegimpuls Achse 2
ZV    Z 2;                // Meterzaehler Achse 2
O(    ;
L     Z 0;                // Meterzaehler Achse 0
L     DB10.DBW 38;        // USER_DATA_INT[19]: Schmierweg [m]
>=I   ;
)     ;
O(    ;
L     Z 1;                // Meterzaehler Achse 1
L     DB10.DBW 38;        // USER_DATA_INT[19]: Schmierweg [m]
>=I   ;
)     ;
O(    ;
L     Z 2;                // Meterzaehler Achse 2
L     DB10.DBW 38;        // USER_DATA_INT[19]: Schmierweg [m]
>=I   ;
)     ;
=     M 163.5;            // Schmierweg erreicht
R     Z 0;
R     Z 1;
R     Z 2;
NETWORK
TITLE = Schmiermittel ein
U     M 107.4;            // Taste AUX-ON
FP    M 131.3;
O(    ;
U     M 131.3;            // Fl. AUX-ON Taste
U     M 136.0;            // AUX-ON
UN    E 9.3;              // DS SM
)     ;
O(    ;
U     M 163.1;            // Schmierzeit erreicht
UN    DB10.DBX 40.2;      // USER_DATA_HEX[0] wegabh. Schmierung
UN    DB2.DBX 6.3;        // FM6051 Schmierueberwachung
)     ;
O(    ;
U     M 163.5;            // Schmierweg erreicht
U     DB10.DBX 40.2;      // USER_DATA_HEX[0] wegabh. Schmierung
UN    DB2.DBX 6.3;        // FM6051 Schmierueberwachung
)     ;
O     M 136.1;            // Fl. AUX-ON
S     M 163.0;            // SM ein
R     DB2.DBX 6.3;        // FM6051 Schmierueberwachung
R     M 146.0;            // VSH/EFG SM
FP    M 163.3;            // Fl. SM ein
NETWORK
TITLE =Zaehler Schmiermittel
U(    ;
U     M 136.4;            // Summe Fahrbefehle
UN    DB10.DBX 40.2;      // USER_DATA_HEX[0] wegabh. Schmierung
O     M 163.0;            // SM ein
)     ;
U     M 110.7;            // 1 HZ
FP    M 163.4;            // Fl. SM Zaehler
UN    M 163.4;            // Fl. SM Zaehler
SPB   M041;
L     DB100.DBW 64;       // Zaehler SM
L     1;
+I    ;
T     DB100.DBW 64;
M041: NOP   0;
UN    M 163.3;            // Fl. SM ein
SPB   M042;
L     0;
T     DB100.DBW 64;       // Zaehler SM
T     DB100.DBW 66;       // Zaehler SM-Fehler
M042: NOP   0;
NETWORK
TITLE = Zeitueberwachung SM-Pumpe
U(    ;
L     DB100.DBW 64;       // Zaehler SM
L     DB10.DBW 0;         // USER_DATA_INT[0] Zeitueberwachung SM
>=I   ;
)     ;
U     M 163.0;            // SM ein
=     M 163.2;            // HM Zeitueberwachung
NETWORK
TITLE = FM6051 "Schmierueberwachung"
U(    ;
U     E 9.3;              // DS SM
UN    A 4.7;              // Lstg.Schmiermittelpumpe
UN    M 163.7;            // FM6051 ausblenden
ON    E 9.2;              // Schwimmerschalter
O     M 163.2;            // HM Zeitueberwachung
)     ;
U     M 163.0;            // SM ein
S     DB2.DBX 6.3;        // FM6051 Schmierueberwachung
NETWORK
TITLE =Schmiermittel ein ruecksetzen
O     DB2.DBX 6.3;        // FM6051 Schmierueberwachung
O     E 9.3;              // DS SM
ON    M 136.0;            // AUX-ON
R     M 163.0;            // SM ein
=     M 163.7;            // FM6051 ausblenden
NETWORK
TITLE = Leistung Schmiermittelpumpe
U     M 163.0;            // SM ein
U     E 4.7;              // Therm. Stoerung Programmende
=     A 4.7;              // Lstg. Schmiermittelpumpe
=     DB20.DBX 311.4;     // Rueckmeldung SM
NETWORK
TITLE =Zaehler EFG/VSH FM6051
U     M 111.7;            // Programm laeuft
U     DB2.DBX 6.3;        // FM6051 Schmierueberwachung
U     M 110.7;            // 1Hz
FP    M 163.6;            // Fl. Fehlerzeit
UN    M 163.6;            // Fl. Fehlerzeit
SPB   M091;
L     DB100.DBW 66;       // Zaehler SM-Fehler
L     1;
+I    ;
T     DB100.DBW 66;
M091: NOP   0;
NETWORK
TITLE =Zeitueberwachung SM-Fehler
L     DB10.DBW 2;         // USER_DATA_INT[1] Pausenzeit SM
L     2;
/I    ;
L     DB100.DBW 66;       // Zaehler SM-Fehler
<=I   ;
S     M 146.0;            // EFG/VSH FM6051
NETWORK
TITLE =Diagnosezaehler
U     M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF
R     Z 20;               // Diagnosezaehler SM
U     E 9.3;              // DS SM
ZV    Z 20;               // Diagnosezaehler SM
END_FUNCTION
//**************************************************************************
FUNCTION FC 82 : VOID
TITLE=WARNLAMPE
NAME: WARNL
BEGIN
NETWORK
TITLE = Abbruch
UN    DB10.DBX 43.7;      // USER_DATA_HEX[3]: Warnlampe
BEB   ;
NETWORK
TITLE = PLC-Alarme
U     M 110.1;            // "1"
R     DB100.DBX 290.0;    // HM PLC-Alarm
L     DB2.DBD 0;
L     0;
<>D   ;
S     DB100.DBX 290.0;    // HM PLC-Alarm
L     DB2.DBD 4;
L     0;
<>D   ;
S     DB100.DBX 290.0;    // HM PLC-Alarm
L     DB2.DBD 8;
L     0;
<>D   ;
S     DB100.DBX 290.0;    // HM PLC-Alarm
NETWORK
TITLE = Warnlampe rot progr.
U     M 110.1;            // "1"
R     DB100.DBX 290.1;    // Warnl. rot progr.
NETWORK
TITLE = Warnlampe ROT ... Fehler
U(    ;
ON    E 2.4;              // Not-Aus
O     DB100.DBX 290.0;    // HM PLC-Alarm
O     DB100.DBX 170.7;    // SI Summenalarm
O     DB100.DBX 290.1;    // Warnl. rot progr.
O     DB1.DBX 1366.3;     // NC-Alarm steht an
)     ;
U     M 110.7;            // Blinktakt
=     A 6.0;              // Warnlampe rot
NETWORK
TITLE = Warnlampe GRUEN ... Automatik
U     M 155.1;            // BA AUTO
U(    ;
O     M 110.7;            // Blinktakt
O     M 111.7;            // Progr. laeuft
)     ;
U     M 136.0;            // AUX-ON
=     A 6.1;              // Warnlampe gruen
END_FUNCTION
//**************************************************************************
FUNCTION FC 83 : VOID
TITLE=AUTO_NC_START
NAME: AUT_STRT
BEGIN
NETWORK
TITLE = M30 gespeichert setzen
U     DB20.DBX 196.0;     // M32 dyn.
UN    DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
S     M 174.7;            // M32 gespeichert
NETWORK
TITLE = M30 gespeichert ruecksetzen
//      O     DB21.DBX 33.7;      // PRT aktiv
O     M 154.1;            // Impuls Programm laeuft
O(    ;
U     M 148.4;            // NC-Start LM/STV/Dauerlauf
UN    M 148.3;            // keine Start Verriegelung
)     ;
O     DB20.DBX 828.6;     // Reset-Taste MSTT
ON    M 136.0;            // AUX-ON
O     M 137.1;            // Einrichte BA
ON    M 155.1;            // BA AUTO
O     M 155.7;            // 1x aktiv
O     DB3.DBX 5.3;        // BM7043 "Sollstueckzahl erreicht"
R     M 174.7;            // M32 gespeichert
NETWORK
TITLE = Programm-laeuft abfallverzoegern
U     M 111.7;            // Programm laeuft
L     S5T#500MS;
SA    T 30;               // Progr.-laeuft abfallverz.
NETWORK
TITLE = NC-Start LM/STV/Dauerlauf
U     M 174.7;            // M32 gespeichert
UN    T 30;               // Progr.-laeuft abfallverz.
UN    DB100.DBX 136.7;    // Anforderung Teststop
=     M 148.4;            // NC-Start LM/STV/Dauerlauf
NETWORK
TITLE = Programm-Ende an NC zuruekmelden
O     DB20.DBX 192.2;     // M02 dyn.
O     DB20.DBX 195.6;     // M30 dyn.
=     DB20.DBX 348.1;     // M30 an NC; Progr. laeuft wird zurueckgesetzt
NETWORK
TITLE = Quittierung BM7043 Sollstueckz. erreicht
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
U     M 102.7;            // Reset-Taste
O     M 138.0;            // Fl. Reset-Taste
R     DB3.DBX 5.3;        // BM7043 Sollstueckz. erreicht
END_FUNCTION
//**************************************************************************
FUNCTION FC 84 : VOID
TITLE=NC_START_STOP
NAME: STRT_STP
BEGIN
NETWORK
TITLE = NC-Stop
O(    ;
U(    ;
O     M 143.0;            // NC-Stop WZW-Alarm
O     M 143.1;            // NC-Stop Zeitueberw. EFG
ON    M 136.0;            // AUX-ON
O     M 104.7;            // Taste NC-Stop
O     DB20.DBX 192.0;     // M00 dyn.
O     DB3.DBX 1.1;        // Meld. 7009 "Referenzpunkt nicht erreicht"
O(    ;
U     DB20.DBX 192.1;     // M01 dyn.
U     DB20.DBX 324.5;     // Opt. Stop aktiv
)     ;
)     ;
U     M 111.7;            // Programm laeuft
)     ;
O     DB20.DBX 0.1;       // NC-Stop vom Kern
=     DB20.DBX 0.1;       // NC-Stop direkt an AC
NETWORK
TITLE = HM3 NC-Start
L     MB 145;             // Startverriegelungen
L     0;
==I   ;
=     M 148.3;            // keine Start Verriegelung
NETWORK
TITLE = HM2 NC-Start
U     M 137.1;            // Einrichte BA
UN    M 145.2;            // Verr.Start Tuer
O     M 155.5;            // BA Jog
O     M 148.3;            // keine Start Verriegelung
=     M 148.1;            // HM  Freigabe NC-Start
NETWORK
TITLE = Flanke Taste NC-Start
U(    ;
U     M 201.5;            // Taste NC-Start extern
UN    DB10.DBX 41.1;      // USER_DATA_HEX[1]: Tuerautomatik angewaehlt
O     M 104.6;            // NC-Start Taste
)     ;
FP    M 154.3;            // Flanke Taste NC-Start
NETWORK
TITLE = NC-Start gesamt
O     M 154.3;            // Fl. NC-Start Taste
O     M 148.4;            // NC-Start LM/STV/Dauerlauf
O     M 148.5;            // NC-Start Tuerautomatik
O     M 190.3;            // Rob. NC-Start
O(    ;
U     DB1.DBX 1366.0;     // DNC aktiv
U     DB20.DBX 0.2;       // NC-Start Anforderung von Oberfläche
)     ;
=     M 148.2;            // NC-Start gesamt
NETWORK
TITLE = Flanke Prg. laeuft
U     M 111.7;            // Programm laeuft
FP    M 154.1;            // Flanke Prg. laeuft
NETWORK
TITLE = NC-Start
U     M 148.1;            // HM  Freigabe NC-Start
U     M 136.0;            // AUX-ON
UN    DB2.DBX 6.3;        // FM6051 Schmierueberwachung
UN    DB3.DBX 0.4;        // BM7004 Vorschubhalt manuell
UN    DB3.DBX 1.1;        // BM7009 Referenzpunkt nicht erreicht
UN    DB3.DBX 1.2;        // BM7010 Referenzieren aktiv
UN    DB3.DBX 1.6;        // BM7014 WZW abgebrochen
UN    DB3.DBX 5.3;        // BM7043 Sollstueckzahl erreicht
UN    DB3.DBX 2.6;        // BM7022 Spindelhalt manuell
UN    DB100.DBX 136.7;    // Anforderung Teststop
UN    DB2.DBX 7.7;        // FM6063 Schwenkarm nicht in Grundstellung
UN    DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
U(    ;
ON    M 111.7;            // Programm laeuft
O     M 111.6;            // Programm unterbrochen
O     DB20.DBX 325.1;     // Stop durch SBL
)     ;
=     M 148.0 ;           // SUM  NC-STARTFREIGABE
U     M 148.2;            // NC-Start gesamt
U     M 148.0 ;           // SUM  NC-STARTFREIGABE
=     DB20.DBX 3.6;       // NC-Start Anforderung von PLC
S     M 30.0;             // HM durch PLC gestartet
U     M 138.0;            // Fl. Reset
R     M 30.0;             // HM durch PLC gestartet
NETWORK
TITLE = NC-START/STARTVERR.
O     DB20.DBX 0.2;       // NC-Start Anforderung von Oberfläche
U(
O     DB20.DBX 326.4;     // BETRIEBSART AUTOMATIK
O     DB20.DBX 326.2;     // BETRIEBSART MDI
)
U     M 148.0 ;           // SUM  NC-STARTFREIGABE
=     DB20.DBX 0.2;       // FC NC-START
END_FUNCTION
//**************************************************************************
FUNCTION FC 85 : VOID
TITLE=PLC_NC_TRANSFER
NAME: PLC_NC_T
BEGIN
NETWORK
TITLE = Hilfsmerker Einlesesperre
L     MW 146;             // Merkerwort Einlesesperren
L     0;
==I   ;
=     M 177.5;            // HM Einlesefreigabe
NETWORK
TITLE = Einlesesperre FM
L     DB2.DBB 1;
L     0;
==I   ;
=     DB100.DBX900.0;     // HM EFG Fehlermeldungen
NETWORK
TITLE = Einlesesperre BM
L     DB3.DBB0;
L     0;
==I   ;
=     DB100.DBX900.1;     // HM EFG Betriebsmeldungen
NETWORK
TITLE = Einlesesperre DB100
L     DB100.DBB1;         // Datenbyte VSH/EFG
L     0;
==I   ;
=     DB100.DBX900.2;     // HM EFG DB100.DBB1
NETWORK
TITLE = Einlesesperre
U(    ;
O(    ;
U     DB100.DBX900.0;     // HM EFG Fehlermeldungen
U     DB100.DBX900.1;     // HM EFG Betriebsmeldungen
U     DB100.DBX900.2;     // HM EFG DB100.DBB1
U     M 177.5;            // HM Einlesesperre
)     ;
O(    ;
U     M 107.4;            // Taste Aux On
U     M 155.5;            // BA JOG
)     ;
O     M 155.2;            // BA REF (ohne EFG ist referenzieren nicht moeglich)
)     ;
UN    M 70.0;             // autom. Offsetabgleich aktiv
=     M 114.0     // HM EFG Kanal 0
U     M 114.0     // HM EFG Kanal 0
UN    M 9.2;              // EINLESEFREIGABE WEGNEHMEN (VON 4.5.RUNDACHSE)
=     DB20.DBX 340.3;     // EFG Kanal 0
NETWORK
TITLE = Zeitueberw. Einlesefreigabe
UN    M 114.0     // HM EFG Kanal 0
;      UN    DB20.DBX 340.3;     // EFG Kanal 0
UN    M 146.3;            // VSH/EFG WZW
U     M 111.7;            // Programm laeuft
UN    DB3.DBX 0.4;        // BM7004 Vorschubhalt man.
UN    DB3.DBX 2.6;        // BM7022 Spindelhalt man.
U     M 110.7;            // 1Hz
ZV    Z 24;
U     M 114.0     // HM EFG Kanal 0
;      U     DB20.DBX 340.3;     // EFG Kanal 0
R     Z 24;
L     Z 24;
T     DB100.DBW 310;
NETWORK
TITLE = FM6055 Zeitueberw. Einlesesperre
L     DB100.DBW 310;      // Zaehlerstand
L     DB10.DBW 8;         // USER_DATA_INT[4] Zeitueberw. EFG
==I   ;
S     DB2.DBX 6.7;        // FM6055 Zeitueberw. Einlesesperre
=     M 143.1;            // NC-Stop Zeitueberw. EFG
R     Z 24;
O     M 138.0;            // Fl. Reset-Taste
O     DB1.DBX 1370.2;     // Alarm-Quittiertaste gedrueckt
R     DB2.DBX 6.7;        // FM6055 Zeitueberw. Einlesesperre
NETWORK
TITLE = Unbedingtes VSH Tippen BA Auto
U     M 155.1;            // BA Auto
UN    M 137.7;            // Antriebe ein
UN    M 104.6;            // NC-Start Taste
UN    M 201.5;            // NC-Start Taste extern
=     M 143.4;            // Unbedingtes VSH Tippen BA Auto
NETWORK
TITLE = Flanke Taste 1x
U     M 101.7;            // Taste 1x
FP    M 153.3;            // Flanke Taste 1x
NETWORK
TITLE = 1x aktiv setzen
O(    ;
U     M 153.3;            // Flanke Taste 1x
UN    M 155.7;            // 1x aktiv
)     ;
O(    ;
UN    M 5.3;              // STV/LM aktiv
UN    M 5.4;              // Handling aktiv
)     ;
O     DB3.DBX 5.3;        // BM 7043 Sollstueckzahl erreicht
O     M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF
O     DB1.DBX 1383.0;     // DNC 1x ein
S     M 155.7;            // 1x aktiv
S     M 176.7;            // HM 1x
NETWORK
TITLE = 1x aktiv rueksetzen
U     M 153.3;            // Flanke Taste 1x
UN    M 176.7;            // HM 1x
O     DB1.DBX 1382.7;     // DNC 1x aus
R     M 155.7;            // 1x aktiv
NETWORK
TITLE = Programmiertes 1x ruecksetzen
U     M 110.1;            // "1"
R     M 176.7;            // HM 1x
NETWORK
TITLE = BM7064 Einzelstueckbetrieb
U     M 153.3;            // Flanke Taste 1x
U     M 155.7;            // 1x aktiv
U(    ;
O     M 5.3;              // STV/LM aktiv
O     M 5.4;              // Handling aktiv
)     ;
S     DB3.DBX 8.0;        // BM7064 Einzelstueck
R     DB3.DBX 8.1;        // BM7065 Dauerbetrieb
NETWORK
TITLE = BM7065 Dauerbetrieb
U(    ;
O     M 153.3;            // Flanke Taste 1x
O(    ;
U     M 154.3;            // Flanke Taste NC-Start
UN    DB20.DBX 324.7;     // SBL aktiv
U     M 155.1;            // BA Auto
)     ;
)     ;
UN    M 155.7;            // 1x aktiv
S     DB3.DBX 8.1;        // BM7065 Dauerbetrieb
R     DB3.DBX 8.0;        // BM7064 Einzelstueck
NETWORK
TITLE =5 sek
U(    ;
O     DB3.DBX 8.0;        // BM7064
O     DB3.DBX 8.1;        // BM7065
)     ;
UN    M 153.3;
L     S5T#5S;
SE    T 51;
NETWORK
TITLE = BM7064, BM7065 ruecksetzen
O     T 51;
ON    DB10.DBX 48.0;      // USER_DATA_HEX[8]: 1x Meldg. anzeigen
R     DB3.DBX 8.0;
R     DB3.DBX 8.1;
NETWORK
TITLE = LED 1x-Taste
U     M 155.7;            // 1x aktiv
=     DB20.DBX 847.3;     // LED 1x
=     DB1.DBX 1390.3;     // Rueckmeldung 1x aktiv
NETWORK
TITLE = DRY
;      U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
;      SPB   M171;
;      U     M 100.6;            // DRY Taste
;      UN    M  10.0;            // HM Fl. DRY
U     DB20.DBX 1.0;       // DRY Toggle
UN    M 111.5;            // M00/M01 aktiv
UN    M 111.6;            // Programm unterbrochen
UN    M 111.7;            // Programm laeuft
=     DB20.DBX 1.0;       // DRY Toggle
;      U     M 100.6;            // DRY Taste
;      =     M  10.0;            // HM Fl. DRY
;M171: NOP   0;
U     DB20.DBX 324.2;     // DRY aktiv
=     DB20.DBX 846.1;     // LED DRY-Taste
NETWORK
TITLE = SKIP
;      U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
;      SPB   M181;
;      U     M 100.7;            // SKIP Taste
;      UN    M 10.1;             // HM Fl. SKIP
;      =     DB20.DBX 2.6;       // SKIP Toggle
;      U     M 100.7;            // SKIP Taste
;      =     M 10.1;             // HM Fl. SKIP
;M181: NOP   0;
U     DB20.DBX 324.6;     // SKIP aktiv
=     DB20.DBX 846.0;     // LED SKIP-Taste
NETWORK
TITLE = M01
;      U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
;      SPB   M191;
;      U     M 101.6;            // OPT Taste
;      UN    M 10.2;             // HM Fl. OPT
;      S     DB20.DBX 3.1;       // OPT Toggle
;      U     M 101.6;            // OPT Taste
;      =     M 10.2;             // HM Fl. OPT
;M191: NOP   0;
U     DB20.DBX 324.5;     // OPT aktiv
=     DB20.DBX 847.4;     // LED OPT-Taste
NETWORK
TITLE = SBL
;      U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
;      SPB   M201;
;      U     M 102.6;            // SBL Taste
;      UN    M 10.3;             // HM Fl. SKIP
;      S     DB20.DBX 0.5;       // SBL Toggle
;      U     M 102.6;            // SBL Taste
;      =     M 10.3;             // HM Fl. SKIP
;M201: NOP   0;
U     DB20.DBX 324.7;     // SBL aktiv
=     DB20.DBX 848.7;     // LED SBL-Taste
NETWORK
TITLE =PRT
// nicht bei PC-Maschine
NETWORK
TITLE =WKS/MKS
// nicht bei PC-Maschine
NETWORK
TITLE = Achsfreigabe Kanal 0
UN    DB100.DBX 22.0;     // VSH unbed. Tippen BA AUTO
UN    M 143.4;            // VSH unbedingt tippen
UN    M 143.5;            // VSH WZW keine Grst.
UN    M 146.3;            // VSH/EFG WZW schwenkt
U     M 137.7;            // Antriebe ein
U     M 114.0     // HM EFG Kanal 0
;      U     DB20.DBX 340.3;     // EFG Kanal 0
U     M 137.7;            // Antriebe ein
=     DB20.DBX 340.4;     // Achsfreigabe Kanal 0
NETWORK
TITLE = Spindelfreigabe Kanal 0, 1
U     A 0.3;              // RFG Spindel 611U
UN    M 110.4;            // Spindel im Rundachsbetrieb
UN    M 165.0;            // M19 aktiv
UN    M 146.3;            // VSH/EFG WZW aktiv
UN    DB20.DBX 328.0;     // Rundachsumschaltung von AC angefordert
=     DB20.DBX 340.5;     // Spindelfreigabe K0 Spindel 0
U     M 110.0;            // "0"
=     DB25.DBX 340.5;     // Spindelfreigabe K1 Spindel 0
U     M 110.4;            // Spindel im Rundachsbetrieb
R     DB20.DBX 328.0;     // Rundachsumschaltung von AC angefordert
NETWORK
TITLE = Vorschubhalt Achse 5  (A1)
;      U     DB10.DBX 42.0;      // USER_DATA_HEX[2] RTA angew.
;      U     DB20.DBX 340.4;     // Achsfreigabe Kanal 0
;      ON    M 202.2;            // Tuer zu und verriegelt
;      =     M 121.5;
NETWORK
TITLE = Reset
// Reset-Taste nur als Flanke anlegen (sonst Kommandopuffer-Ueberlauf!!)
U     DB20.DBX 828.6;     // Reset-Taste MSTT
FP    M 138.0;            // Fl. Reset-Taste
//      O     M 136.1;            // Flanke Aux On
O     M 138.0;            // Fl. Reset-Taste
O(    ;
UN    M 202.2;            // Tuer zu und verriegelt
U     M 138.7;            // HM Fl. Tuer
U     M 137.1;            // Einrichtebetriebsart
U     M 136.6;            // Spindel laeuft
UN    M 110.4;            // Spindel ist Rundachse
)     ;
=     DB20.DBX 4.0;       // Reset ueber Oberflaeche anfordern K0
=     DB25.DBX 4.0;       // Reset ueber Oberflaeche anfordern K1
U     M 202.2;            // Tuer zu und verriegelt
=     M 138.7;            // HM Fl. Tuer
NETWORK
TITLE = Not-Aus Test nach Einschalten
U     M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF
UN    DB10.DBX 41.7;      // USER_DATA_HEX[1]: kein Not-Aus Test
S     M 138.6;            // Not-Aus Test angefordert
UN    E 2.4;              // Not Aus Taste
U     E 2.5;              // Ueberw. Not-Aus Platine
UN    M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF
R     M 138.6;            // Not-Aus Test angefordert
NETWORK
TITLE = Not Aus
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 0.0;        // FM6000 Not-Aus
U     E 2.5;              // Ueberw. Not-Aus Platine
UN    DB100.DBX 169.0;    // Teststop aktiv
UN    M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF
ON    E 2.4;              // Not Aus Taste
O     M 138.6;            // Not-Aus Test angefordert
S     DB2.DBX 0.0;        // FM6000 Not-Aus
U     DB2.DBX 0.0;        // FM6000 Not-Aus
=     DB1.DBX 1390.5;     // Rueckmeldung Not-Aus
NETWORK
TITLE = Takt 2Hz bilden
UN    M 20.5;             // HM 1Hz
L     S5T#500MS;
SE    T 20;               // Zeit Takt High
U     T 20;
S     M 20.5;
U     M 20.5;
L     S5T#500MS;          // Zeit Takt Low
SE    T 21;
U     T 21;
R     M 20.5;
NETWORK
TITLE = Flanke 2Hz
U     M 110.7;            // Takt 1Hz
UN    M 20.5;             // HM 1Hz
O     ;
UN    M 110.7;
U     M 20.5;
=     M 137.4;            // Flanke 2Hz
NETWORK
TITLE = HM 2Hz
U     M 20.5;             // HM 1Hz
=     M 110.7;            // Takt 1Hz
NETWORK
TITLE =Programm laeuft ausschaltverzoegert
U     M 111.7;            // Programm laeuft
L     S5T#500MS;
SA    T 88;
END_FUNCTION
//**************************************************************************
FUNCTION FC 88 : VOID
TITLE=SPM_1
NAME: SPM_1
BEGIN
NETWORK
TITLE =Abbruchkriterium
UN    M 5.0;
UN    M 5.1;
ON    DB10.DBX 44.5;      // USER_DATA_HEX[4]: SPM 1 akt.
R     M 167.6;            // SPM 1 offen
R     M 168.5;            // HM M25 stat.
R     M 168.6;            // HM M26 stat.
R     DB3.DBX 2.0;        // BM7016 "Spannmittel 1 offen"
R     M 145.1;            // Verr.Start SPM 1
R     DB3.DBX 4.0;        // BM7032 "SPM 1 in Zwischenstellung"
R     DB2.DBX 4.3;        // FM6035 "Druckueberw. SPM 1"
R     DB3.DBX 4.4;        // BM7036 "Justierung DS moeglich"
R     M 167.4;            // HM DS Justierung auf
R     M 167.5;            // HM DS Justierung zu
R     M 146.4;            // EFG/VSH SPM 1
BEB   ;
U     M 110.1;            // "1"
=     DB20.DBX 382.1;     // autom. SPM vorhanden
NETWORK
TITLE = Spannmittel einfachwirkend
UN    DB10.DBX 45.5;      // USER_DATA_HEX[5]: SPM1 nur 1 DS
SPB   M_1F;
UN    E 6.6;              // DS SPM 1 zu
=     E 6.7;              // DS SPM 2 offen
U     M 5.1;              // Innen SPM 1
S     M 5.0;              // Aussen SPM 1
R     M 5.1;              // Innen SPM 1
M_1F: NOP   0;
NETWORK
TITLE = Spannmittel Tasten
U     E 3.5;              // Fusstaste SPM auf
UN    DB10.DBX 43.1;      // Fusstaster SPM 1 2fach
U     DB10.DBX 43.3;      // Fusstaster SPM 1 1fach
O     M 106.4;            // Taste SPM 1 auf/zu
=     M 167.0;            // HM Taste SPM 1
NETWORK
TITLE = M25 Spannmittel 1 setzen
O(    ;
U     DB20.DBX 195.1;     // M25 dyn.
U     M 111.7;            // Programm laeuft
//      UN    DB21.DBX 33.7;      // PRT aktiv
)     ;
O(    ;
U     E 3.5;              // Fusstaste SPM auf
UN    E 3.6;              // Fusstaste SPM zu
UN    T 88;               // Progr. laeuft verz.
U     DB10.DBX 43.1;      // Fusstaster SPM 1 2fach
)     ;
O(    ;
U     DB20.DBX 302.1;     // DNC SPM oeffnen
UN    T 88;               // Progr. laeuft verz.
)     ;
O     M 190.4;            // Rob. SPM auf
S     M 168.5;            // M25 stat.
R     M 168.6;            // M26 stat.
NETWORK
TITLE = M26 Spannmittel 1 setzen
O(    ;
U     DB20.DBX 195.2;     // M26 dyn.
U     M 111.7;            // Programm laeuft
//      UN    DB21.DBX 33.7;      // PRT aktiv
)     ;
O(    ;
U     E 3.6;              // Fusstaste SPM zu
UN    E 3.5;              // Fusstaste SPM auf
UN    T 88;               // Progr. laeuft verz.
U     DB10.DBX 43.1;      // Fusstaster SPM 1 2fach
)     ;
O(    ;
U     DB20.DBX 302.2;     // DNC SPM schliessen
UN    T 88;               // Progr. laeuft verz.
)     ;
O     M 190.5;            // Rob. SPM zu
S     M 168.6;            // M26 stat.
R     M 168.5;            // M25 stat.
NETWORK
TITLE = M25 + M26 ruecksetzen
O(    ;
UN    T 88;               // Progr. laeuft verz.
U     M 167.0;            // HM Taste SPM 1
)     ;
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
R     M 168.5;            // M25 stat.
R     M 168.6;            // M26 stat.
NETWORK
TITLE = HM SPM zu AUTOMATIK
O(    ;
U     M 5.1;              // Innen SPM 1
U     M 168.5;            // M25 stat.
)     ;
O(    ;
U     M 5.0;              // Aussen SPM 1
U     M 168.6;            // M26 stat.
)     ;
=     M 171.4;            // HM SPM 1 zu AUTOM.
NETWORK
TITLE = HM SPM auf AUTOMATIK
O(    ;
U     M 5.1;              // Innen SPM 1
U     M 168.6;            // M26 stat.
)     ;
O(    ;
U     M 5.0;              // Aussen SPM 1
U     M 168.5;            // M25 stat.
)     ;
=     M 171.5;            // HM SPM 1 auf AUTOM.
NETWORK
TITLE = Flanke Taste Spannmittel 1
U     M 167.0;            // HM Taste SPM 1
FP    M 167.2;
U     M 167.2;
UN    T 88;               // Progr. laeuft verz.
=     M 167.3;            // Fl. Taste SPM
NETWORK
TITLE = HM DSB
U     T 80;               // HM DS SPM 1 zu
S     M 171.0;            // HM DS B
R     M 171.1;            // HM DS A
NETWORK
TITLE = HM DSA
U     T 81;               // HM DS SPM 1 auf
S     M 171.1;            // HM DS A
R     M 171.0;            // HM DS B
NETWORK
TITLE = HM A
U     M 167.3;            // Fl. Taste SPM
UN    T 80;               // HZ DS SPM 1 zu
S     M 171.2;            // HM A
U     M 167.3;            // Fl. Taste SPM
U     T 80;
R     M 171.2;
NETWORK
TITLE = HM B
U     M 167.3;            // Flanke Taste SPM 1
UN    T 81;               // HM DS SPM 1 auf
S     M 171.3;            // HM B
U     M 167.3;
U     T 81;
R     M 171.3;
NETWORK
TITLE =BM7036 "JUSTIERUNG der DS fuer Spannmittel 1 moeglich"
U     M 202.2;            // Tuer zu und verriegelt
U     M 155.5;            // BA JOG
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
U     M 107.4;            // Aux-On Taste
U     M 106.4;            // Taste SPM 1 auf/zu
U     M 106.3;            // Taste 100%
U     M 136.0;            // AUX-ON
S     DB3.DBX 4.4;        // BM7036
NETWORK
TITLE =BM7036 ruecksetzten
ON    M 136.0;            // AUX-ON
ON    DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
ON    M 155.5;            // BA JOG
ON    M 202.2;            // Tuer zu und verriegelt
R     DB3.DBX 4.4;        // BM7036
NETWORK
TITLE =HM Ventil zu DS-Justierung Spannmittel 1
U     DB3.DBX 4.4;        // BM7036
U     M 167.3;            // Fl. SPM Taste
UN    M 167.4;            // HM DS Justierung auf
S     M 167.4;            // HM DS Justierung auf
=     M 168.0;            // HM
NETWORK
TITLE =HM Ventil RS
U     DB3.DBX 4.4;        // BM7036
U     M 167.3;            // Fl. SPM Taste
U     M 167.4;            // HM DS Justierung auf
UN    M 168.0;            // HM
R     M 167.4;            // HM DS Justierung auf
NETWORK
TITLE =HM Ventil auf DS-Justierung Spannmittel 1
UN    M 167.4;            // HM DS Justierung auf
=     M 167.5;            // HM DS Justierung zu
NETWORK
TITLE =HM Ventil ruecksetzten
UN    DB3.DBX 4.4;        // BM7036
R     M 167.4;            // HM DS Justierung auf
R     M 167.5;            // HM DS Justierung zu
NETWORK
TITLE = HM Ventil Spannmittel 1 zu
U(    ;
UN    M 171.1;            // HM DS A
UN    M 171.0;            // HM DA B
U     M 167.0;            // HM Taste SPM
UN    T 88;               // Progr. laeuft verz.
O     ;
UN    T 80;               // HM DS SPM links zu
U     M 167.0;            // HM Taste SPM
U     M 171.1;            // HM DS A
UN    M 171.7;            // HM Ventil SPM links auf
UN    T 88;               // Progr. laeuft verz.
O     ;
U     T 80;               // HM DS SPM 1 zu
U     M 171.2;            // HM A
)     ;
U     M 136.0;            // AUX-ON
UN    M 136.6;            // Sp. laeuft
O     ;
U     M 171.4;            // HM SPM 1 zu Autom.
UN    M 136.6;            // Sp. laeuft
=     M 168.3;
NETWORK
TITLE =HM Ventil Spannmittel 1 zu
O     M 167.4;            // HM DS Justierung auf
O(    ;
UN    DB3.DBX 4.4;        // BM7036
U     M 168.3;            // HM HM Ventil SPM 1 zu
)     ;
O(    ;
UN    M 136.0;            // AUX-ON
U     M 107.6;            // Taste AUX-OFF
U     M 106.4;            // Taste SPM 1 auf/zu
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
)     ;
=     M 171.6;            // HM Ventil SPM links zu
NETWORK
TITLE = HM Ventil Spannmittel 1 auf
U(    ;
UN    T 81;               // HM DS SPM 1 auf
U     M 171.0;            // HM DS B
U     M 167.0;            // HM Taste SPM 1
UN    M 171.6;            // HM Ventil SPM 1 zu
UN    T 88;               // verz.Programm laeuft
UN    M 136.6;            // Sp.laeuft
O     ;
U     T 81;               // HZ DS SPM 1 auf
U     M 171.3;            // HM DS SPM 1 auf
UN    M 136.6;            // Sp. laeuft
)     ;
U     M 136.0;            // AUX-ON
O     ;
U     M 171.5;            // HM SPM 1 auf autom.
UN    M 136.6;            // Sp. laeuft
=     M 168.4;
NETWORK
TITLE =HM Ventil Spannmittel 1 auf
O     M 167.5;            // HM DS Justierung zu
O     ;
UN    DB3.DBX 4.4;        // BM7036
U     M 168.4;            // HM HM Ventil SPM 1 auf
O     ;
UN    M 136.0;            // AUX-ON
U     M 107.6;            // Taste AUX-OFF
U     M 106.4;            // Taste SPM 1 auf/zu
U     DB20.DBX 833.0;     // Schluesselschalter Hand/Auto
=     M 171.7;            // HM Ventil SPM links auf
NETWORK
TITLE =BM7032 "Spannmittel 1 in Zwischenstellung"
UN    T 80;               // HM DS SPM links zu
UN    T 81;               // HM DS SPM links auf
=     DB3.DBX 4.0;        // BM7032
NETWORK
TITLE =FM6035 "Druckueberwachung Spannmittel 1"
U     M 111.7;            // Programm laeuft
U     M 137.7;            // Antriebe ein
U     M 136.6;            // Sp. laeuft
UN    M 146.3;            // VSH/EFG WZW
U     M 145.1;            // Verr. Start SPM 1
S     DB2.DBX 4.3;        // FM6035
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 4.3;
NETWORK
TITLE = HM DS Spannmittel 1 zu
U     E 6.6;              // DS SPM 1 zu
L     S5T#1S;
SE    T 80;               // HZ DS SPM 1 zu
U     T 80;               // HZ DS SPM 1 zu
UN    M 167.6;            // HM SPM 1 offen
=     DB20.DBX 310.2;     // Rueckmeldung SPM zu
NETWORK
TITLE = HM DS Spannmittel 1 offen
U     E 6.7;              // DS SPM 1 offen
L     S5T#1S;
SE    T 81;               // HZ DS SPM 1 offen
NETWORK
TITLE = Spannmittel 1 offen
U     M 5.1;              // Aussen SPM 1
UN    T 81;               // HZ DS SPM 1 offen
O     ;
U     M 5.0;              // Innen SPM 1
UN    T 80;               // HZ DS SPM 1 zu
=     M 167.6;            // HM SPM 1 offen
=     DB20.DBX 310.1;     // Rueckmeldung SPM offen
NETWORK
TITLE = Verriegelung Start Spannmittel 1 offen
U     M 167.6;            // HM SPM 1 offen
=     M 145.1;            // Verr. Start SPM 1
NETWORK
TITLE =BM7016 "Spannmittel 1 offen"
U     M 137.2;            // Fl. NC-Start
S     DB3.DBX 2.0;        // BM7016
U(    ;
ON    M 145.1;            // Verr. Start SPM 1
O     M 102.7;            // Reset Taste
)     ;
R     DB3.DBX 2.0;
NETWORK
TITLE = VSH/EFG Spannmittel 1
U(    ;
O     M 155.1;            // BA AUTO
O     M 155.4;            // BA MDA
)     ;
U(    ;
U     M 168.5;            // M25 stat.
UN    M 167.6;            // SPM 1 offen
O     ;
U     M 168.6;            // M26 stat.
U     M 167.6;            // SPM 1 offen
O     DB3.DBX 4.0;        // BM7032 SPM 1 in Zwischenstellung
)     ;
=     M 146.4;            // VSH/EFG SPM 1
NETWORK
TITLE =Ventil Spannmittel 1 auf
U     DB10.DBX 44.5;      // USER_DATA_HEX[4]: SPM 1 aktiv
U     M 171.7;            // HM Ventil SPM 1 auf
=     A 9.5;              // MV SPM 1 auf
NETWORK
TITLE =Ventil Spannmittel 1 zu
U     DB10.DBX 44.5;      // USER_DATA_HEX[4]: SPM 1 aktiv
U     M 171.6;            // HM Ventil SPM 1 zu
=     A 9.4;              // MV SPM 1 zu
NETWORK
TITLE =LED SPM-Taste
UN    M 167.6;            // HM SPM 1 offen
=     DB20.DBX 850.0;     // LED SPM 1
END_FUNCTION
//**************************************************************************
FUNCTION FC 109 : VOID
TITLE=FESTO_ROBOTIK
NAME: F_ROBOT
BEGIN
NETWORK
TITLE = Abbruch
U     M 191.0;            // Robotik aktiv
SPB   M011;
L     0;
T     MB190;              // Steuersignale Robotik
T     MW192;              // Tuersignale Robotik
M011: NOP   0;
NETWORK
TITLE = ACC Eingaenge uebernehmen (F50180)
U     DB10.DBX 48.1;      // USER_DATA_HEX[8]=2  ACC Robotik-Interface
UN    DB10.DBX 48.2;      // USER_DATA_HEX[8]=4  USB Robotik-Interface
SPBN    M021;
U     E 9.7;              // SS Handling aktiv
=     M 191.4;            // SS Handling aktiv
UN    M 191.0;            // Robotik aktiv
SPB   M021;
U     E 3.1;              // Robotik Aux-On
UN    M 136.0;            // AUX-ON
=     M 190.0;            // Rob. Aux-On
U     E 3.2;              // Robotik Ref/Auto
FP    M 190.1;            // Fl. Ref/Auto
U     E 3.3;              // Robotik Ref. Start
FP    M 193.7;            // HM Fl. Ref. Start
U     M 193.7;            // HM Fl. Ref. Start
U     M 155.2;            // BA REF
S     M 192.0;            // Ref.-Ablauf Robotik
U     E 3.4;              // Robotik Pr. Start
FP    M 193.6;            // Fl. Pr. Start
U     M 193.6;            // Fl. Pr. Start
U     M 155.1;            // BA AUTO
=     M 190.3;            // NC-Start Robotik
U     E 3.5;              // Robotik SPM auf
UN    E 3.6;              // Robotik SPM zu
=     M 190.4;            // SPM auf Robotik
U     E 3.6;              // Robotik SPM zu
UN    E 3.5;              // Robotik SPM auf
=     M 190.5;            // SPM zu Robotik
U     E 3.7;              // Robotik Tuer zu
UN    E 9.6;              // Robotik Tuer auf
O     M 192.7;            // Ref. Tuer zu
=     M 190.6;            // Tuer zu Robotik
U     E 9.6;              // Robotik Tuer auf
UN    E 3.7;              // Robotik Tuer zu
O     M 192.6;            // Ref. Tuer auf
=     M 190.7;            // Tuer auf Robotik
M021: NOP   0;
NETWORK
TITLE = USB-SPS Eingaenge uebernehmen
U     DB10.DBX 48.2;      // USER_DATA_HEX[8]=4  USB Robotik-Interface
UN    DB10.DBX 48.1;      // USER_DATA_HEX[8]=2  ACC Robotik-Interface
SPBN    M031;
U     DB1.DBX 1608.0;     // Robotik Aux-On
UN    M 136.0;            // AUX-ON
=     M 190.0;            // Rob. Aux-On
U     DB1.DBX 1608.1;     // Robotik Ref/Auto
FP    M 190.1;            // Fl. Ref/Auto
U     DB1.DBX 1608.2;     // Robotik Ref. Start
FP    M 193.7;            // HM Fl. Ref. Start
U     M 193.7;            // HM Fl. Ref. Start
U     M 155.2;            // BA REF
S     M 192.0;            // Ref.-Ablauf Robotik
U     DB1.DBX 1608.3;     // Robotik Pr. Start
FP    M 193.6;            // Fl. Pr. Start
U     M 193.6;            // Fl. Pr. Start
U     M 155.1;            // BA AUTO
=     M 190.3;            // NC-Start Robotik
U     DB1.DBX 1608.4;     // Robotik SPM auf
UN    DB1.DBX 1608.5;     // Robotik SPM zu
=     M 190.4;            // SPM auf Robotik
U     DB1.DBX 1608.5;     // Robotik SPM zu
UN    DB1.DBX 1608.4;     // Robotik SPM auf
=     M 190.5;            // SPM zu Robotik
U     DB1.DBX 1608.6;     // Robotik Pinole vor
UN    DB1.DBX 1608.7;     // Robotik Pinole zurueck
=     M 193.2;            // Pinole vor Robotik
U     DB1.DBX 1608.7;     // Robotik Pinole zurueck
UN    DB1.DBX 1608.6;     // Robotik Pinole vor
=     M 193.3;            // Pinole zurueck Robotik
U     DB1.DBX 1609.1;     // Robotik Tuer zu
UN    DB1.DBX 1609.0;     // Robotik Tuer auf
O     M 192.7;            // Ref. Tuer zu
=     M 190.6;            // Tuer zu Robotik
U     DB1.DBX 1609.0;     // Robotik Tuer auf
UN    DB1.DBX 1609.1;     // Robotik Tuer zu
O     M 192.6;            // Ref. Tuer auf
=     M 190.7;            // Tuer auf Robotik
M031: NOP   0;
NETWORK
TITLE = Robotik Signale bilden
U     E 6.5;              // Tuer offen
O(    ;
UN    DB10.DBX 41.1;      // Option Tuerautomatik
UN    M 202.2;            // Tuer zu und verriegelt
)     ;
=     M 213.5;            // Robotik Tuer offen
U     M 202.2;            // Tuer zu und verriegelt
UN    DB100.DBX 136.7;    // Anforderung Teststop
=     M 213.4;            // Robotik Tuer zu
U     M 155.2;            // BA REF
=     M 212.6;            // Robotik Ref-Mode
U     T 81;               // verz. SPM offen
U     M 167.6;            // HM SPM nicht gespannt
=     M 213.0;            // Robotik SPM offen
U     T 80;               // verz. SPM zu
UN    M 167.6;            // HM SPM nicht gespannt
=     M 213.1;            // Robotik SPM geschlossen
U     T 84;               // Rueckmeldung Pinole gespannt
=     M 213.2;            // Robotik Pinole Teil gespannt
U     T 85;               // Rueckmeldung Pinole hinten
=     M 213.3;            // Robotik Pinole hinten
NETWORK
TITLE = ACC Ausgaenge zuweisen (F50180)
U     DB10.DBX 48.1;      // USER_DATA_HEX[8]=2  ACC Robotik-Interface
UN    DB10.DBX 48.2;      // USER_DATA_HEX[8]=4  USB Robotik-Interface
SPBN    M041;
U     M 213.0;            // Robotik SPM offen
U     M 191.0;            // Robotik aktiv
=     A 8.3;
U     M 213.1;            // Robotik SPM geschlossen
U     M 191.0;            // Robotik aktiv
=     A 8.4;
U     M 213.4;            // Robotik Tuer zu
U     M 191.0;            // Robotik aktiv
=     A 8.1;
U     M 213.5;            // Robotik Tuer offen
U     M 191.0;            // Robotik aktiv
=     A 8.0;
U     M 213.6;            // Robotik Maschine bereit
U     M 191.0;            // Robotik aktiv
=     A 8.5;
U     M 213.7;            // Robotik Programmstatus
U     M 191.0;            // Robotik aktiv
=     A 8.6;
U     M 212.6;            // Robotik Ref-Mode
U     M 191.0;            // Robotik aktiv
=     A 8.2;
U     M 212.7;            // Robotik Maschine referenziert
U     M 191.0;            // Robotik aktiv
=     A 8.7;
M041: NOP   0;
NETWORK
TITLE = USB-SPS Ausgaenge zuweisen
// V2.22: Adressen korrigiert (lt. Ronny)
U     DB10.DBX 48.2;      // USER_DATA_HEX[8]=4  USB Robotik-Interface
UN    DB10.DBX 48.1;      // USER_DATA_HEX[8]=2  ACC Robotik-Interface
SPBN  M051;
U     M 213.0;            // Robotik SPM offen
U     M 191.0;            // Robotik aktiv
=     DB1.DBX 1636.2;
U     M 213.1;            // Robotik SPM geschlossen
U     M 191.0;            // Robotik aktiv
=     DB1.DBX 1636.3;
U     M 213.2;            // Robotik Pinole Teil gespannt
U     M 191.0;            // Robotik aktiv
=     DB1.DBX 1636.4;
U     M 213.3;            // Robotik Pinole hinten
U     M 191.0;            // Robotik aktiv
=     DB1.DBX 1636.5;
U     M 213.4;            // Robotik Tuer zu
U     M 191.0;            // Robotik aktiv
=     DB1.DBX 1636.6;
U     M 213.5;            // Robotik Tuer offen
U     M 191.0;            // Robotik aktiv
=     DB1.DBX 1636.7;
U     M 213.6;            // Robotik Maschine bereit
U     M 191.0;            // Robotik aktiv
=     DB1.DBX 1634.6;
U     M 213.7;            // Robotik Programmstatus
U     M 191.0;            // Robotik aktiv
=     DB1.DBX 1634.7;
U     M 212.6;            // Robotik Ref-Mode
U     M 191.0;            // Robotik aktiv
=     DB1.DBX 1637.0;
U     M 212.7;            // Robotik Maschine referenziert
U     M 191.0;            // Robotik aktiv
=     DB1.DBX 1637.1;
U     DB2.DBX 0.0;        // 6000  NOT-AUS AKTIV
=     DB1.DBX 1634.5      // NOT-AUS AKTIV USB ROBOTIK X2.6
O     DB2.DBX 0.0;        // 6000  NOT-AUS AKTIV
O     DB100.DBX 290.0;    // HM PLC-Alarm
O     DB100.DBX 170.7;    // SI Summenalarm
O     DB100.DBX 290.1;    // Warnl. rot progr.
O     DB1.DBX 1366.3;     // NC-Alarm steht an
=     DB1.DBX 1635.0      // Alarmausgang USB ROBOTIK X2.9
M051: NOP   0;
NETWORK
TITLE = Abbruch wenn Handling aus
UN    M 191.0;            // Robotik aktiv
BEB   ;
NETWORK
TITLE = Umschaltung BA REF/AUTO
U     M 190.1;            // Fl. Ref/Auto
UN    M 155.2;            // BA REF
=     M 191.6;            // Rob. BA Ref anwaehlen
U     M 190.1;            // Fl. Ref/Auto
U     M 155.2;            // BA REF
O     M 192.2;            // Rob. Ref. BA Auto anwaehlen
=     M 191.7;            // Rob. BA Auto anwaehlen
NETWORK
TITLE = Robotik Maschine bereit
U     M 136.0;            // AUX-ON
U     M 213.5;            // Robotik Tuer offen
U(    ;
O(    ;
U     DB10.DBX 44.5;      // USER_DATA_HEX[4]: SPM 1 akt.
U     M 213.0;            // Robotik SPM geloest
)     ;
O(    ;
UN    DB10.DBX 44.5;      // USER_DATA_HEX[4]: SPM 1 akt.
UN    M 213.0;            // Robotik SPM geloest
)     ;
)     ;
U(    ;
O(    ;
U     DB10.DBX 44.4;      // USER_DATA_HEX[4] RS angew.
U     M 213.3;            // Robotik Pinole hinten
)     ;
O(    ;
UN    DB10.DBX 44.4;      // USER_DATA_HEX[4] RS angew.
UN    M 213.3;            // Robotik Pinole hinten
)     ;
)     ;
U     M 212.7;            // Robotik Maschine referenziert
U     M 155.1;            // BA AUTO
U     M 155.7;            // 1x aktiv
=     M 213.6;            // Robotik Maschine bereit
NETWORK
TITLE = Robotik Status
U     DB20.DBX 224.1;     // M1 stat.
U     DB20.DBX 324.5;     // bed. Halt aktiv
O     DB20.DBX 224.0;     // M0 stat.
O     DB20.DBX 224.2;     // M2 stat.
O     DB20.DBX 227.6;     // M30 stat.
=     M 213.7;            // Robotik Programmstatus
NETWORK
TITLE = Robotik Beladeposition erreicht
U(    ;
L     DB10.DBD 72;        // USER_DATA_FLOAT[0]: Beladepos. X
L     +0.1E-003;          // 0.1mm
-R    ;
L     DB1.DBD 0;          // Istpos. X
<R    ;
)     ;
U(    ;
L     DB10.DBD 72;        // USER_DATA_FLOAT[0]: Beladepos. X
L     +0.1E-003;          // 0.1mm
+R    ;
L     DB1.DBD 0;          // Istpos. X
>R    ;
)     ;
=     M 191.1;            // X kollisionsfrei
U(    ;
L     DB10.DBD 76;        // USER_DATA_FLOAT[0]: Beladepos Y
L     +0.1E-003;          // 0.1mm
-R    ;
L     DB1.DBD 4;          // Istpos. Y
<R    ;
)     ;
U(    ;
L     DB10.DBD 76;        // USER_DATA_FLOAT[0]: Beladepos Y
L     +0.1E-003;          // 0.1mm
+R    ;
L     DB1.DBD 4;          // Istpos. Y
>R    ;
)     ;
=     M 191.2;            // Y kollisionsfrei
L     5.8E-01;            // 580mm
L     DB1.DBD 8;          // Istpos. Z
<=R    ;
=     M 191.3;            // Z kollisionsfrei
U     M 191.1;            // X kollisionsfrei
U     M 191.2;            // Y kollisionsfrei
U     M 191.3;            // Z kollisionsfrei
U     DB1.DBX 134.0;      // X: Ref. err.
U     DB1.DBX 134.1;      // Y: Ref. err.
U     DB1.DBX 134.2;      // Z: Ref. err.
UN    DB3.DBX 1.2;        // BM7010 autom. Ref. aktiv
UN    M 192.0;            // Ref.-Ablauf Robotik
=     M 212.7;            // Robotik Maschine referenziert
NETWORK
TITLE = Robotik Ref-Ablauf
UN    DB10.DBX 41.1;      // Option Tuerautomatik
SPB   NOTU;
UN    M 102.7;            // Reset Taste
U     M 136.0;            // AUX-ON
SPB   M081;
L     0;
T     MB 192;
M081: NOP   0;
U     M 192.0;            // Ref.-Ablauf Robotik
U     M 193.7;            // Fl. Ref. Start
S     M 192.6;            // Ref. Tuer auf
U     M 192.6;            // Ref. Tuer auf
U     E 6.3;              // Tuer offen
S     M 192.7;            // Ref. Tuer zu
R     M 192.6;            // Tuer auf
U     M 192.7;            // Ref. Tuer zu
U     M 192.0;            // Ref.-Ablauf Robotik
U     T 64;               // verz. Tuer zu
=     M 190.2;            // Rob. Ref. Start Achsen
U     M 190.2;            // Rob. Ref. Start Achsen
U     DB3.DBX 1.2;        // BM7010 autom. Ref. aktiv
S     M 192.1;            // Rob. Referenzieren aktiv
R     M 192.7;
U     M 192.1;            // Rob. Referenzieren aktiv
UN    M 137.3;            // Referenzpunkt nicht erreicht
=     M 192.2;            // Rob. Ref. BA Auto anwaehlen
R     M 192.0;
R     M 192.1;            // Rob. Referenzieren aktiv
NOTU: UN    M 102.7;            // Reset Taste
U     M 136.0;            // AUX-ON
SPB   M082;
L     0;
T     MW192;
M082: UN    DB10.DBX41.1;       // Option Tuerautomatik
U     M 192.0;            // Ref.-Ablauf Robotik
U     M 193.7;            // Fl. Ref. Start
S     M 190.2;            // Rob. Ref. Start Achsen
U     M 190.2;            // Rob. Ref. Start Achsen
U     DB3.DBX 1.2;        // BM7010 autom. Ref. aktiv
S     M 192.1;            // Rob. Referenzieren aktiv
U     M 192.1;            // Rob. Referenzieren aktiv
UN    M 137.3;            // Referenzpunkt nicht erreicht
=     M 192.2;            // Rob. Ref. BA Auto anwaehlen
R     M 192.0;
R     M 192.1;            // Rob. Referenzieren aktiv
END_FUNCTION
//**************************************************************************
FUNCTION FC 100 : VOID
TITLE=INT_10MS
NAME: INT_10MS
//VAR_TEMP
//  temp : BYTE ; //temp
//END_VAR
BEGIN
NETWORK
TITLE =Vorschuboverride
// VS-Override nur von MSTT zulassen (MF2 uebergibt neg. Wert)
L     DB20.DBW 152;       // VS-Override
L     0;
<I    ;                   // neg. Wert?
R     DB20.DBX 1.6;       // Kommande Feed-Override verhindern
L     DB25.DBW 152;       // VS-Override
L     0;
<I    ;                   // neg. Wert?
R     DB25.DBX 1.6;       // Kommande Feed-Override verhindern
NETWORK
TITLE = Override fuer Referenzieren
UN    DB20.DBX 1.6;       // Kommando Feed-Override
SPB   nsve;
L     DB20.DBW 152;       // VS-Override Kanal 0
T     DB100.DBW 152;      // HM VS-Override Kanal 0
nsve: NOP   0;
O     DB10.DBX 57.0;      // USER_DATA_HEX[17] IB-bit fuer Ref. mit Feedoverride
ON    M 155.2;            // BA Ref
SPB   REFI;
U     DB1.DBX 134.0;      // X: Ref. err.
U     DB1.DBX 134.1;      // Y: Ref. err.
U     DB1.DBX 134.2;      // Z: Ref. err.
U(    ;
ON    M 121.0;            // RTA angeschlossen
O     DB1.DBX 134.4;      // Achse 4 ist ref.
)     ;
FP    M 199.4;            // Flanke X/Z referenziert
U     DB1.DBX 134.0;      // X: Ref. err.
U     DB1.DBX 134.1;      // Y: Ref. err.
U     DB1.DBX 134.2;      // Z: Ref. err.
U(    ;
ON    M 121.0;            // RTA angeschlossen
O     DB1.DBX 134.4;      // Achse 4 ist ref.
)     ;
ON    M 136.0;            // AUX-ON
SPB   MMM;
L     DB20.DBW 152;       // VS-Override Kanal 0
L     20;                 // 2%
>I    ;
SPB   v100;
L     0;                  // 0%
T     DB20.DBW 152;       // VS-Override Kanal 0
T     DB25.DBW 152;       // VS-Override Kanal 1
SPA   vkdo;
v100: L     1000;
T     DB20.DBW 152;       // VS-Override Kanal 0
T     DB25.DBW 152;       // VS-Override Kanal 1
vkdo: U(    ;                   // V1.05: Aenderungsbit nur wenn notwendig
L     DB20.DBW 358;       // VS-Override K0 Ist
L     DB20.DBW 152;       // VS-Override K0 Kommando
<>I   ;
)     ;
=     DB20.DBX 1.6;       // Kommando Feed-Override
=     DB25.DBX 1.6;       // Kommando Feed-Override
MMM:  NOP   0;
UN    M 199.4;            // Flanke X/Z referenziert
U     M 136.0;            // AUX-ON
SPB   BBB;
L     DB100.DBW 152;      // HM VS-Override Kanal 0
T     DB20.DBW 152;       // VS-Override Kanal 0
T     DB25.DBW 152;       // VS-Override Kanal 1
U(    ;                   // V1.05: Aenderungsbit nur wenn notwendig
L     DB20.DBW 358;       // VS-Override K0 Ist
L     DB20.DBW 152;       // VS-Override K0 Kommando
<>I   ;
)     ;
=     DB20.DBX 1.6;       // Kommando Feed-Override
=     DB25.DBX 1.6;       // Kommando Feed-Override
BBB: NOP    0;
REFI:NOP    0;
NETWORK
TITLE = Override im Sonderbetrieb max. 100%
ON    M 210.7;            // Sonderbetriebsart
SPB   M041;
U(    ;
L     DB20.DBW 358;       // VS-Override K0 Ist
L     1000;               // 100%
<=I   ;
)     ;
O     DB100.DBX 168.0;    // SG-Alarm zulassen
SPB   M041;
L     1000;               // 100%
T     DB20.DBW 152;       // VS-Override Kanal 0
T     DB25.DBW 152;       // VS-Override Kanal 1
U     M 110.1;            // "1"
=     DB20.DBX 1.6;       // Kommando Feed-Override
=     DB25.DBX 1.6;       // Kommando Feed-Override
M041: NOP   0;
NETWORK
TITLE =Spindeloverride
// PC-Steuerung: Override an NC uebergeben
L     DB100.DBW 128;
L     10;
*I    ;                   // Wert in Promille umrechnen
T     DB20.DBW 154;       // Spindeloverride an NC Kanal 0
T     DB25.DBW 154;       // Spindeloverride an NC Kanal 1
O     DB100.DBX 127.1;    // Fl. Taste 100%
O     DB100.DBX 127.3;    // Fl. Taste -%
O     DB100.DBX 127.5;    // Fl. Taste +%
O     M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF OB1
=     DB20.DBX 1.7;       // Kommando Spindeloverride setzen
=     DB25.DBX 1.7;       // Kommando Spindeloverride setzen
END_FUNCTION
//**************************************************************************
FUNCTION FC 121 : VOID
TITLE=WZW_RILO
NAME: WZW_RILO
BEGIN
NETWORK
TITLE =T-Wort = 0
M101: L     0;
L     DB100.DBB 32;       // Magazin Sollposition
==I   ;
R     DB100.DBX 35.0;     // WZW Anforderung
BEB   ;
NETWORK
TITLE =T-Wort groesser
L     DB10.DBW 20;        // USER_DATA_INT[10]: Anzahl Magazinplaetze
L     DB100.DBB 32;       // Magazin Sollposition
<I    ;
R     DB100.DBX 35.0;     // WZW Anforderung
BEB   ;
NETWORK
TITLE =T-Wort = Istposition
L     DB100.DBB 29;       // Magazin Istpos. HW
L     DB100.DBB 32;       // Magazin Sollposition
==I   ;
U     DB100.DBX 35.6;     // WZW synchronisiert
R     DB100.DBX 35.0;     // WZW Anforderung
BEB   ;
NETWORK
TITLE =Wegberechnung
L     DB100.DBB 32;       // Magazin Sollposition
L     DB100.DBB 29;       // Magazin Istpos. HW
-I    ;
T     DB100.DBW 33;
L     DB10.DBW 20;        // USER_DATA_INT[10]: Anzahl Magazinplaetze
L     2;
/I    ;
L     DB100.DBW 33;
<I    ;
SPB   M001;
L     DB10.DBW 20;        // USER_DATA_INT[10]: Anzahl Magazinplaetze
L     -2;
/I    ;
L     DB100.DBW 33;
>I    ;
SPB   M002;
SPA   M003;
M001: L     DB100.DBW 33;
L     DB10.DBW 20;        // USER_DATA_INT[10]: Anzahl Magazinplaetze
-I    ;
T     DB100.DBW 33;
SPA   M003;
M002: L     DB10.DBW 20;        // USER_DATA_INT[10]: Anzahl Magazinplaetze
L     DB100.DBW 33;
+I    ;
T     DB100.DBW 33;
M003: NOP   0;
END_FUNCTION
//**************************************************************************
FUNCTION FC 126 : VOID
TITLE=MAG_ISTPOS
NAME: MAG_ISTP
BEGIN
NETWORK
TITLE =Flanke Strobe Position WZW
U     M 7.5;              // WZ-Halter in Position
FP    DB100.DBX 38.2;     // Fl. Strobe
U     DB100.DBX 38.2;     // Fl. Strobe
U     DB100.DBX 35.6;     // WZW synchronisiert
=     DB100.DBX 38.3;
UN    DB100.DBX 38.3;
BEB   ;                   // Abbruch
NETWORK
TITLE =Istpos. mit Strobe mitzaehlen
UN    A 10.2;             // Magazin RL
SPB   M001;
L     DB100.DBB 29;       // Magazin Istpos. HW
L     1;
-I    ;
T     DB100.DBB 29;       // Magazin Istpos. HW
SPA   M002;
M001: NOP   0;
UN    A 10.3;             // Magazin LL
SPB   M002;
L     DB100.DBB 29;       // Magazin Istpos. HW
L     1;
+I    ;
T     DB100.DBB 29;       // Magazin Istpos. HW
M002: NOP   0;
NETWORK
TITLE =Pos. bei 0 und max. Anzahl begrenzen
L     DB100.DBB 29;       // Magazin Istpos. HW
L     DB10.DBW 20;        // USER_DATA_INT[10]: Anzahl Magazinplaetze
<=I   ;
SPB   M100;
L     1;
T     DB100.DBB 29;       // Magazin Istpos. HW
SPA   M200;
M100: NOP   0;
L     DB100.DBB 29;       // Magazin Istpos. HW
L     0;
>I    ;
SPB   M200;
L     DB10.DBW 20;        // USER_DATA_INT[10]: Anzahl Magazinplaetze
T     DB100.DBB 29;       // Magazin Istpos. HW
M200: NOP   0;
END_FUNCTION
//**************************************************************************
FUNCTION FC 2 : VOID
TITLE=STUECKZAEHLER
NAME: STUECKZ
BEGIN
NETWORK
TITLE = STUECKZAEHLER (aus Simulations-PLC uebernommen)
UN    DB20.DBX 192.2;
UN    DB20.DBX 195.6;
O     DB20.DBX 324.2;     // DRY aktiv
SPB   M214;
L     DB15.DBD 4;
L     DW#16#000F_423F;
<>D   ;
SPB   M211;
L     DW#16#0000_0000;
T     DB15.DBD 4;
M211: NOP 0 ;
L     DB15.DBD 4;
L     DW#16#0000_0001;
+D    ;
T     DB15.DBD 4;
L     DB15.DBD 8;
L     DW#16#000F_423F;
<>D   ;
SPB   M213;
L     DW#16#0000_0000;
T     DB15.DBD 8;
M213: NOP 0 ;
L     DB15.DBD 8;
L     DW#16#0000_0001;
+D    ;
T     DB15.DBD 8;
M214: L     DB15.DBD 0;
L     DW#16#0000_0000;
<>D   ;
SPB   M202;
SPA   M201;
M202: NOP 0 ;
U     DB1.DBX 1402.7;     // Steuerungskennung SINUMERIK 840D
SPB   M201;
L     DB15.DBD 4;
L     DB15.DBD 0;
<D    ;
SPB   M201;
U     M 110.1;            // "1"
S     DB3.DBX 5.3;        // BM7043 Sollstueckzahl erreicht
L     DW#16#0;
T     DB15.DBD 4;
M201: NOP   0;
END_FUNCTION
//**************************************************************************
FUNCTION FC 11 : VOID
TITLE=ACC_ANA
NAME: ACC_ANA
BEGIN
NETWORK
TITLE = Analogwerte bei ACC einlesen
U     DB1.DBX 1370.0;     // 1.SPS-SCHLEIFENDURCHLAUF
S     M 199.1;            // AD Schritt 1
SPBN  _AS1;
L     4;                  // Kanal 2 anwaehlen
T     AB 12;              // Ansteuerung AD-Wandler
_AS1: UN    M 199.1;
SPB   _AS2;
U     M 199.1;            // AD Schritt 1
R     M 199.1;            // AD Schritt 1
S     M 199.2;            // AD Schritt 2
SPA   _AE1;
_AS2: UN    M 199.2;            // AD Schritt 2
SPB   _AW1;
U     M 199.2;            // AD Schritt 2
S     A 12.5;             // AD Hold
R     M 199.2;            // AD Schritt 2
S     M 199.3;            // AD Schritt 3
SPA   _AE1;
_AW1: UN    M 199.3;            // AD Schritt 3
SPB   _AE1;
U     A 12.2;             // AD Kanal Bit 0
UN    A 12.3;             // AD Kanal Bit 1
SPBN  _AW2;
L     EW 14;              // AD-Wert
L     1023;
UW;
T     DB1.DBW 1472;       // ACIF AIN 1
L     8;                  // Kanal 3 anwaehlen
T     AB 12;
SPA   _AW4;
_AW2: UN    A 12.2;             // AD Kanal Bit 0
U     A 12.3;             // AD Kanal Bit 1
SPBN  _AW3;
L     EW 14;              // AD-Wert
L     1023;
UW;
T     DB1.DBW 1474;       // ACIF AIN 2
L     12;                 // Kanal 4 anwaehlen
T     AB 12;
SPA   _AW4;
_AW3: U     A 12.2;             // AD Kanal Bit 0
U     A 12.3;             // AD Kanal Bit 1
SPBN  _AW4;
L     EW 14;              // AD-Wert
L     1023;
UW;
T     DB1.DBW 1476;       // ACIF AIN 3
L     4;
T     AB 12;
_AW4: U     M 199.3;            // AD Schritt 3
R     A 12.5;             // AD Hold
R     M 199.3;            // AD Schritt 3
S     M 199.1;            // AD Schritt 1
_AE1: NOP   0;
END_FUNCTION
//**************************************************************************
FUNCTION FC 12 : VOID
TITLE=RD_SIMOD
NAME: RD_SIMOD
BEGIN
NETWORK
TITLE = Abbruch wenn Strobe=0
U     DB100.DBX 170.1;    // HM Strobe
=     DB100.DBX 170.2;    // HM Strobe verz. 1
U     E 8.5;              // USB Strobe
=     DB100.DBX 170.1;    // HM Strobe
U     E 8.5;              // USB Strobe
UN    DB100.DBX 170.2;    // HM Strobe verz. 1
=     DB100.DBX 170.3;    // Fl. Strobe
ON    E 8.5;              // USB Strobe
ON    DB100.DBX 170.2;    // HM Strobe verz. 1
BEB
NETWORK
TITLE = Kanal 0: Analog LSB
UN    E 8.4;              // Kanal Bit 1
UN    E 8.7;              // Kanal Bit 2
SPBN  M021;
L     0;
T     DB100.DBW 178;      // Zwischenspeicher
U     E 8.0;              // Bit 0
=     DB100.DBX 178.0;
U     E 8.1;              // Bit 1
=     DB100.DBX 178.1;
U     E 8.2;              // Bit 2
=     DB100.DBX 178.2;
U     E 8.3;              // Bit 3
=     DB100.DBX 178.3;
U     E 8.6;              // Bit 4
=     DB100.DBX 178.4;
U     M 110.1;            // "1"
S     DB100.DBX 170.0;    // Steuerbit
M021: NOP   0;
NETWORK
TITLE = Kanal 1: Analog MSB
U     E 8.4;              // Kanal Bit 1
UN    E 8.7;              // Kanal Bit 2
SPBN  M031;
U     E 8.0;              // Bit 0
=     DB100.DBX 178.5;
U     E 8.1;              // Bit 1
=     DB100.DBX 178.6;
U     E 8.2;              // Bit 2
=     DB100.DBX 178.7;
U     E 8.3;              // Bit 3
=     DB100.DBX 179.0;
U     E 8.6;              // Bit 4
=     DB100.DBX 179.1;
UN    DB100.DBX 170.0;    // Steuerbit
SPB   M031;
U     M 110.1;            // "1"
R     DB100.DBX 170.0;    // Steuerbit
L     DB100.DBW 178;      // Zwischenspeicher
L     DB10.DBW 22;        // USER_DATA_INT[11]: Korrektur Bere WZ-Spanner
+I    ;
T     DB100.DBW 172;      // Analogwert Werkzeugspanner
M031: NOP   0;
NETWORK
TITLE = Kanal 2: Fehlercodes
UN    E 8.4;              // Kanal Bit 1
U     E 8.7;              // Kanal Bit 2
SPBN  M041;
L     0;
T     DB100.DBB 174;      // Fehlercode
U     E 8.0;              // Bit 0
=     DB100.DBX 174.0;
U     E 8.1;              // Bit 1
=     DB100.DBX 174.1;
U     E 8.2;              // Bit 2
=     DB100.DBX 174.2;
U     E 8.3;              // Bit 3
=     DB100.DBX 174.3;
U     E 8.6;              // Bit 4
=     DB100.DBX 174.4;
M041: NOP   0;
NETWORK
TITLE = Kanal 3: SW-Version
U     E 8.4;              // Kanal Bit 1
U     E 8.7;              // Kanal Bit 2
SPBN  M051;
L     0;
T     DB100.DBB 175;      // SW-Version
U     E 8.0;              // Bit 0
=     DB100.DBX 175.0;
U     E 8.1;              // Bit 1
=     DB100.DBX 175.1;
U     E 8.2;              // Bit 2
=     DB100.DBX 175.2;
U     E 8.3;              // Bit 3
=     DB100.DBX 175.3;
U     E 8.6;              // Bit 4
=     DB100.DBX 175.4;
M051: NOP   0;
END_FUNCTION
//**************************************************************************
FUNCTION FC 170 : VOID
TITLE=SAFETY
NAME: SAFETY
BEGIN
NETWORK
TITLE = Achsrichtungen auswerten
// V2.20: IPO-Fahrbefehle eingebunden (Achsrichtung kommt auch durch Regelung)
U     DB1.DBX 66.0;       // Achsrichtung +X
U(    ;
ON    DB20.DBX842.0;      // Kanal 0 kein IPO-Befehl X
ON    DB25.DBX842.0;      // Kanal 1 kein IPO-Befehl X
)     ;
S     DB100.DBX 171.2;
U     DB1.DBX 68.0;       // Achsrichtung -X
U(    ;
ON    DB20.DBX842.0;      // Kanal 0 kein IPO-Befehl X
ON    DB25.DBX842.0;      // Kanal 1 kein IPO-Befehl X
)     ;
R     DB100.DBX 171.2;
U     DB1.DBX 66.1;       // Achsrichtung +Y
U(    ;
ON    DB20.DBX842.1;      // Kanal 0 kein IPO-Befehl Y
ON    DB25.DBX842.1;      // Kanal 1 kein IPO-Befehl Y
)     ;
S     DB100.DBX 171.4;
U     DB1.DBX 68.1;       // Achsrichtung -Y
U(    ;
ON    DB20.DBX842.1;      // Kanal 0 kein IPO-Befehl Y
ON    DB25.DBX842.1;      // Kanal 1 kein IPO-Befehl Y
)     ;
R     DB100.DBX 171.4;
U     DB1.DBX 66.2;       // Achsrichtung +Z
U(    ;
ON    DB20.DBX842.2;      // Kanal 0 kein IPO-Befehl Z
ON    DB25.DBX842.2;      // Kanal 1 kein IPO-Befehl Z
)     ;
S     DB100.DBX 171.6;
U     DB1.DBX 68.2;       // Achsrichtung -Z
U(    ;
ON    DB20.DBX842.2;      // Kanal 0 kein IPO-Befehl Z
ON    DB25.DBX842.2;      // Kanal 1 kein IPO-Befehl Z
)     ;
R     DB100.DBX 171.6;
O(    ;
U     DB100.DBX 171.2;
UN    DB100.DBX 171.3;
)     ;
O(    ;
UN    DB100.DBX 171.2;
U     DB100.DBX 171.3;
)     ;
L     S5T#300MS;
SV    T 70;
U     T 70;
=     A 5.4;              // Richtung X+
O(    ;
U     DB100.DBX 171.4;
UN    DB100.DBX 171.5;
)     ;
O(    ;
UN    DB100.DBX 171.4;
U     DB100.DBX 171.5;
)     ;
L     S5T#300MS;
SV    T 71;
U     T 71;
=     A 5.5;              // Richtung Y+
O(    ;
U     DB100.DBX 171.6;
UN    DB100.DBX 171.7;
)     ;
O(    ;
UN    DB100.DBX 171.6;
U     DB100.DBX 171.7;
)     ;
L     S5T#300MS;
SV    T 72;
U     T 72;
=     A 5.6;              // Richtung Z+
U     DB100.DBX 171.2;
=     DB100.DBX 171.3;
U     DB100.DBX 171.4;
=     DB100.DBX 171.5;
U     DB100.DBX 171.6;
=     DB100.DBX 171.7;
NETWORK
TITLE = Positionen abspeichern
U     T 26;               // Ausschaltverz. Antriebe
UN    DB100.DBX 168.1;    // SH-Alarm zulassen
ON    M 136.0;            // Aux-On
SPBN  no_p;
L     DB1.DBD 0;          // Pos. X-Achse
T     DB100.DBD 180;
L     DB1.DBD 4;          // Pos. Y-Achse
T     DB100.DBD 184;
L     DB1.DBD 8;          // Pos. Z-Achse
T     DB100.DBD 188;
L     DB1.DBD 12;         // Pos. S-Achse
T     DB100.DBD 192;
no_p: NOP   0;
NETWORK
TITLE = FM6088 PLC SBH X
U(    ;
L     DB1.DBD 0;          // Position X [m]
L     DB100.DBD 180;
-R    ;
ABS   ;
L     +1E-03;             // Positionstoleranz 1mm
>=R   ;
)     ;
S     DB2.DBX 11.0;       // FM6088
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 11.0;       // FM6088
NETWORK
TITLE = FM6089 PLC SBH Y
U(    ;
L     DB1.DBD 4;          // Position Y [m]
L     DB100.DBD 184;
-R    ;
ABS   ;
L     +1E-03;             // Positionstoleranz 1mm
>=R   ;
)     ;
S     DB2.DBX 11.1;       // FM6089
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 11.1;       // FM6089
NETWORK
TITLE = FM6090 PLC SBH Z
U(    ;
L     DB1.DBD 8;          // Position Z [m]
L     DB100.DBD 188;
-R    ;
ABS   ;
L     +1E-03;             // Positionstoleranz 1mm
>=R   ;
)     ;
S     DB2.DBX 11.2;       // FM6090
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 11.2;       // FM6090
NETWORK
TITLE = FM6091 PLC SBH S
NETWORK
TITLE = FM6092 PLC SG X
U(    ;
L     DB1.DBD 1228;       // akt. Vorschubwert X [m/s]
ABS   ;
L     +36.667E-03;        // 2200mm/min
>=R   ;
)     ;
UN    T 27;               // Antriebe ein verz.
U     M 136.0;            // Aux-On
UN    DB100.DBX 168.2;    // PLC-Alarme ausblenden
S     DB2.DBX 11.4;       // FM6092
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 11.4;       // FM6092
NETWORK
TITLE = FM6093 PLC SG Y
U(    ;
L     DB1.DBD 1232;       // akt. Vorschubwert Y [m/s]
ABS   ;
L     +36.667E-03;        // 2200mm/min
>=R   ;
)     ;
UN    T 27;               // Antriebe ein verz.
U     M 136.0;            // Aux-On
UN    DB100.DBX 168.2;    // PLC-Alarme ausblenden
S     DB2.DBX 11.5;       // FM6093
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 11.5;       // FM6093
NETWORK
TITLE = FM6094 PLC SG Z
U(    ;
L     DB1.DBD 1236;       // akt. Vorschubwert Y [m/s]
ABS   ;
L     +36.667E-03;        // 2200mm/min
>=R   ;
)     ;
UN    T 27;               // Antriebe ein verz.
U     M 136.0;            // Aux-On
UN    DB100.DBX 168.2;    // PLC-Alarme ausblenden
S     DB2.DBX 11.6;       // FM6094
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 11.6;       // FM6094
NETWORK
TITLE = FM6095 PLC SG S
NETWORK
TITLE = FM6080 SBD SBH X
U(    ;
L     DB100.DBB 174;      // Fehlercode
L     1;
==I   ;
)     ;
S     DB2.DBX 10.0;       // FM6080
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 10.0;       // FM6080
NETWORK
TITLE = FM6081 SBD SBH Y
U(    ;
L     DB100.DBB 174;      // Fehlercode
L     2;
==I   ;
)     ;
S     DB2.DBX 10.1;       // FM6081
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 10.1;       // FM6081
NETWORK
TITLE = FM6082 SBD SBH Z
U(    ;
L     DB100.DBB 174;      // Fehlercode
L     3;
==I   ;
)     ;
S     DB2.DBX 10.2;       // FM6082
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 10.2;       // FM6082
NETWORK
TITLE = FM6083 SBD SBH S
U(    ;
L     DB100.DBB 174;      // Fehlercode
L     4;
==I   ;
)     ;
S     DB2.DBX 10.3;       // FM6083
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 10.3;       // FM6083
NETWORK
TITLE = FM6084 SBD SG X
U(    ;
L     DB100.DBB 174;      // Fehlercode
L     5;
==I   ;
)     ;
S     DB2.DBX 10.4;       // FM6084
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 10.4;       // FM6084
NETWORK
TITLE = FM6085 SBD SG Y
U(    ;
L     DB100.DBB 174;      // Fehlercode
L     6;
==I   ;
)     ;
S     DB2.DBX 10.5;       // FM6085
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 10.5;       // FM6085
NETWORK
TITLE = FM6086 SBD SG Z
U(    ;
L     DB100.DBB 174;      // Fehlercode
L     7;
==I   ;
)     ;
S     DB2.DBX 10.6;       // FM6086
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 10.6;       // FM6086
NETWORK
TITLE = FM6087 SBD SG S
U(    ;
L     DB100.DBB 174;      // Fehlercode
L     8;
==I   ;
)     ;
S     DB2.DBX 10.7;       // FM6087
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 10.7;       // FM6087
NETWORK
TITLE = FM6096 SBD Riemenriss Z-Achse
U(    ;
L     DB100.DBB 174;      // Fehlercode
L     9;
==I   ;
)     ;
S     DB2.DBX 12.0;       // FM6096
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 12.0;       // FM6096
NETWORK
TITLE = FM6097 PLC Riemenriss Z-Achse
O(    ;
UN    E 4.4;              // Bero Motor Z-Achse
U     E 4.5;              // Bero Spindel 1 Z-Achse
)     ;
O(    ;
U     E 4.4;              // Bero Motor Z-Achse
U     E 4.1;              // Bero Spindel 2 Z-Achse
)     ;
O(    ;
U     E 4.5;              // Bero Spindel 1 Z-Achse
U     E 4.1;              // Bero Spindel 2 Z-Achse
)     ;
S     DB2.DBX 12.1;       // FM6097
O     M 138.0;            // Fl. Reset Taste
O     DB100.DBX 168.2;    // PLC-Alarme ausblenden
R     DB2.DBX 12.1;       // FM6097
NETWORK
TITLE = FM6098 SBD Fehler Not-Aus Test
U     E 2.4;              // Not-Aus
U     A 3.5;              // Not-Aus Test
L     S5T#500MS;
SE    T 79;
U     T 79;
U(    ;
L     DB100.DBB 174;      // Fehlercode
L     10;
==I   ;
)     ;
O     T 25;               // verz. Ueberw. Teststop
S     DB2.DBX 12.2;       // FM6098
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 12.2;       // FM6098
NETWORK
TITLE = FM6099 SBD Not-Aus Relais defekt
U(    ;
L     DB100.DBB 174;      // Fehlercode
L     11;
==I   ;
)     ;
S     DB2.DBX 12.3;       // FM6099
U     M 138.0;            // Fl. Reset
R     DB2.DBX 12.3;       // FM6099
NETWORK
TITLE = FM6106 SBD Richtungswechsel länger als 3 sec
U(    ;
L     DB100.DBB 174;      // Fehlercode
L     12;
==I   ;
)     ;
S     DB2.DBX 13.2;       // FM6106
U     M 138.0;            // Fl. Reset
R     DB2.DBX 13.2;       // FM6106
NETWORK
TITLE = FM6100 SBD Lebenszeichenueberwachung
UN    DB100.DBX 170.3;    // Fl. Strobe
L     S5T#60MS;
SE    T 78;               // USB Ueberwachungszeit
U     T 78;
S     DB2.DBX 12.4;       // FM6100
U     M 138.0;            // Fl. Reset
R     DB2.DBX 12.4;       // FM6100
NETWORK
TITLE = FM6101 Schuetzueberwachung
U(    ;
UN    E 2.1;              // Ueberw. SPF Schuetze
U     DB10.DBX 46.0;      // USER_DATA_HEX[6]: SPF vorhanden
ON    E 7.7;              // Ueberw. WZW Schuetze
)     ;
UN    M 202.2;            // Tuer zu und verr.
L     S5T#250MS;
SE    T 73;               // verz. Uerberw. Schuetze
U     T 73;               // verz. Uerberw. Schuetze
S     DB2.DBX 12.5;       // FM6101
U     M 138.0;            // Fl. Reset
R     DB2.DBX 12.5;       // FM6101
NETWORK
TITLE = FM6102 SBD Stoerung Drehzahlueberwachung
U(    ;
ON    E 6.0;              // n<Nx X-Achse 611U (150U)
ON    E 6.1;              // n<Nx Y-Achse 611U (150U)
ON    E 6.2;              // n<Nx Z-Achse 611U (150U)
O(    ;
UN    E 5.2;              // n<Nx Spindel 611U (20U)
UN    DB10.DBX 50.4;      // USER_DATA_HEX[10]: IFG Spindel mit Tür auf wegsch.
)     ;
)     ;
UN    E 3.0;              // Drehzahl nicht 0 Sicherheitsmodul
L     S5T#250MS;
SE    T 22;               // verz. Uerberw. Bewegung
UN    DB1.DBX 1358.0;     // Achse 0 in Bewegung (X)
UN    DB1.DBX 1358.1;     // Achse 1 in Bewegung (Y)
UN    DB1.DBX 1358.2;     // Achse 2 in Bewegung (Z)
UN    DB1.DBX 1358.3;     // Achse 3 in Bewegung (C)
U     DB1.DBX 1356.3;     // Spindelstillstand
U     E 3.0;              // Drehzahl nicht 0 Sicherheitsmodul
L     S5T#500MS;
SE    T 23;               // verz. Uerberw. Stillstand
O     T 22;               // verz. Uerberw. Bewegung
O     T 23;               // verz. Uerberw. Stillstand
S     DB2.DBX 12.6;       // FM6102
U     M 138.0;            // Fl. Reset Taste
R     DB2.DBX 12.6;       // FM6102
NETWORK
TITLE = Anforderung Teststop
//      U     M 110.7;            // Blinktakt
//      FP    DB100.DBX 169.6;    // Fl. 1Hz
U     DB100.DBX 169.6;    // Fl. 1Hz
UN    DB100.DBX 136.7;    // Anforderung Teststop
SPBN  _tz1;
L     DB100.DBW 200;
L     1;
+I    ;
T     DB100.DBW 200;
_tz1: U(    ;
L     DB100.DBW 200;
L     28800;
>=I   ;
)     ;
O     M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF
S     DB100.DBX 136.7;    // Anforderung Teststop
SPBN  _tz2;
L     0;
T     DB100.DBW 200;
_tz2: NOP   0;
NETWORK
TITLE = Ablauf Teststop
U     DB100.DBX 136.7;    // Anforderung Teststop
U     M 136.0;            // Aux-On
UN    M 136.4;            // Summe Fahrbefehl
U     DB1.DBX 1356.3;     // Spindel steht
U     M 202.2;            // Tuer zu und verriegelt
UN    E 2.0;              // Rueckmeldung Netzschuetz E/R
U     E 2.4;              // Not-Aus
UN    M 138.6;            // Not-Aus Test angefordert
UN    M 111.7;            // Programm laeuft
UN    M 111.6;            // Programm unterbrochen
UN    DB20.DBX 325.1;     // Stop durch SBL
UN    DB100.DBX 169.7;    // Teststop laeuft
S     DB100.DBX 169.0;    // Teststop aktiv
L     S5T#100MS;
SE    T 74;               // verz. Teststop start
U     T 74;               // verz. Teststop start
S     DB100.DBX 169.1;    // Test Schritt 1
U     DB100.DBX 169.1;    // Test Schritt 1
U     E 2.0;              // Rueckmeldung Netzschuetz E/R
S     DB100.DBX 169.2;    // Test Schritt 2
R     DB100.DBX 169.1;    // Test Schritt 1
U     DB100.DBX 169.2;    // Test Schritt 2
UN    E 2.0;              // Rueckmeldung Netzschuetz E/R
S     DB100.DBX 169.3;    // Test Schritt 3
R     DB100.DBX 169.2;    // Test Schritt 2
U     DB100.DBX 169.3;    // Test Schritt 3
U     E 2.0;              // Rueckmeldung Netzschuetz E/R
S     DB100.DBX 169.4;    // Test Schritt 4
R     DB100.DBX 169.3;    // Test Schritt 3
U     DB100.DBX 169.4;    // Test Schritt 4
UN    E 2.0;              // Rueckmeldung Netzschuetz E/R
R     DB100.DBX 169.4;    // Test Schritt 4
R     DB100.DBX 169.0;    // Teststop aktiv
R     DB100.DBX 136.7;    // Anforderung Teststop
O     DB100.DBX 169.1;    // Test Schritt 1
O     DB100.DBX 169.2;    // Test Schritt 2
O     DB100.DBX 169.3;    // Test Schritt 3
O     DB100.DBX 169.4;    // Test Schritt 4
=     DB100.DBX 169.7;    // Teststop laeuft
L     S5T#5S;
SE    T 25;               // verz. Ueberw. Teststop
NETWORK
TITLE = SI Summenalarm
O(    ;
L     DB2.DBW 10;
L     0;
<>I   ;
)     ;
O(    ;
L     DB2.DBB 12;
L     0;
<>I   ;
)     ;
=     DB100.DBX 170.7;    // SI Summenalarm
NETWORK
TITLE = Testbits zuruecksetzen
U     M 110.2;            // 1.SPS-SCHLEIFENDURCHLAUF
R     DB100.DBX 168.0;    // SG-Alarm zulassen
R     DB100.DBX 168.1;    // SH-Alarm zulassen
R     DB100.DBX 168.2;    // PLC-Alarme ausblenden
NETWORK
TITLE = Spindeldrehzahl simulieren wg. Not-Aus Relais (Spindel ohne Bero)
UN    E 5.2;              // n<Nx Spindel 611U (20U)
U     A 10.6              // Impulsfreigabe Hauptantrieb
U     E 8.5;              // Strobe USB-Platine
=     A 6.5;              // HA dreht von PLC
END_FUNCTION
//**************************************************************************
