[Info]
Version=3.00
CheckChange=691822b7
PLCFileName=Mill125.awl
//   ***********************************************************************
//   *                                                                     *
//   *                   SPS-PROGRAMM FÜR DIE PCM125                       *
//   *                                                                     *
//   *               EIN/AUSGÄNGE FÜR ACC UND AC95   am 16.11.2006         *
//   *                                                                     *
//   *      Designed by Friedrich Schörghofer          06.02.1998          *
//   *                                                                     *
//   ***********************************************************************
//   INHALTSVERZEICHNIS:
//      FC 0    WERKZEUGWENDER
//      FC 1    SCHMIERMITTEL
//      FC 2    STÜCKZÄHLER
//      FC 4    ZUWEISEN DER EINGÄNGE AC95 - ACC
//      FC 5    ZUWEISEN DER AUSGÄNGE AC95 - ACC
//      FC 10   HA STEUERUNG
//      FC 11   ACHSEN LEISTUNG
//      FC 12   JOG ACHSEN
//      FC 13   REF ACHSEN
//      FC 14   AUX-ON
//      FC 15   NC-STARTVERR.
//      FC 16   RESET
//      FC 17   NC_START_UND_NC_STOP_VON_M0_ODER_M1
//      FC 18   PLC-NC ÜBERGABE
//      FC 19   SONDERBETRIEB
//      FC 20   INITIALISIERUNGEN
//      FC 21   BEI_PROGRAMM_ENDE_RESET_ODER_NEUSTART
//      FC 23   INC ACHSEN
//      FC 24   HA LEISTUNG
//      FC 26   KÜHLMITTEL (M8/M9)
//      FC 27   SPINDEL HALT/START
//      FC 28   VORSCHUB HALT/START
//      FC 29   EINZELSTÜCK
//      FC 30   PNEUM. SCHRAUBSTOCK
//      FC 31   AUSBLASEINRICHTUNG
//      FC 32   TÜRAUTOMATIK
//      FC 34   TEILAPPARAT
//      FC 40   ROBOTIK-INTERFACE
//      FC 50   STEUERUNG ZUM REFERENZIEREN DES WERKZEUGWENDERS
//      FC 51   STEUERUNG ZUM SCHWENKEN DES WERKZEUGWENDERS
//      FC 52   WERKZEUGWENDER FREIFAHREN
//      FC 53   WERKZEUGWENDER REFERENZIEREN UND SCHWENKEN
//      OB 1    ORGANISATIONSBAUSTEIN
//
//    SET = VKE SETZEN
//    CLR = VKE LÖSCHEN
//
//
//   Änderungen:
//
//      Alte SPS-SW zu neuer AC95-CAN SPS-SW V 00.00
//        1. Alarmausgang 4.7 immer aktiv (Bit DB10 0.1 muß nicht mehr gesetzt werden)
//
//
//   Änderungen:
//        SCHMIERMITTEL-Baustein von PCT125 übernommen
//        AUX ON-Baustein von PCT125 übernommen (mit kleine Änderungen!!)
//        STÜCKZAHLER-Baustein von PCT125 übernommen
//        RESET-Baustein von PCT125 übernommen
//        INITIALISIERUNGS-Baustein von PCT125 übernommen
//
//   TIMER:
//   SI = IMPULS
//        DIE MAXIMALE IMPULSLÄNGE ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DIE MINIMALE IMPULSLÄNGE ENTSPRICHT DER LÄNGE DES EINGANGSSIGNALES
//
//   SV = IMPULS (VERLÄNGERT)
//        DIE IMPULSLÄNGE ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DIE DAUER DES EINGANGSSIGNALES HAT KEINEN EINFLUSS
//
//   SE = EINSCHALTVERZÖGERUNG
//        DIE VERZÖGERUNGSDAUER ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DAS EINGANGSSIGNAL MUSS MINDESTENS SOLANGE ANLIEGEN
//
//   SS = EINSCHALTVERZÖGERUNG (SPEICHERND)
//        DIE VERZÖGERUNGSDAUER ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DIE DAUER DES EINGANGSSIGNALES HAT KEINEN EINFLUSS
//
//   SA = AUSSCHALTIMPULS
//        DIE IMPULSLÄNGE ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DER TIMER WECHSELT VON "0"AUF "1" UND DIE ZEIT WIRD GESTARTET
//            WENN DAS EINGANGSSIGNAL VON "1" AUF "0" GEHT.
//
// ZEITBASIS     BELEGUNG DES ZEITWORTES
//               16......8  7.......0
//    0,01 S     0000 0000  0000 0000
//    0,1  S     0001 0000  0000 0000
//    1    S     0010 0000  0000 0000
//   10    S     0011 0000  0000 0000
//                     H     Z     E   = ZEITWERT IM BCD-FORMAT
//                 00                  = BINÄRCODE FÜR ZEITBASIS
//
//
//
//
//;ÄNDERUNGEN ODER PROBLEME BEI 8**********
//;
FUNCTION FC 4 : VOID
NAME: ZUWEISEN DER EINGÄNGE AC 95 - ACC
BEGIN
// EINGÄNGE:
NETWORK
TITLE = TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1 ZUWEISUNG
U   DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E 2.0;          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
O
UN  DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E 1.5;          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
=   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
NETWORK
TITLE = SCHLÜSSELSCHALTER AUF SONDERBETRIEB ZUWEISUNG
U   DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E 2.1;          // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
O
UN  DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E 1.6;          // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
=   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
NETWORK
TITLE = NICHT NOT-AUS ZUWEISUNG
U   DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E 2.2;          // NICHT NOT-AUS
O
UN  DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E 1.4;          // NICHT NOT-AUS
=   M 50.2;         // NICHT NOT-AUS
NETWORK
TITLE = Ausgabemodul Y4A047000 durchgeschaltet
U   DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E 2.3;          // Ausgabemodul Y4A047000 durchgeschaltet
O
UN  DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E 1.7;          // Ausgabemodul Y4A047000 durchgeschaltet
=   M 50.3;         // Ausgabemodul Y4A047000 durchgeschaltet
NETWORK
TITLE = HAUPTANTRIEB LÄUFT (AUSGANG BEDIENMODUL X1:9)
U   DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E 2.4;          // HAUPTANTRIEB LÄUFT (AUSGANG BEDIENMODUL X1:9)
O
UN  DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E 2.3;          // HAUPTANTRIEB LÄUFT (AUSGANG BEDIENMODUL X1:9)
=   M 51.1;         // HAUPTANTRIEB LÄUFT (AUSGANG BEDIENMODUL X1:9)
NETWORK
TITLE = Druckschalter Schmiermittel
U   DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E 2.5;          // Druckschalter Schmiermittel
O
UN  DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E 2.4;          // Druckschalter Schmiermittel
=   M 51.2;         // Druckschalter Schmiermittel
NETWORK
TITLE = SERVO-READY ZUWEISUNG
U   DB10.DBX 2.0;   // ACC AKTIVIEREN
UN  E 0.0;          // X-ACHSE SERVO-READY  (X)
O
UN  DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E  1.0;         // X-ACHSE SERVO-READY  (X)
=   M 50.4;         // X-ACHSE SERVO-READY  (X)
U   DB10.DBX 2.0;   // ACC AKTIVIEREN
UN  E 0.1;          // Y-ACHSE SERVO-READY  (Y)
O
UN  DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E  1.1;         // Y-ACHSE SERVO-READY  (Y)
=   M 50.5;         // Y-ACHSE SERVO-READY  (Y)
U   DB10.DBX 2.0;   // ACC AKTIVIEREN
UN  E 0.2;          // Z-ACHSE/SERVO-READY  (Z)
O
UN  DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E  1.2;         // Z-ACHSE/SERVO-READY  (Z)
=   M 50.6;         // Z-ACHSE/SERVO-READY  (Z)
U   DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E 0.3;          // FRANZ STELLER SERVO-READY
O
UN  DB10.DBX 2.0;   // ACC AKTIVIEREN
U   E  1.3;          // FRANZ STELLER SERVO-READY
=   M 50.7;         // FRANZ STELLER SERVO-READY
END_FUNCTION
FUNCTION FC 5 : VOID
NAME: ZUWEISEN DER AUSGÄNGE AC95 - ACC
BEGIN
// AUSGÄNGE:
//************************     ACC     *********************************************
U   DB10.DBX 2.0;   // ACC  AKTIVIEREN
SPBN   M001;
NETWORK
TITLE = REGLERFREIGABE FÜR HAUPTANTRIEB
O   DB20.DBX 340.0; // SPINDEL EIN IM UHRZEIGER
O   DB20.DBX 340.1; // SPINDEL EIN GEGEN UHRZEIGER
S   M 145.2;
U   M 145.2;
U   M 104.2;        // SUM  SFG
O
UN  M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
U   M 145.7         // HM REGLERFREIGABE FÜR HAUPTANTRIEB
O   M 145.6;          // REGLERFREIGABE FÜR HAUPTANTRIEB für Werkzeugwenden
=   A 3.1           // FREIGABE ACHSEN B = REGLERFREIGABE FÜR HAUPTANTRIEB
S   M 145.7         // HM REGLERFREIGABE FÜR HAUPTANTRIEB
R   M 145.4
NETWORK
TITLE = FREIGABE DIFFERENZIALLEITUNGSTREIBER AUF ACC-MOTHERBOARD
U   M    110.0; // AUX-ON
S   A 3.7;          // FREIGABE DIFFERENZIALLEITUNGSTREIBER TAKTE FÜR SM
UN  M    110.0; // AUX-ON
L   S5TIME#1S;      // 10X0.1S
SE  T 1;
U   T 1;
R   A 3.7;          // FREIGABE DIFFERENZIALLEITUNGSTREIBER TAKTE FÜR SM
// ACC  LEISTUNG FRANZ FREQUENZUMRICHTER
U   M 55.0          // AUSGANGSMERKER LEISTUNG FRANZ FREQUENZUMRICHTER
=   A9.0;           // LEISTUNG FRANZ FREQUENZUMRICHTER
// ACC  LEISTUNG ACHSANTRIEBE
U   M 55.1          // AUSGANGSMERKER LEISTUNG ACHSANTRIEBE
=   A9.1;           // LEISTUNG ACHSANTRIEBE
// ACC  LEISTUNG ACHSEN/SANFTANLAUF
U   M 55.2          // AUSGANGSMERKER LEISTUNG ACHSEN/SANFTANLAUF
=   A9.2;           // LEISTUNG ACHSEN/SANFTANLAUF
M001:     NOP 0;
// AUSGÄNGE:
//************************     AC95    *********************************************
UN  DB10.DBX 2.0;   // ACC  AKTIVIEREN
SPBN   M002;
// AC95 LEISTUNG FRANZ FREQUENZUMRICHTER
U   M 55.0;         // AUSGANGSMERKER LEISTUNG FRANZ FREQUENZUMRICHTER
=   A 0.1;          // LEISTUNG FRANZ FREQUENZUMRICHTER
// AC95 LEISTUNG ACHSANTRIEBE
U   M 55.1;         // AUSGANGSMERKER LEISTUNG ACHSANTRIEBE
=   A 0.2;          // LEISTUNG ACHSANTRIEBE
// AC95 LEISTUNG ACHSEN/SANFTANLAUF
U   M 55.2;         // AUSGANGSMERKER LEISTUNG ACHSEN/SANFTANLAUF
=   A 0.3;          // LEISTUNG ACHSEN/SANFTANLAUF
M002:     NOP 0;
END_FUNCTION
FUNCTION FC 0 : VOID
NAME: WERKZEUGWENDER
BEGIN
; BIT BESCHREIBUNG FÜR WZW :
; WERKZEUGWENDER SCHWENKEN :
;   BYTE 150 WIRD NACH SCHRITT 6 GELÖSCHT
;      M 150.1  SCHRITT 1 SPINDEL IN POSITIONIERBETRIEB SCHALTEN
;      M 150.2  SCHRITT 2 SPINDEL REFERENZIEREN
;      M 150.3  SCHRITT 3 Z-ACHSE AUF WERKZEUGWENDEPOSITION FAHREN
;      M 150.4  SCHRITT 4 WZW AUF GÜLTIGE POSITION SCHWENKEN
;      M 150.5  SCHRITT 5 Z-ACHSE AUF AUSGANGSPOSITION FAHREN
;      M 151.0  WERKZEUGWENDER IST REFERENZIERT
;      M 151.1  WERKZEUGWENDER FREIGABE
;      M 151.2  T-WORT IST GÜLTIG
;      M 151.3  WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
;      M 151.4  WERKZEUGWENDER START VON WZW-TASTE
;      M 151.5  FM WZW TASTE
;      M 151.6  FM WERKZEUGWENDER POSITIONSBERO
;      M 152.0  WERKZEUGWENDEN AKTIV
;      M 152.1  WERKZEUGWENDEN ODER FREIFAHREN AKTIV
;      M 152.2  WERKZEUGWENDER POSITIONSBERO ÜBERWACHUNG
;      M 152.6  ANFORDERUNG WERKZEUGWENDER IM SCHRITT 4 WEITERSCHWENKEN
;      M 152.7  WERKZEUGWENDER IM SCHRITT 4 WEITERSCHWENKEN
;      M 153.0  WZW ABBRUCH MERKER AUS DEN SETTINGDATEN ZAHNRAD IM EINGRIFF
;      M 153.1  WZW ABBRUCH MERKER 1 (NOT AUS UND ZAHNRAD IM EINGRIFF)
;      M 153.3  WZW ABBRUCH MERKER FÜR REFERENZPUNKTANFAHREN
;   BYTE 154 WIRD NACH SCHRITT 6 GELÖSCHT
;      M 154.2  WZW ABBRUCH MERKER 2 (NOT AUS UND ZAHNRAD NICHT IM EINGRIFF)
;      M 154.3  WZW ABBRUCH MERKER 3 (RESET ODER TÜR AUF IM SCHRITT S1 ODER S2)
;      M 154.4  WZW ABBRUCH MERKER 4 (NOT-AUS,TÜR AUF,RESET IM SCHRITT S3,ODER S5)
;      M 154.5  - NEUE WZW-TROMMEL POSITION IST IN MINUSRICHTUNG
;      M 154.6  + NEUE WZW-TROMMEL POSITION IST IN PLUSRICHTUNG
;      M 155.0  T-WORT GRÖSSER 10
;      M 155.1  T-WORT KLEINER 1
;      M 155.2  T-WORT UNGLEICH ISTZÄHLER
;      M 155.3  SOLLZÄHLER VORWÄRTS ZÄHLEN
;      M 155.4  SOLLZÄHLER RÜCKWÄRTS ZÄHLEN
;      M 155.5  SOLLZÄHLER UNGLEICH ISTZÄHLER
;      M 155.6  NEUES T-WORT GESPEICHERT FÜR MELDUNG 7001
;      MD 156    DOPPELWORT MIT SCHWENKPOSITION IN GRAD FÜR HAUPTSPINDEL (FLOATWWERT)
; WERKZEUGWENDER FREIFAHREN :
;      M 160.1  SCHRITT F1  SPINDEL IN POSITIONIERBETRIEB SCHALTEN
;      M 160.2  SCHRITT F2  SPINDEL REFERENZIEREN
;      M 160.3  SCHRITT F3  WZW POSITIONSBERO SUCHEN
;      M 160.4  SCHRITT F4  WZW EINE POSITION WEITERSCHWENKEN
;      M 160.5  SCHRITT F5.2 WERKZEUGWENDER FREIFAHRMODE VERLASSEN
;      M 161.0  HM WZW-AUSBLASMAGNETVENTIL SCHALTEN
;      M 161.4  FM SONDERAUSLÖSUNG SCHRITT F3
;      M 161.5  FM SONDERAUSLÖSUNG SCHRITT F3
;      M 162.0  WERKZEUGWENDER FREIFAHREN AKTIV
;      M 162.1  VON FREIFAHRBAUSTEIN AUF REF.POS. FAHREN
;      M 162.6  FM WERKZEUGWENDEN SOFORT ABBRECHEN BEI /NOT AUS,/TÜR AUF,/AUXON
;      M 162.7  FM WERKZEUGWENDEN SOFORT ABBRECHEN BEI RESET, /NOT AUS,/TÜR AUF,/AUXON
;      Z 50      WZW ISTPOSITIONSZÄHLER
;      Z 51      ZÄHLER FÜR MAXIMAL 8 HAUPTSPINDELUMDREHUNGEN WENN DAS WZW-POSITIONSBERO
;                DEN SIGNALZUSTAND NICHT ÄNDERT. (= ZÄHNE NICHT IM EINGRIFF,DANACH FÄHRT
;                DIE Z-ACHSE AUF DEN REF.PKT. HERUNTER.
;      Z 52      WZW SOLLPOSITIONSZÄHLER
;      T 50
;      T 51
;      T 52
NETWORK
TITLE = WZW-ABBRUCHMERKER IN DEN SETTINGDATEN LOESCHEN
U   M 151.5;           // FM WZW-TASTE
U   DB1.DBX 1374.4;    // TASTE AUX-ON (PC)
SPBN   M001;
L   0;
T   DB15.DBB  25;     // WERKZEUGPOSITION IN DEN SETTINGDATEN
T   DB15.DBB  20;     // SCHRITT 4 IN DEN SETTINGDATEN LOESCHEN
T   MB 150;           //
T   MB 151;           //
T   MB 152;           //
T   MB 153;           //
T   MB 154;           //
=   DB20.DBX 348.3;   // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
R   M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
R   DB3.DBX     2.5;  // 7021  WERKZEUGWENDER FREIFAHREN
M001:  NOP 0;
NETWORK
TITLE = FM WERKZEUGWENDER POSITIONSBERO
U   E 4.0;            // WERKZEUGWENDER POSITIONSBERO
FP  M 151.6;          // FM WERKZEUGWENDER POSITIONSBERO
NETWORK
TITLE = FM WZW-TASTE  (MSTT/PC)
U   DB20.DBX   294.5; // WZW-TASTE  (MSTT/PC)
FP  M 151.5;          // FM WZW-TASTE
NETWORK
TITLE = WERKZEUGWENDEN SOFORT ABBRECHEN BEI /NOT AUS,/TÜR AUF,/AUXON
ON  M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
ON  M 50.2;         // NICHT NOT-AUS
ON  M 110.0;          // AUX-ON
FP  M 162.6      // FM WERKZEUGWENDEN SOFORT ABBRECHEN BEI /NOT AUS,/TÜR AUF,/AUXON
R   A 4.4;            // FRC122C AUF SPINDELBETRIEB SCHALTEN
U   M 162.6      // FM WERKZEUGWENDEN SOFORT ABBRECHEN BEI /NOT AUS,/TÜR AUF,/AUXON
U   M 152.1           // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
SPBN M002;
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX  3.3;    // C-ACHSPOSITIONIERUNG SOFORT STOPPEN
M002:  NOP 0;
NETWORK
TITLE = WERKZEUGWENDEN SOFORT ABBRECHEN BEI  RESET
O   DB1.DBX  1370.3;  // RESETTASTE GEDRÜCKT
O   DB1.DBX  1440.0;  // RESET AUGELÖST
O   DB2.DBX 5.4; // 6044  WZW POSITIONSÜBERWACHUNG
U   M 152.1           // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
SPBN M003;
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX 348.4;   // REFERENZPUNKT DER RUNDACHSEN DEAKTIVIEREN
M003:  NOP 0;
NETWORK
TITLE = WERKZEUGWENDEN SOFORT ABBRECHEN BEI RESET, /NOT AUS,/TÜR AUF,/AUXON
O   DB1.DBX  1370.3;  // RESETTASTE GEDRÜCKT
O   DB1.DBX  1440.0;  // RESET AUGELÖST
ON  M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
ON  M 50.2;         // NICHT NOT-AUS
ON  M 110.0;     // AUX-ON
O   M 152.2;          // WERKZEUGWENDER POSITIONSBERO ÜBERWACHUNG
FP  M 162.7      // FM WERKZEUGWENDEN SOFORT ABBRECHEN BEI RESET, /NOT AUS,/TÜR AUF,/AUXON
U   M 162.7      // FM WERKZEUGWENDEN SOFORT ABBRECHEN BEI RESET, /NOT AUS,/TÜR AUF,/AUXON
U   M 152.1           // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
SPBN M004
R   A 4.5             // WZW-AUSBLASMAGNETVENTIL
R   M 152.6           // ANFORDERUNG WERKZEUGWENDER IM SCHRITT 4 WEITERSCHWENKEN
R   M 151.3;          // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
R   M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
R   Z 50;              // ISTZÄHLER LÖSCHEN
R   Z 52;              // SOLLZÄHLER LÖSCHEN
R   Z 51;
L   0
T   DB15.DBB  25      // WERKZEUGPOSITION IN DEN SETTINGDATEN
T   MB 160;           // SCHRITTKETTE LÖSCHEN
T   MB 161;           // SCHRITTKETTE LÖSCHEN
T   MW 165;           //
=   DB20.DBX 348.3;   // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
M004:  NOP 0
NETWORK
TITLE = WERKZEUGWENDEN ABBRECHEN BEI RESET, /NOT AUS,/TÜR AUF,/AUXON UND S1 ODER S2
O   M 150.1;          // SCHRITT 1 SPINDEL IN POSITIONIERBETRIEB SCHALTEN
O   M 150.2;          // SCHRITT 2 SPINDEL REFERENZIEREN
U   M 162.7      // FM WERKZEUGWENDEN SOFORT ABBRECHEN BEI RESET, /NOT AUS,/TÜR AUF,/AUXON
L   S5TIME#50MS;     //
SS  T 50
R   M 150.1;          // SCHRITT 1 SPINDEL IN POSITIONIERBETRIEB SCHALTEN
R   M 150.2;          // SCHRITT 2 SPINDEL REFERENZIEREN
SPBN   M005
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX 348.4;   // REFERENZPUNKT DER RUNDACHSEN DEAKTIVIEREN
M005:  NOP 0
U   T 50
L   S5TIME#50MS;     //
SS  T 51
R   A 4.4;            // FRC122C AUF SPINDELBETRIEB SCHALTEN
SPBN   M006
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX 549.3;   // HA VON POSITIONIERBETRIEB AUF SPINDEL
R   T 50
M006:  NOP 0
U   T 51
R   M 94.7;           // SPINDELFREIGABE VON KANAL 0 SPERREN
R   M 90.7;           // ACHSFREIGABE    K0
R   T 51
NETWORK
TITLE = WZW ABBRUCH MERKER AUS DEN SETTINGDATEN SETZEN  FREIFAHREN!!
U   DB1.DBX  1370.0;  // 1.SPS SCHLEIFENDURCHLAUF
U   DB15.DBX  20.4    // SCHRITT 4 IN DEN SETTINGDATEN SPEICHERN
S   M 153.0;          // WZW ABBRUCH MERKER AUS DEN SETTINGDATEN ZAHNRAD IM EINGRIFF
NETWORK
TITLE = WERKZEUGPOSITION AUS DEN SETTINGDATEN SETZEN
U   DB1.DBX  1370.0;  // 1.SPS SCHLEIFENDURCHLAUF
UN  M 153.0;          // WZW ABBRUCH MERKER AUS DEN SETTINGDATEN ZAHNRAD IM EINGRIFF
SPBN   M007
L   DB15.DBB  25;     // WERKZEUGPOSITION IN DEN SETTINGDATEN
L   0;
<>I;
SPBN   M007
L   DB15.DBB  25;     // WERKZEUGPOSITION IN DEN SETTINGDATEN
S   Z 50;
S   Z 52;
S   M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
M007:  NOP 0
NETWORK
TITLE = WERKZEUGWENDEN ABBRECHEN MIT NOT AUS,TÜR AUF,RESET UND SCHRITT 4 AKTIV  FREIFAHREN!!
U   M 162.7      // FM WERKZEUGWENDEN SOFORT ABBRECHEN BEI RESET, /NOT AUS,/TÜR AUF,/AUXON
U   M 150.4;     // SCHRITT 4 WZW AUF GÜLTIGE POSITION SCHWENKEN
S   M 153.1;     // WZW ABBRUCH MERKER 1 (NOT AUS ODER TÜR AUF UND ZAHNRAD IM EINGRIFF)
R   M 150.4;     // SCHRITT 4 WZW AUF GÜLTIGE POSITION SCHWENKEN
NETWORK
TITLE = WERKZEUGWENDEN ABBRECHEN VON NOT-AUS,TÜR AUF,RESET,AUX ON IN S3 ODER S5  FREIFAHREN!!
O   M 150.3;          // SCHRITT 3 Z-ACHSE AUF WERKZEUGWENDEPOSITION FAHREN
O   M 150.5;          // SCHRITT 5 Z-ACHSE AUF AUSGANGSPOSITION FAHREN
UN  M 153.1;     // WZW ABBRUCH MERKER 1 (NOT AUS ODER TÜR AUF UND ZAHNRAD IM EINGRIFF)
U   M 162.7      // FM WERKZEUGWENDEN SOFORT ABBRECHEN BEI RESET, /NOT AUS,/TÜR AUF,/AUXON
S   M 154.4;          // WZW ABBRUCH MERKER 4 (NOT-AUS,TÜR AUF,RESET IM SCHRITT S3,ODER S5)
R   M 150.3;          // SCHRITT 3 Z-ACHSE AUF WERKZEUGWENDEPOSITION FAHREN
R   M 150.5;          // SCHRITT 5 Z-ACHSE AUF AUSGANGSPOSITION FAHREN
NETWORK
TITLE = WZW ABBRECHEN VON NOT-AUS,TÜR AUF,RESET,AUX ON IN S3 ODER S5 UND REFPKT NICHT AKTIV
U   M 154.4;          // WZW ABBRUCH MERKER 4 (NOT-AUS,TÜR AUF,RESET IM SCHRITT S3,ODER S5)
UN  DB20.DBX 324.4;   // REFERENZPUNKT AKTIV
R   M 154.4;          // WZW ABBRUCH MERKER 4 (NOT-AUS,TÜR AUF,RESET IM SCHRITT S3,ODER S5)
R   DB3.DBX  2.5;     // 7021  WERKZEUGWENDER FREIFAHREN
R   Z 50;             // ISTZÄHLER LÖSCHEN
R   Z 52;             // SOLLZÄHLER LÖSCHEN
R   M 90.7;           // ACHSFREIGABE    K0
NETWORK
TITLE = WERKZEUGWENDER POSITIONSBERO ÜBERWACHUNG
UN  M 160.2;          // SCHRITT F2  SPINDEL REFERENZIEREN
UN  M 160.3;          // SCHRITT F3  WZW POSITIONSBERO SUCHEN
UN  M 160.4;          // SCHRITT F4  WZW EINE POSITION WEITERSCHWENKEN
UN  M 150.3;          // SCHRITT 3 Z-ACHSE AUF WERKZEUGWENDEPOSITION FAHREN
UN  M 150.4;          // SCHRITT 4 WZW AUF GÜLTIGE POSITION SCHWENKEN
UN  M 150.5;          // SCHRITT 5 Z-ACHSE AUF AUSGANGSPOSITION FAHREN
UN  M 153.0;          // WZW ABBRUCH MERKER AUS DEN SETTINGDATEN ZAHNRAD IM EINGRIFF
UN  M 153.1;          // WZW ABBRUCH MERKER 1 (NOT AUS ODER TÜR AUF UND ZAHNRAD IM EINGRIFF)
UN  M 154.4;          // WZW ABBRUCH MERKER 4 (NOT-AUS,TÜR AUF,RESET IM SCHRITT S3,ODER S5)
U   M 151.6;          // FM WERKZEUGWENDER POSITIONSBERO
S   DB2.DBX     5.4;  // 6044  WZW POSITIONSÜBERWACHUNG
NETWORK
TITLE = WERKZEUGWENDER POSITIONSBERO ÜBERWACHUNG
O   DB2.DBX     5.3;  // 6043  WZW POSITIONSÜBERWACHUNG
O   DB2.DBX     5.4;  // 6044  WZW POSITIONSÜBERWACHUNG
O   DB2.DBX     5.5;  // 6045  WZW POSITIONSÜBERWACHUNG
O   DB2.DBX     5.6;  // 6046  WZW POSITIONSÜBERWACHUNG
=   M 152.2;          // WERKZEUGWENDER POSITIONSBERO ÜBERWACHUNG
SPBN   M008;
L   0;
T   DB15.DBB  25;     // WERKZEUGPOSITION IN DEN SETTINGDATEN
=   DB20.DBX 348.3;   // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
R   M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
M008:  NOP 0
NETWORK
TITLE = WZW-BERO ÜBERWACHUNG RESETIEREN
O   DB1.DBX  1370.0;  // 1.SPS SCHLEIFENDURCHLAUF
O   DB1.DBX  1370.2;  // QUITTIERTASTE GEDRÜCKT (MSTT)
O   DB1.DBX  1370.3;  // RESETTASTE GEDRÜCKT
R   DB2.DBX     5.3;  // 6043  WZW POSITIONSÜBERWACHUNG
R   DB2.DBX     5.4;  // 6044  WZW POSITIONSÜBERWACHUNG
R   DB2.DBX     5.5;  // 6045  WZW POSITIONSÜBERWACHUNG
R   DB2.DBX     5.6;  // 6046  WZW POSITIONSÜBERWACHUNG
NETWORK
TITLE = WZW FREIGABE
UN  M 94.0;           // AUX-ON  SFG
UN  M 94.1;           // HA BETRIEBSBEREIT  SFG
UN  M 113.0;          // SONDERBETRIEB AKTIV
UN  DB3.DBX  0.1;     // 7001  KEIN M6 PROGRAMMIERT
//;
=   M 151.1;          // WZW FREIGABE
NETWORK
TITLE = WZW/SCHWENKEN VON WZW-TASTE
O   M 151.5;          // FM WZW-TASTE
UN  M 100.2;          // VORSCHUBSCHALTER AUF 0%
UN  DB20.DBX  326.1;  // BETRIEBSART EDIT
UN  DB20.DBX  326.2;  // BETRIEBSART MDI
UN  DB20.DBX  326.4;  // BETRIEBSART AUTOMATIK
UN  DB20.DBX  327.2;  // BETRIEBSART REFERENZ
O   DB20.DBX  302.0;  // DNC/WZW SCHWENKEN
U   M 114.3;          // HA ISTDREHZAHL KLEINER 20U/MIN
UN  M 152.0;          // WERKZEUGWECHSEL AKTIV
UN  DB20.DBX  324.0;  // PROGRAMM LÄUFT
//;
=   M 151.4           // WERKZEUGWENDER START VON WZW-TASTE
NETWORK
TITLE = MIT DER WZW-TASTE DEN POSITIONSWERT FÜR HAUPSPINDEL ÜBERGEBEN
U   M 151.4;          // WERKZEUGWENDER START VON WZW-TASTE
U   M 151.1;          // WZW FREIGABE
SPBN   M009;
S   M 155.3;          // SOLLZÄHLER VORWÄRTS ZÄHLEN
L   DB10.DBD 26;      // POSITIONS-WERT AUS MSD-FILE(720 = 2 HAUPTSPINDELUMDR.)
T   MD 156;           // DOPPELWORT MIT SCHWENKPOSITION IN GRAD FÜR HAUPTSPINDEL (FLOATWWERT)
U   DB20.DBX 295.7    // TASTE EILGANGSÜBERLAGERUNG
U   M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
SPBN   M009;
S   M 155.4;          // SOLLZÄHLER RÜCKWÄRTS ZÄHLEN
R   M 155.3;          // SOLLZÄHLER VORWÄRTS ZÄHLEN
L   MD 156;           // DOPPELWORT MIT SCHWENKPOSITION IN GRAD FÜR HAUPTSPINDEL (FLOATWWERT)
L   -1E+00;           // WZW-SCHWENKRICHTUNG UMKEHREN
*R;
T   MD 156;           // DOPPELWORT MIT SCHWENKPOSITION IN GRAD FÜR HAUPTSPINDEL (FLOATWWERT)
M009:  NOP 0;
NETWORK
TITLE = SOLLZÄHLER VON WZW-TASTE SETZEN
U   M 155.3;          // SOLLZÄHLER VORWÄRTS ZÄHLEN
ZV  Z 52;
R   M 155.3;          // SOLLZÄHLER VORWÄRTS ZÄHLEN
L   Z 52;
L   11;
>=I;
L   Z#1;
S   Z 52;
U   M 155.4;          // SOLLZÄHLER RÜCKWÄRTS ZÄHLEN
ZR  Z 52;
R   M 155.4;          // SOLLZÄHLER RÜCKWÄRTS ZÄHLEN
L   Z 52;
L   0;
==I;
L   Z#10;
S   Z 52;
NETWORK
TITLE = 7000  FALSCHES T-WORT PROGRAMMIERT
L   DB20.DBW 184;     // LADE T-WORT
L   10;               // LADE KONSTANTE DEZ 10
>I;                   // VERGLEICH AUF GRÖSSER
=   M 155.0           // T-WORT GRÖSSER 10
L   DB20.DBW 184;     // LADE T-WORT
L   1;                // LADE KONSTANTE DEZ 1
<I;                   // VERGLEICH AUF KLEINER
=   M 155.1           // T-WORT KLEINER 1
O   M 155.0           // T-WORT GRÖSSER 10
O   M 155.1           // T-WORT KLEINER 1
U   DB20.DBX 182.0    // NEUES T-WORT (WERKZEUG)
S   DB3.DBX  0.0;     // 7000  FALSCHES T-WORT
U   DB3.DBX  0.0;     // 7000  FALSCHES T-WORT
=   M 90.2;           // ACHSFREIGABE    K0
=   M 94.5;           // SPINDELFREIGABE VON KANAL 0 SPERREN
U   DB1.DBX  1370.3;  // TASTE RESET GEDRÜCKT (MSTT)
R   DB3.DBX  0.0;     // 7000  FALSCHES T-WORT
NETWORK
TITLE = T-WORT IST GÜLTIG
L   DB20.DBW 184;     // T-WORT
L   Z 50;             // ISTZÄHLER
<>I;                  // VERGLEICH AUF UNGLEICH
=   M 155.2           // T-WORT UNGLEICH ISTZÄHLER
NETWORK
TITLE = SOLLZÄHLER SETZEN ZUM ÜBERWACHEN OB M6 PROGRAMMIERT WIRD (NUR AUTOMATIKBETRIEB)
U   M 155.2           // T-WORT UNGLEICH ISTZÄHLER
U   DB20.DBX 182.0    // NEUES T-WORT (WERKZEUG)
U   DB20.DBX  326.4;  // BETRIEBSART AUTOMATIK
UN  DB3.DBX 0.0;      // 7000  FALSCHES T-WORT
S   M 155.6           // NEUES T-WORT GESPEICHERT FÜR MELDUNG 7001
SPBN M111
L   DB20.DBW 184;     // T-WORT
S   Z 52;             // SOLLZÄHLER
M111:  NOP 0
NETWORK
TITLE = T-WORT IST GÜLTIG HAUPTSPINDEL STOPPEN
O   M 155.2           // T-WORT UNGLEICH ISTZÄHLER
U   M 151.1;          // WZW FREIGABE
UN  DB3.DBX 0.0;      // 7000  FALSCHES T-WORT
U(
O   DB20.DBX 192.6;   // M6 DYN.
O(
U   DB20.DBX 182.0    // NEUES T-WORT (WERKZEUG)
U   DB1.DBX 1402.2;   // EMCOTRONIC TM02 OBERFLÄCHE AKTIV
)
)
S   M 94.7;           // SPINDELFREIGABE VON KANAL 0 SPERREN
S   M 90.7;           // ACHSFREIGABE    K0
S   M 151.2;          // T-WORT IST GÜLTIG
R   M 155.6           // NEUES T-WORT GESPEICHERT FÜR MELDUNG 7001
SPBN M211
L   DB20.DBW 184;     // T-WORT
S   Z 52;             // SOLLZÄHLER
M211:  NOP 0
NETWORK
TITLE = 7001  KEIN M6 PROGRAMMIERT
L   Z 50;             // ISTZÄHLER
L   Z 52;             // SOLLZÄHLER
<>I;                  // VERGLEICH AUF UNGLEICH
=   M 155.5           // SOLLZÄHLER UNGLEICH ISTZÄHLER
O   DB20.DBX 544.0    // GERADENBEWEGUNG AKTIV
O   DB20.DBX 544.1    // GERADE MIT EILGANG AKTIV
O   DB20.DBX 544.2    // KREISBEWEGUNG AKTIV
O   DB20.DBX 544.3    // HELIX AKTIV
O   DB20.DBX 544.4    // GEWINDE AKTIV
O   DB20.DBX 544.5    // C-ACHS-BETRIEB
U   DB20.DBX  326.4;  // BETRIEBSART AUTOMATIK
UN  DB20.DBX  324.1;  // STOP-ZUSTAND
UN  DB20.DBX  324.3;  // SATZVORLAUF
U   M 155.5           // SOLLZÄHLER UNGLEICH ISTZÄHLER
U   M 155.6           // NEUES T-WORT GESPEICHERT FÜR MELDUNG 7001
S   DB3.DBX  0.1;     // 7001  KEIN M6 PROGRAMMIERT
R   M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
R   Z50
R   Z52
U   DB3.DBX  0.1;     // 7001  KEIN M6 PROGRAMMIERT
=   M 90.4;           // ACHSFREIGABE    K0
=   M 94.6;           // SPINDELFREIGABE VON KANAL 0 SPERREN
U   DB1.DBX  1370.3;  // TASTE RESET GEDRÜCKT (MSTT)
R   DB3.DBX  0.1;     // 7001  KEIN M6 PROGRAMMIERT
R   M 155.6           // NEUES T-WORT GESPEICHERT FÜR MELDUNG 7001
NETWORK
TITLE = WZW-SCHWENKEN VON T-WORT
U   M 151.2;          // T-WORT IST GÜLTIG
U   M 114.3;          // HA ISTDREHZAHL KLEINER 20U/MIN
UN  DB20.DBX 340.5;   // SPINDELFREIGABE
S   M 151.3;          // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
R   M 151.2;          // T-WORT IST GÜLTIG
O   DB1.DBX  1370.3;  // RESETTASTE GEDRÜCKT
O   DB1.DBX  1440.0;  // RESET AUGELÖST
U   M 151.2;          // T-WORT IST GÜLTIG
UN  M 151.3;          // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
R   M 94.7;           // SPINDELFREIGABE VON KANAL 0 SPERREN
R   M 90.7;           // ACHSFREIGABE    K0
R   M 151.2;          // T-WORT IST GÜLTIG
NETWORK
TITLE = WEITERSCHWENKEN WENN DER WZW NICHT REFERENZIERT IST
U   M 151.3;          // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
UN  M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
//;
S   M 152.6;          // ANFORDERUNG WERKZEUGWENDER IM SCHRITT 4 WEITERSCHWENKEN
NETWORK
TITLE = 7021 MELDUNG WERKZEUGWENDER FREIFAHREN
O   M 153.0;          // WZW ABBRUCH MERKER AUS DEN SETTINGDATEN ZAHNRAD IM EINGRIFF
O   M 153.1;          // WZW ABBRUCH MERKER 1 (NOT AUS)
O   M 154.4;          // WZW ABBRUCH MERKER 4 (NOT-AUS,TÜR AUF,RESET IM SCHRITT S3,ODER S5)
UN  DB3.DBX     5.2;  // 7042  MASCHINENTÜR INIT.
UN  DB3.DBX     2.4;  // 7020  SONDERBETRIEB AKTIV
UN  DB3.DBX     5.0;  // 7040  MASCHINENTÜR OFFEN
UN  DB3.DBX     2.0;  // 7016  HILFSANTRIEBE EINSCHALTEN
UN  DB3.DBX     2.5;  // 7021  WERKZEUGWENDER FREIFAHREN
UN  DB3.DBX     2.0;  // 7016  HILFSANTRIEBE EINSCHALTEN
S   DB3.DBX     2.5;  // 7021  WERKZEUGWENDER FREIFAHREN
U   DB3.DBX     5.0;  // 7040  MASCHINENTÜR OFFEN
R   DB3.DBX     2.5;  // 7021  WERKZEUGWENDER FREIFAHREN
NETWORK
TITLE = WERKZEUGWENDEN ODER FREIFAHREN AKTIV
O   M 152.0;          // WERKZEUGWENDEN AKTIV
O   M 162.0;          // WERKZEUGWENDER FREIFAHREN AKTIV
=   M 152.1;          // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
NETWORK
TITLE = WERKZEUGPOSITIONEN VORWAERTS ZÄHLEN
U   M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
U   M 150.4;          // SCHRITT 4 WZW AUF GÜLTIGE POSITION SCHWENKEN
U   M 151.6;          // FM WERKZEUGWENDER POSITIONSBERO
U   M 154.5;          // - NEUE WZW-TROMMEL POSITION IST IN MINUSRICHTUNG
ZV  Z 50;
SPBN M010;
CLR;
S   Z 50;
L   Z 50;
L   25;
>=I;
L   Z#1;
S   Z 50;
L   Z 50;
L   11;
>=I;
L   Z#25;
S   Z 50;
M010:  NOP 0;
NETWORK
TITLE = WERKZEUGPOSITIONEN RÜCKWAERTS ZÄHLEN
U   M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
U   M 150.4;          // SCHRITT 4 WZW AUF GÜLTIGE POSITION SCHWENKEN
U   M 151.6;          // FM WERKZEUGWENDER POSITIONSBERO
U   M 154.6;          // + NEUE WZW-TROMMEL POSITION IST IN PLUSRICHTUNG
ZR  Z 50;
SPBN M011;
CLR;
S   Z 50;
L   Z 50;
L   18;
>=I;
L   Z#10;
S   Z 50;
L   Z 50;
L   0;
==I;
L   Z#19;
S   Z 50;
M011:  NOP 0;
NETWORK
TITLE = WERKZEUGPOSITION IN DEN SETTINGDATEN SPEICHERN
U   M 150.4;          // SCHRITT 4 WZW AUF GÜLTIGE POSITION SCHWENKEN
U   M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
SPBN M012;
L   Z 50;             // LADE ISTZÄHLER
T   DB20.DBW 356;     // AKTIVES WERKZEUG IN DB20
T   DB15.DBB 25;      // WERKZEUGPOSITION IN DEN SETTINGDATEN
=   DB20.DBX 348.3;   // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
M012:  NOP 0;
NETWORK
TITLE = STEUERUNG ZUM REFERENZIEREN DES WERKZEUGWENDERS
UN  M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
UN  M 153.0;          // WZW ABBRUCH MERKER AUS DEN SETTINGDATEN ZAHNRAD IM EINGRIFF
UN  M 153.1;          // WZW ABBRUCH MERKER 1 (NOT AUS UND ZAHNRAD IM EINGRIFF)
UN  M 154.4;          // WZW ABBRUCH MERKER 4 (NOT-AUS,TÜR AUF,RESET IM SCHRITT S3,ODER S5)
CC    FC 50;            // STEUERUNG ZUM REFERENZIEREN DES WERKZEUGWENDERS
NETWORK
TITLE = STEUERUNG ZUM SCHWENKEN DES WERKZEUGWENDERS
U   M 151.3;          // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
U   M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
U(
O   M 150.3           // SCHRITT 3 Z-ACHSE AUF WERKZEUGWENDEPOSITION FAHREN
O   M 152.6           // ANFORDERUNG WERKZEUGWENDER IM SCHRITT 4 WEITERSCHWENKEN
)
UN  M 153.0;          // WZW ABBRUCH MERKER AUS DEN SETTINGDATEN ZAHNRAD IM EINGRIFF
UN  M 153.1;          // WZW ABBRUCH MERKER 1 (NOT AUS UND ZAHNRAD IM EINGRIFF)
UN  M 154.4;          // WZW ABBRUCH MERKER 4 (NOT-AUS,TÜR AUF,RESET IM SCHRITT S3,ODER S5)
CC    FC 51;            // STEUERUNG ZUM SCHWENKEN DES WERKZEUGWENDERS
NETWORK
TITLE = WERKZEUGWENDER FREIFAHREN
O   M 153.0;          // WZW ABBRUCH MERKER AUS DEN SETTINGDATEN ZAHNRAD IM EINGRIFF
O   M 153.1;          // WZW ABBRUCH MERKER 1 (NOT AUS UND ZAHNRAD IM EINGRIFF)
O   M 154.4;          // WZW ABBRUCH MERKER 4 (NOT-AUS,TÜR AUF,RESET IM SCHRITT S3,ODER S5)
UN  M 152.0;          // WERKZEUGWENDEN AKTIV
CC    FC 52;            // WERKZEUGWENDER FREIFAHREN
NETWORK
TITLE = WERKZEUGWENDER REFERENZIEREN UND SCHWENKEN
UN  M 153.0;          // WZW ABBRUCH MERKER AUS DEN SETTINGDATEN ZAHNRAD IM EINGRIFF
UN  M 153.1;          // WZW ABBRUCH MERKER 1 (NOT AUS UND ZAHNRAD IM EINGRIFF)
UN  M 154.4;          // WZW ABBRUCH MERKER 4 (NOT-AUS,TÜR AUF,RESET IM SCHRITT S3,ODER S5)
O   M 152.0;          // WERKZEUGWENDEN AKTIV
CC    FC 53;            // WERKZEUGWENDER REFERENZIEREN UND SCHWENKEN
END_FUNCTION
FUNCTION FC 51 : VOID
NAME: STEUERUNG ZUM SCHWENKEN DES WERKZEUGWENDERS
BEGIN
NETWORK
TITLE = DIFFERENZ T-WORT/ISTZÄHLER
L   DB20.DBW 184;     // LADE T-WORT
L   Z 50;             // LADE ISTZÄHLER
-I;                   // SUBTRAKTION
T   MW 156;           // TRANSFERIERE MW156
NETWORK
TITLE = ZÄHLERDIFFERENZ < 0
L   MW 156;           // LADE MW 156
L   0;                // LADE KONSTANTE DEZ 0
>=I;                  // VERGLEICH AUF GRÖSSER GLEICH
SPB M001;
//;
L   MW 156;           // LADE MW 156
L   10;               // LADE KONSTANTE DEZ 10
+I;                   // ADDITION
T   MW 156;           // TRANSFERIERE IN MW 156
M001: NOP 0;
NETWORK
TITLE = ZÄHLERDIFFERENZ > 5
L   MW 156;           // LADE MW 156
L   5;                // LADE KONSTANTE DEZ 5
<=I;                  // VERGLEICH AUF KLEINER GLEICH
SPB M002;
//;
L   MW 156;           // LADE MW 156
L   10;               // LADE KONSTANTE DEZ 10
-I;                   // SUBTRAKTION
T   MW 156;           // TRANSFERIERE IN MW 156
M002: NOP 0;
//;
NETWORK
TITLE = AUSGEBEN EINER NEUEN POSITION FÜR HAUPTSPINDEL
L   MW 156;           // LADE MW 156
ITD                   // VON 16 BIT GANZZAHL AUF 32 BIT GANZZAHL UMWANDELN
T   MD 156;
L   MD 156;           // LADE MD 156
DTR                   // VON 16 BIT GANZZAHL AUF 32 BIT FLOATWERT UMWANDELN
T   MD 156;
L   DB10.DBD 26;      // POSITIONS-WERT AUS MSD-FILE(720 = 2 HAUPTSPINDELUMDR.)
L   MD 156;           // LADE MD 156
*R
T   MD 156;           // DOPPELWORT MIT SCHWENKPOSITION IN GRAD FÜR HAUPTSPINDEL (FLOATWWERT)
SET;
R   M 152.6;          // ANFORDERUNG WERKZEUGWENDER IM SCHRITT 4 WEITERSCHWENKEN
END_FUNCTION
FUNCTION FC 50 : VOID
NAME: STEUERUNG ZUM REFERENZIEREN DES WERKZEUGWENDERS
BEGIN
NETWORK
TITLE = WERKZEUGWENDER REFERENZIEREN
U   M 150.4;          // SCHRITT 4 WZW AUF GÜLTIGE POSITION SCHWENKEN
U   M 151.6;          // FM WERKZEUGWENDER POSITIONSBERO
ZV  Z 50;             // IST-ZÄHLWER VORWÄRTZ ZÄHLEN
L   Z 50;             // IST-ZÄHLER
L   2;                // LADE 2
==I;                  // VERGLEICH AUF GLEICH
S   M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
L   1;                // LADE 1
S   Z 50;             // ISTZÄHLER AUF 1 SETZEN
U   M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
UN  M 151.3;          // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
L   1;                // LADE 1
S   Z 52;             // SOLLZÄHLER AUF 1 SETZEN
U   M 150.4;          // SCHRITT 4 WZW AUF GÜLTIGE POSITION SCHWENKEN
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
UN  M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
R   Z 50;             // ISTZÄHLER LÖSCHEN
U   M 152.6;          // ANFORDERUNG WERKZEUGWENDER IM SCHRITT 4 WEITERSCHWENKEN
=   M 152.7;          // WERKZEUGWENDER IM SCHRITT 4 WEITERSCHWENKEN
SPBN M001
L   DB10.DBD 26;      // POSITIONS-WERT AUS MSD-FILE(720 = 2 HAUPTSPINDELUMDR.)
T   MD 156;           // DOPPELWORT MIT SCHWENKPOSITION IN GRAD FÜR HAUPTSPINDEL (FLOATWWERT)
M001:  NOP 0
END_FUNCTION
FUNCTION FC 53 : VOID
NAME: WERKZEUGWENDER REFERENZIEREN UND SCHWENKEN
BEGIN
NETWORK
TITLE = SCHRITT 1  SPINDEL IN POSITIONIERBETRIEB SCHALTEN
O   M 151.3;          // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
O   M 151.4;          // WERKZEUGWENDER START VON WZW-TASTE
UN  DB3.DBX     2.5;  // 7021  WERKZEUGWENDER FREIFAHREN
UN  M 152.0;          // WERKZEUGWENDEN AKTIV
U   M 151.1;          // WZW FREIGABE
U   DB20.DBX  324.4;  // REFERENZPUNKT AKTIV
S   M 150.1;          // SCHRITT 1
SPBN M101;
S   M 94.7;           // SPINDELFREIGABE K0
S   M 90.7;           // ACHSFREIGABE    K0
S   DB25.DBX 340.3;   // EINLESEFREIGABE K1
S   DB25.DBX 340.4;   // ACHSFREIGABE    K1
S   A 4.4;            // FRC122C AUF POSITIONIERBETRIEB SCHALTEN
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX 549.2;   // SPINDEL in den Positionierbetrieb
M101:  NOP 0;
U   DB10.DBX 2.0;     // ACC AKTIVIEREN
U   A 4.4;            // FRC122C AUF POSITIONIERBETRIEB SCHALTEN
=   M 145.6;          // REGLERFREIGABE FÜR HAUPTANTRIEB für Werkzeugwenden
NETWORK
TITLE = SCHRITT 2  SPINDEL REFERENZIEREN
U   M 150.1;          // SCHRITT 1
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
R   M 150.1;          // SCHRITT 1
S   M 150.2;          // SCHRITT 2
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
SPBN M102;
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX 548.5;   // GENAUHALTMODUS SETZEN
=   DB25.DBX 549.0;   // G1 WIRD AUSGELÖST
=   DB25.DBX 549.4;   // VORSCHUB IN GRAD/S WERT IN DBD556
L   +2.88E+03;
T   DB25.DBD 556;     // F-WERT IN M/S ODER M/U
S   DB25.DBX 21.4;    // POSITIONSANFORDERUNG FÜR KANALACHSE C
L   DB10.DBD 18;      // POSITIONS-OFFSETWERT AUS MSD-FILE
T   DB25.DBD 70;      // POSITIONSWERT FÜR KANALACHSE C
M102:  NOP 0;
NETWORK
TITLE = SCHRITT 3  Z-ACHSE AUF WERKZEUGWENDEPOSITION FAHREN
U   M 150.2;          // SCHRITT 2
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
R   M 150.2;          // SCHRITT 2
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
S   M 150.3;          // SCHRITT 3
SPBN M103;
S   DB15.DBX  20.3    // SCHRITT 3 IN DEN SETTINGDATEN SPEICHERN
=   DB20.DBX 348.3;   // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX 548.5;   // GENAUHALTMODUS SETZEN
=   DB25.DBX 549.0;   // G1 WIRD AUSGELÖST
=   DB25.DBX 548.2;   // VORSCHUB IN METER/SEKUNDE WERT IN DBD556
L   +0.05E+00;        // 0.02 x 60 = 1.2 METER/MINUTE
T   DB25.DBD 556;     // F-WERT IN M/S ODER M/U
=   DB25.DBX 20.2;    // POSITIONSANFORDERUNG FÜR Z-ACHSE
L   DB10.DBD 22;      // POSITIONSWERT FÜR Z-ACHSE AUS MSD-FILE
T   DB25.DBD 30;      // POSITIONSWERT FÜR Z-ACHSE
M103:  NOP 0;
NETWORK
TITLE = WZW-BERO ÜBERWACHUNG BEIM HOCHFAHREN
U   M 150.3;          // SCHRITT 3
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
UN  E 4.0;            // WERKZEUGWENDER POSITIONSBERO
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
S   DB2.DBX     5.5;  // 6045  WZW POSITIONSÜBERWACHUNG
NETWORK
TITLE = SCHRITT 4  WZW AUF GÜLTIGE POSITION SCHWENKEN
U   M 150.3;          // SCHRITT 3
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
O;
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
U   M 152.7;          // WERKZEUGWENDER IN SCHRITT 4 WEITERSCHWENKEN
U   M 150.4;          // SCHRITT 4
R   M 150.3;          // SCHRITT 3
S   M 150.4;          // SCHRITT 4
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
SPBN M104;
R   M 152.7;          // WERKZEUGWENDER IN SCHRITT 4 WEITERSCHWENKEN
L   0;                // WERKZEUGPOSITION IN DEN SETTINGDATEN LÖSCHEN
T   DB15.DBB  25;     // GÜLTIGE WERKZEUGPOSITION IN DEN SETTINGDATEN
R   DB15.DBX  20.3;   // SCHRITT 3 IN DEN SETTINGDATEN LÖSCHEN
S   DB15.DBX  20.4;   // SCHRITT 4 IN DEN SETTINGDATEN SPEICHERN
=   DB20.DBX 348.3;   // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
R   M 154.5;          // - NEUE WZW-TROMMEL POSITION IST IN MINUSRICHTUNG
R   M 154.6;          // + NEUE WZW-TROMMEL POSITION IST IN PLUSRICHTUNG
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX 548.5;   // GENAUHALTMODUS SETZEN
=   DB25.DBX 549.0;   // G1 WIRD AUSGELÖST
=   DB25.DBX 549.4;   // VORSCHUB IN GRAD/S WERT IN DBD556
L   DB10.DBD 34;      // VORSCHUBWERT IN GRAD/SEKUNDEN
T   DB25.DBD 556;     // F-WERT IN M/S ODER M/U
S   DB25.DBX 21.4;    // POSITIONSANFORDERUNG FÜR KANALACHSE C
L   DB25.DBD 70;      // POSITIONSWERT FÜR KANALACHSE C
L   MD 156;           // DOPPELWORT MIT SCHWENKPOSITION IN GRAD FÜR HAUPTSPINDEL (FLOATWWERT)
+R;
T   DB25.DBD 70;      // POSITIONSWERT FÜR KANALACHSE C
L   MD 156;           // DOPPELWORT MIT SCHWENKPOSITION IN GRAD FÜR HAUPTSPINDEL (FLOATWWERT)
L   0;
<R;
S   M 154.5;          // - NEUE WZW-TROMMEL POSITION IST IN MINUSRICHTUNG
L   MD 156;           // DOPPELWORT MIT SCHWENKPOSITION IN GRAD FÜR HAUPTSPINDEL (FLOATWWERT)
L   0;
>R;
S   M 154.6;          // + NEUE WZW-TROMMEL POSITION IST IN PLUSRICHTUNG
L   0;                // ALTEN POSITIONSWERT LÖSCHEN
T   MD 156;           // DOPPELWORT MIT SCHWENKPOSITION IN GRAD FÜR HAUPTSPINDEL (FLOATWWERT)
M104:  NOP 0;
NETWORK
TITLE = WERKZEUGPOSITION ÜBERPRÜFEN
U   M 150.4;          // SCHRITT 4 WZW AUF GÜLTIGE POSITION SCHWENKEN
UN  M 152.7;          // WERKZEUGWENDER IN SCHRITT 4 WEITERSCHWENKEN
U   M 151.0;          // WERKZEUGWENDER IST REFERENZIERT
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
SPBN   M019;
L   Z 50;
L   Z 52
<>I;
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
S   DB2.DBX     5.6;  // 6046  WZW POSITIONSÜBERWACHUNG
M019:  NOP 0;
NETWORK
TITLE = WZW-BERO ÜBERWACHUNG VOR DEM RUNTERFAHREN
UN  M 152.7;          // WERKZEUGWENDER IN SCHRITT 4 WEITERSCHWENKEN
U   M 150.4;          // SCHRITT 4
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
UN  E 4.0;            // WERKZEUGWENDER POSITIONSBERO
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
S   DB2.DBX     5.3;  // 6043  WZW POSITIONSÜBERWACHUNG
NETWORK
TITLE = SCHRITT 5  Z-ACHSE AUF AUSGANGSPOSITION FAHREN
UN  M 152.7;          // WERKZEUGWENDER IN SCHRITT 4 WEITERSCHWENKEN
U   M 150.4;          // SCHRITT 4
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
U   E 4.0;            // WERKZEUGWENDER POSITIONSBERO
O   M 162.1           // VON FREIFAHRBAUSTEIN AUF REF.POS. FAHREN
R   M 150.4;          // SCHRITT 4
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
R   M 162.1           // VON FREIFAHRBAUSTEIN AUF REF.POS. FAHREN
S   M 150.5;          // SCHRITT 5
SPBN M105;
S   DB15.DBX  20.5    // SCHRITT 5 IN DEN SETTINGDATEN SPEICHERN
R   DB15.DBX  20.4    // SCHRITT 4 IN DEN SETTINGDATEN LÖSCHEN
=   DB20.DBX 348.3;   // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX 548.5;   // GENAUHALTMODUS SETZEN
=   DB25.DBX 549.0;   // G1 WIRD AUSGELÖST
=   DB25.DBX 548.2;   // VORSCHUB IN METER/SEKUNDE WERT IN DBD556
S   A 4.5             // WZW-AUSBLASMAGNETVENTIL
L   +0.05E+00;        // 0.02 x 60 = 1.2 METER/MINUTE
T   DB25.DBD 556;     // F-WERT IN M/S ODER M/U
=   DB25.DBX 20.2;    // POSITIONSANFORDERUNG FÜR Z-ACHSE
L   DB1.DBD 78;       // REFERENZPOSITION Z-ACHSE
T   DB25.DBD 30;      // POSITIONSWERT FÜR Z-ACHSE
M105:  NOP 0;
NETWORK
TITLE = SCHRITT 6 WERKZEUGWENDE-MODE VERLASSEN
U   M 150.5;          // SCHRITT 5
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
S   M 150.6;          // SCHRITT 6
R   M 150.5;          // SCHRITT 5
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
SPBN M106;
R   DB15.DBX  20.5    // SCHRITT 5 IN DEN SETTINGDATEN LÖSCHEN
=   DB20.DBX 348.3;   // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
R   A 4.4;            // FRC122C AUF SPINDELBETRIEB SCHALTEN
R   A 4.5             // WZW-AUSBLASMAGNETVENTIL
R   DB3.DBX  2.5;     // 7021  WERKZEUGWENDER FREIFAHREN
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX 549.3;   // HA VON POSITIONIERBETRIEB AUF SPINDEL
M106:  NOP 0
NETWORK
TITLE = WERKZEUGWENDEN BEENDEN
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
U   M 150.6;          // SCHRITT 6
SPBN M107;
L   0;
T   MB 150;           // SCHRITTKETTE LÖSCHEN
T   MB 154;           // ABBRUCHKETTE LÖSCHEN
T   MD 156;           // DOPPELWORT MIT SCHWENKPOSITION IN GRAD FÜR HAUPTSPINDEL (FLOATWWERT)
R   M 151.3;          // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
R   DB3.DBX  2.5;     // 7021  WERKZEUGWENDER FREIFAHREN
R   DB25.DBX 340.3;   // EINLESEFREIGABE K1
R   DB25.DBX 340.4;   // ACHSFREIGABE    K1
R   M 94.7;           // SPINDELFREIGABE K0
R   M 90.7;           // ACHSFREIGABE    K0
R   Z 51;
M107:  NOP 0
NETWORK
TITLE = WERKZEUGWENDEN AKTIV
L   MB 150;            // SCHRITTKETTE
L   0;
<>I;
=   M 152.0;          // WERKZEUGWENDEN AKTIV
END_FUNCTION
FUNCTION FC 52 : VOID
NAME: WZW TROMMEL FREIFAHREN
BEGIN
NETWORK
TITLE = SCHRITT F1  SPINDEL IN POSITIONIERBETRIEB SCHALTEN
UN  M 154.4;          // WZW ABBRUCH MERKER 4 (NOT-AUS,TÜR AUF,RESET IM SCHRITT S3,ODER S5)
UN  M 162.0;          // WERKZEUGWENDER FREIFAHREN AKTIV
U   M 151.1;          // WZW FREIGABE
U   M 151.4           // WERKZEUGWENDER START VON WZW-TASTE
SPBN M101;
S   M 160.1;          // SCHRITT 1
S   M 94.7;           // SPINDELFREIGABE K0
S   M 90.7;           // ACHSFREIGABE    K0
S   DB25.DBX 340.3;   // EINLESEFREIGABE K1
S   DB25.DBX 340.4;   // ACHSFREIGABE    K1
S   A 4.4;            // FRC122C AUF POSITIONIERBETRIEB SCHALTEN
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX 549.2;   // SPINDEL in den Positionierbetrieb
M101:  NOP 0;
NETWORK
TITLE = SCHRITT F2  SPINDEL REFERENZIEREN
U   M 160.1;          // SCHRITT 1
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
R   M 160.1;          // SCHRITT 1
S   M 160.2;          // SCHRITT 2
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
SPBN M102;
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX 548.5;   // GENAUHALTMODUS SETZEN
=   DB25.DBX 549.0;   // G1 WIRD AUSGELÖST
=   DB25.DBX 549.4;   // VORSCHUB IN GRAD/S WERT IN DBD556
L   +2.88E+03;
T   DB25.DBD 556;     // F-WERT IN M/S ODER M/U
S   DB25.DBX 21.4;    // POSITIONSANFORDERUNG FÜR KANALACHSE C
L   DB10.DBD 18;      // POSITIONS-OFFSETWERT AUS MSD-FILE
T   DB25.DBD 70;      // POSITIONSWERT FÜR KANALACHSE C
M102:  NOP 0;
NETWORK
TITLE = SCHRITT F3  WZW POSITIONSBERO SUCHEN
U   M 160.2;          // SCHRITT 2
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
O   M 161.4;          // FM SONDERAUSLÖSUNG SCHRITT F3
O   M 161.5;          // FM SONDERAUSLÖSUNG SCHRITT F3
R   M 160.2;          // SCHRITT F2
S   M 160.3;          // SCHRITT F3
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
SPBN M103;
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX 548.5;   // GENAUHALTMODUS SETZEN
=   DB25.DBX 549.0;   // G1 WIRD AUSGELÖST
=   DB25.DBX 549.4;   // VORSCHUB IN GRAD/S WERT IN DBD556
L   DB10.DBD 34;      // VORSCHUBWERT IN GRAD/SEKUNDEN
T   DB25.DBD 556;     // F-WERT IN M/S ODER M/U
S   DB25.DBX 21.4;    // POSITIONSANFORDERUNG FÜR KANALACHSE C
L   DB10.DBD 26;      // POSITIONS-WERT AUS MSD-FILE(720 = 2 HAUPTSPINDELUMDR.)
L +8.0E+00
/R
L   DB25.DBD 70;      // POSITIONSWERT FÜR KANALACHSE C
+R;
T   DB25.DBD 70;      // POSITIONSWERT FÜR KANALACHSE C
M103:  NOP 0;
NETWORK
TITLE = SONDERAUSLÖSUNG VON SCHRITT F3 VON F3
U   M 160.3;          // SCHRITT F3
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
UN  E 4.0             // WERKZEUGWENDER POSITIONSBERO
FP  M 161.4;          // FM SONDERAUSLÖSUNG SCHRITT F3 VON F3
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
NETWORK
TITLE = SCHRITT F4  WZW EINE POSITION WEITERSCHWENKEN
U   M 160.3;          // SCHRITT F3
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
U   E 4.0             // WERKZEUGWENDER POSITIONSBERO
S   M 160.4;          // SCHRITT F4
R   M 160.3;          // SCHRITT F3
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
SPBN M105;
R   Z 51;
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX 548.5;   // GENAUHALTMODUS SETZEN
=   DB25.DBX 549.0;   // G1 WIRD AUSGELÖST
=   DB25.DBX 549.4;   // VORSCHUB IN GRAD/S WERT IN DBD556
L   DB10.DBD 34;      // VORSCHUBWERT IN GRAD/SEKUNDEN
T   DB25.DBD 556;     // F-WERT IN M/S ODER M/U
S   DB25.DBX 21.4;    // POSITIONSANFORDERUNG FÜR KANALACHSE C
L   DB10.DBD 26;      // POSITIONS-WERT AUS MSD-FILE(720 = 2 HAUPTSPINDELUMDR.)
L   DB25.DBD 70;      // POSITIONSWERT FÜR KANALACHSE C
+R;
T   DB25.DBD 70;      // POSITIONSWERT FÜR KANALACHSE C
M105:  NOP 0;
NETWORK
TITLE = SONDERAUSLÖSUNG VON SCHRITT F3 VON F4
U   M 160.4;          // SCHRITT F4
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
UN  E 4.0;             // WERKZEUGWENDER POSITIONSBERO
FP  M 161.5;          // FM SONDERAUSLÖSUNG SCHRITT F3
R   M 160.4;          // SCHRITT F4
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
NETWORK
TITLE = ZÄHLER FÜR SONDERAUSLÖSUNG VON SCHRITT F5.1 ODER F5.2
O   M 161.4;          // FM SONDERAUSLÖSUNG SCHRITT F3 VON F3
ZV  Z 51;
L   Z 51;
L   9;
>=I;
R   Z 51;
S   M 160.4;          // SCHRITT F4
S   DB25.DBX 332.0;   // NC-SATZ FERTIG
NETWORK
TITLE = SCHRITT F5.1 WENN DER REFPKT. AKTIV IST Z-ACHSE HERUNTERFAHREN (SCHRITT4)
U   M 154.4;          // WZW ABBRUCH MERKER 4 (NOT-AUS,TÜR AUF,RESET IM SCHRITT S3,ODER S5)
UN  M 162.0;          // WERKZEUGWENDER FREIFAHREN AKTIV
U   M 151.1;          // WZW FREIGABE
U   M 151.4           // WERKZEUGWENDER START VON WZW-TASTE
U   DB20.DBX 324.4;   // REFERENZPUNKT AKTIV
S   M 160.4;          // SCHRITT F4
S   DB25.DBX 332.0;   // NC-SATZ FERTIG
U   M 160.4;          // SCHRITT F4
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
U   DB20.DBX 324.4;   // REFERENZPUNKT AKTIV
S   M 162.1           // VON FREIFAHRBAUSTEIN AUF REF.POS. FAHREN
SPBN M106;
L   0;
T   MB 160;           // SCHRITTKETTE LÖSCHEN
T   MB 161;           // SONDERSCHRITTKETTE LÖSCHEN
T   MB 153;           // ABBRUCHMERKER LÖSCHEN
T   MB 154;           // ABBRUCHMERKER LÖSCHEN
M106:  NOP 0;
NETWORK
TITLE = SCHRITT F5.2 WERKZEUGWENDER FREIFAHRMODE VERLASSEN
U   M 160.4;          // SCHRITT F4
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
UN  DB20.DBX 324.4;   // REFERENZPUNKT AKTIV
R   M 160.4;          // SCHRITT F4
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
SPBN M107;
L   0;
T   MB 160;           // SCHRITTKETTE LÖSCHEN
T   MB 153;           // ABBRUCHMERKER LÖSCHEN
T   MB 154;           // ABBRUCHMERKER LÖSCHEN
T   MD 156;           // DOPPELWORT MIT SCHWENKPOSITION IN GRAD FÜR HAUPTSPINDEL (FLOATWWERT)
S   M 161.0;           // HM WZW-AUSBLASMAGNETVENTIL SCHALTEN
R   M 90.7;           // ACHSFREIGABE    K0
R   DB3.DBX  2.5;     // 7021  WERKZEUGWENDER FREIFAHREN
M107:  NOP 0;
NETWORK
TITLE = WERKZEUGWENDER FREIFAHREN AKTIV
L   MB 160;            // SCHRITTKETTE
L   0;
<>I;
=   M 162.0;          // WERKZEUGWENDER FREIFAHREN AKTIV
END_FUNCTION
FUNCTION FC 1 : VOID
NAME: SCHMIERMITTEL
BEGIN
NETWORK
TITLE = FM WEGIMPULS X-ACHSE
U     DB1.DBX 1422.0; // WEGIMPULS X-ACHSE
FP    M    129.3; // FM WEGIMPULS X-ACHSE
NETWORK
TITLE = VERFAHRWEG X-ACHSE MESSEN
U     M    129.3; // FM WEGIMPULS X-ACHSE
ZV    Z      2; // ZÄHLE VORWÄRTS Z2
NETWORK
TITLE = FM WEGIMPULS Y-ACHSE
U     DB1.DBX 1422.1; // WEGIMPULS Y-ACHSE
FP    M    129.5; // FM WEGIMPULS Y-ACHSE
NETWORK
TITLE = VERFAHRWEG Y-ACHSE MESSEN
U     M    129.5; // FM WEGIMPULS Y-ACHSE
ZV    Z      4; // ZÄHLE VORWÄRTS Z4
NETWORK
TITLE = FM WEGIMPULS Z-ACHSE (2M)
U     DB1.DBX 1422.2; // WEGIMPULS Z-ACHSE
FP    M    129.7; // FM WEGIMPULS Z-ACHSE
NETWORK
TITLE = VERFAHRWEG Z-ACHSE MESSEN
U     M    129.7; // SPRUNG BEDINGT
ZV    Z      6; // ZÄHLE VORWÄRTS Z6
NETWORK
TITLE = WEGINITIALISIERUNG/SCHMIERIMPULS
U(    ;
L     DB10.DBW   10; // SPS-MSD X-ACHSE ALLE 8 METER EINEN SCHMIERIMPULS
L     Z      2; // LADE Z2
<>I   ; // VERGLEICH AUF UNGLEICH
)     ;
U(    ;
L     DB10.DBW   12; // SPS-MSD Y-ACHSE ALLE 8 METER EINEN SCHMIERIMPULS
L     Z      4; // LADE Z4
<>I   ; // VERGLEICH AUF UNGLEICH
)     ;
U(    ;
L     DB10.DBW   14; // SPS-MSD Z-ACHSE ALLE 8 METER EINEN SCHMIERIMPULS
L     Z      6; // LADE Z6
<>I   ; // VERGLEICH AUF UNGLEICH
)     ;
//;
UN(   ;
U     DB1.DBX 1374.4; // TASTE AUX-ON (PC)
UN    T     24; // PAUSENZEIT SCHMIERMITTEL
)     ;
U(    ;
ON    M    110.0; // AUX-ON
O     T     24; // PAUSENZEIT SCHMIERMITTEL
O(    ;
UN    DB1.DBX 1374.4; // TASTE AUX-ON (PC)
)     ;
)     ;
O     A      4.0; // LEISTUNG SCHMIERMITTEL
SPB   M3; // SPRUNG BEDINGT
//;
SET;
R     Z      2; // RÜCKSETZEN Z2
R     Z      4; // RÜCKSETZEN Z4
R     Z      6; // RÜCKSETZEN Z6
S     A      4.0; // LEISTUNG SCHMIERMITTEL
//;
//;
U     M 51.2;         // Druckschalter Schmiermittel
S     DB3.DBX    4.6; // 7038  DRUCKSCHALTER SCHM. DEFEKT
M3:   NOP   0;
NETWORK
TITLE = DRUCKSCHALTER SCHMIERMITTEL
U     A      4.0; // LEISTUNG SCHMIERMITTEL
U     M 51.2;         // Druckschalter Schmiermittel
//;
L     S5T#0S500MS; // 5X0,1S
SE    T     22; // STARTEN T22 ALS EINSCHALTVERZ.
NETWORK
TITLE = ÜBERWACHUNGSZEIT SCHMIERMITTEL
U     A      4.0; // LEISTUNG SCHMIERMITTEL
UN    M 51.2;         // Druckschalter Schmiermittel
//;
L     S5T#3S; // 30X0,1S
SE    T     23; // STARTEN T23 ALS EINSCHALTVERZ.
NETWORK
TITLE = RÜCKSETZEN SCHMIERMITTEL
O     T     22; // DRUCKSCHALTER SCHMIERMITTEL
O     T     23; // ÜBERWACHUNGSZEIT ABGELAUFEN
//;
R     A      4.0; // LEISTUNG SCHMIERMITTEL
NETWORK
TITLE = /FM LEISTUNG SCHMIERMITTEL
U     A      4.0; // LEISTUNG SCHMIERMITTEL
FN    M    129.1; // FM LEISTUNG SCHMIERMITTEL
NETWORK
TITLE = PAUSENZEIT SCHMIERMITTEL
U     M    129.1; // FM LEISTUNG SCHMIERMITTEL
//;
L     S5T#6S; // 60X0,1S
SV    T     24; // STARTEN T24 ALS VERL. IMPULS
NETWORK
TITLE = 7039  SCHMIERMITTEL DEFEKT
U     T     23; // ÜBERWACHUNGSZEIT ABGELAUFEN
S     DB3.DBX    4.7; // 7039  SCHMIERMITTEL DEFEKT
//;
U     T     22; // DRUCKSCHALTER SCHMIERMITTEL
R     DB3.DBX    4.7; // 7039  SCHMIERMITTEL DEFEKT
NETWORK
TITLE = MELDG. SCHMIERM./NC-STARTVERR.
L     DB3.DBW    4; // WORT/MELDUNGEN SCHMIERMITTEL
L     W#16#0; // LADE KONSTANTE HEX 0
<>I   ; // VERGLEICH AUF UNGLEICH
//;
=     M     97.0; // MELDG. SCHMIERM./NC-STARTVERR.
NETWORK
TITLE = SPS/OB-->SCHMIERIMPULS AKTIV
U     A      4.0; // LEISTUNG SCHMIERMITTEL
=     DB20.DBX  311.4; // SPS/RM-->SCHMIERIMPULS AKTIV
END_FUNCTION
FUNCTION FC 2 : VOID
NAME: STÜCKZÄHLER
BEGIN
NETWORK
TITLE = STÜCKZÄHLER
//;
UN    DB20.DBX  192.2;    // M2 dyn
UN    DB20.DBX  195.6;    // M30 dyn
O     DB20.DBX  324.2;    // DRYRUN AKTIV
SPB     M214;
SET;
L     DB15.DBD    4;      // Zähler
L     DW#16#000F_423F;    // 999999 STÜCK
<>D   ;
SPB     M211;
SET;
L     DW#16#0000_0000;
T     DB15.DBD    4;
M211: NOP   0;
L     DB15.DBD    4;
L     DW#16#0000_0001;
+D    ; // Stückzähler +1 low
T     DB15.DBD    4;
//;
L     DB15.DBD    8;      // CHARGENZÄHLER
L     DW#16#000F_423F;    // 999999 STÜCK
<>D   ;
SPB     M213;
SET;
L     DW#16#0000_0000;
T     DB15.DBD    8;
M213: NOP   0;
L     DB15.DBD    8;
L     DW#16#0000_0001;
+D    ;                   // Stückzähler +1 low
T     DB15.DBD    8;
//;
M214: L     DB15.DBD    0;      // Sollstückz
L     DW#16#0000_0000;
<>D   ;
SPB     M202;
SPA     M201;
M202: NOP   0;
L     DB15.DBD    4;      // Stückz.
L     DB15.DBD    0;      // Sollstückz.
<D    ;
SPB     M201;
//;
SET;
S     DB3.DBX    5.3;     // 7043  SOLLSTÜCKZAHL ERREICHT
L     DW#16#0;
T     DB15.DBD    4;
M201: NOP   0;
NETWORK
TITLE = NC-STARTVERRIEGELUNG
U   DB3.DBX     5.3;     // Meldung Sollstückzahl erreicht
=   M 96.6;              // NC-Startverr.
//;
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   DB3.DBX     5.3;     // Meldung Sollstückzahl erreicht
END_FUNCTION
FUNCTION FC 10 : VOID
NAME: HA_STEUERUNG
BEGIN
NETWORK
TITLE = ISTDREHZAHL KLEINER 20U/MIN
U   DB1.DBX  1370.0;      // 1.SPS SCHLEIFENDURCHLAUF
S   M 114.1;              // HM-ENTPRELLZEIT
U   DB1.DBX 1356.3;       // HAUPTANTRIEB STILLSTAND
L   S5T#800MS;            // 800MS
SE  T 20;                 // STARTEN T38 ALS EINSCHALTVERZ.
U   T 20;                 // T38 ENTPRELLZEIT FÜR HAUPTANTRIEB
R   M 114.1;              // HM-ENTPRELLZEIT
O   M 114.1;              // HM-ENTPRELLZEIT
O   T 20;                 // T38 ENTPRELLZEIT FÜR HAUPTANTRIEB
=   M 114.3;              // ISTDREHZAHL KLEINER 20U/MIN
R   M 145.7;              // HM REGLERFREIGABE FÜR HAUPTANTRIEB
NETWORK
TITLE = HA N=0/NC
U     M    114.3; // ISTDREHZAHL KLEINER 20U/MIN
=     A 4.2; // HA N=0/NC
NETWORK
TITLE = ANWAHL HAUPTANTRIEB EIN (M3)
UN  DB20.DBX   224.3;    // M3 STATISCH
U   M 104.2;             // SUM  SFG
U   DB20.DBX   324.4;    // REFERENZPUNKT AKTIV
U(;
O   DB20.DBX   192.3;    // M3 DYN.
O(;
U   M 148.2;             // HM M3 GESPEICHERT M0/M1
U   DB20.DBX     0.2;              // FC NC-START
);
O(;
U   T 43;                // ÜBERWACHUNGSZEIT FÜR HA/M3
U   M 132.3;             // /FM TASTE SPINDEL START
U   M 104.5;             // SUM  SPINDEL EIN/AUS
UN    DB20.DBX  324.0;   // PROGRAMM LÄUFT
);
);
//;
=   DB20.DBX   256.3;    // M3 AUSLÖSEN
S   DB20.DBX   340.0;    // SPINDEL EIN IM UHRZEIGER
R   DB20.DBX   340.1;    // SPINDEL EIN GEGEN UHRZEIGER
R   M 148.2;             // HM M3 GESPEICHERT M0/M1
NETWORK
TITLE = ANWAHL HAUPTANTRIEB EIN (M4)
UN  DB20.DBX   224.4;    // M4 STATISCH
U   M 104.2;             // SUM  SFG
U   DB20.DBX   324.4;              // REFERENZPUNKT AKTIV
U(;
O   DB20.DBX   192.4;              // M4 DYN.
O(;
U   M 148.3;             // HM M4 GESPEICHERT M0/M1
U   DB20.DBX     0.2;              // FC NC-START
);
O(;
UN  T 43;                // ÜBERWACHUNGSZEIT FÜR HA/M4
U   M 132.3;             // /FM TASTE SPINDEL START
U   M 104.5;             // SUM  SPINDEL EIN/AUS
UN    DB20.DBX  324.0;   // PROGRAMM LÄUFT
);
);
//;
=   DB20.DBX   256.4;    // M4 AUSLÖSEN
R   DB20.DBX   340.0;    // SPINDEL EIN IM UHRZEIGER
S   DB20.DBX   340.1;    // SPINDEL EIN GEGEN UHRZEIGER
R   M 148.3;             // HM M4 GESPEICHERT M0/M1
NETWORK
TITLE = HM M3/M4 GESPEICHERT M0/M1
U   M 34.1;              // M0/M1 AKTIV STATISCH
U   DB20.DBX   224.3;    // M3 STATISCH
UN  M 148.4;             // HM M3/M4 GESPEICHERT M0/M1
S   M 148.2;             // HM M3 GESPEICHERT M0/M1
//;
U   M 34.1;              // M0/M1 AKTIV STATISCH
U   DB20.DBX   224.4;    // M4 STATISCH
UN  M 148.4;             // HM M3/M4 GESPEICHERT M0/M1
S   M 148.3;             // HM M4 GESPEICHERT M0/M1
//;
O   DB1.DBX  1440.0;     // RESET AUSGELÖST
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   M 148.2;             // HM M3 GESPEICHERT M0/M1
R   M 148.3;             // HM M4 GESPEICHERT M0/M1
NETWORK
TITLE = ANWAHL HAUPTANTRIEB   M5
U     M     34.1; // HM M0/M1 DYNAMISCH
UN    M    148.4; // HM M3/M4 GESPEICHERT M0/M1
O;
U     M    132.7; // FM SPINDEL HALT
U     M    104.5; // HM BETRIEBSART JOG UND INC
UN    DB20.DBX  324.0; // PROGRAMM LÄUFT
O;
U     M    132.7; // FM SPINDEL HALT
U     M    104.5; // HM BETRIEBSART JOG UND INC
UN    DB20.DBX  324.0; // PROGRAMM LÄUFT
O     DB20.DBX  192.5; // M5 dynamisch
O     DB1.DBX  1370.0; // 1.SPS SCHLEIFENDURCHLAUF
O     DB1.DBX 1440.0      // RESET AUSGELÖST
O     DB1.DBX  1370.3; // RESETTASTE GEDRÜCKT
O     DB20.DBX  324.2; // DRYRUN AKTIV
O     DB20.DBX  192.2; // M2 dyn
O     DB20.DBX  195.6; // M30 dyn
ON    M    110.0; // AUX-ON
=     DB20.DBX  256.5; // M5 RÜCKMELDEN
R     DB20.DBX  340.0; // SPINDEL EIN IM UHRZEIGER
R     DB20.DBX  340.1; // SPINDEL EIN GEGEN UHRZEIGER
R   M 145.2
R   M 145.4
NETWORK
TITLE = HM M3/M4 GESPEICHERT M0/M1
O   M 148.2;            // HM M3 GESPEICHERT M0/M1
O   M 148.3;            // HM M4 GESPEICHERT M0/M1
S   M 148.4;            // HM M3/M4 GESPEICHERT M0/M1
//;
UN  M 34.1;             // M0/M1 STATISCH
R   M 148.4;            // HM M3/M4 GESPEICHERT M0/M1
NETWORK
TITLE = DRYRUN  SFG
U   DB20.DBX   324.2;             // DRYRUN
//;
=   M 94.2;             // DRYRUN  SFG
NETWORK
TITLE = HA LAUFT AUS  AFG/EFG
U   DB20.DBX   224.5;   // M5 STATISCH
UN  M 114.3;            // ISTDREHZAHL KLEINER 20U/MIN
//;
=   M 90.6;             // HA LAUFT AUS  AFG/EFG
NETWORK
TITLE = 6014  KEINE HAUPTSPINDELDREHZAHL
U   DB20.DBX   224.3;   // M3 STATISCH
U   DB20.DBX  182.2;    // NEUE DREHZAHL
UN  M 148.0;            // HM M3 N-ÜBERWACHUNG
S   M 148.0;            // HM M3 N-ÜBERWACHUNG
//;
UN  DB20.DBX   224.3;   // M3 STATISCH
R   M 148.0;            // HM M3 N-ÜBERWACHUNG
//;
//;
U   M 104.2;            // SUM  SFG
U   M 148.0;            // HM M3 N-ÜBERWACHUNG
UN  M 152.1              // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
L   S5TIME#1S500MS;     // 15X0,1S
SE  T 27;               // STARTEN T27 ALS EINSCHALTVERZ.
//;
//;
//;
U   DB20.DBX   224.4;    // M4 STATISCH
U   DB20.DBX  182.2;     // NEUE DREHZAHL
UN  M 148.1;            // HM M4 N-ÜBERWACHUNG
S   M 148.1;            // HM M4 N-ÜBERWACHUNG
//;
UN  DB20.DBX   224.4;   // M4 STATISCH
R   M 148.1;            // HM M4 N-ÜBERWACHUNG
//;
//;
U   M 104.2;            // SUM  SFG
U   M 148.1;            // HM M4 N-ÜBERWACHUNG
UN  M 152.1              // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
L   S5TIME#1S500MS;     // 15X0,1S
SE  T 28;               // STARTEN T28 ALS EINSCHALTVERZ.
//;
//;
U(;
O   T 27;               // M3/VERZÖGERUNGSZ. ABGELAUFEN
O   T 28;               // M4/VERZÖGERUNGSZ. ABGELAUFEN
);
U   M 114.3;            // ISTDREHZAHL KLEINER 20U/MIN
L   S5TIME#200MS;       // 2X0,1S
SE  T 31;               // STARTEN T31 ALS EINSCHALTVERZ.
//;
//;
U   T 31;               // VERZÖGERUNGSZEIT ABGELAUFEN
S   DB2.DBX     1.6;            // 6014  KEINE HAUPTSPINDELDREHZAHL
//;
//;
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   DB2.DBX     1.6;            // 6014  KEINE HAUPTSPINDELDREHZAHL
END_FUNCTION
FUNCTION FC 11 : VOID
NAME: ACHSEN_LEISTUNG
BEGIN
NETWORK
TITLE = LEISTUNG ACHSANTRIEBE
U   M 110.0;              // AUX-ON
S   M 55.1; // LEISTUNG ACHSANTRIEBE
NETWORK
TITLE = LEISTUNG ACHSEN/SANFTANFLAUF
U   M 55.1; // LEISTUNG ACHSANTRIEBE
L   S5TIME#1S;            // 10X0,1S
SE  T 2;                  // STARTEN T2 ALS EINSCHALTVERZ.
//;
U   T 2;                  // VERZÖGERUNGSZEIT ABGELAUFEN
=   M 55.2; // LEISTUNG ACHSEN/SANFTANLAUF
NETWORK
TITLE = ACHSANTRIEBE BETRIEBSBEREIT
UN  M 50.4;             // X-ACHSE SERVO-READY  (X)
UN  M 50.5;             // Y-ACHSE SERVO-READY  (Y)
UN  M 50.6;             // Z-ACHSE/SERVO-READY  (Z)
//;
=   M 111.0;             // ACHSANTRIEBE BETRIEBSBEREIT
NETWORK
TITLE = ACHSEN BETRIEBSBEREIT  AFG/EFG
UN  M 111.0;             // ACHSEN BETRIEBSBEREIT
=   M 90.1;              // ACHSEN BETRIEBSBEREIT  AFG/EFG
NETWORK
TITLE = TOR-SIGNAL ACHSANTRIEBE
UN  M 50.3;         // Ausgabemodul Y4A047000 durchgeschaltet
L   S5TIME#800MS;        // 8X0,1S
SE  T 3;                 // STARTEN T3 ALS EINSCHALTVERZ.
//;
U   T 3;                 // VERZÖGERUNGSZEIT ABGELAUFEN
=   A 0.4;               // TOR-SIGNAL ACHSANTRIEBE
NETWORK
TITLE = ÜBERWACHUNG ACHSANTRIEBE SCHARF
U   M 110.0;             // AUX-ON
L   S5TIME#3S;           // 3 SEKUNDEN
SE  T 4;                 // STARTEN T4 ALS EINSCHALTVERZ.
NETWORK
TITLE = 6010  ANTRIEB X-ACHSE N. BEREIT
U   M 50.4;              // X-ACHSE SERVO-READY  (X)
L   S5TIME#200MS;        // 2X0,1S
SE  T 5;                 // STARTEN T5 ALS EINSCHALTVERZ.
//;
U   T 4;                 // ACHSÜBERWACHUNG SCHARF
U   T 5;                 // X-ACHSE/SERVO-READY (0,2S)
S   DB2.DBX     1.2;     // 6010  ANTRIEB X-ACHSE N. BEREIT
//;
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   DB2.DBX     1.2;             // 6010  ANTRIEB X-ACHSE N. BEREIT
NETWORK
TITLE = 6011  ANTRIEB Y-ACHSE N. BEREIT
U  M 50.5;               // Y-ACHSE SERVO-READY  (Y)
L   S5TIME#200MS;        // 2X0,1S
SE  T 30;                // STARTEN T30 ALS EINSCHALTVERZ.
//;
U   T 4;                 // ACHSÜBERWACHUNG SCHARF
U   T 30;                // Y-ACHSE/SERVO-READY (0,2S)
S   DB2.DBX     1.3;             // 6011  ANTRIEB Y-ACHSE N. BEREIT
//;
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   DB2.DBX     1.3;             // 6011  ANTRIEB Y-ACHSE N. BEREIT
NETWORK
TITLE = 6012  ANTRIEB Z-ACHSE N. BEREIT
U   M 50.6;              // Z-ACHSE/SERVO-READY  (Z)
L   S5TIME#200MS;        // 2X0,1S
SE  T 6;                 // STARTEN T6 ALS EINSCHALTVERZ.
//;
U   T 4;                 // ACHSÜBERWACHUNG SCHARF
U   T 6;                 // Z-ACHSE/SERVO-READY (0,2S)
S   DB2.DBX     1.4;             // 6012  ANTRIEB Z-ACHSE N. BEREIT
//;
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   DB2.DBX     1.4;             // 6012  ANTRIEB Z-ACHSE N. BEREIT
NETWORK
TITLE = ACHSEN NICHT BEREIT
O   DB2.DBX     1.2;             // 6010  ANTRIEB X-ACHSE N. BEREIT
O   DB2.DBX     1.3;             // 6011  ANTRIEB Y-ACHSE N. BEREIT
O   DB2.DBX     1.4;             // 6012  ANTRIEB Z-ACHSE N. BEREIT
//;
S   M 111.1;             // ACHSEN NICHT BEREIT
NETWORK
TITLE = 6009  SICHERHEITSSCH. DEFEKT
UN  M 50.3;         // Ausgabemodul Y4A047000 durchgeschaltet
U   M 29.5;              // DREHÜBERWACHUNG ACHSEN AKTIV
S   DB2.DBX     1.1;             // 6009  SICHERHEITSSCH. DEFEKT
//;
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   DB2.DBX     1.1;             // 6009  SICHERHEITSSCH. DEFEKT
NETWORK
TITLE = RÜCKSETZEN LEISTG. ACHSANTRIEBE
O   DB2.DBX     0.0;             // 6000  NOT-AUS
O   DB2.DBX     1.1;     // 6009  SICHERHEITSSCH. DEFEKT
O(;
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
U   M 111.1;             // ANTRIEB ACHSEN NICHT BEREIT
);
//;
R   M 55.1;              // AUSGANGSMERKER LEISTUNG ACHSANTRIEBE
R   M 111.1;             // ANTRIEB ACHSEN NICHT BEREIT
END_FUNCTION
FUNCTION FC 12 : VOID
NAME: JOG_ACHSEN
BEGIN
NETWORK
TITLE = JOG-ACHSEN FREIGABE
UN  E 7.7;               // FMS-BETRIEB AKTIV
U   M 104.0;             // SUM  AFG/EFG
U(  ;
O   DB20.DBX  324.4;     // REFERENZPUNKT AKTIV
O   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
)   ;
//;
=   M 112.0;             // JOG-ACHSEN FREIGABE
NETWORK
TITLE = JOG-ACHSEN/STARTVERR.
U   DB20.DBX    1.2;     // FC JOG-ACHSEN
UN  M    112.0;          // JOG-ACHSEN FREIGABE
SPBN  M051
L   DW#16#0000_0000;     // LADE 0
T   DB20.DBD    10;      // JOG IN ALLEN ACHSEN AUS
M051: NOP 0
END_FUNCTION
FUNCTION FC 13 : VOID
NAME: REF_ACHSEN
BEGIN
NETWORK
TITLE = FREIGABE REFERENZIEREN
U   M 104.0;             // SUM AFG/EFG
UN  M 113.0;             // SONDERBETRIEB AKTIV
UN  M 100.2;             // VORSCHUBSCHALTER AUF 0%
UN  DB1.DBX 1358.0;      // ACHSEN IN BEWEGUNG
UN  DB3.DBX     2.5;     // 7021  WERKZEUGWENDER FREIFAHREN
//;
=   M 120.6;             // FREIGABE REFERENZIEREN
NETWORK
TITLE = REFERENZPUNKT ANFAHREN ABBRECHEN (RESET AUSLÖSEN)
U   DB20.DBX  327.2;     // BETRIEBSART REFERENZ
UN  M 50.3;         // Ausgabemodul Y4A047000 durchgeschaltet
FP  M 122.4              // FLANKENMERKER RESET AUSLÖSEN
//;
O   M 122.4              // FLANKENMERKER RESET AUSLÖSEN
O   DB1.DBX  1440.0;     // NC-RESET AUSLÖSEN
=   DB1.DBX  1440.0;     // NC-RESET AUSLÖSEN
NETWORK
TITLE = REFPKT.X,Y,Z, ZUGLEICH ANFAHREN
U   M 120.6;             // FREIGABE REFERENZIEREN
UN  M 121.3;             // REFPKT.X,Z,ZUGLEICH ANFAHREN
U   DB20.DBX    8.0;     // REFERENZIEREN X-ACHSE
U   DB20.DBX    8.1;     // REFERENZIEREN Y-ACHSE
U   DB20.DBX    8.2;     // REFERENZIEREN Z-ACHSE
//;
S   M 121.3;             // REFPKT X,Y,Z,ZUGLEICH ANFAHREN
FP  M 121.5;             // FM REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
//;
O   DB1.DBX  1440.0;     // RESET AUSGELÖST
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
O   M 122.5;             // FM REFERENZPUNKT AKTIV
R   M 121.3;             // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
//;
NETWORK
TITLE = REFERENZPUNKT VERLOREN
O   M 121.5;             // FM REFPKT X,Y,Z ZUGLEICH ANFAHREN
O   DB2.DBX     1.2;     // ALARM X-ACHSE DEFEKT
O   DB2.DBX     1.3;     // ALARM Y-ACHSE DEFEKT
O   DB2.DBX     1.4;     // ALARM Z-ACHSE DEFEKT
U   DB20.DBX   324.4;    // REFERENZPUNKT AKTIV
//;
=   DB20.DBX   348.0;    // REFERENZPUNKT VERLOREN
NETWORK
TITLE = REFPKT.ANFAHREN SPERREN
U   DB20.DBX     1.1;    // REFERENZPKT ANFAHREN (UMLENKUNG)
UN  M 120.6;             // FREIGABE REFERENZIEREN
//;
R   DB20.DBX     1.1;    // REFERENZPKT ANFAHREN (UMLENKUNG)
S   DB20.DBX   318.7;    // NEG.QUITTUNG DNC
NETWORK
TITLE = Z-ACHSE REFERENZIEREN (FC-CODE)
U   M 121.5;             // FM REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   DB20.DBX    8.2;     // REFERENZIEREN Z-ACHSE
O(;
U   DB20.DBX    8.2;     // REFERENZIEREN Z-ACHSE
U   M 120.6;             // FREIGABE REFERENZIEREN
UN  M 121.3;             // REFPKT.X,Z,ZUGLEICH ANFAHREN
);
=   DB20.DBX    8.2;     // REFERENZIEREN Z-ACHSE
NETWORK
TITLE = AUSBLASMAGNETVENTIL EINSCHALTEN UND SCHRITTE IN DEN SETTINGDATEN LÖSCHEN
O   DB15.DBX   20.4      // WZW SCHRITT 4 GESPEICHERT (ZAHNRAD IM EINGRIFF)
O   DB15.DBX   20.3;     // SCHRITT 3 IN SETTINGDATEN GESPEICHERT
O   DB15.DBX   20.5;     // SCHRITT 5 IN SETTINGDATEN GESPEICHERT
O   M 161.0;           // HM WZW-AUSBLASMAGNETVENTIL SCHALTEN
U   DB20.DBX    8.2;     // REFERENZIEREN Z-ACHSE
S   A 4.5;               // WZW-AUSBLASMAGNETVENTIL
R   DB15.DBX  20.3;      // SCHRITT 3 IN DEN SETTINGDATEN LÖSCHEN
R   DB15.DBX  20.4       // SCHRITT 4 IN DEN SETTINGDATEN SPEICHERN
R   DB15.DBX  20.5;      // SCHRITT 5 IN DEN SETTINGDATEN LÖSCHEN
R   M 161.0;           // HM WZW-AUSBLASMAGNETVENTIL SCHALTEN
=   DB20.DBX 348.3;      // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
NETWORK
TITLE = FM REFERENZPUNKT Z-ACHSE AKTIV
U   DB1.DBX   134.2;     // REFERENZPUNKT Z-ACHSE AKTIV
FP  M 120.7;             // FM REFERENZPUNKT Z-ACHSE AKTIV
NETWORK
TITLE = WERKZEUGWENDE-MODE VERLASSEN UND AUSBLASMV. AUSSCHALTEN
ON  M 110.0;             // AUX-ON
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
O   DB1.DBX  1440.0;     // RESET AUGELÖST
O   M 120.7;             // FM REFERENZPUNKT Z-ACHSE AKTIV
R   A 4.5;               // WZW-AUSBLASMAGNETVENTIL
NETWORK
TITLE = REFERENZPUNKT Z-ACHSE AKTIV UND WERKZEUGWENDE-MODE VERLASSEN
U   M 120.7;          // FM REFERENZPUNKT Z-ACHSE AKTIV
R   A 4.4;            // FRC122C AUF SPINDELBETRIEB SCHALTEN
L   S5TIME#100MS;     // 100MS
SS  T 52;             // SPEICHERNDE EINSCHALTVERZOEGERUNG
SPBN M171;
=   DB25.DBX 1.4;     // NC-SATZ ABSCHICKEN
=   DB25.DBX 549.3;   // HA VON POSITIONIERBETRIEB AUF SPINDEL
M171:  NOP 0;
O   T 52;
R   M 94.7;           // SPINDELFREIGABE K0
R   T 52;
NETWORK
TITLE = X-ACHSE REFERENZIEREN (FC-CODE)
U   M 121.3;             // REFPKT.X,Z,ZUGLEICH ANFAHREN
U   M 120.7;             // FM REFERENZPUNKT Z-ACHSE AKTIV
O(;
U   M 120.6;             // FREIGABE REFERENZIEREN
UN  M 121.3;             // REFPKT.X,Z,ZUGLEICH ANFAHREN
U   DB20.DBX    8.0;     // REFERENZIEREN X-ACHSE
);
=   DB20.DBX    8.0;     // REFERENZIEREN X-ACHSE
R   DB20.DBX    8.2;     // REFERENZIEREN Z-ACHSE
SPBN M701;
SET;
=   DB20.DBX    1.1;     // REFERENZPKT.ANFAHREN (UMLENKUNG)
M701: NOP 0;
NETWORK
TITLE = Y-ACHSE REFERENZIEREN (FC-CODE)
U   M 121.3;             // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.7;             // FM REFERENZPUNKT Z-ACHSE AKTIV
O(;
U   M 120.6;             // FREIGABE REFERENZIEREN
UN  M 121.3;             // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   DB20.DBX    8.1;     // REFERENZIEREN Y-ACHSE
);
//;
=   DB20.DBX    8.1;     // REFERENZIEREN Y-ACHSE
NETWORK
TITLE = FM REFERENZPUNKT AKTIV
U   DB20.DBX  324.4;     // REFERENZPUNKT AKTIV
FP  M 122.5;             // FM REFERENZPUNKT AKTIV
//;
NETWORK
TITLE = 7017 MELDUNG REFERENZPUNKT ANFAHREN
UN  DB20.DBX   324.4;    // REFERENZPUNKT AKTIV
UN  DB3.DBX     5.2;     // 7042  MASCHINENTÜR INIT.
UN  DB3.DBX     2.4;     // 7020  SONDERBETRIEB AKTIV
UN  DB3.DBX     5.0;     // 7040  MASCHINENTÜR OFFEN
UN  DB3.DBX     2.0;     // 7016  HILFSANTRIEBE EINSCHALTEN
UN  DB3.DBX     2.5;     // 7021  WERKZEUGWENDER FREIFAHREN
//;
=   DB3.DBX     2.1;     // 7017  REFERENZPUNKT ANFAHREN
END_FUNCTION
FUNCTION FC 14: VOID
NAME: AUX_ON
BEGIN
NETWORK
TITLE = 6000  ALARM NOT-AUS
UN    M 50.2;         // NICHT NOT-AUS
S     DB2.DBX     0.0; // 6000  NOT-AUS
//;
//;
O     DB1.DBX  1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O     DB1.DBX  1370.3; // RESET-TASTE GEDRÜCKT
R     DB2.DBX     0.0; // 6000 ALARM NOT-AUS
NETWORK
TITLE = FM NOT-AUS
U     DB2.DBX     0.0; // 6000  NOT-AUS
FP    M 102.0;         // FM 6000  NOT-AUS
NETWORK
TITLE = MASCHINENTÜR OFFEN
ON    M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
U     M 50.2;         // NICHT NOT-AUS
//;
=     M 110.1;         // MASCHINENTÜR OFFEN
NETWORK
TITLE = 6024  ALARM MASCHINENTÜR OFFEN
UN    T     25;           // SONDERBETRIEB AKTIV (T25)
UN    E      7.7;         // FG AKTIV VON FMS SCHUTZZAUN
U(    ;
O     DB1.DBX 1358.0;     // ACHSEN IN BEWEGUNG
O     M    152.1          // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
)     ;
O     DB1.DBX   1358.0;   // ACHSE IN BEWEGUNG
O     M    152.1          // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
U     DB20.DBX   326.4;   // BETRIEBSART AUTO
O     ;
U     DB20.DBX   324.0;   // PROGRAMM LÄUFT
UN    DB20.DBX   324.1;   // STOP-ZUSTAND
UN    DB20.DBX   325.1;   // STOP-ZUSTAND DURCH SINGLE
U     DB20.DBX   326.4;   // BETRIEBSART AUTO
O     M 51.1;         // HA N=0/SICHERHEITSSCHALTUNG
ON    M    114.3;         // ISTDREHZAHL KLEINER 20U/MIN
=     M    110.2;         // HM ALARM MASCHINENTÜR OFFEN
//;
U     M    110.2;         // HM ALARM MASCHINENTÜR OFFEN
U     M    110.1;         // MASCHINENTÜR OFFEN
UN    DB2.DBX    0.0;     // 6000  NOT-AUS
UN    DB3.DBX    5.2;     // 7042  MASCHINENTÜR INIT.
S     DB2.DBX    3.0;     // 6024 ALARM MASCHINENTÜR OFFEN
//;
O     DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (PC/MSTT)
O     DB1.DBX 1370.3; // RESET-TASTE GEDRÜCKT
R     DB2.DBX    3.0; // 6024 ALARM MASCHINENTÜR OFFEN
NETWORK
TITLE = SUM ALARME 1
L     DB2.DBW    0; // WORT/ALARME 1
L     W#16#0; // LADE KONSTANTE HEX 0
<>I   ; // VERGLEICH AUF UNGLEICH
//;
=     M    105.0; // SUM ALARME 1
NETWORK
TITLE = SUM ALARME 2
L     DB2.DBW    2; // WORT/ALARME 2
L     W#16#0; // LADE KONSTANTE HEX 0
<>I   ; // VERGLEICH AUF UNGLEICH
//;
=     M    105.1; // SUM ALARME 2
NETWORK
TITLE = SUM ALARME 3/THERM. ALARME
L     DB2.DBW    4; // BYTE/ALARME 3
L     W#16#0; // LADE KONSTANTE HEX 0
<>I   ; // VERGLEICH AUF UNGLEICH
//;
=     M    105.2; // SUM ALARME 3/THERM. ALARME
NETWORK
TITLE = SUM ALARME 5
L     DB2.DBW    6; // WORT/ALARME 5
L     W#16#0; // LADE KONSTANTE HEX 0
<>I   ; // VERGLEICH AUF UNGLEICH
//;
=     M    105.3; // SUM ALARME 5
NETWORK
TITLE = SUM ALARME 6
L     DB2.DBW    8; // WORT/ALARME 6
L     W#16#0; // LADE KONSTANTE HEX 0
<>I   ; // VERGLEICH AUF UNGLEICH
//;
=     M    105.4; // SUM ALARME 6
NETWORK
TITLE = FM TASTE AUX-ON (MSTT/PC)
U     DB1.DBX 1374.4; // TASTE AUX-ON (PC)
UN    DB1.DBX 1374.3; // TASTE AUX-OFF (PC)
FP    M    110.5; // FM TASTE AUX-ON
NETWORK
TITLE = FM TASTE AUX-OFF (MSTT/PC)
U     DB1.DBX 1374.3; // TASTE AUX-OFF (PC)
UN    DB1.DBX 1374.4; // TASTE AUX-ON (PC)
FP    M    110.7; // FM TASTE AUX-OFF
NETWORK
TITLE = ANWAHL AUX-ON
U     DB1.DBX 1374.4; // TASTE AUX-ON (PC)
UN    DB1.DBX 1374.3; // TASTE AUX-OFF (PC)
O     M    168.1; // ROBOTIK/FM AUX-ON
O     DB1.DBX 1382.4; // DNC/AUX-ON
S     M    110.0; // AUX-ON
NETWORK
TITLE = FM AUX-ON
U     M    110.0; // AUX-ON
FP    M    110.3; // FM AUX-ON
U     M    110.0; // AUX-ON
FN    M    110.4; // /FM AUX-ON
NETWORK
TITLE = ABWAHL AUX-ON
UN    DB20.DBX  324.0;    // PROGRAMM LÄUFT
U     A 4.2;              // HA N=0/NC
UN    DB1.DBX 1358.0;     // ACHSEN IN BEWEGUNG
UN    M    152.1          // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
U(    ;
O     M    110.7; // FM TASTE AUX-OFF
O     DB1.DBX 1382.3; // DNC/AUX-OFF
)     ;
//;
O     M    105.0; // SUM ALARME 1
O     M    105.1; // SUM ALARME 2
O     M    105.2; // SUM ALARME 3/THERM. ALARME
O     M    105.3; // SUM ALARME 5
O     M    105.4; // SUM ALARME 6
//;
//;
R     M    110.0; // AUX-ON
NETWORK
TITLE = AUX-ON  SFG
ON    M    110.0; // AUX-ON
O     M    110.1; // MASCHINENTÜR OFFEN
//;
=     M     94.0; // AUX-ON  SFG
NETWORK
TITLE = AUX-ON   AFG/EFG
ON    M    110.0; // AUX-ON
ON    M 50.3;     // Ausgabemodul Y4A047000 durchgeschaltet
//;
=     M     90.0; // AUX-ON   AFG/EFG
NETWORK
TITLE = 7042  MASCHINENTÜR INIT.
O     DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
O     DB2.DBX     0.0; // 6000  NOT-AUS
//;
S     DB3.DBX    5.2; // 7042  MASCHINENTÜR INIT.
//;
//;
O     E      5.7; // ENDSCH. TÜR OFFEN
ON    M    110.1; // MASCHINENTÜR OFFEN
R     DB3.DBX    5.2; // 7042  MASCHINENTÜR INIT.
NETWORK
TITLE = 7040  MASCHINENTÜR OFFEN
U     M    110.1; // MASCHINENTÜR OFFEN
UN    DB2.DBX    3.0; // 6024  MASCHINENTÜR OFFEN
UN    DB3.DBX    5.2; // 7042  MASCHINENTÜR INIT.
UN    DB3.DBX    2.4; // 7020  SONDERBETRIEB AKTIV
//;
=     DB3.DBX    5.0; // 7040  MASCHINENTÜR OFFEN
NETWORK
TITLE = 7016  HILFSANTRIEBE EINSCHALTEN
UN    M    110.0; // AUX-ON
UN    DB3.DBX    5.2; // 7042  MASCHINENTÜR INIT.
UN    DB3.DBX    2.4; // 7020  SONDERBETRIEB AKTIV
UN    DB3.DBX    5.0; // 7040  MASCHINENTÜR OFFEN
//;
=     DB3.DBX    2.0; // 7016  HILFSANTRIEBE EINSCHALTEN
NETWORK
TITLE = SPS/OB-->NOT-AUS AKTIV
U     DB2.DBX     0.0; // 6000  NOT-AUS
=     DB1.DBX 1390.5; // SPS/RM-->NOT-AUS AKTIV
NETWORK
TITLE = SPS/OB-->MASCHINENTÜR GESCHL.
U     E      1.4; // NOT-AUS INAKTIV
U     E      1.5; // TÜR IST ZU VON TÜRMODUL (FREIGABEKR.1)
=     DB1.DBX 1390.1; // SPS/RM-->MASCHINENTÜR GESCHL.
NETWORK
TITLE = SPS/OB-->AUX-ON
U     M    110.0; // AUX-ON
=     DB1.DBX 1390.2; // SPS/RM-->AUX-ON
NETWORK
TITLE = AUX-ON/NEG.DNC-QUITTUNG
U     DB1.DBX 1366.0; // DNC-BETRIEB AKTIV
U(    ;
U     DB1.DBX 1382.4; // DNC/AUX-ON
UN    M    110.0; // AUX-ON
O     ;
U     DB1.DBX 1382.3; // DNC/AUX-OFF
U     M    110.0; // AUX-ON
)     ;
//;
S     DB1.DBX 1398.1; // AUX-ON/NEG.DNC-QUITTUNG
END_FUNCTION
FUNCTION FC 15 : VOID
NAME: NC_STARTVERR
BEGIN
NETWORK
TITLE = AUX_ON_NC_STARTVERR.
O(;
UN  M 104.2;          // SUM  SFG
UN  DB20.DBX 324.2;   // DRYRUN
UN  M 113.0;          // SONDERBETRIEB AKTIV
);
ON  M 104.0;          // SUM  AFG/EFG
O   DB1.DBX 1366.3;   // ALARM AKTIV
//;
=   M 96.0;           // AUX-ON/NC-STARTVERR.
NETWORK
TITLE = SCHLÜSSEL BA AUTO/NC-STARTVERR.
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
UN  M 113.0;             // SONDERBETRIEB AKTIV
//;
=   M 96.6;              // SCHLÜSSEL BA AUTO/NC-STARTVERR.
NETWORK
TITLE = REFERENZPUNKT NICHT AKTIV
UN  DB20.DBX 324.4; // REFERENZPUNKT AKTIV
=   M 97.1;         // NC-STARTVERRIEGELUNG
NETWORK
TITLE = SUM NC-START VERR.
L   MW 96;               // WORT/NC-STARTVERR.
L   W#16#0;              // LADE KONSTANTE HEX 0
<>I;                     // VERGEICH AUF UNGEICH
//;
=   M 104.3;             // SUM NC-STARTVERR.
NETWORK
TITLE = 7018  SCHLÜSSEL UMSCHALTEN
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
UN  M 113.0;             // SONDERBETRIEB AKTIV
U(;
O   DB20.DBX   326.2;    // KERN/BA AUTO
O   DB20.DBX   326.4;    // KERN/BA MDI
);
U   DB20.DBX     0.2;    // FC NC-START
S   DB3.DBX     2.2;     // 7018  SCHLÜSSEL ALTEN
//;
ON  M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   DB3.DBX     2.2;             // 7018  SCHLÜSSEL ALTEN
NETWORK
TITLE = NC-START/STARTVERR.
U   DB20.DBX     0.2;              // FC NC-START
U   M 104.3;             // SUM  NC-STARTVERR.
//;
R   DB20.DBX     0.2;              // FC NC-START
NETWORK
TITLE = NC-START/FC DNC-QUITTUNG
U   DB1.DBX  1366.0;              // DNC-BETRIEB AKTIV
U   DB20.DBX     0.2;              // FC NC-START
U   M 104.3;             // SUM  NC-STARTVERR.
//;
S   DB20.DBX   319.0;              // NC-START/FC DNC-QUITTUNG
END_FUNCTION
FUNCTION FC 16 : VOID
NAME: RESET
BEGIN
NETWORK
TITLE = /FM AUX-ON
U   M 110.0;             // AUX-ON
FN  M 109.5;             // /FM AUX-ON
NETWORK
TITLE = /FM AUX-ON/WORT RESET
U   M 109.5;             // /FM AUX-ON
=   M 98.1;              // /FM AUX-ON/WORT RESET
NETWORK
TITLE = /FM SONDERBETRIEB AKTIV
U   M 113.0;             // SONDERBETRIEB AKTIV
FN  M 113.7;             // /FM SONDERBETRIEB AKTIV
NETWORK
TITLE = /FM SONDERBETRIEB/WORT RESET
U   M 113.7;             // /FM SONDERBETRIEB AKTIV
=   M 98.3;              // /FM SONDERBETRIEB/WORT RESET
NETWORK
TITLE = SUM RESET
L   MW 98;               // LADE WORT RESET
L   W#16#0;              // LADE KONSTANTE HEX 0
<>I;                     // VERGLEICH AUF UNGLEICH
//;
=    M 104.4;             // SUM RESET
NETWORK
TITLE = FC RESET/SPS
U    M 104.4;             // SUM RESET
S    DB1.DBX 1440.0;      // FC RESET/SPS
END_FUNCTION
FUNCTION FC 17 : VOID
NAME: NC_START_UND_NC_STOP_VON_M0_ODER_M1
BEGIN
NETWORK
TITLE = FC NC-STOP
O   M 34.1;           // HM M0/M1 DYNAMISCH
O   DB20.DBX    0.1;  // FC NC_STOP
//;
=   DB20.DBX    0.1;  // FC NC_STOP
NETWORK
TITLE = FC NC-START
UN  DB10.DBX   1.6;   // SPINDELSTART NACH M0
U   M 101.0;          // NC-START SPEICHER
U(;
O   DB20.DBX   224.3; // M3 STATISCH
O   DB20.DBX   224.4; // M4 STATISCH
);
O   M    168.7;       // ROBOTIK/FM NC-START
S   DB20.DBX     0.2; // FC NC-START
//;
U   DB20.DBX     0.2; // FC NC-START
R   M 101.0;          // NC-START SPEICHER
END_FUNCTION
FUNCTION FC 18 : VOID
NAME: PLC_NC_ÜBERGABE
BEGIN
NETWORK
TITLE = SUM MELDUNGEN  AFG/EFG
L   DB3.DBW    0;         // WORT/MELDUNGEN  AFG/EFG
L   W#16#0;               // LADE KONSTANTE HEX 0
<>I;                      // VERGLEICH AUF UNGLEICH
//;
=   M 91.0;               // SUM MELDUNGEN  AFG/EFG
NETWORK
TITLE = SUM  AFG/EFG
L   MW 90;                // WORT  AFG/EFG
L   W#16#0;               // LADE KONSTANTE HEX 0
==I;                     // VERGLEICH AUF GLEICH
//;
=   M 104.0;             // SUM  AFG/EFG
=   M 104.1;             // SUM  EFG
NETWORK
TITLE = AFG/EFG
U   M 104.0;             // SUM  AFG/EFG
//;
=   DB20.DBX   340.4;    // ACHSFREIGABE
=   DB20.DBX   340.3;    // EINLESEFREIGABE
NETWORK
TITLE = SUM  SFG
L   MW 94;               // WORT  SFG
L   W#16#0;              // LADE KONSTANTE HEX 0
==I;                     // VERGLEICH AUF GLEICH
//;
=   M 104.2;             // SUM  SFG
NETWORK
TITLE = SPINDELFREIGABE
U   M 104.2;             // SUM  SFG
=   DB20.DBX   340.5;    // SPINDELFREIGABE
NETWORK
TITLE = ALARME/REFERENZPUNKTVERLUST
O   DB2.DBX     0.0;             // 6000  NOT-AUS
O   DB2.DBX     1.1;             // 6009  SICHERHEITSSCH. DEFEKT
O   DB2.DBX     1.2;             // 6010  ANTRIEB X-ACHSE N. BEREIT
O   DB2.DBX     1.3;             // 6011  ANTRIEB Y-ACHSE N. BEREIT
O   DB2.DBX     1.4;             // 6012  ANTRIEB Z-ACHSE N. BEREIT
FP  M 104.6;
//;
U   M 104.6;
=   DB20.DBX   348.0;            // REFERENZPUNKT DEAKTIVIEREN
NETWORK
TITLE = SPS/OB-->WZW AKTIV
U   DB10.DBX   75.5;              // SPS-SET /WZW
=   DB20.DBX   382.0;    // SPS/OB-->WZW AKTIV
NETWORK
TITLE = SPS/OB-->TÜRAUTOMATIK AKTIV
U   DB10.DBX   75.0;              // SPS-SET /TÜRAUTO
=   DB1.DBX  1418.0;     // SPS/OB-->TÜRAUTOMATIK AKTIV
NETWORK
TITLE = SPS/OB-->PNEUM. SCHRAUBST. AKTIV
U   DB10.DBX   75.1;              // SPS-SET /PNEUM. SCHR.
UN  DB10.DBX   75.6;              // SPS-SET /TEILAPPARAT
//;
=   DB20.DBX   382.1;    // SPS/OB-->PNEUM. SCHRAUBST. AKTIV
NETWORK
TITLE = SPS/OB-->AUSBLASEINR. AKTIV
U   DB10.DBX   75.3;              // SPS-SET /AUSBLASEN
=   DB20.DBX   382.4;    // SPS/OB-->AUSBLASEINR. AKTIV
NETWORK
TITLE = SPS/OB-->KÜHLMITTEL AKTIV
SET
=   DB20.DBX   382.3;    // SPS/OB-->KÜHLMITTEL AKTIV
NETWORK
TITLE = SPS/OB-->TEILAPPARAT AKTIV
U   DB10.DBX   75.6;              // SPS-SET /TEILAPPARAT
UN  DB10.DBX   75.1;              // SPS-SET /PNEUM. SCHR.
//;
=   DB20.DBX   382.5;    // SPS/OB-->TEILAPPARAT AKTIV
NETWORK
TITLE = SUM  SPINDEL EIN/AUS
UN  DB20.DBX  326.1;     // BETRIEBSART EDIT
UN  DB20.DBX  326.2;     // BETRIEBSART MDI
UN  DB20.DBX  326.3;     // BETRIEBSART REPOS
UN  DB20.DBX  326.4;     // BETRIEBSART AUTOMATIK
UN  DB20.DBX  327.2;     // BETRIEBSART REFERENZ
UN  DB20.DBX  327.4;     // BETRIEBSART PRESET
//;
=   M 104.5;             // SUM  SPINDEL EIN/AUS
END_FUNCTION
FUNCTION FC 19 : VOID
NAME: SONDERBETRIEB
BEGIN
NETWORK
TITLE = FG SONDERBETRIEB/SICHERHEITSSCH.
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
U   M 110.1;             // MASCHINENTÜR OFFEN
UN  M 153.0;             // WZW ABBRUCH MERKER AUS DEN SETTINGDATEN ZAHNRAD IM EINGRIFF
U(;
ON  DB20.DBX   326.2;    // KERN/BA AUTO
O(;
U   DB20.DBX   324.7;    // EINZELSATZ AKTIV
U   DB20.DBX   324.2;    // DRYRUN
);
);
O;
U   DB10.DBX   75.0;     // SPS-SET /TÜRAUTO
U   A 5.0;               // LEISTUNG TÜR ZU
U   M 110.1;             // MASCHINENTÜR OFFEN
//;
=   A 4.3;               // FG SONDERBETRIEB/SICHERHEITSSCH.
NETWORK
TITLE = SONDERBETRIEB AKTIV
UN  E 7.7;               // FMS-BETRIEB AKTIV
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
U   M 110.0;             // AUX-ON
=   M 113.0;             // SONDERBETRIEB AKTIV
//;
//;
U   M 113.0;             // SONDERBETRIEB AKTIV
L   S5TIME#800MS;        // 8X0,1S
SA  T 25;                // STARTEN T25 ALS AUSSCHALTVERZ.
NETWORK
TITLE = SPS/NC-->SB AKTIV/ 2M/MIN
U   M 113.0;             // SONDERBETRIEB AKTIV
=   DB20.DBX   340.2;    // SB AKTIV/VORSCHUBBEGR.FÜR AC 2M/MIN
NETWORK
TITLE = 7020  SONDERBETRIEB AKTIV
U   M 113.0;             // SONDERBETRIEB AKTIV
UN  DB3.DBX     5.2;             // 7042  MACHINETÜR INIT.
//;
=   DB3.DBX     2.4;             // 7020  SONDERBETRIEB AKTIV
NETWORK
TITLE = SPS/OB-->HANDBETRIEB EIN
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
=   DB1.DBX  1390.6;     // SPS/OB->SCHLÜSSELSCH.SONDERBETRIEB EIN
END_FUNCTION
FUNCTION FC 20 : VOID
NAME: INITIALISIERUNGEN
BEGIN
NETWORK
TITLE = SCHLÜSSELSCHALTER VORHANDEN
U     DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
S     DB1.DBX 1418.2; // SCHLÜSSELSCHALTER VORHANDEN
NETWORK
TITLE = M0,M1 AUSLÖSUNG
O   DB20.DBX  192.0;     // M0 DYNAMISCH
=   DB20.DBX  256.0;     // M0 RÜCKMELDEN
U   DB20.DBX  192.1;     // M1 DYNAMISCH
U   DB20.DBX  324.5;     // BEDINGTER HALT AKTIV
=   DB20.DBX  256.1;     // M1 RÜCKMELDEN
U   DB20.DBX  192.1;     // M1 DYNAMISCH
U   DB20.DBX  324.5;     // BEDINGTER HALT AKTIV
O   DB20.DBX  192.0;     // M0 DYNAMISCH
=   M 34.1;              // HM M0/M1 DYNAMISCH
NETWORK
TITLE = SPS/OB-->EINZELSTÜCK AKTIV
UN    DB10.DBX    1.0; // SPS-MSD DAUERLAUF AKTIV
=     DB1.DBX 1390.3; // SPS/RM-->EINZELSTÜCK AKTIV
NETWORK
TITLE = VORSCHUBSCHALTER AUF 0%
L     DB20.DBW  358;      // FEED-OVERRIDE-ISTWERT
L     W#16#0000;          // LADE 0
==I;                      // VERGLEICH AUF GLEICH
=     M    100.2;         //VORSCHUBSCHALTER AUF 0%
NETWORK
TITLE = ALARMAUSGANG
U   DB1.DBX  1366.3;      // MASCHINENALARM AKTIV
//;
=   A 4.7;                // ALARMAUSGANG
//;
END_FUNCTION
FUNCTION FC 21 : VOID
NAME: BEI_PROGRAMM_ENDE_RESET_ODER_NEUSTART
BEGIN
NETWORK 1
TITLE = PROGRAMM ENDE AN NC
O   M 10.1;              // HM
UN  DB10.DBX   1.0;      // SPS-MSD (DAUERLAUF)
R   M 10.1;              // HM
=   DB20.DBX   348.1;    // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT
NETWORK 2
TITLE = PROGRAMM ENDE UND NEUSTART (DAUERLAUF) AN NC
O   M 10.1;              // HM
U   DB10.DBX   1.0;      // SPS-MSD (DAUERLAUF)
R   M 10.1;              // HM
=   DB20.DBX   348.2;    // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT UND GESTARTET
NETWORK 3
TITLE = ISTDREHZAHL KLEINER 20U/MIN
O   DB20.DBX   192.2;    // M2 DYN.
O   DB20.DBX   195.6;    // M30 DYN.
S   M 10.0;              // HM
//;
U   M 10.0;              // HM
U   M 114.3;             // ISTDREHZAHL KLEINER 20U/MIN
S   M 10.1;              // HM
R   M 10.0;              // HM
NETWORK 3
TITLE = M2 und M30 RÜCKMELDEN VON M2,M30 UND RESET
O   DB20.DBX   348.1;    // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT
O   DB20.DBX   348.2;    // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT UND GESTARTET
O   DB1.DBX   1370.3;    // RESETTASTE GEDRÜCKT
O   DB1.DBX   1440.0;    // RESET AUSGELÖST
//;
R   M 10.0;              // HM
R   M 10.1;              // HM
=   DB20.DBX   259.6;    // M30 RÜCKMELDEN
=   DB20.DBX   256.2;    // M2 RÜCKMELDEN
//;
END_FUNCTION
FUNCTION FC 23 : VOID
NAME: INC_ACHSEN
BEGIN
NETWORK
TITLE = ACHSEN INC FREIGABE
U   M 104.0;              // SUM  AFG/EFG
//;
=   M 106.0;              // ACHSEN INC FREIGABE
NETWORK
TITLE = ACHSEN INC/STARTVERR.
U   DB20.DBX     1.3;               // FC INC ANFORDERUNG
UN  M 106.0;              // ACHSEN INC FREIGABE
//;
R   DB20.DBX     1.3;               // FC INC ANFORDERUNG
END_FUNCTION
FUNCTION FC 24 : VOID
NAME: HA_LEISTUNG
BEGIN
NETWORK
TITLE = LEISTUNG HAUPTANTRIEB
U   M 110.0;              // AUX-ON
UN  M 110.1;              // MASCHINENTÜR OFFEN
//;
=   M 55.0;               // LEISTUNG HAUPTANTRIEB
NETWORK
TITLE = HAUPTANTRIEB BETRIEBSBEREIT
U   M 50.7;              // FRANZ STELLER SERVO-READY
=   M 131.0;             // HA BETRIEBSBEREIT
NETWORK
TITLE = HA BETRIEBSBEREIT  SFG
UN  M 131.0;             // HA BETRIEBSBEREIT
=   M 94.1;              // HA BETRIEBSBEREIT  SFG
NETWORK
TITLE = ÜBERWACHUNG HA SCHARF
U   M 55.0;              // AUSGANGSMERKER LEISTUNG FRANZ FREQUENZUMRICHTER
L   S5TIME#2S500MS;      // 25X0,1S
SE  T 8;                 // STARTEN T8 ALS EINSCHALTVERZ.
NETWORK
TITLE = 6013  HAUPTANTRIEB NICHT BEREIT
UN  M 50.7;              // FRANZ STELLER SERVO-READY
L   S5TIME#200MS;        // 2X0,1S
SE  T 9;                 // STARTEN T9 ALS EINSCHALTVERZ.
//;
U   T 8;                 // HA ÜBERWACHUNG SCHARF
U   T 9;                 // HA/SERVO-READY (0,2S)
S   DB2.DBX     1.5;     // 6013  HAUPTANTRIEB NICHT BEREIT
//;
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   DB2.DBX     1.5;     // 6013  HAUPTANTRIEB NICHT BEREIT
END_FUNCTION
FUNCTION FC 26 : VOID
NAME: KÜHLMITTEL
BEGIN
NETWORK
TITLE = FM TASTE KÜHLMITTEL (MSTT/PC)
UN  M 110.1;             // MASCHINENTÜR OFFEN
U   DB20.DBX   294.6;    // TASTE KÜHLMITTEL (PC)
FP   M 107.7;            // FM TASTE KÜHLMITTEL
//;
NETWORK
TITLE = ANWAHL KÜHLMITTEL EIN
UN  M 107.1;             // KÜHLMITTEL EIN
U   M 107.7;             // FM TASTE KÜHLMITTEL
O;
U   DB20.DBX   225.1;    // M9 STATISCH
U   DB20.DBX   193.0;    // M8 DYN.
O   DB20.DBX   302.6;    // DNC/KÜHLMITTEL EIN
S   M 107.1;             // KÜHLMITTEL EIN
=   M 107.2;             // HM KÜHLMITTEL EIN
//;
//;
=   DB20.DBX   257.0;    // M8 AUSLÖSEN
NETWORK
TITLE = KÜHLMITTEL PROGRAMMIERT
U   DB20.DBX   326.2;    // KERN/BA AUTO
U   DB20.DBX   257.0;    // M8 AUSLÖSEN
U   DB20.DBX   193.0;    // M8 DYN.
//;
S   M 107.3;             // KÜHLMITTEL PROGRAMMIERT
NETWORK
TITLE = ABWAHL KÜHLMITTEL EIN
U   M 107.1;             // KÜHLMITTEL EIN
UN  M 107.2;             // HM KÜHLMITTEL EIN
U   M 107.7;             // FM TASTE KÜHLMITTEL
UN  M 107.3;             // KÜHLMITTEL PROGRAMMIERT
O;
U   DB20.DBX   225.0;    // M8 STATISCH
U   DB20.DBX   193.1;    // M9 DYN.
O   DB20.DBX   302.5;    // DNC/KÜHLMITTEL AUS
O;
U   M 107.4;             // KÜHLMITTEL UNTERBRECHUNG
UN  DB20.DBX   326.2;    // KERN/BA AUTO
O   DB1.DBX  1370.0;     // 1.SPS-SCHLEIFENDURCHLAUF
O   DB1.DBX  1440.0;     // RESET AUSGELÖST
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
ON  M 110.0;             // AUX-ON
//;
R   M 107.1;             // KÜHLMITTEL EIN
R   M 107.3;             // KÜHLMITTEL PROGRAMMIERT
//;
//;
=   DB20.DBX   257.1;    // M9 AUSLÖSEN
NETWORK
TITLE = KÜHLMITTEL UNTERBRECHUNG
U   M 107.3;             // KÜHLMITTEL PROGRAMMIERT
UN  M 107.4;             // KÜHLMITTEL UNTERBRECHUNG
U   M 107.7;             // FM TASTE KÜHLMITTEL
S   M 107.4;             // KÜHLMITTEL UNTERBRECHUNG
=   M 107.5;             // HM KÜHLMITTEL UNTERBRECHUNG
//;
//;
U   M 107.4;             // KÜHLMITTEL UNTERBRECHUNG
UN  M 107.5;             // HM KÜHLMITTEL UNTERBRECHUNG
U   M 107.7;             // FM TASTE KÜHLMITTEL
ON  M 107.1;             // KÜHLMITTEL EIN
R   M 107.4;             // KÜHLMITTEL UNTERBRECHUNG
NETWORK
TITLE = KÜHLMITTEL FREIGABE
U(;
ON  M 110.1;             // MASCHINENTÜR OFFEN
O   DB20.DBX   294.6;             // TASTE KÜHLMITTEL (PC)
);
UN  M 107.4;             // KÜHLMITTEL UNTERBROCHEN
U(;
ON  DB20.DBX   324.1;              // KERN/ZUSTAND STOP
ON  A 4.2;               // HA N=0/NC
);
UN  M 34.1;              // M0/M1 AKTIV STATISCH
UN  DB20.DBX   324.2;              // DRYRUN
UN  M 152.1              // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
//;
=   M 107.0;             // KÜHLMITTEL FREIGABE
NETWORK
TITLE = KÜHLMITTEL LEISTUNG
U   M 107.1;             // KÜHLMITTEL EIN
U   M 107.0;             // KÜHLMITTEL FREIGABE
//;
=   A 4.1;              // KÜHLMITTEL LEISTUNG
NETWORK
TITLE = SPS/OB-->KÜHLMITTEL EIN
U   M 107.1;            // KÜHLMITTEL EIN
=   DB20.DBX   310.5;             // SPS/OB-->KÜHLMITTEL EIN
NETWORK
TITLE = SPS/OB-->KÜHLMITTEL SPRITZT
U   A 4.1;              // KÜHLMITTEL LEISTUNG
=   DB20.DBX   310.6;             // SPS/OB-->KÜHLMITTEL SPRITZT
NETWORK
TITLE = KÜHLMITTEL/FC DNC-QUITTUNG
U   DB1.DBX  1366.0;             // DNC-BETRIEB AKTIV
U(;
U   DB20.DBX   302.6;            // DNC/KÜHLMITTEL EIN
UN  DB20.DBX   225.0;              // M8 STATISCH
O;
U   DB20.DBX   302.5;            // DNC/KÜHLMITTEL AUS
UN  DB20.DBX   225.1;              // M9 STATISCH
);
//;
S   DB20.DBX   318.3;             // KÜHLMITTEL/FC DNC-QUITTUNG
END_FUNCTION
FUNCTION FC 27 : VOID
NAME: SPINDEL_HALT_START
BEGIN
NETWORK
TITLE = FM TASTE SPINDEL START (MSTT/PC)
UN  M    110.1;          // MASCHINENTÜR OFFEN
U   DB20.DBX   295.4;    // TASTE SPINDEL START (PC)
UN  DB20.DBX   295.3;    // TASTE SPINDEL HALT (PC)
FP  M 132.5;             // FM TASTE SPINDEL START
NETWORK
TITLE = FM TASTE SPINDEL HALT (MSTT/PC)
UN  M 114.3;             // ISTDREHZAHL KLEINER 20U/MIN
U   DB20.DBX   295.3;    // TASTE SPINDEL HALT (PC)
UN  DB20.DBX   295.4;    // TASTE SPINDEL START (PC)
FP  M 132.7;             // FM TASTE SPINDEL HALT
NETWORK
TITLE = SPINDEL HALT/START FREIGABE
U   M 104.2;             // SUM  SFG
UN    DB1.DBX 1358.0;    // ACHSEN IN BEWEGUNG
UN  M 152.1              // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
//;
=   M 132.0;             // SPINDEL HALT/START FREIGABE
NETWORK
TITLE = SPINDEL HALT AKTIV
U   M 132.0;             // SPINDEL HALT/START FREIGABE
U   M 132.7;             // FM TASTE SPINDEL HALT
U(;
ON  M 104.5;             // SUM  SPINDEL EIN/AUS
O     DB20.DBX  324.0;   // PROGRAMM LÄUFT
);
UN    M    110.1;        // MASCHINENTÜR OFFEN
//;
S   M 132.1;             // SPINDEL HALT AKTIV
//;
//;
U   M 132.5;             // FM TASTE SPINDEL START
U(;
ON  M 104.5;             // SUM SPINDEL EIN/AUS
O     DB20.DBX  324.0;   // PROGRAMM LÄUFT
);
O   DB1.DBX  1440.0;     // RESET AUSGELÖST
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   M 132.1;             // SPINDEL HALT AKTIV
NETWORK
TITLE = SPINDEL HALT AKTIV  SFG/AFG/EFG
U   M 132.1;             // SPINDEL HALT AKTIV
=   M 95.7;              // SPINDEL HALT AKTIV  SFG
//;
//;
U   M 132.1;             // SPINDEL HALT AKTIV
U   DB20.DBX   326.2;              // KERN/BA AUTO
=   M 91.7;              // SPINDEL HALT AKTIV  AFG/EFG
NETWORK
TITLE = ÜBERWACUNGSZEIT FÜR HA M3/M4
U   M 132.5;             // FM TASTE SPINDEL START
U   M 104.5;             // SUM  SPINDEL EIN/AUS
UN    DB20.DBX  324.0;   // PROGRAMM LÄUFT
//;
L   S5TIME#1S;           // 10X0,1S
SV  T 43;                // STARTEN T43 ALS VERL. IMPULS
NETWORK
TITLE = /FM TASTE SPINDEL START (MT/PC)
U  DB20.DBX   295.4;     // TASTE SPINDEL START (PC)
UN  DB20.DBX   295.3;    // TASTE SPINDEL HALT (PC)
FN  M 132.3;             // FM TASTE SPINDEL START
END_FUNCTION
FUNCTION FC 28 : VOID
NAME: VORSCHUB_HALT_START
BEGIN
NETWORK
TITLE = FM TASTE VORSCHUB START(MSTT/PC)
U   DB20.DBX  295.2;     // TASTE VORSCHUB START (PC)
UN  DB20.DBX  295.1;     // TASTE VORSCHUB HALT (PC)
FP  M 133.5;             // FM TASTE VORSCHUB START
NETWORK
TITLE = FM TASTE VORSCHUB HALT (MSTT/PC)
U   DB20.DBX  295.1;     // TASTE VORSCHUB HALT (PC)
UN  DB20.DBX  295.2;     // TASTE VORSCHUB START (PC)
FP  M 133.7;             // FM TASTE VORSCHUB HALT
NETWORK
TITLE = VORSCHUB HALT/START FREIGABE
U   M 104.0;             // SUM  AFG/EFG
UN  M 152.1              // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
//;
=   M 133.0;             // VORSCHUB HALT/START FREIGABE
NETWORK
TITLE = VORSCHUB HALT AKTIV
O   DB20.DBX  326.4;     // BETRIEBSART AUTOMATIK
O   DB20.DBX  326.2;     // BETRIEBSART MDI
O   DB20.DBX  327.2;     // BETRIEBSART REFERENZ
U(
U   M 133.0;             // VORSCHUB HALT/START FREIGABE
U   M 133.7;             // FM TASTE VORSCHUB HALT
UN  M 110.1;             // MASCHINENTÜR OFFEN
U   DB1.DBX 1358.0;      // ACHSEN IN BEWEGUNG
)
//;
S   M 133.1;             // VORSCHUB HALT AKTIV
//;
U   M 133.5;             // FM TASTE VORSCHUB START
UN  M 132.1;             // SPINDEL HALT AKTIV
UN  M 110.1;             // MASCHINENTÜR OFFEN
O   DB1.DBX  1440.0;     // RESET AUSGELÖST
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
//;
R   M 133.1;             // VORSCHUB HALT AKTIV
NETWORK
TITLE = VORSCHUB HALT AKTIV  AFG/EFG
O   M 133.1;             // VORSCHUB HALT AKTIV
O   E 6.6;               // ROBOTIK/VORSCHUB HALT
//;
=   M 91.6;              // VORSCHUB HALT AKTIV  AFG/EFG
END_FUNCTION
FUNCTION FC 29 : VOID
NAME: EINZELSTÜCK
BEGIN
NETWORK
TITLE = SPS/OB-->EINZELSTÜCK AKTIV
UN  DB10.DBX   1.0;      // SPS-MSD(B1571/BIT0)/DAUERLAUF
//;
=   DB1.DBX  1390.3;     // SPS/OB-->EINZELSTÜCK AKTIV
END_FUNCTION
FUNCTION FC 30 : VOID
NAME: PNEUM_SCHRAUBSTOCK
BEGIN
NETWORK
TITLE = FM TASTE SCHRAUBSTOCK ZU
U   DB20.DBX   294.4;    // TASTE SCHRAUBSTOCK ZU (PC)
UN  E 7.7;               // FG AKTIV SCHUTZZAUN
UN  DB20.DBX   294.3;    // TASTE SCHRAUBSTOCK AUF (PC)
FP  M 138.7;             // FM TASTE SCHRAUBSTOCK ZU
NETWORK
TITLE = FM TASTE SCHRAUBSTOCK AUF
U   DB20.DBX   294.3;             // TASTE SCHRAUBSTOCK AUF (PC)
UN  E 7.7;              // FG AKTIV SCHUTZZAUN
UN  DB20.DBX   294.4;             // TASTE SCHRAUBSTOCK ZU (PC)
FP  M 139.1;             // FM TASTE SCHRAUBSTOCK AUF
NETWORK
TITLE = FREIGABE SCHRAUBSTOCK ZU/AUF
U   M 110.0;             // AUX-ON
U   A 4.2;               // HA N=0/NC
UN  M 34.5;              // ACHSEN IN BEWEGUNG
UN  M 152.1              // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
//;
=   M 138.0;             // FREIGABE SCHRAUBSTOCK ZU/AUF
NETWORK
TITLE = HM SCHRAUBSTOCK ZU (M26)
U     DB20.DBX  324.0; // PROGRAMM LÄUFT
U   DB20.DBX   195.2;              // M26 DYN.
//;
S   M 139.5;             // HM SCHRAUBSTOCK ZU (M26)
NETWORK
TITLE = LEISTUNG SCHRAUBSTOCK ZU
U   M 138.0;             // FREIGABE SCHRAUBSTOCK ZU/AUF
U(;
U   M 138.7;             // FM TASTE SCHRAUBSTOCK ZU
UN  M 139.1;             // FM TASTE SCHRAUBSTOCK AUF
O   DB20.DBX   195.2;              // M26 DYN.
O(;
O   M 169.1;             // ROBOTIK/FM SCHRAUBSTOCK ZU
O   DB20.DBX   302.2;             // DNC/SCHRAUBSTOCK ZU
U   E 7.7;              // FG AKTIV SCHUTZZAUN
);
U(;
ON    DB20.DBX  324.0;   // PROGRAMM LÄUFT
O   DB20.DBX   324.1;              // KERN/STOP
);
O(;
U   M 139.5;             // HM SCHRAUBSTOCK ZU (M26)
UN  M 34.5;              // ACHSEN IN BEWEGUNG
);
);
S   A 5.3;               // LEISTUNG SCHRAUBSTOCK ZU
R   A 5.4;               // LEISTUNG SCHRAUBSTOCK AUF
//;
//;
=   DB20.DBX   259.2;    // M26 AUSLÖSEN
S   M 139.7;             // HM M26 STATISCH
R   M 139.6;             // HM M25 STATISCH
NETWORK
TITLE = RÜCKS. HM SCHRAUBSTOCK ZU (M26)
U   M 139.5;             // HM SCHRAUBSTOCK ZU (M26)
UN  M 34.5;              // ACHSEN IN BEWEGUNG
//;
R   M 139.5;             // HM SCHRAUBSTOCK ZU (M26)
NETWORK
TITLE = HM SCHRAUBSTOCK AUF (M25)
U     DB20.DBX  324.0; // PROGRAMM LÄUFT
U   DB20.DBX   195.1;              // M25 DYN.
//;
S   M 139.4;             // HM SCHRAUBSTOCK AUF (M26)
NETWORK
TITLE = LEISTUNG SCHRAUBSTOCK AUF
U   M 138.0;             // FREIGABE SCHRAUBSTOCK ZU/AUF
U(;
U   M 139.1;             // FM TASTE SCHRAUBSTOCK AUF
UN  M 138.7;             // FM TASTE SCHRAUBSTOCK ZU
O   DB20.DBX   195.1;              // M25 DYN.
O(;
O   M 169.3;             // ROBOTIK/FM SCHRAUBSTOCK AUF
O   DB20.DBX   302.1;             // DNC/SCHRAUBSTOCK AUF
U   E 7.7;             // FG AKTIV SCHUTZZAUN
);
U(;
ON    DB20.DBX  324.0;   // PROGRAMM LÄUFT
O   DB20.DBX   324.1;             // KERN/STOP
);
O(;
U   M 139.4;            // HM SCHRAUBSTOCK AUF (M25)
UN  M 34.5;             // ACHSEN IN BEWEGUNG
);
);
S   A 5.4;              // LEISTUNG SCHRAUBSTOCK AUF
R   A 5.3;              // LEISTUNG SCHRAUBSTOCK ZU
//;
//;
=   DB20.DBX   259.1;   // M25 AUSLÖSEN
S   M 139.6;            // HM M25 STATISCH
R   M 139.7;            // HM M26 STATISCH
NETWORK
TITLE = RÜCK. HM SCHRAUBSTOCK AUF (M26)
U   M 139.4;            // HM SCHRAUBSTOCK AUF (M26)
UN  M 34.5;             // ACHSEN IN BEWEGUNG
//;
R   M 139.4;            // HM SCHRAUBSTOCK AUF (M26)
NETWORK
TITLE = SCHRAUBSTOCK GESPANNT
U   A 5.3;              // LEISTUNG SCHRAUBSTOCK ZU
U   E 5.0;              // DRUCKSCHALTER SCHRAUBSTOCK
//;
=   M 138.5;            // SCHRAUBSTOCK GESPANNT
NETWORK
TITLE = SCHRAUBSTOCK ENTSPANNT
U   A 5.4;              // LEISTUNG SCHRAUBSTOCK AUF
UN  E 5.0;              // DRUCKSCHALTER SCHRAUBSTOCK
//;
=   M 138.4;            // SCHRAUBSTOCK ENTSPANNT
NETWORK
TITLE = SCHRAUBSTOCKBEW. AKTIV  AFG/EFG
U   A 5.3;              // LEISTUNG SCHRAUBSTOCK ZU
UN  E 5.0;              // DRUCKSCHALTER SCHRAUBSTOCK
O;
U   A 5.4;              // LEISTUNG SCHRAUBSTOCK AUF
U   E 5.0;              // DRUCKSCHALTER SCHRAUBSTOCK
O   M 139.5;            // HM SCHRAUBSTOCK ZU (M26)
O   M 139.4;            // HM SCHRAUBSTOCK AUF (M25)
//;
=   M 91.5;             // SCHRAUBSTOCKBEW. AKTIV  AFG/EFG
NETWORK
TITLE = 6072  SCHRAUBSTOCK NICHT BEREIT
U   A 5.3;              // LEISTUNG SCHRAUBSTOCK ZU
L   S5TIME#1S;          // 10X0,1S
SE  T 32;               // STARTEN T32 ALS EINSCHALTVERZ.
//;
UN  E 5.0;              // DRUCKSCHALTER SCHRAUBSTOCK
L   S5TIME#200MS;       // 2X0,1S
SE  T 33;               // STARTEN T33 ALS EINSCHALTVERZ.
//;
//;
U   A 5.4;              // LEISTUNG SCHRAUBSTOCK AUF
L   S5TIME#1S;          // 10X0,1S
SE  T 34;               // STARTEN T34 ALS EINSCHALTVERZ.
//;
U   E 5.0;              // DRUCKSCHALTER SCHRAUBSTOCK
L   S5TIME#200MS;       // 2X0,1S
SE  T 35;               // STARTEN T35 ALS EINSCHALTVERZ.
//;
//;
//;
U   T 32;               // SCHRAUBST. ZU/ÜBERW. SCHARF
U   T 33;               // DRUCKSCHALTER SCHRAUBST. (0,2S)
O;
U   T 34;               // SCHRAUBST. AUF/ÜBERW. SCHARF
U   T 35;               // DRUCKSCHALTER SCHRAUBST. (0,2S)
O;
UN  M 138.5;            // SCHRAUBSTOCK GESPANNT
U(;
O   DB20.DBX   224.3;   // M3 STATISCH
O   DB20.DBX   224.4;   // M4 STATISCH
);
S   DB2.DBX     9.0;            // 6072  SCHRAUBSTOCK NICHT BEREIT
//;
//;
U   DB2.DBX     9.0;            // 6072  SCHRAUBSTOCK NICHT BEREIT
U   A 4.2;              // HA N=0/NC
R   A 5.3;              // LEISTUNG SCHRAUBSTOCK ZU
R   A 5.4;              // LEISTUNG SCHRAUBSTOCK AUF
//;
//;
UN  A 5.3;              // LEISTUNG SCHRAUBSTOCK ZU
UN  A 5.4;              // LEISTUNG SCHRAUBSTOCK AUF
U(
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
)
R   DB2.DBX     9.0;            // 6072  SCHRAUBSTOCK NICHT BEREIT
NETWORK
TITLE = 6050  M25/M26 BEI LAUFENDEM HA
UN  A 4.2;              // HA N=0/NC
U   DB20.DBX   195.1;             // M25 DYN.
S   DB2.DBX     6.2;            // 6050  M25 BEI LAUFENDEM HA
//;
//;
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   DB2.DBX     6.2;            // 6050  M25 BEI LAUFENDEM HA
NETWORK
TITLE = 7054  SCHRAUBSTOCK SCHLIESSEN
UN  M 138.5;            // SCHRAUBSTOCK GESPANNT
UN  DB3.DBX     5.2;            // 7042  MASCHINENTÜR INIT.
UN  DB3.DBX     2.4;            // 7020  SONDERBETRIEB AKTIV
UN  DB3.DBX     5.0;            // 7040  MASCHINENTÜR OFFEN
UN  DB3.DBX     2.0;            // 7016  HILFSANTRIEBE EINSCHALTEN
UN  DB3.DBX     2.1;            // 7017  REFERENZPUNKT ANFAHREN
UN  DB3.DBX     2.5;            // 7021  WERKZEUGWENDER FREIFAHREN
//;
=   DB3.DBX     6.6;            // 7054  SCHRAUBSTOCK SCHLIESSEN
NETWORK
TITLE = SPS/OB-->SCHRAUBSTOCK GESPANNT
U   M 138.5;            // SCHRAUBSTOCK GESPANNT
=   DB20.DBX   310.2;             // SPS/OB-->SCHRAUBSTOCK GESPANNT
NETWORK
TITLE = SPS/OB-->SCHRAUBSTOCK ENTSPANNT
U   M 138.4;             // SCHRAUBSTOCK ENTSPANNT
=   DB20.DBX   310.1;    // SPS/OB-->SCHRAUBSTOCK ENTSPANNT
NETWORK
TITLE = SCHRAUBSTOCK/FC DNC-QUITTUNG
U   DB1.DBX  1366.0;     // DNC-BETRIEBN AKTIV
U(;
U   DB20.DBX   302.2;    // DNC/SCHRAUBSTOCK ZU
UN  M 139.7;             // HM M26 STATISCH
O;
U   DB20.DBX   302.1;    // DNC/SCHRAUBSTOCK AUF
UN  M 139.6;             // HM M25 STATISCH
);
//;
S   DB20.DBX   318.1;    // SCHRAUBSTOCK/FC DNC-QUITTUNG
END_FUNCTION
FUNCTION FC 31 : VOID
NAME: AUSBLASEINRICHTUNG
BEGIN
NETWORK
TITLE = ANWAHL AUSBLASEN EIN (M71)
U   DB20.DBX   233.0;    // M72 STATISCH
U   DB20.DBX   200.7;    // M71 DYN.
O   DB20.DBX   303.0;    // DNC/AUSBLASEN EIN
S   M 140.1;             // AUSBLASEN EIN
//;
//;
=   DB20.DBX   264.7;    // M71 AUSLÖSEN
NETWORK
TITLE = ABWAHL AUSBLASEN EIN (M72)
U   DB20.DBX   232.7;    // M71 STATISCH
U   DB20.DBX   201.0;    // M72 DYN.
O   DB20.DBX   302.7;    // DNC/AUSBLASEN AUS
O   DB1.DBX  1370.0;     // 1.SPS-SCHLEIFENDURCHLAUF
ON  M 110.0;             // AUX-ON
O   DB1.DBX  1440.0;     // RESET AUSGELÖST
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
O   DB20.DBX   324.2;    // DRYRUN
O   M 110.1;             // MASCHINENTÜR OFFEN
R   M 140.1;             // AUSBLASEN EIN
//;
//;
=   DB20.DBX   265.0;    // M72 AUSLÖSEN
NETWORK
TITLE = LEISTUNG AUSBLASEN
U   M 140.1;             // AUSBLASEN EIN
=   A 5.5;               // LEISTUNG AUSBLASEN
NETWORK
TITLE = SPS/OB-->AUSBLASEN AKTIV
U   A 5.5;               // LEISTUNG AUSBLASEN
=   DB20.DBX   310.7;    // SPS/OB-->AUSBLASEN AKTIV
NETWORK
TITLE = AUSBLASEN/FC DNC-QUITTUNG
U   DB20.DBX   303.0;    // DNC/AUSBLASEN EIN
UN  DB20.DBX   264.7;    // M71 AUSLÖSEN
O;
U   DB20.DBX   302.7;    // DNC/AUSBLASEN AUS
UN  DB20.DBX   265.0;    // M72 AUSLÖSEN
//;
U   DB1.DBX  1366.0;     // DNC-BETRIEB AKTIV
S   DB20.DBX   318.4;    // AUSBLASEN/FC DNC-QUITTUNG
END_FUNCTION
FUNCTION FC 32 : VOID
NAME: TÜRAUTOMATIK
BEGIN
NETWORK
TITLE = /FM MASCHINENTÜR OFFEN
UN  DB1.DBX  1370.0;      // SPS 1.SCHLEIFENDURCHLAUF
U   M 110.1;              // MASCHINENTÜR OFFEN
FN  M 144.5;              // FM MASCHINENTÜR OFFEN
NETWORK
TITLE = FM ENDSCH. TÜR OFFEN
U   E 5.7;                // ENDSCH. TÜR OFFEN
FP  M 144.3;             // FM ENDSCH. TÜR OFFEN
NETWORK
TITLE = FREIGABE TÜR ZU/AUF
UN  DB2.DBX     0.0;     // 6000  NOT-AUS
UN  M 152.1              // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
UN  M 110.2;             // HM ALARM MASCHINENTÜR OFFEN
=   M 143.0;             // FREIGABE TÜR ZU/AUF
NETWORK
TITLE = HM TIPPBETRIEB TÜR ZU
U   M 143.0;             // FREIGABE TÜR ZU/AUF
U   E 5.6;               // ZUSTIMMTASTE GEDRÜCKT
UN  E 7.7;               // FG AKTIV VON FMS SCHUTZZAUN
U(;
U   DB1.DBX  1374.1;             // TASTE TÜR ZU (MSTT/PC)
UN  DB1.DBX  1374.0;             // TASTE TÜR AUF (MSTT/PC)
O;
UN  M 143.1;             // HM TASTE TÜR ZU/AUF
U   DB1.DBX  1374.2;             // TASTE TÜR ZU/AUF (MSTT/PC)
);
//;
=   M 143.7;             // HM TIPPBETRIEB TÜR ZU
R   M 143.6;             // HM TIPPBETRIEB TÜR AUF
R   M 143.4;             // HM TÜR AUF STATISCH
NETWORK
TITLE = HM TIPPBETRIEB TÜR AUF
U   M 143.0;             // FREIGABE TÜR ZU/AUF
U   E 5.6;               // ZUSTIMMTASTE GEDRÜCKT
UN  E 7.7;              // FG AKTIV VON FMS SCHUTZZAUN
U(;
U   DB1.DBX  1374.0;             // TASTE TÜR AUF (MSTT/PC)
UN  DB1.DBX  1374.1;             // TASTE TÜR ZU (MSTT/PC)
O;
U   M 143.1;             // HM TASTE TÜR ZU/AUF
U   DB1.DBX  1374.2;             // TASTE TÜR ZU/AUF (MSTT/PC)
);
//;
=   M 143.6;             // HM TIPPBETRIEB TÜR AUF
R   M 143.7;             // HM TIPPBETRIEB TÜR ZU
R   M 143.5;             // HM TÜR ZU STATISCH
NETWORK
TITLE = HM TÜR ZU STATISCH
O   M 170.1;             // ROBOTIK /FM TÜR ZU
O   DB1.DBX  1382.1;             // DNC TÜR ZU
U   E 7.7;              // FG AKTIV VON FMS SCHUTZZAUN
U   M 143.0;             // FREIGABE TÜR ZU/AUF
O;
U   M 144.5;             // /FM MASCHINENTÜR OFFEN
U   M 143.0;             // FREIGABE TÜR ZU/AUF
//;
S   M 143.5;             // HM TÜR ZU STATISCH
R   M 143.4;             // HM TÜR AUF STATISCH
R   M 143.6;             // HM TIPPBETRIEB TÜR AUF
S   M 143.1;             // HM TASTE TÜR ZU/AUF
NETWORK
TITLE = HM TÜR AUF STATISCH (M0/M1/M2/M30)
O   DB20.DBX 348.1; // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT (M2,M30)
O   M 34.1;         // HM M0/M1 DYNAMISCH
S   M 144.0;        // HM TÜR AUF STAT. (M0/M1/M2/M30)
NETWORK
TITLE = HM TÜR AUF STATISCH
U   M 143.0;            // FREIGABE TÜR ZU/AUF
U   E 5.6;              // ZUSTIMMTASTE GEDRÜCKT
U   M 144.3;            // FM ENDSCHALTER TÜR OFFEN
O;
U   M 143.0;            // FREIGABE TÜR ZU/AUF
U   M 144.0;            // HM TÜR AUF STATISCH (M0/M1/M2/M30)
O(;
O   M 170.3;            // ROBOTIK FM TÜR AUF
O   DB1.DBX  1382.0;    // DNC TÜR AUF
U   E 7.7;              // FG AKTIV VON FMS SCHUTZZAUN
U   M 143.0;            // FREIGABE TÜR ZU/AUF
);
O;
U   DB1.DBX  1370.0;    // 1.SPS-SCHLEIFENDURCHLAUF
U   E 5.7;              // ENDSCH. TÜR OFFEN
//;
S   M 143.4;            // HM TÜR AUF STATISCH
R   M 143.5;            // HM TÜR ZU STATISCH
R   M 143.7;            // HM TIPPBETRIEB TÜR ZU
R   M 143.1;            // HM TASTE TÜR ZU/AUF
R   M 144.0;        // HM TÜR AUF STAT. (M0/M1/M2/M30)
NETWORK
TITLE = ABBRUCH TÜRBEWEGUNG
U   M 91.3;         // TÜRBEWEGUNG AKTIV  AFG/EFG
U(  ;
O   DB2.DBX 0.0;    // 6000  NOT-AUS
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // TASTE RESET
O   DB1.DBX 1382.2; // DNC/TÜR STOP
)   ;
R   M 143.7;            // HM TIPPBETRIEB TÜR ZU
R   M 143.6;            // HM TIPPBETRIEB TÜR AUF
R   M 143.5;            // HM TÜR ZU STATISCH
R   M 143.4;            // HM TÜR AUF STATISCH
R   M 144.0;        // HM TÜR AUF STAT. (M0/M1/M2/M30)
NETWORK
TITLE = LEISTUNG TÜR ZU
U   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
UN  M 143.6;            // HM TIPPBETRIEB AUF
UN  M 144.0;            // HM TÜR AUF VON M0/M1/M2/M30
UN  M 143.4;            // HM TÜR AUF STATISCH
O   M 143.7;            // HM TIPPBETRIEB TÜR ZU
O   M 143.5;            // HM TÜR ZU STATISCH
//;
=   A 5.0;              // LEISTUNG TÜR ZU
NETWORK
TITLE = LEISTUNG TÜR AUF
O   M 143.6;            // HM TIPPBETRIEB TÜR AUF
O   M 143.4;            // HM TÜR AUF STATISCH
//;
=   A 5.1;              // LEISTUNG TÜR AUF
NETWORK
TITLE = TÜR OFFEN
U   A 5.1;              // LEISTUNG TÜR ZU
U   E 5.7;              // ENDSCH. TÜR OFFEN
//;
=   M 143.2;            // TÜR OFFEN
NETWORK
TITLE = TÜR ZU
U   A 5.0;              // LEISTUNG TÜR ZU
UN  M 110.1;            // MASCHINENTÜR OFFEN
//;
=   M 143.3;            // TÜR ZU
NETWORK
TITLE = TÜRBEWEGUNG AKTIV  AFG/EFG
U   A 5.0;              // LEISTUNG TÜR ZU
U   M 110.1;            // MASCHINENTÜR OFFEN
O;
U   A 5.1;              // LEISTUNG TÜR AUF
UN  E 5.7;              // ENDSCH. TÜR OFFEN
//;
=   M 91.3;             // TÜRBEWEGUNG AKTIV  AFG/EFG
NETWORK
TITLE = 6064  TÜR NICHT BEREIT
U   A 5.0;              // LEISTUNG TÜR ZU
L   S5TIME#5S;          // 50X0,1S
SE  T 36;               // STARTEN T36 ALS EINSCHALTVERZ.
//;
U   M 110.1;            // MASCHINENTÜR OFFEN
L   S5TIME#200MS;       // 2X0,1S
SE  T 37;               // STARTEN T37 ALS EINSCHALTVERZ.
//;
//;
U   A 5.1;              // LEISTUNG TÜR AUF
L   S5TIME#5S;          // 50X0,1S
SE  T 38;               // STARTEN T38 ALS EINSCHALTVERZ.
//;
UN  E 5.7;              // ENDSCH. TÜR OFFEN
L   S5TIME#200MS;       // 2X0,1S
SE  T 39;               // STARTEN T39 ALS EINSCHALTVERZ.
//;
//;
//;
U   T 36;               // TÜR ZUR/ÜBERWACHUNG SCHARF
U   T 37;               // MASCHINENTÜR OFFEN (0,2S)
O;
U   T 38;               // TÜR AUF/ÜBERWACHUNG SCHARF
U   T 39;               // ENDSCH. TÜR OFFEN (0,2S)
S   DB2.DBX     8.0;            // 6064  TÜR NICHT BEREIT
//;
//;
U   DB2.DBX     8.0;            // 6064  TÜR NICHT BEREIT
U   A 4.2;              // HA N=0/NC
R   A 5.0;              // LEISTUNG TÜR ZU
R   A 5.1;              // LEISTUNG TÜR AUF
//;
//;
UN  A 5.0;              // LEISTUNG TÜR ZU
UN  A 5.1;              // LEISTUNG TÜR AUF
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   DB2.DBX     8.0;            // 6064  TÜR NICHT BEREIT
NETWORK
TITLE = SPS/OB-->TÜR ZU
U   M 143.3;            // TÜR ZU
=   DB1.DBX  1390.1;             // SPS/OB-->TÜR ZU
NETWORK
TITLE = SPS/OB-->TÜR OFFEN
U   M 143.2;            // TÜR OFFEN
=   DB1.DBX  1390.0;             // SPS/OB-->TÜR OFFEN
NETWORK
TITLE = TÜR/FC DNC-QUITTUNG
U   DB1.DBX  1366.0;             // DNC-BETRIEB AKTIV
U(;
U   DB1.DBX  1382.1;            // DNC/TÜR ZU
UN  M 143.5;            // HM TÜR ZU STATISCH
O;
U   DB1.DBX  1382.0;            // DNC/TÜR AUF
UN  M 143.4;            // HM TÜR AUF STATISCH
O;
U   DB1.DBX  1382.2;            // DNC/TÜR STOP
U(;
O   M 143.7;            // HM TIPPBETRIEB TÜR ZU
O   M 143.6;            // HM TIPPBETRIEB TÜR AUF
O   M 143.5;            // HM TÜR ZU STATISCH
O   M 143.4;            // HM TÜR AUF STATISCH
);
);
//;
S   DB1.DBX  1398.0;             // TÜR/FC DNC-QUITTUNG
END_FUNCTION
FUNCTION FC 40: VOID
NAME: ROBOTIK_INTERFACE
VERSION : 0.0
BEGIN
NETWORK
TITLE = 6008  FEHLENDER CAN-TEILN.
ON    DB1.DBX 1414.1; // 2.CAN-EINGANGSPORT (CAN-ADRESSE 1)
ON    DB1.DBX 1415.1; // 2.CAN-AUSGANGSPORT (CAN-ADRESSE 8)
UN    DB10.DBX 2.0;   // AC 95 AKTIVIEREN
=     DB2.DBX    1.0; // 6008  FEHLENDER CAN-TEILN.
NETWORK
TITLE = ROBOTIK/FM AUX-ON
U     E      6.2; // ROBOTIK/AUX-ON
FP    M    168.1; // ROBOTIK/FM AUX-ON
NETWORK
TITLE = ROBOTIK/FM UMSCH. BA REF/AUTO
U     E      6.3; // ROBOTIK/UMSCH. BA REF/AUTO
FP    M    168.3; // ROBOTIK/FM UMSCH. BA REF/AUTO
NETWORK
TITLE = REFERENZPUNKT ANFAHREN
U     E      6.4; // ROBOTIK/REF ACHSEN
FP    M    168.5; // ROBOTIK/FM REF ACHSEN
//;
U     M    168.5; // ROBOTIK/REF ACHSEN
SPBN  X000;
SET
S     DB20.DBX  289.2; // BETRIEBSART REFERENZPUNKT ANFAHREN
S     DB20.DBX    0.0; // BETRIEBSARTUMSCHALTUNG AKTIVIEREN
L     W#16#7; // LADE HEX 7 (X,Y UND Z-ACHSE REFERENZIEREN)
T     DB20.DBB    8; // REFERENZIEREN DER KANALACHSEN
S     DB20.DBX    1.1; // ANFORDERUNG REFERENZPKT.ANFAHREN
X000: NOP   0;
NETWORK
TITLE = ROBOTIK/FM NC-START
U     E      6.5; // ROBOTIK/NC-START
FP    M    168.7; // ROBOTIK/FM NC-START
NETWORK
TITLE = ROBOTIK/FM SPM ZU
U     E      7.0; // ROBOTIK/SPM ZU
UN    E      7.1; // ROBOTIK/SPM AUF
FP    M    169.1; // ROBOTIK/FM SPM ZU
NETWORK
TITLE = ROBOTIK/FM SPM AUF
U     E      7.1; // ROBOTIK/SPM AUF
UN    E      7.0; // ROBOTIK/SPM ZU
FP    M    169.3; // ROBOTIK/FM SPM AUF
NETWORK
TITLE = ROBOTIK/FM TÜR ZU
U     E      7.4; // ROBOTIK/TÜR ZU
UN    E      7.5; // ROBOTIK/TÜR AUF
FP    M    170.1; // ROBOTIK/FM TÜR ZU
NETWORK
TITLE = ROBOTIK/TÜR AUF
U     E      7.5; // ROBOTIK/TÜR AUF
UN    E      7.4; // ROBOTIK/TÜR ZU
FP    M    170.3; // ROBOTIK/FM TÜR AUF
NETWORK
TITLE = ROBOTIK/NOT-AUS AKTIV
U     DB2.DBX     0.0; // 6000  NOT-AUS AKTIV
=     A      6.3; // ROBOTIK/NOT-AUS AKTIV
NETWORK
TITLE = ROBOTIK/MASCHINE READY
U     M    110.0; // AUX-ON
U     M    110.1; // MASCHINENTÜR OFFEN
U     DB20.DBX  324.4; // REFERENZPUNKT AKTIV
U     M    138.4; // SPM ENTSPANNT
U     DB20.DBX  326.4; // BETRIEBSART AUTOMATIK
//;
=     A      6.4; // ROBOTIK/MASCHINE READY
NETWORK
TITLE = ROBOTIK/BA REF-AUTO
U     DB20.DBX  327.2; // BETRIEBSART REFERENZ
=     A      6.5; // ROBOTIK/BA REFERENZ
NETWORK
TITLE = ROBOTIK/PROGRAMMSTATUS
U     DB20.DBX  224.1;    // M1 STATISCH
U     DB20.DBX  324.5;    // BEDINGTER HALT AKTIV
O     DB20.DBX  224.0;    // M0 STATISCH
O     DB20.DBX  224.2;    // M2 STATISCH
O     DB20.DBX  227.6;    // M30 STATISCH
//;
=     A      6.6;         // ROBOTIK/PROGRAMMENDE
NETWORK
TITLE = ROBOTIK/SPM GESPANNT
U     M    138.5; // SCHRAUBSTOCK GESPANNT
=     A      7.0; // ROBOTIK/SPM GESPANNT
NETWORK
TITLE = ROBOTIK/SPM ENTSPANNT
U     M    138.4; // SCHRAUBSTOCK ENTSPANNT
=     A      7.1; // ROBOTIK/SPM ENTSPANNT
NETWORK
TITLE = ROBOTIK/TÜR ZU
U     DB1.DBX 1390.1; // SPS/RM-->MASCHINENTÜR GESCHL.
=     A      7.6; // ROBOTIK/TÜR ZU
NETWORK
TITLE = ROBOTIK/TÜR OFFEN
U     M    143.2; // TÜR OFFEN
=     A      7.7; // ROBOTIK/TÜR OFFEN
NETWORK
TITLE = UMSCH. BA REF/AUTO
UN    M    168.3; // ROBOTIK/FM UMSCH. BA REF/AUTO
BEB   ;
//;
//;
UN    A      6.5; // ROBOTIK/BA REF-AUTO
S     DB20.DBX  289.2; // FC BETRIEBSART REFERENZ
S     DB20.DBX    0.0; // BETRIEBSARTENUMSCHALTUNG
BEB   ;
//;
//;
U     A      6.5; // ROBOTIK/BA REF-AUTO
S     DB20.DBX  288.4; // FC BETRIEBSART AUTOMATIK
S     DB20.DBX    0.0; // BETRIEBSARTENUMSCHALTUNG
END_FUNCTION
FUNCTION FC 34 : VOID
NAME: TEILAPPARAT
BEGIN
NETWORK
TITLE = FM TASTE TA
U   DB20.DBX   294.1;    // TASTE TA (PC)
FP  M 172.7;             // FM TASTE TA
NETWORK
TITLE = FREIGABE TA TEILEN
U   M 110.0;             // AUX-ON
UN  M 34.5;              // ACHSEN IN BEWEGUNG
UN  M 152.1              // WERKZEUGWENDEN ODER FREIFAHREN AKTIV
UN  M 173.4;             // TA TEILEN AKTIV
//;
=   M 172.0;             // FREIGABE TA TEILEN
NETWORK
TITLE = HM TA TEILEN (M27)
U     DB20.DBX  324.0; // PROGRAMM LÄUFT
U   DB20.DBX   195.3;              // M27 DYN.
//;
S   M 172.2;             // HM TA TEILEN (M27)
NETWORK
TITLE = TA STARTBEFEHL
U   M 172.0;             // FREIGABE TA TEILEN
U(;
O   M 172.7;             // FM TASTE TA
O   DB20.DBX   195.3;              // M27 DYN.
O   DB20.DBX   303.3;             // DNC/TA TEILEN
U(;
ON    DB20.DBX  324.0;   // PROGRAMM LÄUFT
O   DB20.DBX   324.1;              // KERN/STOP
);
O(;
U   M 172.2;             // HM TA TEILEN (M27)
UN  M 34.5;              // ACHSEN IN BEWEGUNG
);
);
//;
//;
=   M 172.1;             // TA TEILEN STARTBEFEHL
=   DB20.DBX   259.3;    // M27 AUSLÖSEN
R   M 173.0;             // TA FERTIG UND VERRIEGELT
S   M 173.4;             // TA TEILEN AKTIV
NETWORK
TITLE = RÜCKS. HM TEILEN (M27)
U   M 172.2;             // HM TA TEILEN (M27)
UN  M 34.5;              // ACHSEN IN BEWEGUNG
//;
R   M 172.2;             // HM TA TEILEN (M27)
NETWORK
TITLE = TA SCHRITT 1
U   M 172.1;             // TA TEILEN STARTBEFEHL
S   M 173.1;             // TA SCHRITT 1
NETWORK
TITLE = TA SCHRITT 2
U   M 173.1;             // TA SCHRITT 1
UN  E 4.1;               // BERO/TA TEILEN FERTIG
//;
R   M 173.1;             // TA SCHRITT 1
S   M 173.2;             // TA SCHRITT 2
NETWORK
TITLE = TA SCHRITT 3
L   DB10.DBW   8;        // LADE ZEIT AUS SPS-MSD
ITB;                     // UMWANDELN EINER GANZZAHL IN EINEN BCD-WERT
T   MW 80;              // TRANSFERIERE IN MW80
//;
SET
=   M 81.4              // SETZE ZEITRASTER 0,1S
//;
U   M 173.2;             // TA SCHRITT 2
L   MW 80;              // LADE MW80
SE  T 40;                // STARTEN T40 ALS EINSCHALTVERZ.
//;
//;
U   T 40;                // VERZÖGERUNGSZEIT ABGELAUFEN
R   M 173.2;             // TA SCHRITT 2
S   M 173.3;             // TA SCHRITT 3
NETWORK
TITLE = TA TEILEN FERTIG
O   M 173.3;             // TA SCHRITT 3
O   DB1.DBX  1370.0;              // 1.SPS-SCHLEIFENDURCHLAUF
U   E 4.1;               // BERO/TA TEILEN FERTIG
//;
R   M 173.3;             // TA SCHRITT 3
R   M 173.4;             // TA TEILEN AKTIV
S   M 173.0;             // TA FERTIG UND VERRIEGELT
NETWORK
TITLE = TA TEILEN AKTIV  AFG/EFG
O   M 173.4;             // TA TEILEN AKTIV
O   M 172.2;             // HM TA TEILEN (M27)
//;
=   M 91.2;              // TA TEILEN AKTIV  AFG/EFG
NETWORK
TITLE = 6073  TA NICHT BEREIT
U   M 173.0;             // TA FERTIG UND VERRIEGELT
UN  E 4.1;               // BERO/TA TEILEN FERTIG
O;
UN  M 173.0;             // TA FERTIG UND VERRIEGELT
U(;
O   DB20.DBX   224.3;    // M3 STATISCH
O   DB20.DBX   224.4;    // M4 STATISCH
);
S   DB2.DBX     9.1;            // 6073  TA NICHT BEREIT
//;
//;
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   DB2.DBX     9.1;            // 6073  TA NICHT BEREIT
NETWORK
TITLE = 6074  TA ZEITÜBERWACHUNG
U   M 173.1;            // TA SCHRITT 1
L   S5TIME#1S;          // 10X0,1S
SE  T 41;               // STARTEN T41 ALS EINSCHALTVERZ.
//;
U   M 173.3;            // TA SCHRITT 3
L   S5TIME#1S500MS;     // 15X0,1S
SE  T 42;               // STARTEN T42 ALS EINSCHALTVERZ.
//;
//;
O   T 41;               // VERZÖGERUNGSZ. ABGEL./TA 1
O   T 42;               // VERZÖGERUNGSZ. ABGEL./TA 3
S   DB2.DBX     9.2;            // 6074  TA ZEITÜBERWACHUNG
//;
//;
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   DB2.DBX     9.2;            // 6074  TA ZEITÜBERWACHUNG
NETWORK
TITLE = 6075  M27 BEI AKTIVEM HA
UN  A 4.2;              // HA N=0/NC
U   DB20.DBX   195.3;             // M27 DYN.
S   DB2.DBX     9.3;            // 6075  M27 BEI AKTIVEM HA
//;
//;
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   DB2.DBX     9.3;            // 6075  M27 BEI AKTIVEM HA
NETWORK
TITLE = TA ABBRUCHBEDINGUNGEN
U   M 173.4;            // TA TEILEN AKTIV
U(;
ON  M 110.0;            // AUX-ON
O   DB1.DBX  1440.0;     // RESET AUSGELÖST
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
);
//;
=   M 173.5;            // SUM ABBRECHEN
NETWORK
TITLE = FM SUM ABBRECHEN
U   M 173.5;            // SUM ABBRECHEN
FP  M 173.7;            // FM SUM ABBRECHEN
NETWORK
TITLE = TA ABBRUCH
U   M 173.7;            // FM SUM ABBRECHEN
//;
R   M 173.0;            // TA FERTIG UND VERRIEGELT
R   M 173.1;            // TA SCHRITT 1
R   M 173.2;            // TA SCHRITT 2
R   M 173.3;            // TA SCHRITT 3
R   M 173.4;            // TA TEILEN AKTIV
NETWORK
TITLE = LEISTUNG TA
O   M 173.1;            // TA SCHRITT 1
O   M 173.2;            // TA SCHRITT 2
//;
=   A 4.6;              // LEISTUNG TA
NETWORK
TITLE = 7055  TA NICHT VERRIEGELT
UN  M 173.4;            // TA TEILEN AKTIV
UN  M 173.0;            // TA FERTIG UND VERRIEGELT
UN  DB3.DBX     5.2;            // 7042  MASCHINENTÜR INIT.
UN  DB3.DBX     2.4;            // 7020  SONDERBETRIEB AKTIV
UN  DB3.DBX     5.0;            // 7040  MASCHINENTÜR OFFEN
UN  DB3.DBX     2.0;            // 7016  HILFSANTRIEBE EINSCHALTEN
UN  DB3.DBX     2.1;            // 7017  REFERENZPUNKT ANFAHREN
UN  DB3.DBX     2.5;            // 7021  WERKZEUGWENDER FREIFAHREN
//;
=   DB3.DBX     6.7;            // 7055  TA NICHT VERRIEGELT
NETWORK
TITLE = SPS/OB-->TA FERTIG UND VERR.
//
//
U   M 173.0;            // TA FERTIG UND VERRIEGELT
=   DB20.DBX   311.0;             // SPS/OB-->TA FERTIG UND VERR.
NETWORK
TITLE = TA/FC DNC-QUITTUNG
U   DB1.DBX  1366.0;             // DNC-BETRIEB AKTIV
U   DB20.DBX   303.3;            // DNC/TEILAPPARAPT TEILEN
UN  M 172.1;            // TA TEILEN STARTBEFEHL
//;
S   DB20.DBX   318.5;             // TA/FC DNC-QUITTUNG
END_FUNCTION
ORGANIZATION_BLOCK OB 1
NAME: ORGANISATIONSBAUSTEIN
BEGIN
//;
ON    DB1.DBX 1414.0;    // 1.CAN-EINGANGSPORT (CAN-ADRESSE 0)
ON    DB1.DBX 1415.0;    // 1.CAN-AUSGANGSPORT (CAN-ADRESSE 7)
UN  DB10.DBX 2.0;   // AC 95 AKTIVIEREN
=     DB2.DBX    1.0;    // 6008  FEHLENDER CAN-TEILN.
CALL FC 4 ;               // ZUWEISEN DER EINGÄNGE AC 95 - ACC
//;
CALL FC 20;                // INITIALISIERUNGEN
//;
U   DB1.DBX  1426.1;     // ROBOTIK SCHNITTSTELLE AKTIVIERT MIT DISKETTE
U   DB10.DBX   75.7;     // ROBOTIKSCHNITTSTELLE AKTIVIEREN
CC    FC 40;               // ROBOTIK-INTERFACE
//;
CALL FC 16;                // RESET
//;
CALL FC 21;                // BEI_PROGRAMM_ENDE_RESET_ODER_NEUSTART
//;
CALL FC 17;                // NC_START_UND_NC_STOP_VON_M0_ODER_M1
//;
CALL FC 24;                // HA LEISTUNG
//;
CALL FC 10;                // HA STEUERUNG
//;
CALL FC 11;                // ACHSEN LEISTUNG
//;
U   DB10.DBX   75.1;     // PNEUM. SCHRAUBSTOCK AKTIVIEREN
UN  DB10.DBX   75.6;     // TEILAPPARAT AKTIVIEREN
CC FC 30;                  // PNEUM. SCHRAUBSTOCK
//;
U   DB10.DBX   75.3;     // AUSBLASEN AKTIVIEREN
CC FC 31;                  // AUSBLASEINRICHTUNG
//;
U   DB10.DBX   75.0;     // TÜRAUTOMATIK AKTIVIEREN
CC FC 32;                  // TÜRAUTOMATIK
//;
U   DB10.DBX   75.6;     // TEILAPPARAT AKTIVIEREN
UN  DB10.DBX   75.1;     // PNEUM. SCHRAUBSTOCK AKTIVIEREN
CC FC 34;                  // PB TEILAPPARAT
//;
CALL FC 14;                // AUX-ON
//;
CALL FC 19;                // SONDERBETRIEB
//;
CALL FC 13;                // REF ACHSEN
//;
CALL FC 12;                // JOG ACHSEN
//;
CALL FC 23;                // INC ACHSEN
//;
CALL FC 27;                // SPINDEL HALT/START
//;
CALL FC 28;                // VORSCHUB HALT/START
//;
CALL FC 1 ;                // SCHMIERMITTEL
//;
CALL FC 2 ;                // STÜCKZÄHLER
//;
CALL FC 26;                // KÜHLMITTEL (M8/M9)
//;
CALL FC 29;                // EINZELSTÜCK
//;
CALL FC 15;                // NC-STARTVERR.
//;
U   DB10.DBX   75.5;     // WERKZEUGWENDER AKTIVIEREN
CC FC 0 ;                  // WERKZEUGWENDER
//;
CALL FC 18;                // PLC-NC ÜBERGABE
//;
CALL FC 5 ;                // ZUWEISEN DER AUSGÄNGE AC95 - ACC
U   DB25.DBX 332.0;   // NC-SATZ FERTIG
R   DB25.DBX 332.0;   // NC-SATZ FERTIG
END_ORGANIZATION_BLOCK
