[Info]
Version=1.0
CheckChange=73e0b091
//*****************************************************************************
//*                                                                           *
//*  ***********************************************************************  *
//*  *                                                                     *  *
//*  *        Simulations SPS-Programm für WIN-NC ohne Maschine            *  *
//*  *                                                                     *  *
//*  ***********************************************************************  *
//*                                                                           *
//*****************************************************************************
FUNCTION FC 0 : VOID
NAME: WERKZEUGWENDER SIMULIEREN
BEGIN
NETWORK
UN  DB20.DBX  182.0;     // NEUES T-WORT
SPB M001;
L   DB20.DBW   184;      // T-WORT
T   DB20.DBW   356;      // AKTIVES WERKZEUG IN DB20
M001:NOP 0;
NETWORK
L   DB20.DBW   184;      // T-WORT
L   W#16#1;              // HEX 1
<I;                      // VERGLEICH AUF KLEINER
U   DB20.DBX  182.0;     // NEUES T-WORT
=   M 125.1;             // T-WORT KLEINER 1
NETWORK
L   DB20.DBW   184;      // T-WORT
L   W#16#63;             // WERKZEUGNR.99
>I;                      // VERGLEICH AUF GRÖSSER
U   DB20.DBX  182.0;     // NEUES T-WORT
=   M 125.2;             // T-WORT GRÖSSER 99
NETWORK
O   M 125.1;             // T-WORT KLEINER 1
O   M 125.2;             // T-WORT GRÖSSER 99
S   DB3.DBX     0.0;     // T-WORT UNGÜLTIG
//;
U   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
R   DB3.DBX     0.0;
END_FUNCTION
FUNCTION FC 1 : VOID
NAME: INITIALISIERUNGEN
BEGIN
NETWORK
TITLE = 1/0-MERKER
UN  M 100.1;
S   M 100.1;             // 1 Merker
S   M 110.0;             // AUX-ON auto
S   M 110.3;             // AUX-ON manual
//;
U   M 100.1;
R   M 100.0;             // 0 Merker
NETWORK
TITLE = M0,M1 AUSLÖSUNG
U   DB20.DBX  192.1;     // M1 DYNAMISCH
U   DB20.DBX  324.5;     // BEDINGTER HALT AKTIV
O   DB20.DBX  192.0;     // M0 DYNAMISCH
=   M 34.1;              // HM M0/M1 DYNAMISCH
NETWORK
TITLE = SPS/OB-->EINZELSTÜCK AKTIV
U   M 100.1;             // 1-MERKER
UN  DB10.DBX    1.0;     // SPS-MSD DAUERLAUF AKTIV
//;
=   DB1.DBX 1390.3;      // SPS/RM-->EINZELSTÜCK AKTIV
NETWORK
TITLE = SUM SPINDEL EIN/AUS
UN    DB20.DBX  327.2;    // BETRIEBSART REFERENZ
UN    DB20.DBX  326.4;    // BETRIEBSART AUTOMATIK
UN    DB20.DBX  326.2;    // BETRIEBSART MDI
UN    DB20.DBX  326.1;    // BETRIEBSART EDIT
UN    DB20.DBX  326.3;    // BETRIEBSART REPOS
UN    DB20.DBX  327.4;    // BETRIEBSART PRESET
//;
=   M 106.7;              // M3/M4 SPINDEL FREIGABE IN
// BETRIEBSART JOG UND INC
NETWORK
TITLE = VORSCHUBSCHALTER AUF 0%
L     DB20.DBW  358;      // FEED-OVERRIDE-ISTWERT
L     W#16#0000;          // LADE 0
==I;                      // VERGLEICH AUF GLEICH
=     M 100.2;          //VORSCHUBSCHALTER AUF 0%
END_FUNCTION
FUNCTION FC 2 : VOID
NAME: STÜCKZÄHLER
BEGIN
NETWORK
TITLE = STÜCKZÄHLER
//;
UN  DB20.DBX  192.2;     // M2 dyn
UN  DB20.DBX  195.6;     // M30 dyn
O   DB20.DBX  324.2;     // DRYRUN AKTIV
SPB   M214;
L   DB15.DBD    4;       // Zähler
L   DW#16#000F_423F;     // 999999 STÜCK
<>D ;
SPB   M211;
L   DW#16#0000_0000;
T   DB15.DBD    4;
M211:  NOP 0;
L   DB15.DBD    4;
L   DW#16#0000_0001;
+D  ; // Stückzähler +1 low
T   DB15.DBD    4;
L   DB15.DBD    8;       // CHARGENZÄHLER
L   DW#16#000F_423F;     // 999999 STÜCK
<>D ;
SPB   M213;
L   DW#16#0000_0000;
T   DB15.DBD    8;
M213:  NOP  0;
L   DB15.DBD    8;
L   DW#16#0000_0001;
+D  ; // Stückzähler +1 low
T   DB15.DBD    8;
M214:  L   DB15.DBD    0;       // Sollstückz
L   DW#16#0000_0000;
<>D ;
SPB   M202;
SPA   M201;
M202:  NOP 0;
L   DB15.DBD    4;       // Stückz.
L   DB15.DBD    0;       // Sollstückz.
<D  ;
SPB   M201;
U   M 100.1;             // 1 Merker
S   DB3.DBX   5.3;       // 7043  SOLLSTÜCKZAHL ERREICHT
L   DW#16#0;
T   DB15.DBD    4;
M201: NOP   0;
NETWORK
TITLE = NC-STARTVERRIEGELUNG
U   DB3.DBX     5.3;     // Meldung Sollstückzahl erreicht
=   M 96.6;              // NC-Startverr.
//;
O   DB1.DBX  1370.2;     // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   DB3.DBX     5.3;     // Meldung Sollstückzahl erreicht
END_FUNCTION
FUNCTION FC 3 : VOID
NAME: HAUPTANTRIEB
BEGIN
NETWORK
TITLE = FM TASTE SPINDEL START (MSST)
O   DB20.DBX   295.4;    // TASTE SPINDEL START
=   M 108.0;             // HM TASTE SPINDEL START
//;
U   M 108.0;             // HM TASTE SPINDEL START
UN  M 132.4;             // HM
=   M 132.5;             // FM TASTE SPINDEL START
//;
U   M 108.0;             // HM TASTE SPINDEL START
=   M 132.4;             // HM
NETWORK
TITLE = FM TASTE SPINDEL HALT (MSST)
O   DB20.DBX   295.3;    // TASTE SPINDEL HALT
UN  M 132.6;             // HM
=   M 132.7;             // FM TASTE SPINDEL HALT
//;
O   DB20.DBX   295.3;    // TASTE SPINDEL HALT
=   M 132.6;             // HM
NETWORK
U   M 106.7;             // M3/M4 SPINDELSTART FREIGABE
UN    DB20.DBX  324.0;   // PROGRAMM LÄUFT
U   M 132.5;             // FM TASTE SPINDEL START
//;
S   M 108.3;             // HM SPINDEL EINGESCHALTET
L   S5TIME#1s;           // 1 SEKUNDE
SV  T 44;                // VERLÄNGERTER IMPULS
//;
O   DB1.DBX  1370.1;     // RESET AUSGELÖST
R   M 108.3;             // HM SPINDEL EINGESCHALTET
NETWORK
UN  T 44;                // VERLÄNGERTER IMPULS M3/M4
UN  M 108.1;             // HM
//;
=   M 108.2;             // FM /TASTE SPINDEL START
//;
UN  T 44;                // VERLÄNGERTER IMPULS M3/M4
=   M 108.1;             // HM
NETWORK
U   M 104.2;             // SUM SFG
UN  DB1.DBX  1358.0;     // ACHSE IN BEWEGUNG
UN  M 114.5;             // ISTDREHZAHL < 20 U/MIN
U   M 132.7;             // FM TASTE SPINDEL HALT
S   M 95.7;              // SFG
S   M 91.7;              // AFG,EFG
//;
U   M 106.7;             // BETR.MANUAL UND INC1 BIS 10000
R   M 91.7;              // AFG,EFG
//;
U   M 110.0;             // AUX ON
U   M 132.5;             // FM TASTE SPINDEL START
U(;
O   DB20.DBX   326.4;    // BETRIEBSART AUTO
ON    DB20.DBX  324.0;   // PROGRAMM LÄUFT
);
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
O   DB1.DBX  1370.1;     // RESET AUSGELÖST
R   M 95.7;              // SFG
R   M 91.7;              // AFG,EFG
NETWORK
UN  M 110.0;             // AUX-ON
=   M 95.0;              // SPINDELFREIGABE
NETWORK
TITLE = SUMME SFG
L   MW 94;               // WORT SFG (SPINDELFREIGABE)
L   W#16#0;              // H 0
==I;                     // VERGLEICH AUF GLEICH
=   M 104.2;             // SUM SFG
NETWORK
TITLE = HAUPTANTRIEB FREIGABE
U   M 104.2;             // SUM SFG
UN  DB1.DBX  1370.1;     // RESET AUSGELÖST/RS-485
=   M 114.0;             // HAUPTANTRIEB FREIGABE
NETWORK
TITLE = ANWAHL HAUPTANTRIEB  M3
UN  DB10.DBX   1.6;      // Spindel Start nach M0
U   M 104.3;             // SUM NC-STARTVERR.
U   M 101.0;             // NC-Startspeicher
U   M 100.6;             // M3 Speicher
O;
U   DB20.DBX   192.3;    // M3 dyn.
O;
U   T 44;                // VERLÄNGERTER IMPULS M3/M4
UN  M 108.0;             // HM SPINDEL START
//;
U   M 114.0;             // HA FREIGABE
=   DB20.DBX   256.3;    // M3 RÜCKMELDEN
S   DB20.DBX   340.0;    // SPINDEL EIN IM UHRZEIGER
R   DB20.DBX   340.1;    // SPINDEL EIN GEGEN UHRZEIGER
R   M 100.6;             // M3 Speicher
NETWORK
TITLE = ANWAHL HAUPTANTRIEB   M4
UN  DB10.DBX   1.6;      // Spindel Start nach M0
U   M 104.3;             // SUM NC-STARTVERR.
U   M 101.0;             // NC-Startspeicher
U   M 100.7;             // M4 Speicher
O;
U   DB20.DBX   192.4;    // M4 Dyn.
O;
U   M 108.2;             // FM /T44  M3/M4
U   M 108.0;             // HM TASTE SPINDEL START
//;
U   M 114.0;             // HA FREIGABE
=   DB20.DBX   256.4;    // M4 RÜCKMELDEN
S   DB20.DBX   340.1;    // SPINDEL EIN GEGEN UHRZEIGER
R   DB20.DBX   340.0;    // SPINDEL EIN IM UHRZEIGER
R   M 100.7;             // M4 Speicher
//;
NETWORK
TITLE = ANWAHL HAUPTANTRIEB  M5
U   M 108.3;             // HM SPINDEL EINGESCHALTET
U(;
O   M 132.7;             // FM TASTE SPINDEL HALT
ON  M 106.7;             // M3/M4 SPINDELSTART FREIGABE
);
O   DB1.DBX  1370.0;     // 1.SPS SCHLEIFENDURCHLAUF
O   DB20.DBX  192.5;     // M5 dynamisch
O   DB1.DBX  1370.1;     // RESET AUSGELÖST
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
O   DB20.DBX  324.2;     // DRYRUN AKTIV
//;
=   M 114.3;             // M5 HM
NETWORK
TITLE = M5 von M0,M1 UND M5 HM
U   M 34.1;             // HM M0/M1 DYNAMISCH
U(;
O   DB20.DBX   224.3;   // M3 statisch
O   DB20.DBX   224.4;   // M4 statisch
);
O   M 114.3;            // M5 HM
UN  M 101.0;            // NC-Start Speicher
//;
=   DB20.DBX   256.5;   // M5 RÜCKMELDEN
R   DB20.DBX   340.0;   // SPINDEL EIN IM UHRZEIGER
R   DB20.DBX   340.1;   // SPINDEL EIN GEGEN UHRZEIGER
R   M 108.3;            // HM SPINDEL EINGESCHALTET
NETWORK
TITLE = M3,M4 SPEICHERN BEI M0,M1
U   M 34.1;             // HM M0/M1 DYNAMISCH
UN  DB10.DBX   1.6;             // Spindelstart nach M0
U   DB20.DBX   224.3;   // M3 Statisch
//;
S   M 100.6;            // M3 Speicher
//;
//;
U   M 34.1;             // HM M0/M1 DYNAMISCH
UN  DB10.DBX   1.6;             // Spindelstart nach M0
U   DB20.DBX   224.4;   // M4 STATISCH
//;
S   M 100.7;            // M4 SPEICHER
//;
//;
O   DB1.DBX  1370.1;     // RESET AUSGELÖST
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
R   M 100.6;             // M3 Speicher
R   M 100.7;             // M4 Speicher
NETWORK
TITLE = SPINDELFREIGABE
U   M 114.0;             // HA FREIGABE
=   DB20.DBX   340.5;    // SPINDELFREIGABE
// BAUSTEINENDE
END_FUNCTION
*
FUNCTION FC 7 : VOID
NAME: ACHSEN_REFERENZIEREN
BEGIN
NETWORK
TITLE = AUTOMATISCH IN REF-MODE WECHSELN
ON  DB20.DBX  324.4;     // REFERENZPUNKT AKTIV
UN  DB1.DBX  1366.1;     // MSTT VORHANDEN
UN  M 122.0;
//;
S   M 122.0;             // AUTOMATISCH IN REF-MODE GEHEN
=   M 122.6;             // HM UNTERBETRIEBSART MAN_REF
NETWORK
TITLE = Y, A ODER B-ACHSE VORHANDEN
L   DB20.DBB 399;   // ABSOLUTE ACHSE Y
L   255;            // LADE KONSTANTE 25
<>I;                // VERGLEICH AUF UNGLEICH
=   M 123.0;        // Y-ACHSE VORHANDEN
L   DB20.DBB 408;   // ABSOLUTE ACHSE A
L   255;            // LADE KONSTANTE 255
<>I;                // VERGLEICH AUF UNGLEICH
=   M 123.1;        // A-ACHSE VORHANDEN
L   DB20.DBB 409;   // ABSOLUTE ACHSE B
L   255;            // LADE KONSTANTE 255
<>I;                // VERGLEICH AUF UNGLEICH
=   M 123.2;        // B-ACHSE VORHANDEN
NETWORK
TITLE = REFPKT.X,Z, VON MST ZUGLEICH ANFAHREN
U   DB20.DBX 327.2; // BETRIEBSART REFERENZ
U   DB1.DBX 1402.2; // EMCOTRONIC
FP  M 121.1         // FM BETRIEBSART REFERENZ EMCOTRONIC
UN  M 121.3;        // REFPKT.X,Z,ZUGLEICH ANFAHREN
U   DB20.DBX 830.4; // VON MST ALLE ACHSEN REFERENZIEREN
U   DB20.DBX 327.2; // BETRIEBSART REFERENZ
FP  M 121.0         // FM REFPKT.X,Z, VON MST ZUGLEICH ANFAHREN
O   M 121.0         // FM REFPKT.X,Z, VON MST ZUGLEICH ANFAHREN
O   M 121.1;      // FLANKENMERKER REF-MODE
SPBN M1701;
SET;
=   DB20.DBX 1.1;        // REFERENZPKT.ANFAHREN (UMLENKUNG)
=   DB20.DBX 8.0;        // REFERENZIEREN X-ACHSE
=   DB20.DBX 8.2;        // REFERENZIEREN Z-ACHSE
U   M 123.0        // Y-ACHSE VORHANDEN
S   DB20.DBX 8.1;        // REFERENZIEREN Y-ACHSE
M1701: NOP 0;
NETWORK
TITLE = REFPKT.X,Z,ZUGLEICH ANFAHREN
UN  M 100.2;             // VORSCHUBSCHALTER AUF 0%
UN  M 121.3;             // REFPKT.X,Z,ZUGLEICH ANFAHREN
U   DB20.DBX    8.0;     // REFERENZIEREN X-ACHSE
UN  DB20.DBX    8.1;     // REFERENZIEREN Y-ACHSE
U   DB20.DBX    8.2;     // REFERENZIEREN Z-ACHSE
//;
S   M 121.3;             // REFPKT.X,Z,ZUGLEICH ANFAHREN
FP  M 121.5;             // FM REFPKT.X,Z,ZUGLEICH ANFAHREN
//;
O   DB1.DBX  1370.3;     // RESET TASTE GEDRÜCKT
R   M 121.3;             // REFPKT.X,Z,ZUGLEICH ANFAHREN
NETWORK
TITLE = RUNDACHSEN MITREFERENZIEREN
U   M 121.2
SPBN M1702;
SET;
S   DB20.DBX 1.1;        // REFERENZIEREN B-ACHSE
R   M 121.2
U   M 123.1        // A-ACHSE VORHANDEN
S   DB20.DBX 9.2;        // REFERENZIEREN A-ACHSE
U   M 123.2        // B-ACHSE VORHANDEN
S   DB20.DBX 9.3;        // REFERENZIEREN B-ACHSE
M1702: NOP 0;
U   DB20.DBX    8.0;     // REFERENZIEREN X-ACHSE
U   DB20.DBX    8.1;     // REFERENZIEREN Y-ACHSE
U   DB20.DBX    8.2;     // REFERENZIEREN Z-ACHSE
S   M 121.2
NETWORK
TITLE = REFERENZPUNKT VERLOREN
O   M 121.5;             // FM REFPKT.X,Z,ZUGLEICH ANFAHREN
O   DB2.DBX     1.2;     // ALARM X-ACHSE DEFEKT
O   DB2.DBX     1.4;     // ALARM Z-ACHSE DEFEKT
U   DB20.DBX  324.4;     // REFERENZPUNKT AKTIV
//;
=   DB20.DBX   348.0;    // REFERENZPUNKT VERLOREN
NETWORK
TITLE = REFPKT.ANFAHREN SPERREN
ON  M 104.0;             // SUM AFG/EFG
ON  M 110.0;             // AUX-ON AUTO
U   DB20.DBX    1.1;     // REFERENZPKT.ANFAHREN (UMLENKUNG)
//;
R   DB20.DBX    1.1;     // REFERENZPKT.ANFAHREN (UMLENKUNG)
S   DB20.DBX   318.7;    // NEG.QUITTUNG DNC REFPKT.ANFAHREN
NETWORK
TITLE = X-ACHSE REFERENZIEREN (FC-CODE)
U   M 121.5;             // FM REFPKT.X,Z,ZUGLEICH ANFAHREN
U   DB20.DBX    8.0;     // REFERENZIEREN X-ACHSE
O(;
UN  M 100.2;             // VORSCHUBSCHALTER AUF 0%
UN  DB1.DBX 1358.0;      // ACHSEN IN BEWEGUNG
UN  M 121.3;             // REFPKT.X,Z,ZUGLEICH ANFAHREN
U   DB20.DBX    8.0;     // REFERENZIEREN X-ACHSE
);
=   DB20.DBX    8.0;     // REFERENZIEREN X-ACHSE
NETWORK
TITLE = FM REFERENZPUNKT X-ACHSE AKTIV
U   DB1.DBX   134.0;     // REFERENZPUNKT X-ACHSE AKTIV
FP  M 120.0;             // FM REFERENZPUNKT X-ACHSE AKTIV
NETWORK
TITLE = FM REFERENZPUNKT Z-ACHSE AKTIV
U   DB1.DBX   134.2;     // REFERENZPUNKT Z-ACHSE AKTIV
FP  M 120.2;             // FM REFERENZPUNKT Z-ACHSE AKTIV
NETWORK
TITLE = Z-ACHSE REFERENZIEREN (FC-CODE)
U   M 121.3;             // REFPKT.X,Z,ZUGLEICH ANFAHREN
O(;
UN  M 100.2;             // VORSCHUBSCHALTER AUF 0%
UN  DB1.DBX 1358.0;      // ACHSEN IN BEWEGUNG
UN  M 121.3;             // REFPKT.X,Z,ZUGLEICH ANFAHREN
U   DB20.DBX    8.2;     // REFERENZIEREN Z-ACHSE
);
=   DB20.DBX    8.2;     // REFERENZIEREN Z-ACHSE
NETWORK
TITLE = FM_REFERENZPUNKT_AKTIV
U   DB20.DBX  324.4;     // REFERENZPUNKT AKTIV
FP   M 122.5;            // FM REFERENZPUNKT AKTIV
//;
U   M 122.5;             // FM REFERENZPUNKT AKTIV
R   M 121.3;             // REFPKT.X,Z,ZUGLEICH ANFAHREN
//;
NETWORK
TITLE = IN_MANUAL-MODE_ZURÜCKSCHALTEN
UN  M 121.3;             // REFPKT.X,Z,ZUGLEICH ANFAHREN
U   M 122.0;             // AUTOMATISCH IN REF-MODE GEHEN
U   M 122.5;             // FM REFERENZPUNKT AKTIV
//;
R   M 122.0;             // AUTOMATISCH IN REF-MODE GEHEN
=   M 122.7;             // IN MANUAL-MODE ZURÜCKSCHALTEN
NETWORK
TITLE = MELDUNG REFERENZP. ANFAHREN
UN  DB3.DBX     5.0;     // MASCHINENTÜR OFFEN (M7040)
UN  DB3.DBX     5.1;     // RÄDERDECKEL OFFEN (M7041)
UN  DB3.DBX     6.0;     // FUTTER N.BEREIT (M7048)
UN  DB3.DBX     6.1;     // ZANGE N. BEREIT (M7049)
UN  DB3.DBX     6.2;     // KEIN TEIL GESPANNT (M7050)
UN  DB20.DBX  324.4;     // REFERENZPUNKT AKTIV
//;
=   DB3.DBX     2.1;     // REFERENZPUNKT ANFAHREN (M7017)
NETWORK
TITLE = BETRIEBSARTEN UMSCHALTEN
UN  M 122.6;             // HM UNTERBETRIEBSART MAN_REF
UN  M 122.7;             // IN MANUAL-MODE ZURÜCKSCHALTEN
BEB;                     // BAUSTEINENDE BEDINGT
//;
U   DB1.DBX  1366.1;     // MASCHINENTASTATUR VORHANDEN
BEB;
//;
U   M 122.7;             // IN MANUAL-MODE ZURÜCKSCHALTEN
=   DB20.DBX  288.0;     // BETRIEBSART MANUAL
=   DB20.DBX    0.0;     // BETRIEBSARTUMSCHALTUNG AKTIVIEREN
BEB;                     // BAUSTEINENDE BEDINGT
//;
U   M 100.1;
S   DB20.DBX  289.2;     // BETRIEBSART REFERENZPKT.ANFAHREN
S   DB20.DBX    0.0;     // BETRIEBSARTUMSCHALTUNG AKTIVIEREN
END_FUNCTION
FUNCTION FC 9 : VOID
NAME: NC-STARTVERRIEGELUNG
BEGIN
NETWORK
TITLE = AUX_ON/WORT NC_START VERR.
UN  DB20.DBX  324.2;     // DRYRUN AKTIV
UN  M 104.2;             // SUM SFG
ON  M 104.0;             // SUM  AFG/EFG
ON  M 110.0;             // AUX-ON auto
=   M 96.0;              // AUX_ON/WORT NC_START VERR.
NETWORK
TITLE = REF.PUNKT/WORT NC_START VERR.
NETWORK
TITLE = SUM NC_START VERR.
L   MW 96;               // WORT/NC_START VERR.
L   W#16#0;              // LADE KONSTANTE HEX 0
==I;                     // VERGLEICH AUF GLEICH
=   M 104.3;             // SUM NC_START VERR.
NETWORK
O   M 100.6;             // M3 Speicher
O   M 100.7;             // M4 Speicher
UN  DB10.DBX   1.6;      // Spindelstart nach M0
U   DB20.DBX     0.2;    // FC NC_Start
U   M 104.3;             // SUM NC_STARTVERR.
//;
S   M 101.0;             // NC-Start Speicher
NETWORK
TITLE = NC_START/STARTVERR.
//;       U   DB20.DBX  324.0;     // PROGRAMM LÄUFT
//;       UN  DB20.DBX  324.1;     // ZUSTAND STOP
//;       UN  DB20.DBX  324.7;     // BETRIEBSART EINZELSATZ
ON  M 104.3;             // SUM NC-START VERR.
O   M 101.0;             // NC-Start Speicher
O   DB1.DBX  1366.3;     // ALARM AKTIV
U   DB20.DBX     0.2;    // FC NC_START
//;
R   DB20.DBX     0.2;    // FC NC_START
=   DB20.DBX   319.0;    // NEG.QUITTUNG DNC
// BAUSTEINENDE
END_FUNCTION
FUNCTION FC 10 : VOID
NAME: BEI PROGRAMM ENDE RESET ODER NEUSTART
BEGIN
NETWORK
TITLE = PROGRAMM ENDE AN NC
O   DB20.DBX   195.6;    // M30 DYN.
O   DB20.DBX   192.2;    // M2 DYN.
UN  DB10.DBX   1.0;      // SPS-MSD (DAUERLAUF)
=   DB20.DBX   348.1;    // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT
NETWORK
TITLE = PROGRAMM ENDE UND NEUSTART (DAUERLAUF) AN NC
O   DB20.DBX   195.6;    // M30 DYN.
O   DB20.DBX   192.2;    // M2 DYN.
U   DB10.DBX   1.0;      // SPS-MSD (DAUERLAUF)
=   DB20.DBX   348.2;    // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT
// UND DAS NC-PROGRAMM NEU GESTARTET)
NETWORK
TITLE = M2 und M30 RÜCKMELDEN
O   DB20.DBX   192.2;    // M2 DYN.
=   DB20.DBX   256.2;    // M2 RÜCKMELDEN
O   DB20.DBX   195.6;    // M30 DYN.
=   DB20.DBX   259.2;    // M30 RÜCKMELDEN
END_FUNCTION
FUNCTION FC 11 : VOID
NAME: NC-START UND NC-STOP VON M0 ODER M1
BEGIN
NETWORK
TITLE = FM NC-STOPP TASTE VON BEDIENPULT oder HM M0/M1 DYNAMISCH
O   DB20.DBX 831.4  // NC-STOPP TASTE VON BEDIENPULT
FP  M 105.6;        // FM NC-STOPP TASTE VON BEDIENPULT
O   M 105.6;        // FM NC-STOPP TASTE VON BEDIENPULT
O   M 34.1;         // HM M0/M1 DYNAMISCH
O   DB20.DBX 0.1;   // NC-STOPP ANFORDERUNG
=   DB20.DBX 0.1;   // NC-STOPP ANFORDERUNG
NETWORK
TITLE = FC NC-START
UN  DB10.DBX   1.6;      // Spindelstart nach M0
U   M 101.0;             // NC-Start Speicher
U(;
O   DB20.DBX   224.3;    // M3 Statisch
O   DB20.DBX   224.4;    // M4 Statisch
);
O   M 150.1;             // FM ROBOTIK INTERFACE NC-START
S   DB20.DBX     0.2;    // FC NC-START
//;
U   DB20.DBX     0.2;    // FC NC-START
R   M 101.0;             // NC-Start Speicher
NETWORK
TITLE = FM CYCLE-START TASTE VON BEDIENPULT
O   DB20.DBX 831.5  // CYCLE-START TASTE VON BEDIENPULT
FP  M 105.5;        // FM CYCLE-START TASTE VON BEDIENPULT
O   M 105.5;        // FM CYCLE-START TASTE VON BEDIENPULT
U   M 104.3;        // SUM  NC-STARTVERR.
=   DB20.DBX 3.6;   // NC-START ANFORDERUNG
NETWORK
TITLE = FM RESET TASTE VON BEDIENPULT
O   DB20.DBX 828.6  // RESET TASTE VON BEDIENPULT
FP  M 105.7;        // FM RESET TASTE VON BEDIENPULT
O   M 105.7;        // FM RESET TASTE VON BEDIENPULT
O   DB20.DBX 4.0;   // RESET ANFORDERUNG
=   DB20.DBX 4.0;   // RESET ANFORDERUNG
NETWORK
END_FUNCTION
FUNCTION FC 12 : VOID
NAME: AFG/EFG
BEGIN
NETWORK
TITLE = FM TASTE VORSCHUB START
O   DB20.DBX   295.2;    // TASTE VORSCHUB START
UN  M 133.4;             // HM
=   M 133.5;             // FM TASTE VORSCHUB START
//;
O   DB20.DBX   295.2;    // TASTE VORSCHUB START
=   M 133.4;             // HM
NETWORK
TITLE = FM TASTE VORSCHUB HALT
O   DB20.DBX   295.1;    // TASTE VORSCHUB HALT
UN  M 133.6;             // HM
=   M 133.7;             // FM TASTE VORSCHUB HALT
//;
O   DB20.DBX   295.1;    // TASTE VORSCHUB HALT
=   M 133.6;             // HM
NETWORK
TITLE =
U   M 104.0;             // SUM AFG,EFG
U   M 133.7;             // FM TASTE VORSCHUB HALT
//;
S   M 91.6;              // AFG,EFG
//;
//;
U   M 133.5;             // FM TASTE VORSCHUB START
U(;
O   M 114.0;             // SPINDELFREIGABE
O   M 106.7;             // BETR.MANUAL UND INC
);
O   DB1.DBX  1370.3;     // RESETTASTE GEDRÜCKT
//;
R   M 91.6;              // AFG,EFG
NETWORK
TITLE = SUM MELDUNGEN  AFG/EFG
L   DB3.DBW      0;     // WORT/MELDUNGEN  AFG/EFG
L   W#16#0;              // LADE KONSTANTE HEX 0
<>I;                     // VERGLEICH AUF UNGLEICH
=   M 91.0;              // SUM MELDUNGEN  AFG/EFG
NETWORK
TITLE = SUM  AFG/EFG
L   MW 90;               // WORT  AFG/EFG
L   W#16#0;              // LADE KONSTANTE HEX 0
==I;                     // VERGLEICH AUF GLEICH
=   M 104.0;             // SUM  AFG/EFG
NETWORK
TITLE = AFG,EFG
UN    DB20.DBX  327.2;   // BETRIEBSART REFERENZ
UN    DB20.DBX  326.4;   // BETRIEBSART AUTOMATIK
UN    DB20.DBX  326.2;   // BETRIEBSART MDI
UN    DB20.DBX  326.1;   // BETRIEBSART EDIT
UN    DB20.DBX  326.3;   // BETRIEBSART REPOS
UN    DB20.DBX  327.4;   // BETRIEBSART PRESET
U   M 104.0;             // Sum AFG/EFG
U   M 110.3;             // AUX-ON manual
O;
UN    DB20.DBX  326.0;   // BETRIEBSART JOG
UN    DB20.DBX  326.1;   // BETRIEBSART EDIT
UN    DB20.DBX  326.3;   // BETRIEBSART REPOS
UN    DB20.DBX  327.4;   // BETRIEBSART PRESET
;       UN    DB20.DBX  326.5;   // BETRIEBSART INC 1
;       UN    DB20.DBX  326.6;   // BETRIEBSART INC 10
;       UN    DB20.DBX  326.7;   // BETRIEBSART INC 100
;       UN    DB20.DBX  327.0;   // BETRIEBSART INC 1000
;       UN    DB20.DBX  327.1;   // BETRIEBSART INC 10000
U   M 104.0;             // Sum AFG/EFG
U   M 110.0;             // AUX-ON auto
//;
=   DB20.DBX   340.4;    // Achsfreigabe
=   DB20.DBX   340.3;    // Einlesefreigabe
END_FUNCTION
FUNCTION FC 30 : VOID
NAME: M-RÜCKMELDUNGEN
BEGIN
NETWORK
O   DB20.DBX  192.0;     // M0 DYNAMISCH
=   DB20.DBX  256.0;     // M0 RÜCKMELDEN
U   DB20.DBX  192.1;     // M1 DYNAMISCH
U   DB20.DBX  324.5;     // BEDINGTER HALT AKTIV
=   DB20.DBX  256.1;     // M1 RÜCKMELDEN
O   DB20.DBX  192.2;     // M2 DYNAMISCH
=   DB20.DBX  256.2;     // M2 RÜCKMELDEN
O   DB20.DBX  192.3;     // M3 DYNAMISCH
=   DB20.DBX  256.3;     // M3 RÜCKMELDEN
O   DB20.DBX  192.4;     // M4 DYNAMISCH
=   DB20.DBX  256.4;     // M4 RÜCKMELDEN
O   DB20.DBX  192.5;     // M5 DYNAMISCH
=   DB20.DBX  256.5;     // M5 RÜCKMELDEN
O   DB20.DBX  192.6;     // M6 DYNAMISCH
=   DB20.DBX  256.6;     // M6 RÜCKMELDEN
O   DB20.DBX  192.7;     // M7 DYNAMISCH
=   DB20.DBX  256.7;     // M7 RÜCKMELDEN
O   DB20.DBX  193.0;     // M8 DYNAMISCH
=   DB20.DBX  257.0;     // M8 RÜCKMELDEN
U   DB20.DBX  193.1;     // M9 DYNAMISCH
=   DB20.DBX  257.1;     // M9 RÜCKMELDEN
O   DB20.DBX  193.2;     // M10 DYNAMISCH
=   DB20.DBX  257.2;     // M10 RÜCKMELDEN
O   DB20.DBX  193.3;     // M11 DYNAMISCH
=   DB20.DBX  257.3;     // M11 RÜCKMELDEN
O   DB20.DBX  193.4;     // M12 DYNAMISCH
=   DB20.DBX  257.4;     // M12 RÜCKMELDEN
O   DB20.DBX  193.5;     // M13 DYNAMISCH
=   DB20.DBX  257.5;     // M13 RÜCKMELDEN
O   DB20.DBX  193.6;     // M14 DYNAMISCH
=   DB20.DBX  257.6;     // M14 RÜCKMELDEN
O   DB20.DBX  193.7;     // M15 DYNAMISCH
=   DB20.DBX  257.8;     // M15 RÜCKMELDEN
O   DB20.DBX  194.0;     // M DYNAMISCH
=   DB20.DBX  258.0;     // M RÜCKMELDEN
U   DB20.DBX  194.1;     // M DYNAMISCH
=   DB20.DBX  258.1;     // M RÜCKMELDEN
O   DB20.DBX  194.2;     // M DYNAMISCH
=   DB20.DBX  258.2;     // M RÜCKMELDEN
O   DB20.DBX  194.3;     // M DYNAMISCH
=   DB20.DBX  258.3;     // M RÜCKMELDEN
O   DB20.DBX  194.4;     // M DYNAMISCH
=   DB20.DBX  258.4;     // M RÜCKMELDEN
O   DB20.DBX  194.5;     // M DYNAMISCH
=   DB20.DBX  258.5;     // M RÜCKMELDEN
O   DB20.DBX  194.6;     // M DYNAMISCH
=   DB20.DBX  258.6;     // M RÜCKMELDEN
O   DB20.DBX  194.7;     // M DYNAMISCH
=   DB20.DBX  258.8;     // M RÜCKMELDEN
O   DB20.DBX  195.0;     // M DYNAMISCH
=   DB20.DBX  259.0;     // M RÜCKMELDEN
U   DB20.DBX  195.1;     // M DYNAMISCH
=   DB20.DBX  259.1;     // M RÜCKMELDEN
O   DB20.DBX  195.2;     // M DYNAMISCH
=   DB20.DBX  259.2;     // M RÜCKMELDEN
O   DB20.DBX  195.3;     // M DYNAMISCH
=   DB20.DBX  259.3;     // M RÜCKMELDEN
O   DB20.DBX  195.4;     // M DYNAMISCH
=   DB20.DBX  259.4;     // M RÜCKMELDEN
O   DB20.DBX  195.5;     // M DYNAMISCH
=   DB20.DBX  259.5;     // M RÜCKMELDEN
O   DB20.DBX  195.6;     // M DYNAMISCH
=   DB20.DBX  259.6;     // M RÜCKMELDEN
O   DB20.DBX  195.7;     // M DYNAMISCH
=   DB20.DBX  259.8;     // M RÜCKMELDEN
O   DB20.DBX  196.0;     // M DYNAMISCH
=   DB20.DBX  260.0;     // M RÜCKMELDEN
U   DB20.DBX  196.1;     // M DYNAMISCH
=   DB20.DBX  260.1;     // M RÜCKMELDEN
O   DB20.DBX  196.2;     // M DYNAMISCH
=   DB20.DBX  260.2;     // M RÜCKMELDEN
O   DB20.DBX  196.3;     // M DYNAMISCH
=   DB20.DBX  260.3;     // M RÜCKMELDEN
O   DB20.DBX  196.4;     // M DYNAMISCH
=   DB20.DBX  260.4;     // M RÜCKMELDEN
O   DB20.DBX  196.5;     // M DYNAMISCH
=   DB20.DBX  260.5;     // M RÜCKMELDEN
O   DB20.DBX  196.6;     // M DYNAMISCH
=   DB20.DBX  260.6;     // M RÜCKMELDEN
O   DB20.DBX  196.7;     // M DYNAMISCH
=   DB20.DBX  260.8;     // M RÜCKMELDEN
O   DB20.DBX  197.0;     // M DYNAMISCH
=   DB20.DBX  261.0;     // M RÜCKMELDEN
U   DB20.DBX  197.1;     // M DYNAMISCH
=   DB20.DBX  261.1;     // M RÜCKMELDEN
O   DB20.DBX  197.2;     // M DYNAMISCH
=   DB20.DBX  261.2;     // M RÜCKMELDEN
O   DB20.DBX  197.3;     // M DYNAMISCH
=   DB20.DBX  261.3;     // M RÜCKMELDEN
O   DB20.DBX  197.4;     // M DYNAMISCH
=   DB20.DBX  261.4;     // M RÜCKMELDEN
O   DB20.DBX  197.5;     // M DYNAMISCH
=   DB20.DBX  261.5;     // M RÜCKMELDEN
O   DB20.DBX  197.6;     // M DYNAMISCH
=   DB20.DBX  261.6;     // M RÜCKMELDEN
O   DB20.DBX  197.7;     // M DYNAMISCH
=   DB20.DBX  261.8;     // M RÜCKMELDEN
O   DB20.DBX  198.0;     // M DYNAMISCH
=   DB20.DBX  262.0;     // M RÜCKMELDEN
U   DB20.DBX  198.1;     // M DYNAMISCH
=   DB20.DBX  262.1;     // M RÜCKMELDEN
O   DB20.DBX  198.2;     // M DYNAMISCH
=   DB20.DBX  262.2;     // M RÜCKMELDEN
O   DB20.DBX  198.3;     // M DYNAMISCH
=   DB20.DBX  262.3;     // M RÜCKMELDEN
O   DB20.DBX  198.4;     // M DYNAMISCH
=   DB20.DBX  262.4;     // M RÜCKMELDEN
O   DB20.DBX  198.5;     // M DYNAMISCH
=   DB20.DBX  262.5;     // M RÜCKMELDEN
O   DB20.DBX  198.6;     // M DYNAMISCH
=   DB20.DBX  262.6;     // M RÜCKMELDEN
O   DB20.DBX  198.7;     // M DYNAMISCH
=   DB20.DBX  262.8;     // M RÜCKMELDEN
O   DB20.DBX  199.0;     // M DYNAMISCH
=   DB20.DBX  263.0;     // M RÜCKMELDEN
U   DB20.DBX  199.1;     // M DYNAMISCH
=   DB20.DBX  263.1;     // M RÜCKMELDEN
O   DB20.DBX  199.2;     // M DYNAMISCH
=   DB20.DBX  263.2;     // M RÜCKMELDEN
O   DB20.DBX  199.3;     // M DYNAMISCH
=   DB20.DBX  263.3;     // M RÜCKMELDEN
O   DB20.DBX  199.4;     // M DYNAMISCH
=   DB20.DBX  263.4;     // M RÜCKMELDEN
O   DB20.DBX  199.5;     // M DYNAMISCH
=   DB20.DBX  263.5;     // M RÜCKMELDEN
O   DB20.DBX  199.6;     // M DYNAMISCH
=   DB20.DBX  263.6;     // M RÜCKMELDEN
O   DB20.DBX  199.7;     // M DYNAMISCH
=   DB20.DBX  263.8;     // M RÜCKMELDEN
O   DB20.DBX  200.0;     // M DYNAMISCH
=   DB20.DBX  264.0;     // M RÜCKMELDEN
U   DB20.DBX  200.1;     // M DYNAMISCH
=   DB20.DBX  264.1;     // M RÜCKMELDEN
O   DB20.DBX  200.2;     // M DYNAMISCH
=   DB20.DBX  264.2;     // M RÜCKMELDEN
O   DB20.DBX  200.3;     // M DYNAMISCH
=   DB20.DBX  264.3;     // M RÜCKMELDEN
O   DB20.DBX  200.4;     // M DYNAMISCH
=   DB20.DBX  264.4;     // M RÜCKMELDEN
O   DB20.DBX  200.5;     // M DYNAMISCH
=   DB20.DBX  264.5;     // M RÜCKMELDEN
O   DB20.DBX  200.6;     // M DYNAMISCH
=   DB20.DBX  264.6;     // M RÜCKMELDEN
O   DB20.DBX  200.7;     // M DYNAMISCH
=   DB20.DBX  264.8;     // M RÜCKMELDEN
O   DB20.DBX  201.0;     // M DYNAMISCH
=   DB20.DBX  265.0;     // M RÜCKMELDEN
U   DB20.DBX  201.1;     // M DYNAMISCH
=   DB20.DBX  265.1;     // M RÜCKMELDEN
O   DB20.DBX  201.2;     // M DYNAMISCH
=   DB20.DBX  265.2;     // M RÜCKMELDEN
O   DB20.DBX  201.3;     // M DYNAMISCH
=   DB20.DBX  265.3;     // M RÜCKMELDEN
O   DB20.DBX  201.4;     // M DYNAMISCH
=   DB20.DBX  265.4;     // M RÜCKMELDEN
O   DB20.DBX  201.5;     // M DYNAMISCH
=   DB20.DBX  265.5;     // M RÜCKMELDEN
O   DB20.DBX  201.6;     // M DYNAMISCH
=   DB20.DBX  265.6;     // M RÜCKMELDEN
O   DB20.DBX  201.7;     // M DYNAMISCH
=   DB20.DBX  265.8;     // M RÜCKMELDEN
O   DB20.DBX  202.0;     // M DYNAMISCH
=   DB20.DBX  266.0;     // M RÜCKMELDEN
U   DB20.DBX  202.1;     // M DYNAMISCH
=   DB20.DBX  266.1;     // M RÜCKMELDEN
O   DB20.DBX  202.2;     // M DYNAMISCH
=   DB20.DBX  266.2;     // M RÜCKMELDEN
O   DB20.DBX  202.3;     // M DYNAMISCH
=   DB20.DBX  266.3;     // M RÜCKMELDEN
O   DB20.DBX  202.4;     // M DYNAMISCH
=   DB20.DBX  266.4;     // M RÜCKMELDEN
O   DB20.DBX  202.5;     // M DYNAMISCH
=   DB20.DBX  266.5;     // M RÜCKMELDEN
O   DB20.DBX  202.6;     // M DYNAMISCH
=   DB20.DBX  266.6;     // M RÜCKMELDEN
O   DB20.DBX  202.7;     // M DYNAMISCH
=   DB20.DBX  266.8;     // M RÜCKMELDEN
O   DB20.DBX  203.0;     // M DYNAMISCH
=   DB20.DBX  267.0;     // M RÜCKMELDEN
U   DB20.DBX  203.1;     // M DYNAMISCH
=   DB20.DBX  267.1;     // M RÜCKMELDEN
O   DB20.DBX  203.2;     // M DYNAMISCH
=   DB20.DBX  267.2;     // M RÜCKMELDEN
O   DB20.DBX  203.3;     // M DYNAMISCH
=   DB20.DBX  267.3;     // M RÜCKMELDEN
O   DB20.DBX  203.4;     // M DYNAMISCH
=   DB20.DBX  267.4;     // M RÜCKMELDEN
O   DB20.DBX  203.5;     // M DYNAMISCH
=   DB20.DBX  267.5;     // M RÜCKMELDEN
O   DB20.DBX  203.6;     // M DYNAMISCH
=   DB20.DBX  267.6;     // M RÜCKMELDEN
O   DB20.DBX  203.7;     // M DYNAMISCH
=   DB20.DBX  267.8;     // M RÜCKMELDEN
O   DB20.DBX  204.0;     // M DYNAMISCH
=   DB20.DBX  268.0;     // M RÜCKMELDEN
U   DB20.DBX  204.1;     // M DYNAMISCH
=   DB20.DBX  268.1;     // M RÜCKMELDEN
O   DB20.DBX  204.2;     // M DYNAMISCH
=   DB20.DBX  268.2;     // M RÜCKMELDEN
O   DB20.DBX  204.3;     // M DYNAMISCH
=   DB20.DBX  268.3;     // M RÜCKMELDEN
O   DB20.DBX  204.4;     // M DYNAMISCH
=   DB20.DBX  268.4;     // M RÜCKMELDEN
O   DB20.DBX  204.5;     // M DYNAMISCH
=   DB20.DBX  268.5;     // M RÜCKMELDEN
O   DB20.DBX  204.6;     // M DYNAMISCH
=   DB20.DBX  268.6;     // M RÜCKMELDEN
O   DB20.DBX  204.7;     // M DYNAMISCH
=   DB20.DBX  268.8;     // M RÜCKMELDEN
END_FUNCTION
ORGANIZATION_BLOCK OB 1
BEGIN
//;
CALL  FC 0;                // WERKZEUGWENDER SIMULIEREN
//;
CALL  FC 1;                // INITIALISIERUNGEN
//;
CALL  FC 2;                // STÜCKZÄHLER
//;
CALL  FC 3;                // HAUPTANTRIEB
//;
CALL  FC 7;                // ACHSEN REFERENZIEREN
//;
CALL  FC 10;               // BEI PROGRAMM ENDE RESET ODER NEUSTART
//;
CALL  FC 11;               // NC-START UND NC-STOP VON M0 ODER M1
//;
CALL  FC 30;               // M-RÜCKMELDUNGEN
//;
CALL  FC 12;               // AFG/EFG
//;
CALL  FC 9;                // NC-STARTVERRIEGELUNG
END_ORGANIZATION_BLOCK
