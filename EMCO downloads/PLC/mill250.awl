[Info]
Version=2.04
CheckChange=4596b76e
//
//   *************************************************************************
//   *        DB1.DBX       1460.0 Reglerfreigabe absolute Achse 0           *
//   *                SPS-PROGRAMM FÜR DIE CM250                             *
//   *                   MIT SIEMENS-Antriebe                                *
//   *                 (aus CT250 entstanden)                                *
//   *      Designed by Friedrich Schörghofer          10.11.2008            *
//   *  DB1.DBX 1466.0 Offsetabgleich für alle analogen Linear-und Rundachsen*
//   *  db20.dbx 348.6 Bit für AC zum Positionen merken (WZW)                *
//   *  db20.dbw 960 für welche Achse Position merken (nur Y)  (WZW)         *
//   *  DB1.DBX 1366.4 = 1 Chaotischer-  0 Normaler WZW                      *
//   *************************************************************************
//
//   DB20.DBX 828.4    // Taste Werkzeugwechseln
//   Einen Cyclus setzen DB20.DBX 349.1    // dyn NC Interface Taste Werkzeugwechseln
//   DB20.DBX 344.0    // dyn NC Interface Werkzeugspindel belegt
//   DB20.DBX 348.7  // Rückmelden Werkzeugwenden beendet Kanal 0
//   DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
//   DB1.DBX 1366.5  // 1= neue ACC-SW die Normalen- und Chaotischenwerkzeugwechsel übernimmt
//   DB1.DBX 1366.6  // 1= neue PLC-SW mit Richtungslogik
//
// Bei Entladung  die neue Werkzeugnummer bei chaotischer WZ-Beladung auf 0 setzen
// L 0
// T DB20.DBW 958  // die neue Werkzeugnummer bei chaotischer WZ-Beladung
// DB20.DBX 5.0    // und setzen wirklich nur für einen Durchlauf setzen " S  DB20.DBX 5.0 "//
//
//
// DB20.DBX 5.1    // nur für einen Durchlauf setzen dann werden die Werkzeuge für nichtchaotischen Betrieb
//                    den Werkzeugnummern nach in dem Werkzeugmagazin umsortiert
//
//
//   INHALTSVERZEICHNIS:
//      FC  1   SCHMIERMITTEL und PNEUMATIKÖLSTANDÜBERWACHUNG
//      FC  2   STÜCKZÄHLER
//      FC  3   HA STEUERUNG und LEISTUNG SPINDEL0 (M3/M4/M5)
//      FC  4   X,Y,Z-ACHSE, Bremssteuerung, Imp.und Reglerfreigabe
//      FC  5   JOG ACHSEN
//      FC  6   REF ACHSEN
//      FC  7   AUX-ON
//      FC  8   NC-STARTVERR. UND BEDIENTASTEN
//      FC  9   RESET
//      FC 10   BEI PROGRAMM ENDE RESET ODER NEUSTART
//      FC 11   Neues Sicherheitskonzept
//      FC 12   SONDERBETRIEB
//      FC 13   INITIALISIERUNGEN ÜBERWACHUNGEN UND ALARME
//      FC 14   PLC NC-ÜBERGABE UND AFG/EFG/SFG
//      FC 15   MELDUNGEN 7000
//      FC 16   INC ACHSEN
//      FC 17   UE Modul Netzeinspeisung
//      FC 18   KÜHLMITTEL (M8/M9)
//      FC 19   SPINDEL HALT/START
//      FC 20   VORSCHUB HALT/START
//      FC 21   FUSZTASTER FÜR PNEUMATISCHES SPANNMITTEL
//      FC 22   CYCLISCHE ÜBERWACHUNG PIC
//      FC 23
//      FC 24   Späneförderer
//      FC 25   AUSBLASEINRICHTUNG
//      FC 26
//      FC 27   TÜRAUTOMATIK
//      FC 28   WARNLEUCHTE
//      FC 30   PNEUM_SCHRAUBSTOCK
//      FC 31
//      FC 32   LED ANSTEUERUNG
//      FC 33
//      FC 34   ZUWEISEN DER EINGÄNGE ACC
//      FC 35   ZUWEISEN DER AUSGÄNGE ACC
//      FC 37   RUNDACHSE WALTER
//      FC 38   4.5. Schrittmotor Rundachse
//      FC 40   ROBOTIK-INTERFACE
//      FC 49   Werkzeugwender Eingangsfilter
//      FC 50   Werkzeugmagazin und Werkzeugköcheransteuerung
//      FC 51   Von T-Wort Werkzeugmagazinpositionierung
//      FC 52   WERKZEUGWENDER
//      FC 53   /Normaler/ Von T-Wort Werkzeugmagazinpositionierung
//      FC 55   WERKZEUGWENDER EINSTELLARBEITEN mit gesetztem Merker M 200.0
//      FC 56   Positionsvergleicher Werkzeugmagazin (Z 3) und neues T-Wort (MB45)
//      FC 57   Positionsvergleicher Werkzeugmagazin (Z 3) und Werkzeug in der Spindel (MB44)
//      FC 59   Werkzeugspannsystem
//      FC 60   FREI PROGRAMMIERBARE M-BEFEHLE
//      FC 61   Steuerung zum Positionieren des Werkzeugmagazines
//      FC 63
//      FC 65   HANDRAD
//      FC 70
//      FC 90
//      FC 91
//      FC 92
//      FC 100
//      OB 1    ORGANISATIONSBAUSTEIN
//
//
//   SET; = 1
//   CLR; = 0
//
//   TIMER:
//   SI = IMPULS
//        DIE MAXIMALE IMPULSLÄNGE ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DIE MINIMALE IMPULSLÄNGE ENTSPRICHT DER LÄNGE DES EINGANGSSIGNALES
//
//   SV = IMPULS (VERLÄNGERT)
//        DIE IMPULSLÄNGE ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DIE DAUER DES EINGANGSSIGNALES HAT KEINEN EINFLUSS
//
//   SE = EINSCHALTVERZÖGERUNG
//        DIE VERZÖGERUNGSDAUER ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DAS EINGANGSSIGNAL MUSS MINDESTENS SOLANGE ANLIEGEN
//
//   SS = EINSCHALTVERZÖGERUNG (SPEICHERND)
//        DIE VERZÖGERUNGSDAUER ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DIE DAUER DES EINGANGSSIGNALES HAT KEINEN EINFLUSS
//
//   SA = AUSSCHALTIMPULS
//        DIE IMPULSLÄNGE ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DER TIMER WECHSELT VON "0"AUF "1" UND DIE ZEIT WIRD GESTARTET
//            WENN DAS EINGANGSSIGNAL VON "1" AUF "0" GEHT.
//
// ZEITBASIS     BELEGUNG DES ZEITWORTES
//               15......8  7.......0
//    0,01 S     0000 0000  0000 0000
//    0,1  S     0001 0000  0000 0000
//    1    S     0010 0000  0000 0000
//   10    S     0011 0000  0000 0000
//                     H     Z     E   = ZEITWERT IM BCD-FORMAT
//                 00                  = BINÄRCODE FÜR ZEITBASIS
//
//
//
//  ÄNDERUNGEN ODER PROBLEME BEI 8**********
//  M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
//
//  LEGENDE:  //*  Signale von CM250 in der PLC schon verarbeitet
//            //*? Signale von CM250 noch nicht in der PLC schon verarbeitet
//
//
//******************       EIN UND AUSGÄNGE ACC FÜR CM250         *************************
//
//
//
//  1. STECKERBELEGUNG Digitale Servomodule Y5A028000
//
//
//  -A111 X-ACHSE
//
//  X3:5  Led H2   E 0.0  =  X-ACHSE SERVO-READY
//  X3:11 Led H1   A 0.0  =  X-ACHSE Reglerfreigabe
//  X3:12 Led H3   A 11.0
//  X3:13 Led H4   A 11.1
//
//  -A112 Y-ACHSE
//
//  X3:5  Led H2   E 0.1  =  Y-ACHSE SERVO-READY
//  X3:11 Led H1   A 0.1  =  Y-ACHSE Reglerfreigabe
//  X3:12 Led H3   A 12.1
//  X3:13 Led H4   A 11.2
//
//  -A113 Z-ACHSE
//
//  X3:5  Led H2   E 0.2  =  Z-ACHSE SERVO-READY
//  X3:11 Led H1   A 0.2  =  Z-ACHSE Reglerfreigabe
//  X3:12 Led H3   A 11.3
//  X3:13 Led H4   A 11.4
//
//  -A114 Hauptantrieb
//
//  X3:1  Stromistwert HAUPTSPINDEL
//  X3:2  GND
//  X3:5  Led H2   E 0.3  =  Hauptantrieb SERVO-READY
//  X3:11 Led H1   A 0.3  =  Hauptantrieb Reglerfreigabe
//  X3:12 Led H3   A 11.5 =  Parametersatz Umschaltung 1.Eingang (Bit0 9V=3000Upm)
//  X3:13 Led H4   A 11.6
//
//  -A115 (res. für Walter Rundachse)
//
//  X3:5  Led H2   E 0.4
//  X3:11 Led H1   A 0.4
//  X3:12 Led H3   A 11.7
//  X3:13 Led H4   A 12.0
//
//
//  4. Zweites REF-MODUL Y5A017000
//
//  -A130
//
//  X5:1     E16.5   REF5     // REF
//  X5:2     E16.6   REF6     // REF Schalter A-Achse
//  X5:3     E16.7   REF7     // REF
//  X5:6     E17.5   SYNC5    // SYNC
//  X5:7     E17.6   SYNC6    // SYNC BERO A-Achse
//  X5:8     E17.7   SYNC7    // SYNC BERO B-Achse
//
//
//  4. EIN UND AUSGANGSBELEGUNG AUF DEM REF-MODUL Y5A017000
//
//  -A131
//
//  X5:1     E16.0   REF1     // REF Schalter X-Achse
//  X5:2     E16.1   REF2     // REF Schalter Y-Achse
//  X5:3     E16.2   REF3     // REF Schalter Z-Achse
//  X5:4     E16.3   REF4
//  X5:5     E16.4   REF5
//  X5:6     E17.0   SYNC1    // SYNC BERO X-Achse
//  X5:7     E17.1   SYNC2    // SYNC BERO Y-Achse
//  X5:8     E17.2   SYNC3    // SYNC BERO Z-Achse
//  X5:9     E17.3   SYNC4
//  X5:10    E17.4   SYNC5
//  X5:11    E 2.3   // Nicht NOT AUS von USBSPS X2.9
//  X6:1     E 2.0   // TÜR geschlossen von Türsicherheitsverriegelung ÜK
//  X6:2     E 2.1   // TÜR verriegelt von Verriegelungsmagnet SK
//  X6:3     E 2.2   // NICHT NOT-AUS von Bedienpult
//  X6:4     E 2.4   // Alive Eingang von PIC-Controller
//  X6:5     E 2.5   // NOT-AUS Extern (Wartungstür)
//  X6:6     E 2.6   // Öffnerkontakt von Not Aus Schütz Türautomatik
//  X6:7     E 2.7   5V  EINGANG
//  X6:8     E 3.0   5V  EINGANG
//  X6:9     A 3.5   // LEISTUNG TÜR AUF
//  X6:10    A 3.4 = M 54.3 // AUSGANGSMERKER ALARMAUSGANGANG
//  X6:11    A 3.3   // Alive Impuls zum PIC-Controller
//
//  5. PINBELEGUNG AUF ERSTEM EINGANGS-MODUL Y5A018000
//
//  -A132
//
//  X5:1     E 4.0       //*? Überwachung AS Signal X/Y
//  X5:2     E 4.1       //*? Überwachung AS Signal HS/Z
//  X5:3     E 4.2       // Überwachung AS Signal U/E
//  X5:4     E 4.3       // UE-Modul betriebsbereit
//  X5:5     E 4.4       // Rückmeldung Netzschütz UE-Modul abgefallen
//  X5:6     E 4.5       // Motorschutz Lüfter HA
//  X5:7     E 4.6       // Motorschutz Kühlmittel
//  X5:8     E 4.7       // Motorschutz Werkzeugmagazin
//  X5:9     E 5.0       // Überwachung NOT AUS -Relais
//  X5:10    E 5.1       // Überwachung von K2 und K3 auf Sicherheitsmodul Y4A135000
//  X5:11    E 5.2       // Überwachung von K4 und K5 auf Sicherheitsmodul Y4A135000
//  X6:1     E 5.3       // Motorschutz Werkzeugwechsler
//  X6:2     E 5.4       // Motorschutz Späneförderer
//  X6:3     E 5.5       // Bremse öffnen von Z-Achsreglerbaugruppe
//  X6:4     E 5.6       // Druckschalter Luftversorgung
//  X6:5     E 5.7       // Druckschalter Schraubstock
//  X6:6     E 6.0       // Druckschalter Klemmung Rundachse
//  X6:7     E 6.1       //*? Überwachung AS Signal Rundachse Welter 80/100
//  X6:8     E 6.2       // Türautomatik Sicherheitstürschaltleiste ausgelöst
//  X6:9     E 6.3       // Türautomatik Enschalter Tür offen
//  X6:10    E 6.4       // Störung Türsteuergerät Langer&Laumann
//  X6:11    E 6.5       // Schutzschalter Türsteuergerät
//
//
//  6. PINBELEGUNG AUF DEM ZWEITEN EINGANGS-MODUL Y5A018000
//
//  -A133
//
//  X5:1     E 6.6    // Magazin in Position =  M 6.6 Gefilterter E 6.6 Strobe und Magazin in Position
//  X5:2     E 6.7    // Werkzeughalter zurückgeschwenkt
//  X5:3     E 7.0    // Werkzeughalter in Übernahmeposition
//  X5:4     E 7.1    // Magazin auf Positition 1
//  X5:5     E 7.2    // Homeposition Werkzeugwechselarm = M 7.2 Gefilterter E 7.2 Homeposition Werkzeugwechselarm
//  X5:6     E 7.3    // Werkzeugwechselarm in Wechselposition M 7.3 Gefilterter E 7.3 Werkzeugwechselarm in Wechselposition
//  X5:7     E 7.4    // Türautomatik Motorschützüberwachung
//  X5:8     E 7.5    //*?    Fußtaster Spannmittel zu
//  X5:9     E 7.6    //*ß    Fußtaster Spannmittel auf
//  X5:10    E 7.7    // Wartung und Überlastschalter Späneförderer
//  X5:11    E 8.0
//  X6:1     E 8.1
//  X6:2     E 8.2
//  X6:3     E 8.3
//  X6:4     E 8.4
//  X6:5     E 8.5
//  X6:6     E 8.6
//  X6:7     E 8.7
//  X6:8     E 9.0
//  X6:9     E 9.1
//  X6:10    E 9.2
//  X6:11    E 9.3
//
//
//
//  7. PINBELEGUNG EINGÄNGE AUF DER BACK-PLANE Y5A023000
//
//  -A100
//
//  X422:1   E 3.1  //  Überwachung Spänefördererschütze
//  X422:2   E 3.2  //  Zustimmtaste Handrad
//  X422:3   E 3.3  //* Takt Handrad
//  X422:4   E 3.4  //* Richtung Handrad
//  X422:5   E 3.5  //* Druckschalter Schmiermittel
//  X422:6   E 3.6  // Schwimmerschalter Schmiermittelpumpe
//  X422:7   E 3.7  // n=0 von Relais
//  X422:9   E 9.5
//  X422:10  E 9.6
//  X422:11  E 9.7  //* NOT AUS EINGANG FÜR FREI PROGRAMMIERBARE AUSGÄNGE
//
//  8. PINBELEGUNG AUSGÄNGE AUF DER BACK-PLANE Y5A023000
//
//  -A100
//
//  X421:1   +24 V     24V VERSORGUNG VON NOTAUSTASTER
//  X421:2   +24 V
//  X421:3   GND
//  X421:4   A 9.0    //*? Impulsfreigabe X/Y
//  X421:5   A 9.1    //*  NOT AUS für NE Modul Klemme 64
//  X421:6   A 9.2    //*  Verriegelunsmagnet Maschinentür entriegeln (ohne Türautomatik)
//  X421:7   A 9.3    //*? Bremse Z-Motor
//  X421:8   A 9.4    // Leistung Tuer zu
//  X421:9   A 9.5    // Werkzeughalter zurückschwenken
//  X421:10  A 9.6    // Werkzeughalter auf Übernahmeposition schwenken
//  X421:11  A 9.7    // Türmotorschütze ein
//
//
//  X420:1   +24 V     24V VERSORGUNG VON TÜR ZU
//  X420:2   +24 V
//  X420:3   GND
//  X420:4   A 10.0   //*?  Impulsfreigabe X/Y
//  X420:5   A 10.1   // Schütz Werkzeugwechselarm
//  X420:6   A 10.2   //*?  Magazin WZW rechtslauf
//  X420:7   A 10.3   //*?  Magazin WZW linkslauf
//  X420:8   A 10.4   //*? Späneförderer vorwärts
//  X420:9   A 10.5   //*? Späneförderer rückwärts
//  X420:10  A 10.6   // Minimalmengenkühlung
//  X420:11  A 10.7   // Kühlmittelpumpe
//  9. PINBELEGUNG AUF DEM ERSTEM AUSGANGS-MODUL Y5A019000
//
//  -A134
//
//  X5:1     A 4.0 // Türfreigaberelais K3 ein auf Sicherheitsmodul Y4A135000M
//  X5:2     A 4.1 //* Antriebsfreigaberelais K5 ein auf Sicherheitsmodul Y4A135000
//  X5:3     A 4.2 // Impulsfreigabe UE-Modul und LEISTUNG Zwischenkreis
//  X5:4     A 4.3 // Impulsfreigabe Hauptantrieb und Z-Achse
//  X5:5     A 4.4 //*? Bremse WZW Magazin
//  X5:6     A 4.5 //*? Bremse WZW Arm
//  X5:7     A 4.6 // Klemmung Werkzeug öffnen
//  X5:8     A 4.7 // Klemmung Werkzeug schließen
//  X5:9     A 5.0 // Ausblasen Frässpindel
//  X5:10    A 5.1 // AUX ON Schütz
//  X6:1     A 5.2  //* Leistung Schmiermittelpumpe
//  X6:2     A 5.3  //* WEISSE WARNLEUCHTE LADEMAGAZIN STEHT
//  X6:3     A 5.4  // GRÜNE WARNLEUCHTE AUTOMATIK
//  X6:4     A 5.5  // ROTE WARNLEUCHTE ALARM
//  X6:5     A 5.6  // Ventil Schraubstock ZU
//  X6:6     A 5.7  // Ventil Schraubstock AUF
//  X6:7     A 6.0  // Ausblasventil Schraubstock
//  X6:8     A 6.1  // RUNDACHSE KLEMMEN
//  X6:9     A 6.2  // RUNDACHSE FREIGEBEN
//  X6:10    A 6.3  // LEISTUNG TÜR ZU
//
//  10. PINBELEGUNG AUF DEM ZWEITEM AUSGANGS-MODUL Y5A019000
//
//  -A135
//
//  X5:1     A 6.4
//  X5:2     A 6.5 = M 59.3 // ROBOTIK NOT-AUS AKTIV
//  X5:3     A 6.6 = M 59.4 // ROBOTIK MASCHINE BETRIEBSBEREIT
//  X5:4     A 6.7 = M 59.5 // ROBOTIK BETRIEBSART REFERENZ
//  X5:5     A 7.0 = M 59.6 // ROBOTIK M0,M1,M2,ODER M30 AKTIV
//  X5:6     A 7.1 = M 60.0 // ROBOTIK SPANNMITTEL GESPANNT
//  X5:7     A 7.2 = M 60.1 // ROBOTIK SPANNMITTEL OFFEN
//  X5:8     A 7.3 = M 60.2 // ROBOTIK SPANNMITTEL KEIN TEIL GESPANNT
//  X5:9     A 7.4 = M 60.3 // ROBOTIK PINOLE GESPANNT
//  X5:10    A 7.5 = M 60.4 // ROBOTIK PINOLE ENDLAGE HINTEN
//
//
//  X6:1     A 7.6 = M 60.5 // ROBOTIK PINOLE KEIN TEIL GESPANNT
//  X6:2     A 7.7 = M 60.6 // ROBOTIK MASCHINENTÜR GESCHLOSSEN
//  X6:3     A 8.0 = M 60.7 // ROBOTIK MASCHINENTÜR OFFEN
//  X6:4     A 8.1 = M 59.7 // ROBOTIK ACHSEN STEHEN AUF REF.PKT.
//  X6:5     A 8.2 = M 61.4 // M78/M79
//  X6:6     A 8.3 = M 61.5 // M80/M81
//  X6:7     A 8.4 = M 61.6 // M82/M83
//  X6:8     A 8.5 = M 61.7 // M84/M85
//  X6:9     A 8.6
//  X6:10    A 8.7
//
//  DB20.DBX 833.0  = M 50.1  RAFI Tastatur X2.1/2 Schlüsselschalter auf Sonderbetrieb
//  DB20.DBX 833.2  RAFI Tastatur X2.3/4 SCHLÜSSELSCHALTER ENDLAGENÜBERWACHUNG DEAKTIVIEREN
//  DB20.DBX 833.3  RAFI Tastatur X3.1/2 CYCLE START Taste
//  DB20.DBX 833.4  RAFI Tastatur X4.1/2 Einzelstücktaste
//  DB20.DBX 833.6  RAFI Tastatur X5.1/2 Werkzeugspann/entspanntaste
//  DB20.DBX 834.0  = M 51.2  RAFI Tastatur X6.1/2 Zustimmtaste Maschinenständer
; Mit gesetzten Bit M 200.0 werden folgende Befehle ausgeführt::
;DB20.DBX 294.5 // CTRL 1  // POSITION FÜR Y-ACHSE ÜBERNEHMEN
;DB20.DBX 294.6 // CTRL 2  // POSITION FÜR Z-ACHSE ÜBERNEHMEN
;DB20.DBX 294.1 // CTRL 3  // Hauptspindelposition übernehmen
;DB20.DBX 295.1 // CTRL 4  // Offsetabgleich für alle analogen Linear- und Rundachsen
;DB20.DBX 295.4 // CTRL 7  // Werkzeughalter zurückschwenken (wenn kein Programm läuft )
;DB1.DBX 1374.4 // CTRL 8  // Werkzeughalter auf Wechselposition schwenken (wenn kein Programm läuft )
;DB20.DBX 294.3 // CTRL 0  // Der Werkzeugwechselarmschütz wird eingeschaltet (wenn kein Programm läuft )
;  BITNUMMERN VON DEN MASCHINENSTEUERTAFELTASTEN:
;DB20.DBX 827.0  // Taste Tür
;DB20.DBX 827.1  // Taste Tür
;DB20.DBX 827.2  // Taste Späneförderer
;DB20.DBX 827.6  // Taste Eilgangüberlagerung
;DB20.DBX 828.3  // Taste Werkzeugmagazin nach links schwenken
;DB20.DBX 828.4  // Taste Werkzeugwechseln
;DB20.DBX 828.5  // Taste Werkzeugmagazin nach rechts schwenken
;DB20.DBX 829.6  // Taste SCHRAUBSTOCK ZU
;DB20.DBX 829.7  // Taste SCHRAUBSTOCK AUF
;DB20.DBX 831.4  // Taste NC-STOPP VON BEDIENPULT
;DB20.DBX 831.1  // Taste KÜHLMITTEL (MSTT)
;DB20.DBX 831.5  // Taste CYCLE-START VON BEDIENPULT
;DB20.DBX 831.6  // TASTE VORSCHUB HALT  (MSTT)
;DB20.DBX 832.0  // TASTE VORSCHUB START (MSTT)
;DB20.DBX 832.1  // TASTE SPINDEL HALT  (MSTT)
;DB20.DBX 832.3  // TASTE SPINDEL START (MSTT)
;DB20.DBX 832.4  // TASTE AUX-OFF (MSTT)
;DB20.DBX 832.6  // TASTE AUX-ON  (MSTT)
;DB20.DBX 833.3  // RAFI Tastatur X3.1/2 CYCLE START Taste
;
;       U   DB20.DBX 294.3; // TASTE SCHRAUBSTOCK ZU (PC)
;       UN  DB20.DBX 294.4; // TASTE SCHRAUBSTOCK AUF (PC)
;
;  BITNUMMERN DER PC-UND MASCHINENSTEUERTAFEL TASTEN:
; (!!!!Werden auf der CONCEPT MILL250 nicht mehr verwendet!!!!!)
;  BITNR.:        PC:     MSTT:      MSTT:
;DB1.DBX 1374.0   -----     1        ----------
;DB1.DBX 1374.1   -----     2        | 1| 2| 3|
;DB1.DBX 1374.7   -----     3        ----------
;DB1.DBX 1374.6   -----     6        | 4| 5| 6|
;DB20.DBX 294.7   -----    11        ----------
;DB20.DBX 295.0   -----    12        | 7| 8| 9|
;DB20.DBX 294.2   CTRL ^    5        ----------
;DB20.DBX 294.5   CTRL 1    9        |10|11|12|
;DB20.DBX 294.6   CTRL 2   10        ----------
;DB20.DBX 294.1   CTRL 3    4        |13|  |14|
;DB20.DBX 295.1   CTRL 4    -        ----------
;DB20.DBX 295.2   CTRL 5    -
;DB20.DBX 295.3   CTRL 6    -
;DB20.DBX 295.4   CTRL 7    -
;DB1.DBX 1374.4   CTRL 8   14
;DB20.DBX 294.4   CTRL 9    8
;DB20.DBX 294.3   CTRL 0    7
;DB1.DBX 1374.3   CTRL ß   13
;DB1.DBX 1374.2   CTRL `    -
FUNCTION FC 38 : VOID
NAME: 4.5. Schrittmotor Rundachse
BEGIN
NETWORK
TITLE = A-Achse Jogfreigabe nur wenn A-Achse referenziert ist
O   DB20.DBX 11.2;  // JOG in pos. Richtung für Kanalachse A
O   DB20.DBX 13.2;  // JOG in neg. Richtung für Kanalachse A
U(
UN  DB1.DBX 134.6;  // REFERENZPUNKT A-ACHSE AKTIV
UN  M 201.1         // Jogfreigabe zum Testen erlauben
)
R   DB20.DBX 11.2;  // JOG in pos. Richtung für Kanalachse A
R   DB20.DBX 13.2;  // JOG in neg. Richtung für Kanalachse A
NETWORK
TITLE = A-Achse INC-freigabe nur wenn A-Achse referenziert ist
U   DB20.DBX 1.3;   // FC INC ANFORDERUNG
UN  DB1.DBX 134.6;  // REFERENZPUNKT A-ACHSE AKTIV
SPBN   M3800;
L   DB20.DBW 14;
L   10
==I
R   DB20.DBX 1.3;   // FC INC ANFORDERUNG
M3800:  NOP 0;
NETWORK  1
TITLE = ENABLE SM MODUL C und Differenzialleitungstreiber
U   M 2.0           // SUMME aller NOT-AUS Möglichkeiten
=   A 3.2;          // ENABLE SM MODUL C
=   A 3.7;          // Freigabe Differenzialleitungstreiber Dir/Takte für SM Modul A, B und C.
NETWORK
TITLE = A-ACHSE REFERENZIEREN
U   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.7;        // FM REFERENZPUNKT Z-ACHSE AKTIV
O(;
UN  M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.1;        // REF ACHSEN FREIGABE
U   DB20.DBX 9.2;   // REFERENZIEREN VON KANALACHSE A
);
=   DB20.DBX 9.2;   // REFERENZIEREN VON KANALACHSE A
SPBN   M3801;
SET;
=   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
M3801:  NOP 0;
NETWORK
TITLE = B-ACHSE REFERENZIEREN
U   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.7;        // FM REFERENZPUNKT Z-ACHSE AKTIV
O(;
UN  M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.1;        // REF ACHSEN FREIGABE
U   DB20.DBX 9.3;   // REFERENZIEREN VON KANALACHSE B
);
=   DB20.DBX 9.3;   // REFERENZIEREN VON KANALACHSE B
SPBN   M3802;
SET;
=   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
M3802:  NOP 0;
NETWORK  3
TITLE = ALARM ANTRIEB A,B-ACHSE DEFEKT
U   A 3.2;          // ENABLE SM MODUL C
L   S5TIME#500MS;   // 500MS
SE  T 54;           // STARTEN T 54 ALS EINSCHALTVERZ.
U   E 0.6;          // Servo Ready  Achse 6, =  A-ACHSE)
U   T 54;           // ÜBERWACHUNG ACHSEN SCHARF
S   DB2.DBX 10.7;   // 6087 ANTRIEB A-ACHSE N. BEREIT
U   E 0.7;          // Servo Ready  Achse 7, =  B-ACHSE)
U   T 54;           // ÜBERWACHUNG ACHSEN SCHARF
S   DB2.DBX 11.1;   // 6089 ANTRIEB B-ACHSE N. BEREIT
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 10.7;   // 6087 ANTRIEB A-ACHSE N. BEREIT
R   DB2.DBX 11.1;   // 6089 ANTRIEB B-ACHSE N. BEREIT
NETWORK
TITLE = A-Achse steht innerhalb +-5°
L   DB1.DBD 24;     // IST POSITION A
L   -5.0E-03;       // -5°
>R  ;
=   M 6.0           // A-Achse steht innerhalb -5°
L   DB1.DBD 24;     // IST POSITION A
L   +5.0E-03;       // +5°
<R  ;
=   M 6.1           // A-Achse steht innerhalb +5°
ON  M 6.0           // A-Achse steht innerhalb -5°
ON  M 6.1           // A-Achse steht innerhalb +5°
=   M 6.2           // A-Achse steht nicht innerhalb +-5°
NETWORK
TITLE = 7085  Rundachse A auf 0° fahren
U   DB1.DBX 1440.1; // PROGRAMM BEENDEN
U   M 6.2           // A-Achse steht nicht innerhalb +-5°
S   M 25.0;         // HM 7085  Rundachse A auf 0° fahren !
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   M 25.0;         // HM 7085  Rundachse A auf 0° fahren !
END_FUNCTION
FUNCTION FC 22 : VOID
NAME: CYCLISCHE ÜBERWACHUNG PIC
BEGIN
//   A 3.3  // Alive Impuls zum PIC-Controller
//   E 2.4  // Alive Eingang von PIC-Controller
//   E 2.3  // Nicht NOT AUS von USBSPS X2.9
NETWORK 1
TITLE = Reset-Taste an USBSPS weiterleiten
U   DB1.DBX 1440.0  // RESET AUSGELÖST
U   DB1.DBX 1370.3  // RESET-TASTE GEDRUECKT
=   DB1.DBX 1598.3  // Reset-Taste an USBSPS weiterleiten
NETWORK 2
TITLE =  Impuls für PIC
U   M 11.7          // USBSPS auf Ueberwachungsbetrieb eingestellt
FP  M 10.3          // FM USBSPS auf Ueberwachungsbetrieb eingestellt
U   E 2.4;          // Alive Eingang von PIC-Controller
FN  M 24.0;         // Negativ FM Alive Eingang E4.7 von PIC-Controller
U   E 2.4;          // Alive Eingang von PIC-Controller
FP  M 24.1;         // Positiv FM Alive Eingang E4.7 von PIC-Controller
U   E 2.3           // Nicht NOT AUS von USBSPS X2.9
FN  M 24.6          // Nicht NOT AUS von USBSPS X2.9
U   E 2.3           // Nicht NOT AUS von USBSPS X2.9
FP  M 24.7;         // Positiv FM NOT AUS von USBSPS X2.9
O   M 24.7;         // Positiv FM NOT AUS von USBSPS X2.9
O   M 24.0;         // Negativ FM Alive Eingang E4.7 von PIC-Controller
O   M 10.3          // FM USBSPS auf Ueberwachungsbetrieb eingestellt
L   S5TIME#40MS
SV  T 2;            // IMPULS (VERLÄNGERT)
U   T 2;
=   A 3.3           // Alive Impuls zum PIC-Controller
U   A 3.3           // Alive Impuls zum PIC-Controller
FN  M 24.2;         // Negativ FM Alive Impuls A10.5 zum PIC-Controller
U   A 3.3           // Alive Impuls zum PIC-Controller
FP  M 24.3;         // Positiv FM Alive Impuls A10.5 zum PIC-Controller
NETWORK 3
TITLE =  Zeitüberwachung PIC
// Überwachungszeit Resetieren:
O   M 24.3;         // Positiv FM Alive Impuls A10.5 zum PIC-Controller
O   M 24.6;         // Negativ FM NOT AUS von USBSPS X2.9
ON  E 2.3           // Nicht NOT AUS von USBSPS X2.9
R   T 1;
// Überwachungszeit Starten:
O   M 24.2;         // Negativ FM Alive Impuls A10.5 zum PIC-Controller
O   M 24.1;         // Positiv FM Alive Eingang E4.7 von PIC-Controller
U   E 2.3           // Nicht NOT AUS von USBSPS X2.9
L   S5TIME#70MS  // Überwachungszeit 1,5S für PIC
SS  T 1;            // Einschaltverzögerung (SPEICHERND)
NETWORK 4
TITLE =  Alarm 6074 Keine Rückmeldung von USB-SPS
U   A 3.3           // Alive Impuls zum PIC-Controller
S   M 24.4          // 8((( AUX ON PIC OK
U   T 1;
R   M 24.4          // 8((( AUX ON PIC OK
U   M 24.4          // 8((( AUX ON PIC OK
FN  M 24.5          // Meldung Time-Out PIC
U   M 24.5          // Meldung Time-Out PIC
S   DB2.DBX 9.2;    // Alarm 6074 Keine Rückmeldung von USB-SPS
NETWORK 4
TITLE =  Neustart von USB-SPS nach resetieren (nach USB-SPS Notabschaltung)
U   M 12.2          // USBSPS sendet 255 (nach Neustart)
U   DB2.DBX 9.2;    // Alarm 6074 Keine Rückmeldung von USB-SPS
R   DB2.DBX 9.2;    // Alarm 6074 Keine Rückmeldung von USB-SPS
R   M 11.7          // USBSPS auf Ueberwachungsbetrieb eingestellt
R   T 2;
R   A 3.3           // Alive Impuls zum PIC-Controller
NETWORK 5
TITLE =  Überwachung X-Achse ausschalten bei Richtungsumkehr
U   DB1.DBX 66.0    // positive Richtung für Achse 0
UN  T 31
L   S5T#500MS
SA  T 30
U   DB1.DBX 68.0    // negative Richtung für Achse 0
UN  T 30
L   S5T#500MS
SA  T 31
U   DB1.DBX 68.0    // negative Richtung für Achse 0
U   T 30
O(
U   DB1.DBX 66.0    // positive Richtung für Achse 0
U   T 31
)
L   S5T#500MS
SV  T 36
U   T 36
U   M 113.0;        // SONDERBETRIEB AKTIV
=   DB1.DBX 1598.0  // Richtungsumkehr X-Achse für USBSPS-Überwachung
NETWORK 6
TITLE =  Überwachung Y-Achse ausschalten bei Richtungsumkehr
U   DB1.DBX 66.1    // positive Richtung für Achse 1
UN  T 41
L   S5T#500MS
SA  T 40
U   DB1.DBX 68.1    // negative Richtung für Achse 1
UN  T 40
L   S5T#500MS
SA  T 41
U   DB1.DBX 68.1    // negative Richtung für Achse 1
U   T 40
O(
U   DB1.DBX 66.1    // positive Richtung für Achse 1
U   T 41
)
L   S5T#500MS
SV  T 42
U   T 42
U   M 113.0;        // SONDERBETRIEB AKTIV
=   DB1.DBX 1598.1  // Richtungsumkehr Y-Achse für USBSPS-Überwachung
NETWORK 7
TITLE =  Überwachung Z-Achse ausschalten bei Richtungsumkehr
U   DB1.DBX 66.2    // positive Richtung für Achse 2
UN  T 51
L   S5T#500MS
SA  T 50
U   DB1.DBX 68.2    // negative Richtung für Achse 2
UN  T 50
L   S5T#500MS
SA  T 51
U   DB1.DBX 68.2    // negative Richtung für Achse 2
U   T 50
O(
U   DB1.DBX 66.2    // positive Richtung für Achse 2
U   T 51
)
L   S5T#500MS
SV  T 52
U   T 52
U   M 113.0;        // SONDERBETRIEB AKTIV
=   DB1.DBX 1598.2  // Richtungsumkehr Z-Achse für USBSPS-Überwachung
NETWORK 7
TITLE = Türzustand an die USB-SPS weiterleiten
U   E 2.0;          // Tür geschlossen von Türsicherheitsverriegelung ÜK
U   A 4.0           // Türfreigaberelais K3 ein auf Sicherheitsmodul Y4A135000M
UN  M 201.0         //
=   DB1.DBX 1598.4  // Türzustand für USB_SPS-Überwachungsplatine
;       =   DB1.DBX 1598.5  // Sonderbetrieb für USB_SPS-Überwachungsplatine
;       U   DB1.DBX 1564.6  // Türzustand von USB_SPS-Überwachungsplatine (Kontakt X3: auf Sicherheitsplatine)
;       U   DB1.DBX 1564.7  // Antriebsfreigabe von USB_SPS-Überwachungsplatine (Kontakt X4:5,6 auf Sicherheitsplatine)
END_FUNCTION
FUNCTION FC 53
NAME: Normaler Von T-Wort Werkzeugmagazinpositionierung
BEGIN
;;8((((( ACHTUNG NUR zum TESTEN wieder löschen!!!!!!!!!!!!!!!!!!!!
;       U   DB20.DBX 831.6  // TASTE VORSCHUB HALT  (MSTT)
;    SPBN M5398
;       L   0
;       T   DB15.DBB 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
;       =   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
;M5398:  NOP 0
;
;       U   DB20.DBX 832.1  // TASTE SPINDEL HALT  (MSTT)
;    SPBN M5399
;       L   5
;       T   DB15.DBB 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
;       =   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
;M5399:  NOP 0
;
NETWORK
TITLE = /Normaler/ Werkzeugwenden abrechen
ON  M 2.0           // SUMME aller NOT-AUS Möglichkeiten
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUGELÖST
SPBN M5397
L   0
T   MW 40
T   MB 42
M5397:  NOP 0
NETWORK
TITLE = /Normaler/ WZW FREIGABE
UN  M 94.0;         // AUX-ON / MASCHINENTÜR OFFEN  SFG
U   M 131.0;        // UE Modul BETRIEBSBEREIT
UN  M 113.0;        // SONDERBETRIEB AKTIV
U   M 124.1         // Werkzeugmagazin referenziert
U   DB20.DBX 324.4; // REFERENZPUNKT AKTIV
=   M 152.3;        // WZW FREIGABE
NETWORK
TITLE = /Normaler/ Werkzeugmagazin Freigabe
UN  M 94.0;         // AUX-ON / MASCHINENTÜR OFFEN  SFG
U   M 131.0;        // UE Modul BETRIEBSBEREIT
UN  M 113.0;        // SONDERBETRIEB AKTIV
U   DB20.DBX 324.4; // REFERENZPUNKT AKTIV
=   M 127.0;        // Werkzeugmagazin Freigabe
NETWORK
TITLE = /Normaler/ Werkzeugmagazinpositions Zähler auf 1 setzen
U   M 124.6         // FM Werkzeugmagazin referenziert
SPBN M5300
L   1;              // ISTZÄHLER AUF 1 SETZEN
S   Z 3             // Istzähler Werkzeugmagazin
T   DB20.DBW956     // Bildschirmanzeige
M5300:  NOP 0;
NETWORK
TITLE = /Normaler/ WERKZEUGPOSITION AUS DEN SETTINGDATEN SETZEN
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
SPBN   M5302
L   DB95.DBW 4;     // EINGESPANNTES WERKZEUG vom ACC
L   0;
==I;
=   M 39.0          // Settingdaten WERKZEUG 0 GESPEICHERT = kein WZ in der Spindel (überprüfen)
SPB  M5302
L   DB95.DBW 4;     // EINGESPANNTES WERKZEUG vom ACC
L   20
>I
=   M 39.1          // Settingdaten Ungültige WERKZEUGNUMMER IN DEN SETTINGDATEN
=   DB3.DBX 7.0;    // MELDUNG 7056 Ungültige WERKZEUGNUMMER IN DEN SETTINGDATEN
=   DB1.DBX 1440.0; // NC-RESET AUSLÖSEN
SPB  M5302
L   DB95.DBW 4;     // EINGESPANNTES WERKZEUG vom ACC
T   MB 44           // Aktuelles Werkzeug in der Spindel
L   DB95.DBW 4;     // EINGESPANNTES WERKZEUG vom ACC
L   1;
<>I;
=   M 39.2          // Settingdaten Gültiges Wekzeug in der Spindel lt. Settingdaten = WZ in der Spindel (überprüfen)
M5302:  NOP 0
NETWORK
TITLE = /Normaler/ Neues T-Wort und Werkzeugmagazin nicht referenziert
U   DB20.DBX 182.0  // NEUES T-WORT (WERKZEUG)
UN  M 124.1         // Werkzeugmagazin referenziert
U   M 127.0;        // Werkzeugmagazin Freigabe
SPBN M5304
=   M 124.3         // Referenzieren Werkzeugmagazin nach rechts schwenken
=   M 127.1         // Warten auf Werkzeugmagazin referenziert
M5304:  NOP 0;
NETWORK
TITLE = /Normaler/ Werkzeugmagazin auf Rückgabeposition schwenken (eingespanntes Werkzeug)
U   M 39.2          // Settingdaten Gültiges Wekzeug in der Spindel lt. Settingdaten = WZ in der Spindel (überprüfen)
U   M 124.1         // Werkzeugmagazin referenziert
U   M 127.0;        // Werkzeugmagazin Freigabe
SPBN M5305
L   MB 44           // Aktuelles Werkzeug in der Spindel
L   Z 3             // Istzähler Werkzeugmagazin
==I;                // VERGLEICH AUF GLEICH
=   M 40.5          // /NORMAL\ Werkzeug in der Spindel gleich Werkzeugmagazin-Istzähler
SPB  M5305
L   MB 44           // Aktuelles Werkzeug in der Spindel
T   MB 160          // Sollzähler Werkzeugmagazin
=   M 155.3         // Sollzähler Werkzeugmagazin mit neuem T-WORT geladen
=   M 40.6          // Rückgabeposition
M5305:  NOP 0;
NETWORK
TITLE = /Normaler/ Neues T-Wort überprüfen und Sollzähler setzen
O   DB20.DBX 182.0  // NEUES T-WORT (WERKZEUG)
O   M 127.1         // Warten auf Werkzeugmagazin referenziert
U   M 124.1         // Werkzeugmagazin referenziert
U   M 127.0;        // Werkzeugmagazin Freigabe
SPBN M5306
R   M 127.1         // Warten auf Werkzeugmagazin referenziert
L   DB20.DBW 184;   // LADE T-WORT
L   20;             // LADE KONSTANTE DEZ 20
>I;                 // VERGLEICH AUF GRÖSSER
=   M 155.0         // T-WORT GRÖSSER 20
SPB  M5306
L   MB 44           // Aktuelles Werkzeug in der Spindel
L   0;              // LADE 0
==I;                // VERGLEICH GLEICH
=   M 40.0          // Kein Werkzeug in der Spindel 8((((( Überprüfen
L   DB20.DBW 184;   // LADE T-WORT
L   0;              // LADE 0
==I;                // VERGLEICH GLEICH
=   M 40.1          // Neues T-WORT=0 (Werkzeug nur zurückgeben)
L   DB20.DBW 184;   // T-WORT
L   MB 44           // Aktuelles Werkzeug in der Spindel
==I;                // VERGLEICH AUF UNGLEICH
=   M 40.4          // (Neues T-Wort gleich T-Spindel) nur Werkzeugnummer rückmelden
L   DB20.DBW 184;   // T-WORT
L   MB 44           // Aktuelles Werkzeug in der Spindel
<>I;                // VERGLEICH AUF UNGLEICH
=   M 40.3          // (Neues T-Wort ungleich T-Spindel) Werkzeug zurückgeben und neues einschwenken
U   M 40.3          // (Neues T-Wort ungleich T-Spindel) Werkzeug zurückgeben und neues einschwenken
UN  M 40.1          // Neues T-WORT=0 (Werkzeug nur zurückgeben)
SPBN M5306
L   DB20.DBW 184;   // T-WORT
T   MB 45           // Neue Position Werkzeugmagazin
=   M 40.2          // T1 bis T20 neue  Werkzeugnummer in MB45 gespeichert
M5306:  NOP 0;
U   M 40.4          // (Neues T-Wort gleich T-Spindel) nur Werkzeugnummer rückmelden
UN  M 40.1          // Neues T-WORT=0 (Werkzeug nur zurückgeben)
U   M 71.2          // Werkzeug sicher eingespannt ( 300 <= 450 )
SPBN M5309
L   Z 3             // Istzähler Werkzeugmagazin
T   DB20.DBW 958    // die neue Werkzeugnummer bei chaotischer WZ-Beladung
S   DB20.DBX 5.0    // Werkzeugnummer in Spindel laden
R   M 40.4          // (Neues T-Wort gleich T-Spindel) nur Werkzeugnummer rückmelden
R   M 72.0          // Werkzeug manuell eingespannt
R   M 72.6          // Werkzeug sicher manuell eingespannt
M5309:  NOP 0;
;1.In der Spindel is nix und im neichn T-wort steht a nix (neues T-WORT=0)
U   M 40.0          // Kein Werkzeug in der Spindel 8((((( Überprüfen
U   M 40.1          // Neues T-WORT=0 (Werkzeug nur zurückgeben)
R   M 40.0          // Kein Werkzeug in der Spindel 8((((( Überprüfen
R   M 40.1          // Neues T-WORT=0 (Werkzeug nur zurückgeben)
R   M 72.0          // Werkzeug manuell eingespannt
R   M 72.6          // Werkzeug sicher manuell eingespannt
;2.In der Spindel is nix und im neichn T-wort steht was richtigs (neues T-WORT= 1-20)
U   M 40.0          // Kein Werkzeug in der Spindel 8((((( Überprüfen
U   M 40.2          // T1 bis T20 neue  Werkzeugnummer in MB45 gespeichert
UN  M 40.1          // Neues T-WORT=0 (Werkzeug nur zurückgeben)
R   M 72.0          // Werkzeug manuell eingespannt
R   M 72.6          // Werkzeug sicher manuell eingespannt
S   M 42.0          // (T0 in Spindel)  (Neues T-WORT=Txx) Werkzeugmagazin auf neues T-Wort (MB45) fahren und einschwenken
FP  M 43.0          // Werkzeugmagazin auf neues T-Wort (MB45) fahren
U   M 43.0          // Werkzeugmagazin auf neues T-Wort (MB45) fahren
CC  FC 56             // Positionsvergleicher Werkzeugmagazin (Z 3) und neues T-Wort (MB45)
U   M 42.4          // Werkzeugmagazin (Z 3) und neues T-Wort (MB45) sind ungleich
U   M 155.4         // Neues Werkzeug vorbereitet zum Ein- oder Ausschwenken
O   M 42.5          // Werkzeugmagazin (Z 3) und neues T-Wort (MB45) sind gleich
U   M 42.0          // (T0 in Spindel)  (Neues T-WORT=Txx) Werkzeugmagazin auf neues T-Wort (MB45) fahren und einschwenken
S   M 40.7          // Werkzeugmagazin ist auf Rückgabeposition und Start zurückgeben
R   M 40.0          // Kein Werkzeug in der Spindel 8((((( Überprüfen
R   M 40.2          // T1 bis T20 neue  Werkzeugnummer in MB45 gespeichert
R   M 42.4          // Werkzeugmagazin (Z 3) und neues T-Wort (MB45) sind ungleich
R   M 42.5          // Werkzeugmagazin (Z 3) und neues T-Wort (MB45) sind gleich
;Beenden von M 42.0 Werkzeugmagazin auf neues T-Wort (MB45) fahren und einschwenken
U   M 40.7          // Werkzeugmagazin ist auf Rückgabeposition und Start zurückgeben
U   M 41.0          // Werkzeugwechsel abgeschlossen
U   M 42.0          // (T0 in Spindel)  (Neues T-WORT=Txx) Werkzeugmagazin auf neues T-Wort (MB45) fahren und einschwenken
R   M 40.7          // Werkzeugmagazin ist auf Rückgabeposition und Start zurückgeben
R   M 41.0          // Werkzeugwechsel abgeschlossen
R   M 42.0          // (T0 in Spindel)  (Neues T-WORT=Txx) Werkzeugmagazin auf neues T-Wort (MB45) fahren und einschwenken
;3.In der Spindel is was und im neichn T-wort steht nix (neues T-WORT=0)
U   M 40.3          // (Neues T-Wort ungleich T-Spindel) Werkzeug zurückgeben und neues einschwenken
U   M 40.1          // Neues T-WORT=0 (Werkzeug nur zurückgeben)
O(
U   DB20.DBX 828.4  // Taste Werkzeugwechseln
U   M 123.0         // Freigabe Werkzeugmagazin für Handbetrieb
UN  DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
U   M 72.6          // Werkzeug sicher manuell eingespannt
;       U   M 72.0          // Werkzeug manuell eingespannt
)
S   M 42.1          // (Txx in Spindel) (Neues T-WORT=0)   Werkzeugmagazin auf Werkzeug in der Spindel (MB44) fahren und ausschwenken
FP  M 43.1          // Werkzeugmagazin auf Werkzeug in der Spindel (MB44) fahren
U   M 43.1          // Werkzeugmagazin auf Werkzeug in der Spindel (MB44) fahren
CC  FC 57             // Positionsvergleicher Werkzeugmagazin (Z 3) und Werkzeug in der Spindel (MB44)
U   M 42.7          // Werkzeugmagazin (Z 3) und Werkzeug in der Spindel (MB44) sind ungleich
U   M 155.4         // Neues Werkzeug vorbereitet zum Ein- oder Ausschwenken
O   M 42.6          // Werkzeugmagazin (Z 3) und Werkzeug in der Spindel (MB44) sind gleich
U   M 42.1          // (Txx in Spindel) (Neues T-WORT=0)   Werkzeugmagazin auf Werkzeug in der Spindel (MB44) fahren und ausschwenken
S   M 40.7          // Werkzeugmagazin ist auf Rückgabeposition und Start zurückgeben
R   M 40.3          // (Neues T-Wort ungleich T-Spindel) Werkzeug zurückgeben und neues einschwenken
R   M 40.1          // Neues T-WORT=0 (Werkzeug nur zurückgeben)
R   M 42.7          // Werkzeugmagazin (Z 3) und Werkzeug in der Spindel (MB44) sind ungleich
R   M 42.6          // Werkzeugmagazin (Z 3) und Werkzeug in der Spindel (MB44) sind gleich
;Beenden von M 42.1 Werkzeugmagazin auf Werkzeug in der Spindel (MB44) fahren und ausschwenken
U   M 40.7          // Werkzeugmagazin steht in gültiger Position und Werkzeugwechsel starten
U   M 41.0          // Werkzeugwechsel abgeschlossen
U   M 42.1          // (Txx in Spindel) (Neues T-WORT=0)   Werkzeugmagazin auf Werkzeug in der Spindel (MB44) fahren und ausschwenken
R   M 40.7          // Werkzeugmagazin ist auf Rückgabeposition und Start zurückgeben
R   M 41.0          // Werkzeugwechsel abgeschlossen
R   M 42.1          // (Txx in Spindel) (Neues T-WORT=0)   Werkzeugmagazin auf Werkzeug in der Spindel (MB44) fahren und ausschwenken
// Z 3    Position Werkzeugmagazin
// MB 44  Aktuelles Werkzeug in der Spindel
// MB 45  Neue Position Werkzeugmagazin
;  M 40.0 // Kein Werkzeug in der Spindel 8((((( Überprüfen
;  M 40.1 // Neues T-WORT=0 (Werkzeug nur zurückgeben)
;  M 40.2 // T1 bis T20 neue gültige Werkzeugnummer in MB45 gespeichert (kein T0)
;  M 40.3 // (Neues T-Wort ungleich T-Spindel) Werkzeug zurückgeben und neues einschwenken
;4.In der Spindel is was und im neichn T-wort steht was richtigs (neues T-WORT= 1-20)
// Werkzeugmagazin auf Werkzeug in der Spindel (MB44) fahren und ausschwenken
// dann Werkzeugmagazin auf neues T-Wort (MB45) fahren und einschwenken
U   M 40.3          // (Neues T-Wort ungleich T-Spindel) Werkzeug zurückgeben und neues einschwenken
U   M 40.2          // T1 bis T20 neue  Werkzeugnummer in MB45 gespeichert
UN  M 40.1          // Neues T-WORT=0 (Werkzeug nur zurückgeben)
UN  M 40.0          // Kein Werkzeug in der Spindel 8((((( Überprüfen
R   M 72.6          // Werkzeug sicher manuell eingespannt
R   M 72.0          // Werkzeug manuell eingespannt
S   M 42.2          // (Neues T-WORT=Txx)(Txx in Spindel) zuerst ausschwenken dann einschwenken
FP  M 43.2          // Werkzeugmagazin auf Werkzeug in der Spindel (MB44) fahren
U   M 43.2          // Werkzeugmagazin auf Werkzeug in der Spindel (MB44) fahren
CC  FC 57             // Positionsvergleicher Werkzeugmagazin (Z 3) und Werkzeug in der Spindel (MB44)
; Werkzeugwenden starten M 41.1 Werkzeugmagazin ist auf Rückgabeposition und Start zurückgeben
U   M 42.7          // Werkzeugmagazin (Z 3) und Werkzeug in der Spindel (MB44) sind ungleich
U   M 155.4         // Neues Werkzeug vorbereitet zum Ein- oder Ausschwenken
O   M 42.6          // Werkzeugmagazin (Z 3) und Werkzeug in der Spindel (MB44) sind gleich
U   M 42.2          // (Neues T-WORT=Txx)(Txx in Spindel) zuerst ausschwenken dann einschwenken
S   M 41.1          // Werkzeugmagazin ist auf Rückgabeposition und Start zurückgeben
R   M 40.3          // (Neues T-Wort ungleich T-Spindel) Werkzeug zurückgeben und neues einschwenken
R   M 40.2          // T1 bis T20 neue  Werkzeugnummer in MB45 gespeichert
R   M 42.7          // Werkzeugmagazin (Z 3) und Werkzeug in der Spindel (MB44) sind ungleich
R   M 42.6          // Werkzeugmagazin (Z 3) und Werkzeug in der Spindel (MB44) sind gleich
; Werkzeugwenden beenden M 41.1 Werkzeugmagazin ist auf Rückgabeposition und Start zurückgeben
U   M 41.4          // Werkzeugwechsel abgeschlossen
U   M 41.1          // Werkzeugmagazin ist auf Rückgabeposition und Start zurückgeben
U   M 42.2          // (Neues T-WORT=Txx)(Txx in Spindel) zuerst ausschwenken dann einschwenken
S   M 41.2          // Werkzeugmagazin auf neues T-Wort (MB45) fahren
FP  M 43.3          // Werkzeugmagazin auf neues T-Wort (MB45) fahren
R   M 41.4          // Werkzeugwechsel abgeschlossen
R   M 41.1          // Werkzeugmagazin ist auf Rückgabeposition und Start zurückgeben
; Werkzeugmagazin auf neues T-Wort (MB45) fahren
U   M 43.3          // Werkzeugmagazin auf neues T-Wort (MB45) fahren
CC  FC 56             // Positionsvergleicher Werkzeugmagazin (Z 3) und neues T-Wort (MB45)
; Wechsler auf neues T-Wort (MB45)  einschwenken
U   M 42.4          // Werkzeugmagazin (Z 3) und neues T-Wort (MB45) sind ungleich
U   M 155.4         // Neues Werkzeug vorbereitet zum Ein- oder Ausschwenken
O   M 42.5          // Werkzeugmagazin (Z 3) und neues T-Wort (MB45) sind gleich
U   M 41.2          // Werkzeugmagazin auf neues T-Wort (MB45) fahren
U   M 42.2          // (Neues T-WORT=Txx)(Txx in Spindel) zuerst ausschwenken dann einschwenken
S   M 41.3          // Werkzeugmagazin ist auf gültiger Position und Start einschwenken
R   M 41.2          // Werkzeugmagazin auf neues T-Wort (MB45) fahren
R   M 42.4          // Werkzeugmagazin (Z 3) und neues T-Wort (MB45) sind ungleich
R   M 42.5          // Werkzeugmagazin (Z 3) und neues T-Wort (MB45) sind gleich
;Beenden von M 42.2 (Neues T-WORT=Txx)(Txx in Spindel) zuerst ausschwenken dann einschwenken
U   M 41.0          // Werkzeugwechsel abgeschlossen
U   M 41.3          // Werkzeugmagazin ist auf gültiger Position und Start einschwenken
U   M 42.2          // (Neues T-WORT=Txx)(Txx in Spindel) zuerst ausschwenken dann einschwenken
R   M 42.2          // (Neues T-WORT=Txx)(Txx in Spindel) zuerst ausschwenken dann einschwenken
R   M 41.3          // Werkzeugmagazin ist auf gültiger Position und Start einschwenken
R   M 41.0          // Werkzeugwechsel abgeschlossen
; fedisch!!!
NETWORK
TITLE = /Normaler/ ACHSFREIGABE/EINLESEFREIGABE WZW
U    M 42.2          // Werkzeugmagazin auf Werkzeug in der Spindel (MB44) fahren und ausschwenken
FN   M 43.5
U   M 43.5
R   M 90.7;         // ACHSFREIGABE/EINLESEFREIGABE    K0
// Nur für Aufzeichnung:
L   Z 3 ;;8((((((((((            // ISTZÄHLER
T   MW 162
NETWORK
TITLE = /Normaler/ 7000  FALSCHES T-WORT
O   M 155.0         // T-WORT GRÖSSER 20
S   M 17.0;         // HM 7000  FALSCHES T-WORT
U   M 17.0;         // HM 7000  FALSCHES T-WORT
=   M 94.5;         // SPINDELFREIGABE VON KANAL 0 SPERREN
U   DB1.DBX 1370.3; // TASTE RESET GEDRÜCKT (MSTT)
R   M 17.0;         // HM 7000  FALSCHES T-WORT
R   M 155.0         // T-WORT GRÖSSER 20
NETWORK
TITLE = /Normaler/ Werkzeugmagazin Positionszähler
U   M 16.0          // FM Gefilterter E 6.6 Strobe und Magazin in Position
U   M 123.7         // Werkzeugmagazin Rechtslauf in Bewegung
U   M 124.1         // Werkzeugmagazin referenziert
ZV  Z 3             // Istzähler Werkzeugmagazin
U   M 16.0          // FM Gefilterter E 6.6 Strobe und Magazin in Position
U   M 123.1         // Werkzeugmagazin Linkslauf in Bewegung
U   M 124.1         // Werkzeugmagazin referenziert
ZR  Z 3             // Istzähler Werkzeugmagazin
NETWORK
TITLE = /Normaler/ Werkzeugmagazin Positionszähler Überwachung und Bildschirmanzeige
L   Z 3             // Istzähler Werkzeugmagazin
L   0;
==I;                // VERGLEICH AUF grösser gleich
L   Z#20;
S   Z 3             // Istzähler Werkzeugmagazin
L   Z 3             // Istzähler Werkzeugmagazin
L   20;
>I;                // VERGLEICH AUF grösser gleich
L   Z#1;
S   Z 3             // Istzähler Werkzeugmagazin
U   M 16.0          // FM Gefilterter E 6.6 Strobe und Magazin in Position
U   M 122.0         // Werkzeugmagazin in Bewegung
U   M 155.3         // Sollzähler Werkzeugmagazin mit neuem T-WORT geladen
U   M 124.1         // Werkzeugmagazin referenziert
SPBN M5307
L   Z 3             // Istzähler Werkzeugmagazin
T   DB20.DBW956     // Bildschirmanzeige
L   Z 3             // Istzähler Werkzeugmagazin
L   MB 160          // SOLLZÄHLER
==I;                // VERGLEICH AUF GLEICH
SPBN M5307
R   M 155.3         // Sollzähler Werkzeugmagazin mit neuem T-WORT geladen
S   M 155.4         // Neues Werkzeug vorbereitet zum Ein- oder Ausschwenken
R   M 155.2         // T-WORT UNGLEICH ISTZÄHLER
;Werkzeugmagazin nur auf Werkzeugnummer vom Werkzeug in der Spindel stellen nicht schwenken!!!!
U   M 39.2          // Settingdaten Gültiges Wekzeug in der Spindel lt. Settingdaten = WZ in der Spindel (überprüfen)
R   M 155.4         // Neues Werkzeug vorbereitet zum Ein- oder Ausschwenken
R   M 39.2          // Settingdaten Gültiges Wekzeug in der Spindel lt. Settingdaten = WZ in der Spindel (überprüfen)
M5307:  NOP 0;
NETWORK
TITLE = /Normaler/ ISTZÄHLER und SOLLZÄHLER synchronisieren bei Handbetrieb und Bildschirmanzeige
U   M 16.0          // FM Gefilterter E 6.6 Strobe und Magazin in Position
U   M 122.0         // Werkzeugmagazin in Bewegung
UN  M 155.3         // Sollzähler Werkzeugmagazin mit neuem T-WORT geladen
U   M 124.1         // Werkzeugmagazin referenziert
SPBN M5308
L   Z 3             // Istzähler Werkzeugmagazin
T   MB 160          // SOLLZÄHLER
T   DB20.DBW956     // Bildschirmanzeige
M5308:  NOP 0;
NETWORK
TITLE = /Normaler/ Werkzeugmagazin Start/Stopp
U   M 155.3         // Sollzähler Werkzeugmagazin mit neuem T-WORT geladen
UN  DB20.DBX 182.0  // NEUES T-WORT (WERKZEUG)
FP  M 124.4         // FM Werkzeugmagazin auf Position fahren
U   M 155.3         // Sollzähler Werkzeugmagazin mit neuem T-WORT geladen
FN  M 124.5         // FM Werkzeugmagazin in Position stoppen
END_FUNCTION
FUNCTION FC 56 : VOID
NAME:
BEGIN
; Positionsvergleicher Werkzeugmagazin (Z 3) und neues T-Wort (MB45)
L   Z 3             // Istzähler Werkzeugmagazin
L   MB 45           // Neue Position Werkzeumagazin
==I;                // VERGLEICH AUF GLEICH
=   M 42.5          // Werkzeugmagazin (Z 3) und neues T-Wort (MB45) sind gleich
SPB  M5601
L   MB 45           // Neue Position Werkzeumagazin
T   MB 160          // Sollzähler Werkzeugmagazin
SET
=   M 42.4          // Werkzeugmagazin (Z 3) und neues T-Wort (MB45) sind ungleich
=   M 155.3         // Sollzähler Werkzeugmagazin mit neuem T-WORT geladen
M5601:  NOP 0;
END_FUNCTION
FUNCTION FC 57 : VOID
NAME:
BEGIN
; Positionsvergleicher Werkzeugmagazin (Z 3) und Werkzeug in der Spindel (MB44)
L   Z 3             // Istzähler Werkzeugmagazin
L   MB 44           // Aktuelles Werkzeug in der Spindel
==I;                // VERGLEICH AUF GLEICH
=   M 42.6          // Werkzeugmagazin (Z 3) und Werkzeug in der Spindel (MB44) sind gleich
SPB  M5701
L   MB 44           // Aktuelles Werkzeug in der Spindel
T   MB 160          // Sollzähler Werkzeugmagazin
SET
=   M 42.7          // Werkzeugmagazin (Z 3) und Werkzeug in der Spindel (MB44) sind ungleich
=   M 155.3         // Sollzähler Werkzeugmagazin mit neuem T-WORT geladen
M5701:  NOP 0;
END_FUNCTION
FUNCTION FC 24 : VOID
NAME: Späneförderer
BEGIN
;//  X420:8   A 10.4 //*? Späneförderer vorwärts
;//  X420:9   A 10.5 //*? Späneförderer rückwärts
;//  X422:3   E 3.1  //*? Überwachung Spänefördererschütze
;//  X6:2     E 5.4  //*? Motorschutz Späneförderer
;//  X5:10    E 7.7    // Wartung und Überlastschalter Späneförderer
NETWORK
TITLE = 6090 Späneförderer Schützüberwachung
UN  A 10.4          // Späneförderer vorwärts
UN  A 10.5          // Späneförderer rückwärts
UN  E 3.1           // Spänefördererschütze Öffnerkontakt
L   S5T#300MS       //
SE  T 44            //
U   T 44            //
S   DB2.DBX 11.2;   // 6090 Spänefördererschütz nicht abgefallen
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 11.2;   // 6090 Spänefördererschütz nicht abgefallen
NETWORK
TITLE = 7082 Schutzschalter Späneförderer ausgelöst
UN  E 5.4           // Motorschutz Späneförderer
S   M 23.5          // HM 7082 Schutzschalter Späneförderer ausgelöst
U   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
U   E 5.4           // Motorschutz Späneförderer
R   M 23.5          // HM 7082 Schutzschalter Späneförderer ausgelöst
NETWORK
TITLE = Freigabe Späneförderer
U   M 12.0          // Tür zu und Verriegelt
U   M 110.0;        // AUX-ON
UN  M 23.5          // HM 7082 Schutzschalter Späneförderer ausgelöst
U   E 7.7           // Wartung und Überlastschalter Späneförderer
=   M 82.0          // Freigabe Späneförderer
U   DB20.DBX 827.2  // Taste Späneförderer
UN  DB20.DBX 324.0; // Programm läuft
FP  M 82.3          // FM Taste Späneförderer
U   M 82.3          // FM Taste Späneförderer
S   M 82.4          // HM FM Taste Späneförderer
UN  DB20.DBX 827.2  // Taste Späneförderer
R   M 82.4          // HM FM Taste Späneförderer
U   DB20.DBX 827.2  // Taste Späneförderer
UN  DB20.DBX 324.0; // Programm läuft
FN  M 82.1          // /FM Taste Späneförderer
U   M 82.1          // /FM Taste Späneförderer
U   M 82.0          // Freigabe Späneförderer
UN  T 12
UN  T 16
S   M 82.2          // HM Späneförderer vorwärts
U   DB20.DBX 827.2  // Taste Späneförderer
L   S5T#1S          // Tastenumschaltzeit vor/rückwärts
SE  T 12
U   M 82.2          // HM Späneförderer vorwärts
L   S5T#3S          // Laufzeit vom Späneförderer
SE  T 13;           // STARTEN T 13
O   T 13;           // STARTEN T 13
ON  M 82.0          // Freigabe Späneförderer
R   M 82.2          // HM Späneförderer vorwärts
O   M 82.2          // HM Späneförderer vorwärts
O   T 14            // IMPULSDAUER
UN  A 10.5          // Späneförderer rückwärts
U   M 82.0          // Freigabe Späneförderer
R   M 82.4          // HM FM Taste Späneförderer
=   A 10.4          // Späneförderer vorwärts
U   M 82.4          // HM FM Taste Späneförderer
U   T 12
U   M 82.0          // Freigabe Späneförderer
UN  A 10.4          // Späneförderer vorwärts
=   A 10.5          // Späneförderer rückwärts
U   A 10.5          // Späneförderer rückwärts
L   S5T#1S          // Sicherheitsumschaltzeit für Wendeschützschaltung
SA  T 16
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUGELÖST
ON  M 82.0          // Freigabe Späneförderer
R   M 82.4          // HM FM Taste Späneförderer
R   M 82.2          // HM Späneförderer vorwärts
U   DB20.DBX 324.0; // Programm läuft
UN  DB20.DBX 324.1; // STOP-ZUSTAND
U   M 82.0          // Freigabe Späneförderer
=   M 82.5          // Automatischer Spänefördererbetrieb
NETWORK
TITLE = IMPULSDAUER
U   M 82.5          // Automatischer Spänefördererbetrieb
UN  T 15            // PAUSEDAUER
L   S5T#3S          // Laufzeit vom Späneförderer
SV  T 14            // IMPULSDAUER
NETWORK
TITLE = PAUSENDAUER
U   M 82.5          // Automatischer Spänefördererbetrieb
UN  T 14            // IMPULSDAUER
L   S5T#6S          // Pausenzeit vom Späneförderer
SV  T 15            // PAUSEDAUER
UN  M 82.5          // Automatischer Spänefördererbetrieb
UN  M 82.0          // Freigabe Späneförderer
R   T 14            // IMPULSDAUER
R   T 15            // PAUSEDAUER
END_FUNCTION
FUNCTION FC 52 : VOID
NAME: WERKZEUGWENDER
BEGIN
; Werkzeugspannsystem:
; A 4.6           // Klemmung Werkzeug öffnen
; A 4.7           // Klemmung Werkzeug schließen
; M 71.1          // Werkzeugfehler ( 220 <= 300 )
; M 71.2          // Werkzeug sicher eingespannt ( 300 <= 450 )
; M 71.3          // Kein Werkzeug eingespannt
; M 71.0          // Werkzeug sicher ausgestossen ( <= 220 )
//  X5:9     A 5.0    // Ausblasen Frässpindel
; Werkzeugwechselarm:
//  X5:6     A 4.5    // Bremse Werkzeugwechselarm
//  X420:5   A 10.1   // Schütz Werkzeugwechselarm
//  X5:5     E 7.2    // Homeposition Werkzeugwechselarm
//  X5:6     E 7.3    // Werkzeugwechselarm in Wechselposition
; Werkzeugmagazin:
//  X421:9   A 9.5    // Werkzeughalter zurückschwenken
//  X421:10  A 9.6    // Werkzeughalter auf Wechselposition schwenken
//  X5:2     E 6.7    // Werkzeughalter zurückgeschwenkt
//  X5:3     E 7.0    // Werkzeughalter in Wechselposition
//  X5:5     A 4.4    // Bremse WZW Magazin
//  X420:6   A 10.2   // Rechtslauf WZW-Magazin
//  X420:7   A 10.3   // Linkslauf WZW-Magazin
//  X5:1     E 6.6    // Strobe und Magazin in Position
//  X5:4     E 7.1    // Magazin auf Positition 1
;F40=0.1076660 in 1000 Grad  ; Haupspindel Referenzpunkt
;F36=0.3409844 in Meter!!!!! ; Z-Achse Werkzeugwendeposition
;F32=0.1767572 in Meter!!!!! ; Y-Achse Werkzeugwendeposition
NETWORK
TITLE = Einlesefreigabe Werkzeugwenden aktiv
U   M 46.3;         // \FN WERKZEUGWENDEN AKTIV
UN  M 42.2          // (Neues T-WORT=Txx)(Txx in Spindel) zuerst ausschwenken dann einschwenken
U   DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
R   M 90.7          // Werkzeugwenden aktiv
U   M 152.0;        // WERKZEUGWENDEN AKTIV
FN  M 46.3;         // \FN WERKZEUGWENDEN AKTIV
NETWORK
TITLE = ALARM 6078 Schutzschalter Werkzeugmagazin ausgelöst
UN  E 4.7           // Motorschutz Werkzeugmagazin
S   DB2.DBX 9.6;    // 6078 Schutzschalter Werkzeugmagazin ausgelöst
U   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
U   E 4.7           // Motorschutz Werkzeugmagazin
R   DB2.DBX 9.6;    // 6078 Schutzschalter Werkzeugmagazin ausgelöst
NETWORK
TITLE = ALARM 6079 Schutzschalter Werkzeuwechsler ausgelöst
UN  E 5.3           // Motorschutz Werkzeugwechsler
S   DB2.DBX 9.7;    // 6079 Schutzschalter Werkzeuwechsler ausgelöst
U   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
U   E 5.3           // Motorschutz Werkzeugwechsler
R   DB2.DBX 9.7;    // 6079 Schutzschalter Werkzeuwechsler ausgelöst
NETWORK
TITLE = Letzten WZW-Schritt AUS DEN SETTINGDATEN SETZEN
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
SPBN   M5250
L   DB15.DBB 20     // Fehlerspeicher Werkzeugwender
L   0;
<>I;
SPBN   M5250
SET
=   M 127.2         // Werkzeugköcher auf Wechselposition schwenken
S   M 127.3         // HM 7021  WERKZEUGWENDER FREIFAHREN
L   DB15.DBB 20     // Fehlerspeicher Werkzeugwender
T   MB 150          // Schrittkette
M5250:  NOP 0
NETWORK
TITLE = WERKZEUGWENDEN AKTIV
L   MW 150;         // SCHRITTKETTE
L   0;
<>I;
=   M 152.0;        // WERKZEUGWENDEN AKTIV
NETWORK
TITLE = Alarm 6041 WZW-Schwenkzeitüberschreitung
U   M 152.0;        // WERKZEUGWENDEN AKTIV
U   M 100.4         // VORSCHUBSCHALTER GRÖSSER 4%
UN  M 127.3         // HM 7021  WERKZEUGWENDER FREIFAHREN
L   S5T#15S         // Werkzeugwechselüberwachungszeit
SE  T 21
U   T 21
S   DB2.DBX 5.1;    // 6041 WZW-Schwenkzeitüberschreitung
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 5.1;    // 6041 WZW-Schwenkzeitüberschreitung
NETWORK
TITLE = Meldung 7021  WERKZEUGWENDER FREIFAHREN
U   M 127.3         // HM 7021  WERKZEUGWENDER FREIFAHREN
=   M 18.5;         // HM 7021  WERKZEUGWENDER FREIFAHREN
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUGELÖST
UN  M 152.0;        // WERKZEUGWENDEN AKTIV
R   M 127.3         // HM 7021  WERKZEUGWENDER FREIFAHREN
R   M 90.7          // Werkzeugwenden aktiv
NETWORK
TITLE = WERKZEUGWENDEN SCHRITT 3,4,5,6,7, SOFORT ABBRECHEN BEI RESET, /NOT AUS,
U(
ON  M 2.0           // SUMME aller NOT-AUS Möglichkeiten
ON  M 110.0;        // AUX-ON
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUGELÖST
O   M 127.6         // FM Freigabe Werkzeugmagazin für Handbetrieb
)
U   M 152.0;        // WERKZEUGWENDEN AKTIV
UN  M 150.0         // Werkzeugwenden abgebrochen
U(
O   M 126.3         // HM im SCHRITT 3
O   M 150.4;        // SCHRITT 4
O   M 150.5;        // SCHRITT 5
O   M 150.6;        // SCHRITT 6
O   M 150.7;        // SCHRITT 7
)
S   M 150.0         // Werkzeugwenden abgebrochen
S   M 127.3         // HM 7021  WERKZEUGWENDER FREIFAHREN
S   DB15.DBX 20.0   // Werkzeugwenden abgebrochen in den SETTINGDATEN SPEICHERN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
R   A 4.5           // Bremse Werkzeugwechselarm
R   A 10.1          // Schütz Werkzeugwechselarm
R   A 5.0           // Ausblasen Frässpindel
R   DB25.DBX 340.3; // EINLESEFREIGABE K1
R   DB25.DBX 340.4; // ACHSFREIGABE    K1
R   M 72.0          // Werkzeug manuell eingespannt
R   M 72.6          // Werkzeug sicher manuell eingespannt
SPBN M5252
;Hilfsschrittkette löschen:
L   0
T   MW 125;         // Hilfsschrittkette
=   DB1.DBX 1440.0  // NC-RESET AUSLÖSEN
M5252:NOP 0
NETWORK
TITLE = WERKZEUGWENDEN im SCHRITT 1,2,8, SOFORT ABBRECHEN BEI RESET, /NOT AUS,
U(
ON  M 2.0           // SUMME aller NOT-AUS Möglichkeiten
ON  M 110.0;        // AUX-ON
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUGELÖST
O   M 127.6         // FM Freigabe Werkzeugmagazin für Handbetrieb
)
U   M 152.0;        // WERKZEUGWENDEN AKTIV
UN  M 150.0         // Werkzeugwenden abgebrochen
U(
O   M 150.1;        // SCHRITT 1
O   M 150.2;        // SCHRITT 2
O(
U   M 150.3;        // SCHRITT 3
UN  M 126.3         // HM im SCHRITT 3
)
;       O   M 151.0;        // SCHRITT 8
)
R   M 127.2         // Werkzeugköcher auf Wechselposition schwenken
R   M 124.7         // Warten auf neues Wekzeug
R   M 90.7          // Werkzeugwenden aktiv
R   DB25.DBX 340.3; // EINLESEFREIGABE K1
R   DB25.DBX 340.4; // ACHSFREIGABE    K1
R   M 115.4;        // SPINDELFREIGABE Rundachsbetrieb K0
R   M 72.0          // Werkzeug manuell eingespannt
R   M 72.6          // Werkzeug sicher manuell eingespannt
SPBN M5253
;Schritt und Hilfsschrittkette löschen:
L   0
T   MW 150;         // Schrittkette
T   MW 125;         // Hilfsschrittkette
=   DB1.DBX 1440.0  // NC-RESET AUSLÖSEN
M5253:NOP 0
NETWORK
TITLE = Werkzeugwender freifahren
UN  M 94.0;         // AUX-ON / MASCHINENTÜR OFFEN  SFG
UN  M 113.0;        // SONDERBETRIEB AKTIV
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
U   DB20.DBX 326.0; // BETRIEBSART JOG
U   M 150.0         // Werkzeugwenden abgebrochen
U   DB20.DBX 828.4  // Taste Werkzeugwechseln
=   M 127.4         // Werkzeugwenden freifahren
U   M 150.0         // Werkzeugwenden abgebrochen
UN  M 127.4         // Werkzeugwenden freifahren
=   M 127.5         // HM Werkzeugwenden abgebrochen
NETWORK
TITLE = Meldung 7077 Werkzeugwender einstellen
U   DB1.DBX 1370.0; // 1. PLC-Durchlauf
SPBN   M5201;
L   DB10.DBD 32;    // Maschinendaten Y-Achse Werkzeugwendeposition
L   0
==I
O(
L   DB10.DBD 36;    // Maschinendaten Z-Achse Werkzeugwendeposition
L   0
==I
)
S   M 23.0          // HM 7077 Meldung Werkzeugwender einstellen
M5201: NOP 0;
NETWORK
TITLE = SFG und NC-STARTVERRIEGELUNG
O   M 23.1          // HM 7078 Werkzeugköcher nicht zurückgeschwenkt
O   M 23.2          // HM 7079 Werkzeugwechselarm nicht in Grundstellung
=   M 94.7          // SFG Werkzeugwender nicht in Grundstellung
=   M 97.0;         // NC-STARTVERRIEGELUNG
NETWORK
TITLE = T-Wort und M6 fast gleichzeitig
U   DB20.DBX 192.6; // M6 DYN.
U   DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
S   M 90.7;         // ACHSFREIGABE/EINLESEFREIGABE    K0
U   DB20.DBX 192.6; // M6 DYN.
U   M 155.3         // SOLLZÄHLER mit neuem T-WORT geladen
UN  M 155.4         // Neues Werkzeug vorbereitet zum Einschwenken
O   M 127.1         // Warten auf Werkzeugmagazin referenziert
S   M 124.7         // Warten auf neues Wekzeug
O   M 124.7         // Warten auf neues Wekzeug
O   M 127.3         // HM 7021  WERKZEUGWENDER FREIFAHREN
S   M 90.7;         // ACHSFREIGABE/EINLESEFREIGABE    K0
NETWORK
TITLE = SCHRITT 1  SPINDEL IN POSITIONIERBETRIEB SCHALTEN
;Werkzeuwechsel von Hand bei nicht Chaotischer Ansteuerung:
U   M 40.7          // Werkzeugmagazin ist auf Rückgabeposition und Start zurückgeben
U   M 72.6          // Werkzeug sicher manuell eingespannt
UN  DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
;Werkzeuwechsel von nicht Chaotischer Ansteuerung:
O(
O   M 40.7          // Werkzeugmagazin ist auf Rückgabeposition und Start zurückgeben
O   M 41.1          // Werkzeugmagazin ist auf Rückgabeposition und Start zurückgeben
U   DB20.DBX 192.6; // M6 DYN.
O   M 41.3          // Werkzeugmagazin ist auf gültiger Position und Start einschwenken
UN  DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
)
;T-Wort vor M6 oder gleichzeitig programmiert:
O(
O   DB20.DBX 192.6; // M6 DYN.
O   M 124.7         // Warten auf neues Wekzeug
U   M 155.4         // Neues Werkzeug vorbereitet zum Einschwenken
)
;Nur M6 programmiert:
O(
U   DB20.DBX 192.6; // M6 DYN.
UN  M 155.3         // SOLLZÄHLER mit neuem T-WORT geladen
U   DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
)
;Werkzeugwechsel mit WZW-Taste in Betriebsart JOG und Schlüsselschalter auf Hand:
O(
U   DB20.DBX 828.4  // Taste Werkzeugwechseln
U   M 123.0         // Freigabe Werkzeugmagazin für Handbetrieb
U   DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
UN  DB1.DBX 1366.5  // 1= neue ACC-SW die Normalen- und Chaotischenwerkzeugwechsel übernimmt
)
U   M 152.3;        // WZW FREIGABE
UN  M 23.0          // HM 7077 Meldung Werkzeugwender einstellen
UN  M 127.3         // HM 7021  WERKZEUGWENDER FREIFAHREN
UN  M 152.0;        // WERKZEUGWENDEN AKTIV
R   M 124.7         // Warten auf neues Wekzeug
S   M 115.4;        // SPINDELFREIGABE Rundachsbetrieb K0
S   M 90.7;         // ACHSFREIGABE/EINLESEFREIGABE    K0
S   M 150.1;        // SCHRITT 1
U   M 150.1;        // SCHRITT 1
U   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
UN  M 125.1         // HM im SCHRITT 1
U(
O   M 100.4         // VORSCHUBSCHALTER GRÖSSER 4%
O   M 100.3         // FM VORSCHUBSCHALTER GRÖSSER/GLEICH 3%
)
SPBN M5202;
SET;
=   M 125.1         // HM im SCHRITT 1
L   2               // Y-Achse Position merken (nur Y)  (WZW)
T   DB20.DBW 960    // Y-Achse Position merken (nur Y)  (WZW)
S   DB25.DBX 3.5;   // Umschaltung von Spindel auf Rundachsbetrieb oder umgekehrt
S   DB25.DBX 823.0; // 1=Rundachsbetrieb 0=Spindelbetrieb
L   0
T   DB25.DBB 822;   // Spindelnummer welche umgeschaltet wird
S   DB25.DBX 340.3; // EINLESEFREIGABE K1
S   DB25.DBX 340.4; // ACHSFREIGABE    K1
S   M 115.4;        // SPINDELFREIGABE Rundachsbetrieb K0
=   DB25.DBX 1.4;   // NC-SATZ ABSCHICKEN
=   DB25.DBX 549.2; // SPINDEL in den Positionierbetrieb
U   DB20.DBX 324.0; // Programm läuft
=   DB20.DBX 348.6  // Bit für AC zum Positionen merken (WZW)
M5202:  NOP 0;
NETWORK
TITLE = SCHRITT 2  Spindel und Achsen auf Werkzeugwendeposition fahren,Werkzeugköcher in Wechselposition
U   M 150.1;        // SCHRITT 1
U   DB25.DBX 332.0; // NC-SATZ FERTIG
R   M 150.1;        // SCHRITT 1
S   M 150.2;        // SCHRITT 2
R   DB25.DBX 332.0; // NC-SATZ FERTIG
U   M 150.2;        // SCHRITT 2
UN  M 125.2         // HM im SCHRITT 2
U(
O   M 100.4         // VORSCHUBSCHALTER GRÖSSER 4%
O   M 100.3         // FM VORSCHUBSCHALTER GRÖSSER/GLEICH 3%
)
U   E 0.3;          // Hauptantrieb SERVO-READY
SPBN M5203;
=   M 125.2         // HM im SCHRITT 2
=   M 127.2         // Werkzeugköcher auf Wechselposition schwenken
;Vorschubwerte und Genauhalt einstellen:
=   DB25.DBX 548.2; // VORSCHUB IN METER/SEKUNDE WERT IN DBD556
=   DB25.DBX 1.4;   // NC-SATZ ABSCHICKEN
=   DB25.DBX 548.5; // GENAUHALTMODUS SETZEN
=   DB25.DBX 548.7  // G0 wird ausgelöst
;Haupspindel auf Werkzeugwendeposition fahren:
S   DB25.DBX 21.4;  // POSITIONSANFORDERUNG FÜR KANALACHSE C
L   0
T   DB25.DBD 70;    // POSITIONSWERT FÜR KANALACHSE C
;Y- und Z-ACHSE auf Werkzeugwendeposition fahren:
=   DB25.DBX 20.1;  // POSITIONSANFORDERUNG FÜR Y-ACHSE
L   DB10.DBD 32     // Y-Achse Werkzeugwendeposition
T   DB25.DBD 26;    // POSITIONSWERT FÜR Y-ACHSE
=   DB25.DBX 20.2;  // POSITIONSANFORDERUNG FÜR Z-ACHSE
L   DB10.DBD 36     // Z-Achse Werkzeugwendeposition
T   DB25.DBD 30;    // POSITIONSWERT FÜR Z-ACHSE
M5203:  NOP 0;
NETWORK
TITLE = SCHRITT 3  Werkzeugwechselarm auf Übernahmeposition schwenken
U   M 150.2;        // SCHRITT 2
U   DB25.DBX 332.0; // NC-SATZ FERTIG
R   M 150.2;        // SCHRITT 2
R   DB25.DBX 332.0; // NC-SATZ FERTIG
S   M 150.3;        // SCHRITT 3
U   M 150.3;        // SCHRITT 3
U   E 7.0           // Werkzeugköcher in Wechselposition
UN  M 126.3         // HM im SCHRITT 3
UN  M 127.5         // HM Werkzeugwenden abgebrochen
U(
O   M 100.4         // VORSCHUBSCHALTER GRÖSSER 4%
O   M 100.3         // FM VORSCHUBSCHALTER GRÖSSER/GLEICH 3%
)
SPBN M5204;
S   M 126.3         // HM im SCHRITT 3
S   A 4.5           // Bremse Werkzeugwechselarm
S   A 10.1          // Schütz Werkzeugwechselarm
S   DB15.DBX 20.3   // SCHRITT 3 IN DEN SETTINGDATEN SPEICHERN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
M5204:  NOP 0;
NETWORK
TITLE = SCHRITT 4  Werkzeugwechselarm auf Übernahmeposition stoppen,Werkzeugspannsystem öffnen
//  X5:5     E 7.2    //*?  Homeposition Werkzeugwechselarm
//  X5:6     E 7.3    //*?  Werkzeugwechselarm in Wechselposition
U   M 150.3;        // SCHRITT 3
U   E 7.0           // Werkzeugköcher in Wechselposition
U   M 16.2          // FM Gefilterter E 7.3 Werkzeugwechselarm in Wechselposition
R   M 150.3;        // SCHRITT 3
R   M 126.3         // HM im SCHRITT 3
S   M 150.4;        // SCHRITT 4
U   M 150.4;        // SCHRITT 4
UN  M 126.4         // HM im SCHRITT 4
SPBN M5205;
S   M 126.4         // HM im SCHRITT 4
S   M 128.0         // Werkzeugspannsystem öffnen
R   A 4.5           // Bremse Werkzeugwechselarm
R   A 10.1          // Schütz Werkzeugwechselarm
R   DB15.DBX 20.3   // SCHRITT 3 IN DEN SETTINGDATEN SPEICHERN
S   DB15.DBX 20.4   // SCHRITT 4 IN DEN SETTINGDATEN SPEICHERN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
M5205:  NOP 0;
NETWORK
TITLE = SCHRITT 5 Werkzeug austauschen
U   M 150.4;        // SCHRITT 4
U   M 126.4         // HM im SCHRITT 4
U   M 71.0          // Werkzeug sicher ausgestossen ( <= 220 )
R   M 150.4;        // SCHRITT 4
R   M 126.4         // HM im SCHRITT 4
S   M 150.5;        // SCHRITT 5
U   M 150.5;        // SCHRITT 5
UN  M 126.5         // HM im SCHRITT 5
UN  M 127.5         // HM Werkzeugwenden abgebrochen
U(
O   M 100.4         // VORSCHUBSCHALTER GRÖSSER 4%
O   M 100.3         // FM VORSCHUBSCHALTER GRÖSSER/GLEICH 3%
)
SPBN M5206;
S   M 126.5         // HM im SCHRITT 5
S   M 128.0         // Werkzeugspannsystem öffnen
S   A 4.5           // Bremse Werkzeugwechselarm
S   A 10.1          // Schütz Werkzeugwechselarm
S   A 5.0           // Ausblasen Frässpindel
M5206:  NOP 0;
NETWORK
TITLE = SCHRITT 6 Werkzeugwechselarm auf Rückgabeposition schwenken,Werkzeugspannsystem schliessen
U   M 150.5;        // SCHRITT 5
U   M 126.5         // HM im SCHRITT 5
U   E 7.0           // Werkzeugköcher in Wechselposition
U   M 16.2          // FM Gefilterter E 7.3 Werkzeugwechselarm in Wechselposition
R   M 150.5;        // SCHRITT 5
R   M 126.5         // HM im SCHRITT 5
S   M 150.6;        // SCHRITT 6
U   M 150.6;        // SCHRITT 6
UN  M 126.6         // HM im SCHRITT 6
SPBN M5207;
S   M 126.6         // HM im SCHRITT 6
R   M 128.0         // Werkzeugspannsystem öffnen
R   A 4.5           // Bremse Werkzeugwechselarm
R   A 10.1          // Schütz Werkzeugwechselarm
R   A 5.0           // Ausblasen Frässpindel
R   DB15.DBX 20.5   // SCHRITT 5 IN DEN SETTINGDATEN SPEICHERN
R   DB15.DBX 20.4   // SCHRITT 4 IN DEN SETTINGDATEN SPEICHERN
S   DB15.DBX 20.6   // SCHRITT 6 IN DEN SETTINGDATEN SPEICHERN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
M5207:  NOP 0;
NETWORK
TITLE = SCHRITT 7 Werkzeugwechselarm nach Hause fahren
U   M 126.6         // HM im SCHRITT 6
U   M 150.6;        // SCHRITT 6
U(
O   M 71.2          // Werkzeug sicher eingespannt ( 300 <= 450 )
O   M 71.3          // Kein Werkzeug eingespannt
)
R   M 150.6;        // SCHRITT 6
R   M 126.6         // HM im SCHRITT 6
S   M 150.7;        // SCHRITT 7
U   M 150.7;        // SCHRITT 7
UN  M 126.7         // HM im SCHRITT 7
UN  M 127.5         // HM Werkzeugwenden abgebrochen
U(
O   M 100.4         // VORSCHUBSCHALTER GRÖSSER 4%
O   M 100.3         // FM VORSCHUBSCHALTER GRÖSSER/GLEICH 3%
)
SPBN M5208;
S   M 126.7         // HM im SCHRITT 7
S   A 4.5           // Bremse Werkzeugwechselarm
S   A 10.1          // Schütz Werkzeugwechselarm
R   DB15.DBX 20.6   // SCHRITT 6 IN DEN SETTINGDATEN SPEICHERN
S   DB15.DBX 20.7   // SCHRITT 7 IN DEN SETTINGDATEN SPEICHERN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
M5208: NOP 0;
NETWORK
TITLE = SCHRITT 8 Werkzeugwechselarm ist Daheim,Werkzeugköcher einschwenken,WZW beenden
U   M 126.7         // HM im SCHRITT 7
U   M 150.7;        // SCHRITT 7
U   M 7.2           // Gefilterter E 7.2 Homeposition Werkzeugwechselarm
R   M 150.7;        // SCHRITT 7
R   M 126.7         // HM im SCHRITT 7
S   M 151.0;        // SCHRITT 8
U   M 151.0;        // SCHRITT 8
UN  M 125.3         // HM im SCHRITT 8
SPBN M5209;
U   DB1.DBX 1366.5  // 1= neue ACC-SW die Normalen- und Chaotischenwerkzeugwechsel übernimmt
S   DB20.DBX 348.7  // Rückmelden Werkzeugwenden beendet Kanal 0
SET
S   M 125.3         // HM im SCHRITT 8
R   M 115.4;        // SPINDELFREIGABE Rundachsbetrieb K0
R   M 127.2         // Werkzeugköcher auf Wechselposition schwenken
R   A 4.5           // Bremse Werkzeugwechselarm
R   A 10.1          // Schütz Werkzeugwechselarm
R   DB25.DBX 340.3; // EINLESEFREIGABE K1
R   DB25.DBX 340.4; // ACHSFREIGABE    K1
;Hauptspindel auf Spindelbetrieb schalten:
S   DB25.DBX 3.5;   // Umschaltung von Spindel auf Rundachsbetrieb oder umgekehrt
R   DB25.DBX 823.0; // 1=Rundachsbetrieb 0=Spindelbetrieb
L   0
T   DB25.DBB 822;   // Spindelnummer welche umgeschaltet wird
R   DB15.DBX 20.7   // SCHRITT 7 IN DEN SETTINGDATEN SPEICHERN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
M5209:  NOP 0;
NETWORK
TITLE = Werkzeugwenden beenden
U   M 151.0;        // SCHRITT 8
U   M 125.3         // HM im SCHRITT 8
U   T 11            // 500MS verzögert Werkzeugköcher zurückgeschwenkt
U   M 80.4;         // SPINDEL0 SPINDELBETRIEB
R   M 151.0;        // SCHRITT 8
R   M 125.3         // HM im SCHRITT 8
SPBN M5212;
UN  DB1.DBX 1366.5  // 1= neue ACC-SW die Normalen- und Chaotischenwerkzeugwechsel übernimmt
S   DB20.DBX 348.7  // Rückmelden Werkzeugwenden beendet Kanal 0
SET
R   M 155.4         // Neues Werkzeug vorbereitet zum Einschwenken
;im vorherigem Schritt gelöscht8(((((       R   M 115.4;        // SPINDELFREIGABE Rundachsbetrieb K0
;Schritt und Hilfsschrittkette löschen:
L   0
T   MW 150;         // Schrittkette
T   MW 125;         // Hilfsschrittkette
L   0
T   DB15.DBW 20     // Werkzeugwender Fehlerspeicher löschen und in den SETTINGDATEN SPEICHERN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
UN  DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
UN  M 42.2          // (Neues T-WORT=Txx)(Txx in Spindel) zuerst ausschwenken dann einschwenken
R   M 90.7;         // ACHSFREIGABE/EINLESEFREIGABE    K0
U   M 41.1          // Werkzeugmagazin ist auf Rückgabeposition und Start zurückgeben
SPBN M5210;
SET
S   M 41.4          // Werkzeugwechsel abgeschlossen (zurückgeben)
L   0
T   MB 44           // Aktuelles Werkzeug in der Spindel
T   DB15.DBB 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
L   Z 3             // Istzähler Werkzeugmagazin
T   DB20.DBW 958    // die neue Werkzeugnummer bei chaotischer WZ-Beladung
S   DB20.DBX 5.0    // Werkzeugnummer in Spindel laden
M5210:  NOP 0;
O   M 42.0          // (T0 in Spindel)  (Neues T-WORT=Txx) Werkzeugmagazin auf neues T-Wort (MB45) fahren und einschwenken
O   M 42.1          // (Txx in Spindel) (Neues T-WORT=0)   Werkzeugmagazin auf Werkzeug in der Spindel (MB44) fahren und ausschwenken
O   M 41.3          // Werkzeugmagazin ist auf gültiger Position und Start einschwenken
UN  M 72.6          // Werkzeug sicher manuell eingespannt
SPBN M5211;
SET
S   M 41.0          // Werkzeugwechsel abgeschlossen
L   DB20.DBW 184;   // T-WORT
T   MB 44           // Aktuelles Werkzeug in der Spindel
T   DB15.DBB 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
M5211:  NOP 0;
U   M 42.1          // (Txx in Spindel) (Neues T-WORT=0)   Werkzeugmagazin auf Werkzeug in der Spindel (MB44) fahren und ausschwenken
U   M 72.6          // Werkzeug sicher manuell eingespannt
SPBN M5212;
SET
S   M 41.0          // Werkzeugwechsel abgeschlossen
S   DB20.DBX 5.0    // Werkzeugnummer in Spindel laden
R   M 72.0          // Werkzeug manuell eingespannt
R   M 72.6          // Werkzeug sicher manuell eingespannt
L   0
T   MB 44           // Aktuelles Werkzeug in der Spindel
T   MB 45           // Neue Position Werkzeugmagazin
T   DB15.DBB 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
L   Z 3             // Istzähler Werkzeugmagazin
T   DB20.DBW 958    // die neue Werkzeugnummer bei chaotischer WZ-Beladung
M5212:  NOP 0;
END_FUNCTION
FUNCTION FC 51
NAME: Von T-Wort Werkzeugmagazinpositionierung
BEGIN
NETWORK
TITLE = WZW FREIGABE
UN  M 94.0;         // AUX-ON / MASCHINENTÜR OFFEN  SFG
U   M 131.0;        // UE Modul BETRIEBSBEREIT
UN  M 113.0;        // SONDERBETRIEB AKTIV
U   M 124.1         // Werkzeugmagazin referenziert
U   DB20.DBX 324.4; // REFERENZPUNKT AKTIV
=   M 152.3;        // WZW FREIGABE
NETWORK
TITLE = Werkzeugmagazin Freigabe
UN  M 94.0;         // AUX-ON / MASCHINENTÜR OFFEN  SFG
U   M 131.0;        // UE Modul BETRIEBSBEREIT
UN  M 113.0;        // SONDERBETRIEB AKTIV
U   DB20.DBX 324.4; // REFERENZPUNKT AKTIV
=   M 127.0;        // Werkzeugmagazin Freigabe
NETWORK
TITLE = Werkzeugzurücksortieren für nichtchaotischen Betrieb
U   M 127.0;        // Werkzeugmagazin Freigabe
U   DB20.DBX 326.0; // BETRIEBSART JOG
U   DB20.DBX 833.2  // RAFI Tastatur X2.3/4 SCHLÜSSELSCHALTER ENDLAGENÜBERWACHUNG DEAKTIVIEREN
U   DB20.DBX 831.5  // Taste CYCLE-START VON BEDIENPULT
U   DB20.DBX 828.4  // Taste Werkzeugwechseln
FP  M 127.7         // FP Werkzeugzurücksortieren für nichtchaotischen Betrieb
U   M 127.7         // FP Werkzeugzurücksortieren für nichtchaotischen Betrieb
UN  M 50.1          // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
SPBN M5105
SET
S   DB20.DBX 5.1    // für einen Durchlauf setzen,dann werden die Werkzeuge für nichtchaotischen Betrieb zurücksortiert
M5105:  NOP 0;
NETWORK
TITLE = Werkzeugmagazinpositions Zähler auf 1 setzen
U   M 124.6         // FM Werkzeugmagazin referenziert
SPBN M5100
L   1;              // LADE 1
S   Z 3;            // ISTZÄHLER AUF 1 SETZEN
L   1;              // LADE 1
T   DB20.DBW956     // Bildschirmanzeige
M5100:  NOP 0;
NETWORK
TITLE = Neues T-Wort und Werkzeugmagazin nicht referenziert
U   DB20.DBX 182.0  // NEUES T-WORT (WERKZEUG)
UN  M 124.1         // Werkzeugmagazin referenziert
U   M 127.0;        // Werkzeugmagazin Freigabe
SPBN M5101
=   M 124.3         // Referenzieren Werkzeugmagazin nach rechts schwenken
=   M 127.1         // Warten auf Werkzeugmagazin referenziert
M5101:  NOP 0;
NETWORK
TITLE = Neues T-Wort überprüfen und Sollzähler setzen
O   DB20.DBX 182.0  // NEUES T-WORT (WERKZEUG)
O   M 127.1         // Warten auf Werkzeugmagazin referenziert
U   M 124.1         // Werkzeugmagazin referenziert
U   M 127.0;        // Werkzeugmagazin Freigabe
SPBN M5102
R   M 127.1         // Warten auf Werkzeugmagazin referenziert
L   DB20.DBW 184;   // T-WORT
L   20;             // LADE KONSTANTE DEZ 20
>I;                 // VERGLEICH AUF GRÖSSER
=   M 155.0         // T-WORT GRÖSSER 20
SPB  M5102
L   DB20.DBW 184;   // LADE T-WORT
L   1;              // LADE KONSTANTE DEZ 1
<I;                 // VERGLEICH AUF KLEINER
=   M 155.1         // T-WORT KLEINER 1
SPB  M5102
L   DB20.DBW 184;   // T-WORT
L   Z 3;            // ISTZÄHLER
<>I;                // VERGLEICH AUF UNGLEICH
=   M 155.2         // T-WORT UNGLEICH ISTZÄHLER
O   M 155.2         // T-WORT UNGLEICH ISTZÄHLER
O   M 155.3         // SOLLZÄHLER mit neuem T-WORT geladen
SPBN M5102
L   DB20.DBW 184;   // T-WORT
T   MB 160          // SOLLZÄHLER
=   M 155.3         // SOLLZÄHLER mit neuem T-WORT geladen
R   M 155.4         // Neues Werkzeug vorbereitet zum Einschwenken
M5102:  NOP 0;
;8((((((((((
L   Z 3;            // ISTZÄHLER
T   MW 162
NETWORK
TITLE = 7000  FALSCHES T-WORT
O   M 155.0         // T-WORT GRÖSSER 20
S   M 17.0;         // HM 7000  FALSCHES T-WORT
U   M 17.0;         // HM 7000  FALSCHES T-WORT
=   M 94.5;         // SPINDELFREIGABE VON KANAL 0 SPERREN
U   DB1.DBX 1370.3; // TASTE RESET GEDRÜCKT (MSTT)
R   M 17.0;         // HM 7000  FALSCHES T-WORT
R   M 155.0         // T-WORT GRÖSSER 20
NETWORK
TITLE = Werkzeugmagazin Positionszähler
U   M 16.0          // FM Gefilterter E 6.6 Strobe und Magazin in Position
U   M 123.7         // Werkzeugmagazin Rechtslauf in Bewegung
ZV  Z 3;            // ISTZÄHLER
U   M 16.0          // FM Gefilterter E 6.6 Strobe und Magazin in Position
U   M 123.1         // Werkzeugmagazin Linkslauf in Bewegung
ZR  Z 3;            // ISTZÄHLER
NETWORK
TITLE = Werkzeugmagazin Positionszähler Überwachung und Bildschirmanzeige
L   Z 3;            // ISTZÄHLER
L   0;
==I;                // VERGLEICH AUF gleich
L   Z#20;
S   Z 3;            // ISTZÄHLER
L   Z 3;            // ISTZÄHLER
L   20;
>I;                // VERGLEICH AUF grösser
L   Z#1;
S   Z 3;            // ISTZÄHLER
U   M 16.0          // FM Gefilterter E 6.6 Strobe und Magazin in Position
U   M 122.0         // Werkzeugmagazin in Bewegung
U   M 155.3         // SOLLZÄHLER mit neuem T-WORT geladen
SPBN M5103
L   Z 3;            // ISTZÄHLER
T   DB20.DBW956     // Bildschirmanzeige
L   Z 3;            // ISTZÄHLER
L   MB 160          // SOLLZÄHLER
==I;                // VERGLEICH AUF GLEICH
SPBN M5103
R   M 155.3         // SOLLZÄHLER mit neuem T-WORT geladen
S   M 155.4         // Neues Werkzeug vorbereitet zum Einschwenken
R   M 155.2         // T-WORT UNGLEICH ISTZÄHLER
M5103:  NOP 0;
NETWORK
TITLE = ISTZÄHLER und SOLLZÄHLER synchronisieren bei Handbetrieb und Bildschirmanzeige
U   M 16.0          // FM Gefilterter E 6.6 Strobe und Magazin in Position
U   M 122.0         // Werkzeugmagazin in Bewegung
UN  M 155.3         // SOLLZÄHLER mit neuem T-WORT geladen
U   M 124.1         // Werkzeugmagazin referenziert
SPBN M5104
L   Z 3;            // ISTZÄHLER
T   MB 160          // SOLLZÄHLER
T   DB20.DBW956     // Bildschirmanzeige
M5104:  NOP 0;
NETWORK
TITLE = Werkzeugmagazin Start/Stopp
U   M 155.3         // SOLLZÄHLER mit neuem T-WORT geladen
UN  DB20.DBX 182.0  // NEUES T-WORT (WERKZEUG)
U   M 124.2         // Freigabe Werkzeugmagazin für Automatikbetrieb
FP  M 124.4         // FM Werkzeugmagazin auf Position fahren
U   M 155.3         // SOLLZÄHLER mit neuem T-WORT geladen
FN  M 124.5         // FM Werkzeugmagazin in Position stoppen
END_FUNCTION
FUNCTION FC 50
NAME: Werkzeugmagazin und Werkzeugköcheransteuerung
BEGIN
; Werkzeugmagazin:
//  X421:9   A 9.5    // Werkzeugköcher zurückschwenken
//  X421:10  A 9.6    // Werkzeugköcher auf Wechselposition schwenken
//  X5:2     E 6.7    // Werkzeugköcher zurückgeschwenkt
//  X5:3     E 7.0    // Werkzeugköcher in Wechselposition
//  X5:5     A 4.4    // Bremse WZW Magazin
//  X420:6   A 10.2   // Rechtslauf WZW-Magazin
//  X420:7   A 10.3   // Linkslauf WZW-Magazin
//  X5:1     E 6.6    // Strobe und Magazin in Position
//  X5:4     E 7.1    // Magazin auf Positition 1
//  DB20.DBX 828.3  // Taste Werkzeugmagazin nach links schwenken
//  DB20.DBX 828.4  // Taste Werkzeugwechseln
//  DB20.DBX 828.5  // Taste Werkzeugmagazin nach rechts schwenken
//  Zum Werkzeugköcher zurückschwenken beide Tasten drücken:
//  DB20.DBX 832.1  // TASTE SPINDEL HALT  (MSTT)
//  DB20.DBX 828.3  // Taste Werkzeugmagazin nach links schwenken
//  Zum Werkzeugköcher auf Wechselposition schwenken beide Tasten drücken:
//  DB20.DBX 832.1  // TASTE SPINDEL HALT  (MSTT)
//  DB20.DBX 828.5  // Taste Werkzeugmagazin nach rechts schwenken
; Werkzeugwechselarm:
//  X5:6     A 4.5    // Bremse Werkzeugwechselarm
NETWORK
TITLE = Dyn NC Interface Taste Werkzeugwechseln
U   DB20.DBX 828.4  // Taste Werkzeugwechseln
FP  M 50.2          // FM Taste Werkzeugwechseln
U   M 50.2          // FM Taste Werkzeugwechseln
U   M 123.0         // Freigabe Werkzeugmagazin für Handbetrieb
U   DB1.DBX 1366.5  // 1= neue ACC-SW die Normalen- und Chaotischenwerkzeugwechsel übernimmt
=   DB20.DBX 349.1  // dyn NC Interface Taste Werkzeugwechseln
NETWORK
TITLE = Bremse von Werkzeugwechslerarm lösen
U   M 50.1          // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
U   DB20.DBX 326.0; // BETRIEBSART JOG
UN  M 2.0           // SUMME aller NOT-AUS Möglichkeiten
U   DB20.DBX 828.4  // Taste Werkzeugwechseln
UN  M 11.0          // HM NOT AUS Test Ersteinschaltung
S   M 46.1          // Notfall Bremse von Werkzeugwechslerarm lösen
S   A 4.5           // Bremse Werkzeugwechselarm
ON  M 50.1          // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
ON  DB20.DBX 326.0; // BETRIEBSART JOG
O   M 2.0           // SUMME aller NOT-AUS Möglichkeiten
O   M 110.0;        // AUX-ON
R   M 46.1          // HM Notfall Bremse von Werkzeugwechslerarm lösen
U   M 46.1          // Notfall Bremse von Werkzeugwechslerarm lösen
FN  M 46.2          // FN Notfall Bremse von Werkzeugwechslerarm lösen
U   M 46.2          // FN Notfall Bremse von Werkzeugwechslerarm lösen
SPBN M5000
R   A 4.5           // Bremse Werkzeugwechselarm
M5000:  NOP 0;
NETWORK
TITLE = Meldung 7078 Werkzeugköcher nicht zurückgeschwenkt
UN  E 6.7           // Werkzeugköcher zurückgeschwenkt
UN  M 152.0;        // WERKZEUGWENDEN AKTIV
UN  A 9.5           // Werkzeugköcher zurückschwenken
S   M 23.1          // HM 7078 Werkzeugköcher nicht zurückgeschwenkt
U   E 6.7           // Werkzeugköcher zurückgeschwenkt
R   M 23.1          // HM 7078 Werkzeugköcher nicht zurückgeschwenkt
NETWORK
TITLE = Meldung 7079 Werkzeugwechselarm nicht in Grundstellung
UN  M 7.2           // Gefilterter E 7.2 Homeposition Werkzeugwechselarm
UN  M 152.0;        // WERKZEUGWENDEN AKTIV
UN  A 10.1          // Schütz Werkzeugwechselarm
S   M 23.2          // HM 7079 Werkzeugwechselarm nicht in Grundstellung
U   M 7.2           // Gefilterter E 7.2 Homeposition Werkzeugwechselarm
R   M 23.2          // HM 7079 Werkzeugwechselarm nicht in Grundstellung
NETWORK
TITLE = Freigabe Werkzeugmagazin für Handbetrieb
UN  M 94.0;         // AUX-ON / MASCHINENTÜR OFFEN  SFG
UN  M 113.0;        // SONDERBETRIEB AKTIV
U(
U   M 7.2           // Gefilterter E 7.2 Homeposition Werkzeugwechselarm
U   T 11            // 500MS verzögert Werkzeugköcher zurückgeschwenkt
UN  E 7.0           // Werkzeugköcher in Wechselposition
O   M 152.0;        // WERKZEUGWENDEN AKTIV
)
UN  DB20.DBX 832.1  // TASTE SPINDEL HALT  (MSTT)
//// Designed by WIMMER:
U(
O   M 50.1          // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
O   M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
)
////
U   DB20.DBX 326.0; // BETRIEBSART JOG
=   M 123.0         // Freigabe Werkzeugmagazin für Handbetrieb
U   M 123.0         // Freigabe Werkzeugmagazin für Handbetrieb
FN  M 127.6         // FM Freigabe Werkzeugmagazin für Handbetrieb
NETWORK
TITLE = Freigabe Werkzeugmagazin für Automatikbetrieb
UN  M 94.0;         // AUX-ON / MASCHINENTÜR OFFEN  SFG
UN  M 113.0;        // SONDERBETRIEB AKTIV
U   M 7.2           // Gefilterter E 7.2 Homeposition Werkzeugwechselarm
U   T 11            // 500MS verzögert Werkzeugköcher zurückgeschwenkt
UN  E 7.0           // Werkzeugköcher in Wechselposition
UN  DB20.DBX 832.1  // TASTE SPINDEL HALT  (MSTT)
;       UN  M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
=   M 124.2         // Freigabe Werkzeugmagazin für Automatikbetrieb
NETWORK
TITLE = Werkzeugmagazin referenziert
U   M 7.1           // Gefilterter E 7.1 Magazin auf Positition 1
U   M 6.6           // Gefilterter E 6.6 Strobe und Magazin in Position
U   E 6.7           // Werkzeugköcher zurückgeschwenkt
UN  M 122.0         // Werkzeugmagazin in Bewegung
S   M 124.1         // Werkzeugmagazin referenziert
U   M 124.1         // Werkzeugmagazin referenziert
UN  DB1.DBX 1370.0; // 1.SPS Schleifendurchlauf
FP  M 124.6         // FM Werkzeugmagazin referenziert
NETWORK
TITLE = Werkzeugmagazinpositionierung abbrechen
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
ON  M 2.0           // SUMME aller NOT-AUS Möglichkeiten
U   M 122.0         // Werkzeugmagazin in Bewegung
S   M 122.1
U   M 122.1
;8(((((((       U   M 6.6           // Gefilterter E 6.6 Strobe und Magazin in Position
SPBN M5001
R   M 155.3         // SOLLZÄHLER mit neuem T-WORT geladen
R   M 127.1         // Warten auf Werkzeugmagazin referenziert
R   M 124.7         // Warten auf neues Wekzeug
L   0;              // LADE 1
T   DB20.DBW956     // Bildschirmanzeige
R   M 122.1
;8((((       SET
R   M 124.1         // Werkzeugmagazin referenziert
M5001:  NOP 0;
NETWORK
TITLE = Überwachung Werkzeugmagazinposition
UN  M 122.0         // Werkzeugmagazin in Bewegung
UN  M 6.6           // Gefilterter E 6.6 Strobe und Magazin in Position
FP  M 122.3         // FM Werkzeugmagazin auf ungültiger Position
U   M 122.3         // FM Werkzeugmagazin auf ungültiger Position
R   M 124.1         // Werkzeugmagazin referenziert
R   M 155.4         // Neues Werkzeug vorbereitet zum Einschwenken
NETWORK
TITLE = FM Taste Werkzeugmagazin nach links schwenken
U   DB20.DBX 828.3  // Taste Werkzeugmagazin nach links schwenken
FP  M 123.2         // FM Taste Werkzeugmagazin nach links schwenken
NETWORK
TITLE = FM Taste Werkzeugmagazin nach rechts schwenken
U   DB20.DBX 828.5  // Taste Werkzeugmagazin nach rechts schwenken
FP  M 123.6         // FM Taste Werkzeugmagazin nach rechts schwenken
NETWORK
TITLE = Werkzeugmagazin nach links schwenken
U   M 123.2         // FM Werkzeugmagazin nach links schwenken
U   M 123.0         // Freigabe Werkzeugmagazin für Handbetrieb
U   T 11            // 500MS verzögert Werkzeugköcher zurückgeschwenkt
O(
U   M 124.4         // FM Werkzeugmagazin auf Position fahren
U   M 158.0         // Werkzeugmagazin linkslauf
UN  M 158.1         // Werkzeugmagazin rechtslauf
U   M 124.2         // Freigabe Werkzeugmagazin für Automatikbetrieb
)
UN  A 9.6           // Werkzeugköcher auf Wechselposition schwenken
UN  A 10.2          // Rechtslauf WZW-Magazin
S   A 4.4           // Bremse WZW Magazin
S   A 10.3          // Linkslauf WZW-Magazin
NETWORK
TITLE = Werkzeugmagazin nach rechts schwenken
U   M 123.6         // FM Taste Werkzeugmagazin nach rechts schwenken
U   M 123.0         // Freigabe Werkzeugmagazin für Handbetrieb
O(
U   M 124.4         // FM Werkzeugmagazin auf Position fahren
U   M 158.1         // Werkzeugmagazin rechtslauf
UN  M 158.0         // Werkzeugmagazin linkslauf
U   M 124.2         // Freigabe Werkzeugmagazin für Automatikbetrieb
)
O(
O   M 124.3         // Referenzieren Werkzeugmagazin nach rechts schwenken
U   M 124.2         // Freigabe Werkzeugmagazin für Automatikbetrieb
)
U   T 11            // 500MS verzögert Werkzeugköcher zurückgeschwenkt
UN  A 9.6           // Werkzeugköcher auf Wechselposition schwenken
UN  A 10.3          // Linkslauf WZW-Magazin
S   A 4.4           // Bremse WZW Magazin
S   A 10.2          // Rechtslauf WZW-Magazin
NETWORK
TITLE = Werkzeugmagazin stoppen
O   DB20.DBX 832.6  // TASTE AUX-ON (MSTT)
O   M 124.3         // Referenzieren Werkzeugmagazin nach rechts schwenken
U   M 16.0          // FM Gefilterter E 6.6 Strobe und Magazin in Position
U   M 7.1           // Gefilterter E 7.1 Magazin auf Positition 1
O(
U   M 16.0          // FM Gefilterter E 6.6 Strobe und Magazin in Position
UN  DB20.DBX 832.6  // TASTE AUX-ON (MSTT)
U   M 123.0         // Freigabe Werkzeugmagazin für Handbetrieb
UN  M 155.3         // SOLLZÄHLER mit neuem T-WORT geladen
)
O   M 124.5         // FM Werkzeugmagazin in Position stoppen
R   M 124.3         // Referenzieren Werkzeugmagazin nach rechts schwenken
R   A 4.4           // Bremse WZW Magazin
R   A 10.3          // Linkslauf WZW-Magazin
R   A 10.2          // Rechtslauf WZW-Magazin
NETWORK
TITLE = Werkzeugmagazin Rechtslauf in Bewegung
U   A 4.4           // Bremse WZW Magazin
U   A 10.2          // Rechtslauf WZW-Magazin
=   M 123.7         // Werkzeugmagazin Rechtslauf in Bewegung
NETWORK
TITLE = Werkzeugmagazin Linkslauf in Bewegung
U   A 4.4           // Bremse WZW Magazin
U   A 10.3          // Linkslauf WZW-Magazin
=   M 123.1         // Werkzeugmagazin Linkslauf in Bewegung
NETWORK
TITLE = Werkzeugmagazin in Bewegung
O   M 123.7         // Werkzeugmagazin Rechtslauf in Bewegung
O   M 123.1         // Werkzeugmagazin Linkslauf in Bewegung
=   M 122.0         // Werkzeugmagazin in Bewegung
NETWORK
TITLE = Nach einschalten und Werkzeugköcher ist zurückgeschwenkt Ventil einschalten
U   E 6.7           // Werkzeugköcher zurückgeschwenkt
UN  DB1.DBX 1370.0; // 1.SPS Schleifendurchlauf
FP  M 123.4         // FM  Werkzeugköcher zurückgeschwenkt 1.SPS Schleifendurchlauf
NETWORK
TITLE = Freigabe Werkzeugköcher mit Tasten schwenken
U   DB20.DBX 832.1  // TASTE SPINDEL HALT  (MSTT)
UN  DB20.DBX 324.0  // PROGRAMM LÄUFT: RÜCKSETZEN MIT PROGRAMM ENDE BZW. RESET.
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
UN  M 122.0         // Werkzeugmagazin in Bewegung
;       UN  M 123.7         // Werkzeugmagazin in Bewegung
U   M 6.6           // Gefilterter E 6.6 Strobe und Magazin in Position
U   M 7.2           // Gefilterter E 7.2 Homeposition Werkzeugwechselarm
U   DB20.DBX 326.0; // BETRIEBSART JOG
=   M 124.0         // Freigabe Werkzeugköcher mit Tasten schwenken
NETWORK
TITLE = Werkzeugköcher zurückschwenken
U   E 6.7           // Werkzeugköcher zurückgeschwenkt
U   E 6.6           // Strobe und Magazin in Position
U   DB1.DBX 1370.0; // 1.SPS Schleifendurchlauf
O(
U   M 123.2         // FM Werkzeugmagazin nach links schwenken
U   M 124.0         // Freigabe Werkzeugköcher mit Tasten schwenken
U   M 7.2           // Gefilterter E 7.2 Homeposition Werkzeugwechselarm
)
O(
UN  M 127.2         // Werkzeugköcher auf Wechselposition schwenken
U   M 152.0;        // WERKZEUGWENDEN AKTIV
U   M 7.2           // Gefilterter E 7.2 Homeposition Werkzeugwechselarm
)
S   A 9.5           // Werkzeugköcher zurückschwenken
R   A 9.6           // Werkzeugköcher auf Wechselposition schwenken
NETWORK
TITLE = Werkzeugköcher auf Wechselposition schwenken
U   E 7.0           // Werkzeugköcher in Wechselposition
U   E 6.6           // Strobe und Magazin in Position
U   DB1.DBX 1370.0; // 1.SPS Schleifendurchlauf
O(
U   M 123.6         // FM Taste Werkzeugmagazin nach rechts schwenken
U   M 124.0         // Freigabe Werkzeugköcher mit Tasten schwenken
)
O(
U   M 127.2         // Werkzeugköcher auf Wechselposition schwenken
U   M 152.0;        // WERKZEUGWENDEN AKTIV
)
UN  A 10.2          // Rechtslauf WZW-Magazin
UN  A 10.3          // Linkslauf WZW-Magazin
S   A 9.6           // Werkzeugköcher auf Wechselposition schwenken
R   A 9.5           // Werkzeugköcher zurückschwenken
NETWORK
TITLE = ACHSFREIGABE/EINLESEFREIGABE WZW
U   M 122.0         // Werkzeugmagazin in Bewegung
UN  DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
U   DB20.DBX 324.4; // REFERENZPUNKT AKTIV
=   M 91.4;         // /Normaler/ ACHSFREIGABE/EINLESEFREIGABE    K0
END_FUNCTION
FUNCTION FC 49
NAME: Werkzeugwender Eingangsfilter
BEGIN
//// Designed by WIMMER:
NETWORK
TITLE = M200.0 Setzen
U   DB20.DBX 833.2  // RAFI Tastatur X2.3/4 SCHLÜSSELSCHALTER ENDLAGENÜBERWACHUNG DEAKTIVIEREN
U   DB20.DBX 831.4  // Taste NC-STOPP VON BEDIENPULT
U   DB20.DBX 831.6  // TASTE VORSCHUB HALT  (MSTT)
U   DB20.DBX 832.1  // TASTE SPINDEL HALT  (MSTT)
U   DB20.DBX 326.3; // BETRIEBSART REPOS
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
FP  M 200.1         // FM WERKZEUGWENDER EINSTELLARBEITEN
U   M 200.1         // FM WERKZEUGWENDER EINSTELLARBEITEN
UN  M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
S   M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
=   M 200.2         // HM WERKZEUGWENDER EINSTELLARBEITEN
U   M 200.1         // FM WERKZEUGWENDER EINSTELLARBEITEN
U   M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
UN  M 200.2         // HM WERKZEUGWENDER EINSTELLARBEITEN
R   M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
////
NETWORK
TITLE = 500MS verzögert Werkzeugköcher zurückgeschwenkt
U   E 6.7           // Werkzeugköcher zurückgeschwenkt
L   S5T#500MS;      // 200MS
SE  T 11            // 500MS verzögert Werkzeugköcher zurückgeschwenkt
NETWORK
TITLE = FM Magazin auf Positition 1
U   E 7.1           // Magazin auf Positition 1
U   M 123.3         // FM Magazin auf Positition 1
S   M 7.1           // Gefilterter E 7.1 Magazin auf Positition 1
U   E 7.1           // Magazin auf Positition 1
UN  DB1.DBX 1370.0; // 1.SPS Schleifendurchlauf
FP  M 123.3         // FM Magazin auf Positition 1
UN  E 7.1           // Magazin auf Positition 1
R   M 7.1           // Gefilterter E 7.1 Magazin auf Positition 1
NETWORK
TITLE = FM Strobe und Magazin in Position
U   E 6.6           // Strobe und Magazin in Position
U   M 123.5         // FM Strobe und Magazin in Position
S   M 6.6           // Gefilterter E 6.6 Strobe und Magazin in Position
U   E 6.6           // Strobe und Magazin in Position
UN  DB1.DBX 1370.0; // 1.SPS Schleifendurchlauf
FP  M 123.5         // FM Strobe und Magazin in Position
UN  E 6.6           // Strobe und Magazin in Position
R   M 6.6           // Gefilterter E 6.6 Strobe und Magazin in Position
U   M 6.6           // Gefilterter E 6.6 Strobe und Magazin in Position
FP  M 16.0          // FM Gefilterter E 6.6 Strobe und Magazin in Position
NETWORK
TITLE = FM Homeposition Werkzeugwechselarm
U   E 7.2           // Homeposition Werkzeugwechselarm
U   M 115.1         // FM Homeposition Werkzeugwechselarm
S   M 7.2           // Gefilterter E 7.2 Homeposition Werkzeugwechselarm
U   E 7.2           // Homeposition Werkzeugwechselarm
UN  DB1.DBX 1370.0; // 1.SPS Schleifendurchlauf
FP  M 115.1         // FM Homeposition Werkzeugwechselarm
UN  E 7.2           // Homeposition Werkzeugwechselarm
R   M 7.2           // Gefilterter E 7.2 Homeposition Werkzeugwechselarm
U   M 7.2           // Gefilterter E 7.2 Homeposition Werkzeugwechselarm
FP  M 16.1          // FM Gefilterter E 7.2 Homeposition Werkzeugwechselarm
NETWORK
TITLE = FM Werkzeugwechselarm in Wechselposition
U   E 7.3           // Werkzeugwechselarm in Wechselposition
U   M 115.2         // FM Werkzeugwechselarm in Wechselposition
S   M 7.3           // Gefilterter E 7.3 Werkzeugwechselarm in Wechselposition
U   E 7.3           // Werkzeugwechselarm in Wechselposition
UN  DB1.DBX 1370.0; // 1.SPS Schleifendurchlauf
FP  M 115.2         // FM Werkzeugwechselarm in Wechselposition
UN  E 7.3           // Werkzeugwechselarm in Wechselposition
R   M 7.3           // Gefilterter E 7.3 Werkzeugwechselarm in Wechselposition
U   M 7.3           // Gefilterter E 7.3 Werkzeugwechselarm in Wechselposition
FP  M 16.2          // FM Gefilterter E 7.3 Werkzeugwechselarm in Wechselposition
END_FUNCTION
FUNCTION FC 55
NAME: WERKZEUGWENDER EINSTELLARBEITEN
BEGIN
NETWORK
TITLE = Reset Meldung 7077 Werkzeugwender einstellen
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   M 23.0          // HM 7077 Meldung Werkzeugwender einstellen
//// Designed by WIMMER
NETWORK
TITLE = SFG und NC-STARTVERRIEGELUNG
O   M 23.1          // HM 7078 Werkzeugköcher nicht zurückgeschwenkt
O   M 23.2          // HM 7079 Werkzeugwechselarm nicht in Grundstellung
UN  M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
=   M 94.7          // SFG Werkzeugwender nicht in Grundstellung
=   M 97.0;         // NC-STARTVERRIEGELUNG
////
NETWORK
TITLE = Werkzeugköcher zurückschwenken
U   DB20.DBX 832.3  // TASTE SPINDEL START (MSTT)
U   E 7.2           // Homeposition Werkzeugwechselarm
S   M 115.6         // HM Werkzeugköcher zurückschwenken
UN  DB20.DBX 295.4  // CTRL 7
UN  DB20.DBX 832.3  // TASTE SPINDEL START (MSTT)
R   M 115.6         // HM Werkzeugköcher zurückschwenken
U   DB20.DBX 295.4  // CTRL 7
UN  M 115.6         // HM Werkzeugköcher zurückschwenken
//// WB UN  DB20.DBX 324.0  // PROGRAMM LÄUFT: RÜCKSETZEN MIT PROGRAMM ENDE BZW. RESET.
S   A 9.5           // Werkzeugköcher zurückschwenken
R   A 9.6           // Werkzeugköcher auf Wechselposition schwenken
NETWORK
TITLE = Werkzeugköcher auf Wechselposition schwenken
U   DB20.DBX 832.6  // TASTE AUX-ON (MSTT)
S   M 115.7         // HM Werkzeugköcher auf Wechselposition schwenken
UN  DB1.DBX 1374.4; // TASTE AUX-ON (PC)
UN  DB20.DBX 832.6  // TASTE AUX-ON (MSTT)
R   M 115.7         // HM Werkzeugköcher auf Wechselposition schwenken
U   DB1.DBX 1374.4  // CTRL 8
//// WB UN  DB20.DBX 324.0  // PROGRAMM LÄUFT: RÜCKSETZEN MIT PROGRAMM ENDE BZW. RESET.
UN  M 115.7         // HM Werkzeugköcher auf Wechselposition schwenken
U   E 7.2           // Homeposition Werkzeugwechselarm
S   A 9.6           // Werkzeugköcher auf Wechselposition schwenken
R   A 9.5           // Werkzeugköcher zurückschwenken
NETWORK
TITLE = Bremse Werkzeugwechselarm
U   DB20.DBX 829.6  // TASTE SCHRAUBSTOCK ZU
S   M 116.0         // HM Werkzeugwechselarmschütz einschalten
UN  DB20.DBX 829.6  // TASTE SCHRAUBSTOCK ZU
UN  DB20.DBX 294.3  // CTRL 0  // Der Werkzeugwechselarmschütz wird eingeschaltet
R   M 116.0         // HM Werkzeugwechselarmschütz einschalten
U   DB20.DBX 294.3  // CTRL 0  // Der Werkzeugwechselarmschütz wird eingeschaltet
//// WB UN  DB20.DBX 324.0  // PROGRAMM LÄUFT: RÜCKSETZEN MIT PROGRAMM ENDE BZW. RESET.
UN  M 116.0         // HM Werkzeugwechselarmschütz einschalten
SPB M5501;
U   M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
=   A 4.5           // Bremse Werkzeugwechselarm
M5501:  NOP 0;
NETWORK
TITLE = Schütz Werkzeugwechselarm
//  X5:5     E 7.2    //*?  Homeposition Werkzeugwechselarm
//  X5:6     E 7.3    //*?  Werkzeugwechselarm in Wechselposition
U   DB20.DBX 294.3  // CTRL 0  // Der Werkzeugwechselarmschütz wird eingeschaltet
//// WB UN   DB20.DBX 324.0 // PROGRAMM LÄUFT: RÜCKSETZEN MIT PROGRAMM ENDE BZW. RESET.
UN  M 116.0         // HM Werkzeugwechselarmschütz einschalten
//// WB U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
FP  M 115.0
U   M 115.0
S   A 4.5           // Bremse Werkzeugwechselarm
S   A 10.1          // Schütz Werkzeugwechselarm
O   M 16.2          // FM Gefilterter E 7.3 Werkzeugwechselarm in Wechselposition
O   M 16.1          // FM Gefilterter E 7.2 Homeposition Werkzeugwechselarm
U   DB20.DBX 294.3  // CTRL 0  // Der Werkzeugwechselarmschütz wird eingeschaltet
UN  M 116.0         // HM Werkzeugwechselarmschütz einschalten
R   A 4.5           // Bremse Werkzeugwechselarm
R   A 10.1          // Schütz Werkzeugwechselarm
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
ON  M 2.0           // SUMME aller NOT-AUS Möglichkeiten
ON  DB20.DBX 294.3  // CTRL 0  // Der Werkzeugwechselarmschütz wird eingeschaltet
R   A 10.1          // Schütz Werkzeugwechselarm
NETWORK
TITLE = OFFSET VON HAUPTANTRIEB ANSCHAUEN
U   DB20.DBX 198.1; // M49 DYN.
S   M 116.1         // OFFSET VON HAUPTANTRIEB ANSCHAUEN
U   DB20.DBX 198.2; // M50 DYN.
R   M 116.1         // OFFSET VON HAUPTANTRIEB ANSCHAUEN
NETWORK
TITLE = Spindel in Rundachsbetrieb schalten
U   DB20.DBX 198.3; // M51 DYN.
SPBN M5502
SET;
S   DB25.DBX 3.5;   // Umschaltung von Spindel auf Rundachsbetrieb oder umgekehrt
S   DB25.DBX 823.0; // 1=Rundachsbetrieb 0=Spindelbetrieb
L   0
T   DB25.DBB 822;   // Spindelnummer welche umgeschaltet wird
S   DB25.DBX 340.3; // EINLESEFREIGABE K1
S   DB25.DBX 340.4; // ACHSFREIGABE    K1
S   M 115.4;        // SPINDELFREIGABE Rundachsbetrieb K0
M5502:  NOP 0;
NETWORK
TITLE = Spindel in Spindelbetrieb schalten
U   DB20.DBX 198.4; // M52 DYN. Achtung M-Befehle ändern
SPBN M5503
R   DB25.DBX 340.3; // EINLESEFREIGABE K1
R   DB25.DBX 340.4; // ACHSFREIGABE    K1
R   M 115.4;        // SPINDELFREIGABE Rundachsbetrieb K0
S   DB25.DBX 3.5;   // Umschaltung von Spindel auf Rundachsbetrieb oder umgekehrt
R   DB25.DBX 823.0; // 1=Rundachsbetrieb 0=Spindelbetrieb
L   0
T   DB20.DBB 822    // UMSCHALTUNG SPINDEL0
T   DB25.DBB 822    // UMSCHALTUNG SPINDEL0
R   M 115.3         // Nachführbetrieb der Haupspindel vorbereiten
M5503:  NOP 0
NETWORK
TITLE = SPINDEL REFERENZIEREN UND AUF 90 GRAD EINSTELLEN
U   DB20.DBX 198.5; // M53 DYN. & H-WORT
U   DB20.DBX 182.1  // Neues H-Wort für Spindel0
SPBN M5504
=   DB25.DBX 1.4;   // NC-SATZ ABSCHICKEN
=   DB25.DBX 548.5; // GENAUHALTMODUS SETZEN
=   DB25.DBX 549.0; // G1 WIRD AUSGELÖST
=   DB25.DBX 549.4; // VORSCHUB IN GRAD/S WERT IN DBD556
L   +2.88E+03;
T   DB25.DBD 556;   // F-WERT IN M/S ODER M/U
S   DB25.DBX 21.4;  // POSITIONSANFORDERUNG FÜR KANALACHSE C
L   DB20.DBD 606    // Daten für H-Code als Float Wert
T   DB25.DBD 70;    // POSITIONSWERT FÜR KANALACHSE C
S   M 115.3         // Nachführbetrieb der Haupspindel vorbereiten
M5504:  NOP 0;
NETWORK
TITLE = Nachführbetrieb der Haupspindel
U   M 115.3         // Nachführbetrieb der Haupspindel vorbereiten
U   M 12.1          // Tür offen
=   DB1.DBX 1734.3  // Deaktivierung absolute Achse 3 (Hauptspindel)
//      ACHSPOSITIONEN INS MILL250_ACC.MSD EINTRAGEN
//  CTRL 1   DB20.DBX 294.5  POSITION FÜR Y-ACHSE ÜBERNEHMEN
//  CTRL 2   DB20.DBX 294.6  POSITION FÜR Z-ACHSE ÜBERNEHMEN
//  CTRL 3   DB20.DBX 294.1  Hauptspindelposition übernehmen
NETWORK
TITLE = POSITION FÜR Y-ACHSE ÜBERNEHMEN
U   DB20.DBX 294.5  // TASTE "CTRL 1" POSITION FÜR Y-ACHSE ÜBERNEHMEN
FP  M 116.4         // FM TASTE "CTRL 1" POSITION FÜR Y-ACHSE ÜBERNEHMEN
U   M 116.4         // FM TASTE "CTRL 1" POSITION FÜR Y-ACHSE ÜBERNEHMEN
SPBN M5505
L   DB1.DBD 4       // VON DER ABSOLUTEN ACHSE 4 DIE ISTPOSITION LADEN
T   DB10.DBD 32     // ISTPOSITION IN DIE MSD-DATEN SCHREIBEN
M5505:  NOP 0;
NETWORK
TITLE = POSITION FÜR FÜR Z-ACHSE ÜBERNEHMEN
U   DB20.DBX 831.1  // TASTE KÜHLMITTEL (MSTT)
S   M 115.5         // HM POSITION FÜR FÜR Z-ACHSE ÜBERNEHMEN
UN  DB20.DBX 294.6; // TASTE KÜHLMITTEL (PC)
UN  DB20.DBX 831.1  // TASTE KÜHLMITTEL (MSTT)
R   M 115.5         // HM POSITION FÜR FÜR Z-ACHSE ÜBERNEHMEN
U   DB20.DBX 294.6  // TASTE "CTRL 2" POSITION FÜR Z-ACHSE ÜBERNEHMEN
FP  M 116.3         // FM TASTE "CTRL 2" POSITION FÜR Z-ACHSE ÜBERNEHMEN
U   M 116.3         // FM TASTE "CTRL 2" POSITION FÜR Z-ACHSE ÜBERNEHMEN
UN  M 115.5         // HM POSITION FÜR FÜR Z-ACHSE ÜBERNEHMEN
SPBN M5506
L   DB1.DBD 8       // VON DER ABSOLUTEN ACHSE 2 DIE ISTPOSITION LADEN
T   DB10.DBD 36     // ISTPOSITION IN DIE MSD-DATEN SCHREIBEN
M5506:  NOP 0;
NETWORK
TITLE = Hauptspindelposition übernehmen
U   DB20.DBX 294.1  // TASTE "CTRL 3" Hauptspindelposition übernehmen
FP  M 116.2         // FM TASTE "CTRL 3" Hauptspindelposition übernehmen
U   M 116.2         // FM TASTE "CTRL 3" Hauptspindelposition übernehmen
U   M 80.5;         // RUNDACHSBETRIEB
=   DB20.DBX 5.2    // Anforderung Kanalachse 12 (C-Achse) zum Refpkt.schreiben
SPBN M5509
L   12
T   DB20.DBW 964    // Kanalachse 12 C-Achse zum Refpkt. schreiben
L   DB1.DBD 12      // VON DER ABSOLUTEN ACHSE 3 DIE ISTPOSITION LADEN (Rundachse in 1000 Grad 360°=0.36°)
L   0
<R
SPBN M5507
L   DB1.DBD 12      // VON DER ABSOLUTEN ACHSE 3 DIE ISTPOSITION LADEN
L   -1.8E-01        // -0,180°
>=R
SPBN M5508
L   DB1.DBD 12      // VON DER ABSOLUTEN ACHSE 3 DIE ISTPOSITION LADEN
L   -1.0E+00;       // MULTIPLIZIEREN MIT -1
*R;
T   DB20.DBD 966    // Referenzpunkt IN DIE MSD-DATEN SCHREIBEN
SPA  M5509
M5507:  NOP 0;
SET
L   DB1.DBD 12      // VON DER ABSOLUTEN ACHSE 3 DIE ISTPOSITION LADEN
L   1.8E-01         // 0,180°
<=R
SPBN M5508
L   DB1.DBD 12      // VON DER ABSOLUTEN ACHSE 3 DIE ISTPOSITION LADEN
L   -1.0E+00;       // MULTIPLIZIEREN MIT -1
*R;
T   DB20.DBD 966    // Referenzpunkt IN DIE MSD-DATEN SCHREIBEN
SPA  M5509
M5508:  NOP 0;
SET
=   M 23.4          // HM 7081 Geht mit weniger Grad!
M5509:  NOP 0;
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
=   M 23.4          // HM 7081 Geht mit weniger Grad!
END_FUNCTION
FUNCTION FC 59 : VOID
NAME: Werkzeugspannsystem
BEGIN
NETWORK
TITLE = 6067  KEINE DRUCKLUFT
UN  E 5.6           // Druckschalter Luftversorgung
U   M 110.0;        // AUX-ON
L   S5T#1000MS;     // 1S
SE  T 39;           // STARTEN T 39 ALS EINSCHALTVERZ.
U   T 39;           // STARTEN T 39 ALS EINSCHALTVERZ.
S   DB2.DBX 8.3;    // 6067  KEINE DRUCKLUFT
//  X5:7     A 4.6 // Klemmung Werkzeug öffnen
//  X5:8     A 4.7 // Klemmung Werkzeug schließen
//  DB20.DBX 833.6  RAFI Tastatur X5.1/2 Werkzeugspann/entspanntaste
NETWORK
TITLE = ALARM 6067  KEINE DRUCKLUFT QUITTIEREN
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
U   E 5.6           // Druckschalter Luftversorgung
R   DB2.DBX 8.3;    // 6067  KEINE DRUCKLUFT
//// Designed by WIMMER:
NETWORK
TITLE = M200.3 SPANNEN BEI TUER ZU
U   DB20.DBX 833.2  // RAFI Tastatur X2.3/4 SCHLÜSSELSCHALTER ENDLAGENÜBERWACHUNG DEAKTIVIEREN
U   DB20.DBX 831.4  // Taste NC-STOPP VON BEDIENPULT
U   DB20.DBX 831.6  // TASTE VORSCHUB HALT  (MSTT)
U   DB20.DBX 832.1  // TASTE SPINDEL HALT  (MSTT)
U   DB20.DBX 327.2; // BETRIEBSART REFERENZ
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
FP  M 200.4         // FM SPANNEN BEI TUER ZU
U   M 200.4         // FM SPANNEN BEI TUER ZU
UN  M 200.3         // SPANNEN BEI TUER ZU
S   M 200.3         // SPANNEN BEI TUER ZU
=   M 200.5         // HM SPANNEN BEI TUER ZU
U   M 200.4         // FM SPANNEN BEI TUER ZU
U   M 200.3         // SPANNEN BEI TUER ZU
UN  M 200.5         // HM SPANNEN BEI TUER ZU
R   M 200.3         // HM SPANNEN BEI TUER ZU
////
NETWORK
TITLE = Freigabe Werkzeugklemmung
U   M 2.0           // SUMME aller NOT-AUS Möglichkeiten
U   M 110.0;        // AUX-ON
U   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
//// Designed by WIMMER:
U(
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
U   M 12.1          // Tür offen
O   M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
O   M 200.3         // SPANNEN BEI TUER ZU
)
////
=   M 70.0          // Freigabe Werkzeugklemmung Handbetrieb
SET
=   A 4.7           // Klemmung Werkzeug schließen
U   DB20.DBX 833.6  // RAFI Tastatur Werkzeugspann/entspanntaste
U   M 70.0          // Freigabe Werkzeugklemmung
U   DB20.DBX 326.0; // BETRIEBSART JOG
O   M 128.0         // Werkzeugspannsystem öffnen
O(
O   M 150.4;        // SCHRITT 4
O   M 150.5;        // SCHRITT 5
U   M 127.3         // HM 7021  WERKZEUGWENDER FREIFAHREN
)
=   A 4.6           // Klemmung Werkzeug öffnen
R   A 4.7           // Klemmung Werkzeug schließen
NETWORK
TITLE = Auswertung Werkzeugklemmung mit Analogbero
L   DB1.DBW 1572    // Analogwert von USB-SPS Werkzeugspannbero
L   DB10.DBW 44;    // SPS-MSD  Offsetwert von Werkzeugspannanalogbero
+I
T   MW 74           // Analogwert mit Offset von USB-SPS Werkzeugspannbero
L   0
T   MB 71
L   MW 74           // Analogwert mit Offset von USB-SPS Werkzeugspannbero
L   220
<=I                 //
=   M 71.0          // Werkzeug sicher ausgestossen ( <= 220 )
SPB   M5901
L   MW 74           // Analogwert mit Offset von USB-SPS Werkzeugspannbero
L   300
<=I                 //
=   M 71.1          // Werkzeugfehler ( 220 <= 300 )
SPB   M5901
L   MW 74           // Analogwert mit Offset von USB-SPS Werkzeugspannbero
L   450
<=I                 //
=   M 71.2          // Werkzeug sicher eingespannt ( 300 <= 450 )
SPB   M5901
SET
=   M 71.3          // Kein Werkzeug eingespannt ( > 450 )
M5901: NOP 0
NETWORK
TITLE = Dyn NC Interface Werkzeugspindel belegt
O   M 71.2          // Werkzeug sicher eingespannt ( 300 <= 450 )
O   DB10.DBX 1.0;   // SPS-MSD DAUERLAUF-BIT
L   S5TIME#300MS
SE  T 53
O   M 152.0;        // WERKZEUGWENDEN AKTIV
ON  M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
SPB    M5902
U   T 53
=   DB20.DBX 344.0  // dyn NC Interface Werkzeugspindel belegt
M5902: NOP 0
NETWORK
TITLE = Werkzeug von Hand ausspannen
U   M 71.0          // Werkzeug sicher ausgestossen ( <= 220 )
FP  M 73.0          // FM Werkzeug sicher ausgestossen ( <= 220 )
U   DB20.DBX 833.6  // RAFI Tastatur Werkzeugspann/entspanntaste
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
U   M 12.1          // Tür offen
U   M 73.0          // FM Werkzeug sicher ausgestossen ( <= 220 )
U   A 4.6           // Klemmung Werkzeug öffnen
S   M 72.3          // Werkzeug mit Werkzeugspann/entspanntaste sicher ausgestossen
NETWORK
TITLE = Werkzeug von Hand eingespannt
U   M 72.3          // Werkzeug mit Werkzeugspann/entspanntaste sicher ausgestossen
U   M 71.2          // Werkzeug sicher eingespannt ( 300 <= 450 )
U   A 4.7           // Klemmung Werkzeug schließen
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
U   M 12.1          // Tür offen
UN  M 152.0;        // WERKZEUGWENDEN AKTIV
S   M 72.0          // Werkzeug manuell eingespannt
U   M 72.0          // Werkzeug manuell eingespannt
L   S5TIME#300MS
SV  T 0             // IMPULS (VERLÄNGERT)
NETWORK
TITLE =  Kein Werkzeug von Hand eingespannt folglich keine WKZ-Nummer eingabe nötig
U   M 72.3          // Werkzeug mit Werkzeugspann/entspanntaste sicher ausgestossen
U   M 71.3          // Kein Werkzeug eingespannt ( > 450 )
U   A 4.7           // Klemmung Werkzeug schließen
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
U   M 12.1          // Tür offen
UN  M 152.0;        // WERKZEUGWENDEN AKTIV
U   T 0             // IMPULS (VERLÄNGERT)
S   M 72.4          // Kein Werkzeug von Hand eingespannt
R   M 72.0          // Werkzeug manuell eingespannt
U   M 72.4          // Kein Werkzeug von Hand eingespannt
FP  M 72.5          // FM Kein Werkzeug von Hand eingespannt
UN  T 0             // IMPULS (VERLÄNGERT)
U   M 72.0          // Werkzeug manuell eingespannt
S   M 72.6          // Werkzeug sicher manuell eingespannt
U   M 72.6          // Werkzeug sicher manuell eingespannt
FP  M 72.1          // FM Werkzeug manuell getauscht oder entfernt
O   M 72.1          // FM Werkzeug manuell getauscht oder entfernt
O   M 16.0          // FM Gefilterter E 6.6 Strobe und Magazin in Position
U   M 124.1         // Werkzeugmagazin referenziert
;       U   M 72.0          // Werkzeug manuell eingespannt
U   M 72.6          // Werkzeug sicher manuell eingespannt
UN  DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
SPBN   M5903
SET
R   M 72.3          // Werkzeug mit Werkzeugspann/entspanntaste sicher ausgestossen
R   M 72.0          // Werkzeug manuell eingespannt
L   Z 3             // Istzähler Werkzeugmagazin
T   MB 44           // Aktuelles Werkzeug in der Spindel
S   DB20.DBX 5.0    // Werkzeugnummer in Spindel laden
L   Z 3             // Istzähler Werkzeugmagazin
T   DB20.DBW 958    // die neue Werkzeugnummer bei chaotischer WZ-Beladung
M5903: NOP 0
U   M 72.5          // FM Kein Werkzeug von Hand eingespannt
UN  DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
SPBN   M5904
SET
R   M 72.3          // Werkzeug mit Werkzeugspann/entspanntaste sicher ausgestossen
R   M 72.4          // Kein Werkzeug von Hand eingespannt
L   0               // 0
T   MB 44           // Aktuelles Werkzeug in der Spindel
S   DB20.DBX 5.0    // Werkzeugnummer in Spindel laden
L 0
T   DB20.DBW 958    // die neue Werkzeugnummer bei chaotischer WZ-Beladung
M5904: NOP 0
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUGELÖST
R   M 72.0          // Werkzeug manuell eingespannt
R   M 72.3          // Werkzeug mit Werkzeugspann/entspanntaste sicher ausgestossen
R   M 72.4          // Kein Werkzeug von Hand eingespannt
R   M 72.6          // Werkzeug sicher manuell eingespannt
NETWORK
TITLE = MELDUNG 7083 Magazinieren aktiv
U   M 72.6          // Werkzeug sicher manuell eingespannt
=   M 23.6          // HM 7083 MELDUNG 7083 Magazinieren aktiv
NETWORK
TITLE = Meldung 7080 Werkzeug nicht richtig eingespannt!
U   A 4.7           // Klemmung Werkzeug schließen
U   M 71.1          // Werkzeugfehler ( 220 <= 300 )
=   M 23.3          // HM 7080 Werkzeug nicht richtig eingespannt!
NETWORK
TITLE = SPINDELFREIGABE von Werkzeug eingespannt
O   M 71.1          // Werkzeugfehler ( 220 <= 300 )
O   M 71.0          // Werkzeug sicher ausgestossen ( <= 220 )
=   M 94.2;         // (SFG)Werkzeug ausgestossen
NETWORK
TITLE = Werkzeug manuell getauscht oder entfernt
U   M 73.0          // FM Werkzeug sicher ausgestossen ( <= 220 )
U   M 72.3          // Werkzeug mit Werkzeugspann/entspanntaste sicher ausgestossen
U   DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
SPBN   M5905
SET
R   M 72.3          // Werkzeug mit Werkzeugspann/entspanntaste sicher ausgestossen
S   DB20.DBX 5.0    // Werkzeugnummer in Spindel laden
L 0
T   DB20.DBW 958    // die neue Werkzeugnummer bei chaotischer WZ-Beladung
M5905: NOP 0
END_FUNCTION
FUNCTION FC 11 : VOID
NAME: Neues Sicherheitskonzept
BEGIN
NETWORK
TITLE = NOT AUS von USB-Tastatur und USB-SPS
//  E 2.2          // NICHT NOT-AUS
//  E 5.0          // Überwachung NOT AUS-Relais K1 auf Sicherheitsmodul
//  E 2.5          // NOT-AUS Extern (Wartungstür)
// USB-SPS:
//  E 2.3   //*? NOT-AUS von Pic    // Nicht NOT AUS von USBSPS X 2.6
//  DB1.DBX 1578.5 // NOT-AUS Relais auf USBSPS eingeschaltet
UN  E 2.5           // NOT-AUS Extern (Wartungstür)
U   E 2.2           // NICHT NOT-AUS von Bedienpult
U   E 2.3           //*? NOT-AUS von Pic Nicht NOT AUS von USBSPS X 2.6
U   DB1.DBX 1578.5  // NOT-AUS Relais auf USBSPS eingeschaltet
UN  E 5.0           // Überwachung NOT AUS-Relais K1 auf Sicherheitsmodul
=   M 2.0           // SUMME aller NOT-AUS Möglichkeiten
NETWORK
TITLE = Warten auf USBSPS
//   DB1.DBB 1558 = 255   USB SPS nicht aktiv
//   DB1.DBB 1558 = 0     Hexschalter auf USBSPS auf 0 = Ueberwachungsschaltung aktiv
//   DB1.DBB 1558 = 14    Hexschalter auf USBSPS auf E = Ueberwachungsschaltung inaktiv
//   DB1.DBX 1578.5       NOT-AUS Relais auf USBSPS eingeschaltet
//   Ausgaenge nur lesen:
//   DB1.DBW 1578 Abbild von X2 auf USBSPS Ausgaenge
//   DB1.DBW 1580 Abbild von X3 auf USBSPS Ausgaenge
//   Ausgaenge setzen:
//   DB1.DBW 1586 = X2 USBSPS Ausgaenge (X2.7-9 werden nicht von der USBSPS kontolliert)
//   DB1.DBW 1588 = X3 USBSPS Ausgaenge (X3.9 wird nicht von der USBSPS kontolliert)
// Überprüfen auf gültige USBSPS-Hexschaltereinstellung
;       U   M 11.7          // USBSPS auf Ueberwachungsbetrieb eingestellt
;    SPB  M001
L   DB1.DBB 1558    // Stellung des HEX-Schalters auf USBSPS
L   255
==I
=   M 12.2          // USBSPS sendet 255 (nach Neustart)
SPB  M001
L   DB1.DBB 1558    // Stellung des HEX-Schalters auf USBSPS
L   0               // Stellung 2 MILLC40 Sicherheitsüberwachung aktiv
==I;                // VERGLEICH AUF GLEICH
S   M 11.7          // USBSPS auf Ueberwachungsbetrieb eingestellt
SPB  M001
L   DB1.DBB 1558    // Stellung des HEX-Schalters auf USBSPS
L   14              // HEX-Schalter auf E
==I;                // VERGLEICH AUF GLEICH
;       =   M          // USBSPS auf nicht Ueberwachungsbetrieb eingestellt
M001:NOP 0;
//  Warten auf NOT-AUS Relais USBSPS eingeschaltet
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
S   M 11.5          // Warten auf USBSPS nicht NOT-AUS
U   M 11.5          // Warten auf USBSPS nicht NOT-AUS
U   DB1.DBX 1578.5  // NOT-AUS Relais auf USBSPS eingeschaltet
R   M 11.5          // Warten auf USBSPS nicht NOT-AUS
FP  M 11.6          // FM 1.SPS SCHLEIFENDURCHLAUF und NOT-AUS Relais auf USBSPS eingeschaltet
NETWORK
TITLE = NOT-AUS Überlistungsschutz
//  X421:4   A 9.1 //* NOT AUS für NE Modul Klemme 64
U   M 2.0           // SUMME aller NOT-AUS Möglichkeiten
U   M 110.0;        // AUX-ON
U   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
S   A 9.1           //* NOT AUS für NE Modul Klemme 64
ON  M 2.0           // SUMME aller NOT-AUS Möglichkeiten
O   DB1.DBX 1740.0  // Schleppfehlerüberwachung ACC
O   DB1.DBX 1740.1  // Schleppfehlerüberwachung ACC
O   DB1.DBX 1740.2  // Schleppfehlerüberwachung ACC
R   A 9.1           //* NOT AUS für NE Modul Klemme 64
// NOT AUS TEST:
//  X6:3  E 2.2            // NICHT NOT-AUS
//  X5:9  E 5.0            // Überwachung NOT AUS-Relais K1 auf Sicherheitsmodul
U   M 11.6          // FM 1.SPS SCHLEIFENDURCHLAUF und NOT-AUS Relais auf USBSPS eingeschaltet
S   M 11.0          // HM NOT AUS Test Ersteinschaltung
U   M 11.6          // FM 1.SPS SCHLEIFENDURCHLAUF und NOT-AUS Relais auf USBSPS eingeschaltet
UN  DB10.DBX 75.0;  // SPS-MSD /TÜRAUTOMATIK
S   M 11.4          // HM Tür auf/zu-Test Ersteinschaltung
U   M 11.1          // FM Test NOT-AUS Schalter und Relais
R   M 11.2
R   M 11.3
NETWORK
TITLE = Test NOT-AUS Schalter und Relais
U   M 11.6          // FM 1.SPS SCHLEIFENDURCHLAUF und NOT-AUS Relais auf USBSPS eingeschaltet
U   E 2.2           // NICHT NOT-AUS
UN  E 5.0           // Überwachung NOT AUS-Relais K1 auf Sicherheitsmodul
S   M 11.2
U   M 11.6          // FM 1.SPS SCHLEIFENDURCHLAUF und NOT-AUS Relais auf USBSPS eingeschaltet
UN  E 2.2           // NICHT NOT-AUS
U   E 5.0           // Überwachung NOT AUS-Relais K1 auf Sicherheitsmodul
S   M 11.3
UN  E 2.2           // NICHT NOT-AUS
U   E 5.0           // Überwachung NOT AUS-Relais K1 auf Sicherheitsmodul
U   M 11.2
O(
U   E 2.2           // NICHT NOT-AUS
UN  E 5.0           // Überwachung NOT AUS-Relais K1 auf Sicherheitsmodul
U   M 11.3
)
FP  M 11.1          // FM Test NOT-AUS Schalter und Relais
NETWORK
TITLE = 6000 ALARM NOT-AUS
;       UN  E 2.2           // NICHT NOT-AUS
UN  M 2.0           // SUMME aller NOT-AUS Möglichkeiten
UN  M 11.5          // Warten auf USBSPS nicht NOT-AUS
O   M 11.0          // HM NOT AUS Test Ersteinschaltung
S   DB2.DBX 0.0;    // 6000  NOT-AUS
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
UN  M 11.2
UN  M 11.3
R   DB2.DBX 0.0;    // 6000 ALARM NOT-AUS
R   M 11.0          // HM NOT AUS Test Ersteinschaltung
NETWORK
TITLE = Alarm 6006 Überwachung NOT AUS-Relais K1 auf Sicherheitsmodul
U   E 2.2           // NICHT NOT-AUS
U   E 5.0           // Überwachung NOT AUS-Relais K1 auf Sicherheitsmodul
O(
UN  E 2.2           // NICHT NOT-AUS
UN  E 5.0           // Überwachung NOT AUS-Relais K1 auf Sicherheitsmodul
)
L   S5T#500MS;      // 200MS
SE  T 80;           // STARTEN  ALS EINSCHALTVERZ.
U   T 80;           // STARTEN  ALS EINSCHALTVERZ.
S   DB2.DBX 0.6;    // A 6006  NOT AUS-Relais K1 auf Sicherheitsmodul nicht Abgefallen
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 0.6;    // A 6006  NOT AUS-Relais K1 auf Sicherheitsmodul nicht Abgefallen
// TÜR auf/zu TEST:
//  Türsicherheitsschalter:
//  X6:1     E 2.0 // TÜR geschlossen von Türsicherheitsverriegelung ÜK
//  X6:2     E 2.1 //* Tür verriegelt von Verriegelungsmagnet SK
//  X421:6   A 9.2 //* Verriegelunsmagnet Maschinentür entriegeln
//  Sicherheitsmodul:
//  X5:1     A 4.1 //* Antriebsfreigaberelais K5 ein auf Sicherheitsmodul Y4A135000
//  X5:2     A 4.0 // Türfreigaberelais K3 ein auf Sicherheitsmodul Y4A135000M
//  X5:10    E 5.1 // Überwachung von K2 und K3 auf Sicherheitsmodul Y4A135000
//  X5:11    E 5.2 // Überwachung von K4 und K5 auf Sicherheitsmodul Y4A135000
//  DB20.DBX 834.0   = M 51.2   // RAFI Tastatur X6.1/2 Zustimmtaste
//  X422:4   E 5.5   // Bremse öffnen von Z-Achsreglerbaugruppe 02.B
//  M 12.0    // Tür zu und Verriegelt
//  M 12.1    // Tür offen
U   E 2.0;          // TÜR geschlossen von Türsicherheitsverriegelung ÜK
U(
U   DB10.DBX 75.0;  // SPS-MSD /TÜRAUTOMATIK
U   A 6.3 ;         // LEISTUNG TÜR ZU
O   E 2.1;          //* Tür verriegelt von Verriegelungsmagnet SK
)
=   M 12.0          // Tür zu und Verriegelt
UN  M 12.0          // Tür zu und Verriegelt
=   M 12.1          // Tür offen
U   M 11.6          // FM 1.SPS SCHLEIFENDURCHLAUF und NOT-AUS Relais auf USBSPS eingeschaltet
U   M 12.1          // Tür offen
S   M 12.4          // HM Tür offen
U   M 11.6          // FM 1.SPS SCHLEIFENDURCHLAUF und NOT-AUS Relais auf USBSPS eingeschaltet
U   M 12.0          // Tür zu und Verriegelt
S   M 12.5          // HM Tür zu und Verriegelt
U   M 12.4          // HM Tür offen
U   M 12.0          // Tür zu und Verriegelt
O(
U   M 12.5          // HM Tür zu und Verriegelt
U   M 12.1          // Tür offen
)
FP  M 12.3          // FM Tür auf/zu
NETWORK
TITLE = FREIGABE ZUM TÜRENTRIEGELN
U(
O   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
ON  M 145.7         // HM REGLERFREIGABE FÜR HAUPTANTRIEB
)
U(
UN  M 152.0;        // WERKZEUGWECHSEL AKTIV
O   M 150.0         // Werkzeugwenden abgebrochen
)
U(
ON  DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
ON  DB20.DBX 324.0; // PROGRAMM LÄUFT: RÜCKSETZEN MIT PROGRAMM ENDE BZW. RESET.
O   DB20.DBX 324.1; // STOP-ZUSTAND
O   DB20.DBX 325.1; // STOP-ZUSTAND DURCH SINGLE
)
U(
ON  DB20.DBX 326.2; // BETRIEBSART MDI
ON  DB20.DBX 324.0; // PROGRAMM LÄUFT: RÜCKSETZEN MIT PROGRAMM ENDE BZW. RESET.
O   DB20.DBX 324.1; // STOP-ZUSTAND
O   DB20.DBX 325.1; // STOP-ZUSTAND DURCH SINGLE
)
UN  M 131.7;        // ACHSEN IN BEWEGUNG
UN  A 0.3           // Hauptantrieb Reglerfreigabe
UN  M 122.0         // Werkzeugmagazin in Bewegung
=   M 55.7;         // FREIGABE ZUM TÜRENTRIEGELN
NETWORK
TITLE = Verriegelunsmagnet Maschinentür entriegeln
O   M 51.2;         // ZUSTIMMTASTEN GEDRÜCKT
ON  E 2.0;          // TÜR geschlossen von Türsicherheitsverriegelung ÜK
UN  DB2.DBX 0.0;    // 6000  NOT-AUS
U   M 55.7;         // FREIGABE ZUM TÜRENTRIEGELN
;       UN  E 0.0;          // X-ACHSE SERVO-READY
;       UN  E 0.1;          // Y-ACHSE SERVO-READY
UN  E 0.2;          // Z-ACHSE SERVO-READY
UN  E 5.5           // Bremse öffnen von Z-Achsreglerbaugruppe
O   DB10.DBX 75.0;  // SPS-MSD /TÜRAUTOMATIK
=   A 9.2;          //* Verriegelunsmagnet Maschinentür entriegeln
NETWORK
TITLE = MELDUNG 7042  MASCHINENTÜR INIT.
O   M 11.4          // HM Tür auf/zu-Test Ersteinschaltung
UN  M 11.0          // HM NOT AUS Test Ersteinschaltung
UN  DB10.DBX 75.0;  // SPS-MSD /TÜRAUTOMATIK
S   M 21.2;         // HM MELDUNG 7042  MASCHINENTÜR INIT.
U   M 12.3          // FM Tür auf/zu
R   M 21.2;         // HM 7042  MASCHINENTÜR INIT.
R   M 11.4          // HM Tür auf/zu-Test Ersteinschaltung
R   M 12.4          // HM Tür offen
R   M 12.5          // HM Tür zu und Verriegelt
NETWORK
TITLE = MELDUNG 7040  MASCHINENTÜR OFFEN
U   M 12.1          // Tür offen
UN  A 4.0           // Türfreigaberelais K3 ein auf Sicherheitsmodul Y4A135000M
=   M 21.0;         // HM MELDUNG 7040  MASCHINENTÜR OFFEN
=   M 110.1;        // MASCHINENTÜR OFFEN
NETWORK
TITLE = ALARM 6005  K2 oder K3 auf Sicherheitsmodul oder n=0 Relais nicht Abgefallen
UN  E 2.0;          // TÜR geschlossen von Türsicherheitsverriegelung ÜK
UN  E 5.1;          // Überwachung von K2 und K3 auf Sicherheitsmodul Y4A135000
UN  A 4.0           // Türfreigaberelais K3 ein auf Sicherheitsmodul Y4A135000M
UN  DB10.DBX 75.0;  // SPS-MSD /TÜRAUTOMATIK
L   S5T#500MS;      // 200MS
SE  T 81;           // STARTEN  ALS EINSCHALTVERZ.
U   T 81;           // STARTEN  ALS EINSCHALTVERZ.
S   DB2.DBX 0.5;    // 6005  K2 oder K3 auf Sicherheitsmodul nicht Abgefallen
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 0.5;    // 6005  K2 oder K3 auf Sicherheitsmodul nicht Abgefallen
;  NETWORK
;  TITLE = ALARM 6085 n=0 Relais nicht abgefallen
;
;    E 3.7     // Kontakt von Relais welches von Ausgang x2:7 der USB-Überwachungsplatine angesteuert wird
;              // (keine Achs oder Spindelbewegungen = Relais abgefallen und Eingang 3.7 hat 0 Signal)
;
;
;       U   E 3.7           // +24V Signal wenn sich ACHSEN oder Spindel BEWEGEN
;       =   M 12.6
;
;       O   M 131.7;        // +24V Signal wenn sich die ACHSEN BEWEGEN
;       ON  M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
;       =   M 12.7
;
;       U   M 12.6
;       UN  M 12.7
;       O(
;       UN  M 12.6
;       U   M 12.7
;       )
;       L   S5T#1500MS;     // 1500MS
;       SE  T
;
;       U   T
;       S   DB2.DBX 10.5;   // A6085 n=0 Relais nicht abgefallen
;
;       O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
;       O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
;       R   DB2.DBX 10.5;   // A6085 n=0 Relais nicht abgefallen
;
;  NETWORK
;  TITLE = ALARM 6086 Unterschiedliche Tür-Signale von USBSPS und ACC-SPS
;
;       S   DB2.DBX 10.6;   // A6086 Unterschiedliche Tür-Signale von USBSPS und ACC-SPS
;
;
NETWORK
TITLE = Türfreigaberelais K3 auf Sicherheitsmodul einschalten
U   M 12.0          // Tür zu und Verriegelt
UN  E 5.1;          // Überwachung von K2 und K3 auf Sicherheitsmodul Y4A135000
U   M 2.0           // SUMME aller NOT-AUS Möglichkeiten
U   A 9.1           //* NOT AUS für NE Modul Klemme 64
S   A 4.0           // Türfreigaberelais K3 ein auf Sicherheitsmodul Y4A135000M
U   M 12.0          // Tür zu und Verriegelt
U   A 4.0           // Türfreigaberelais K3 ein auf Sicherheitsmodul Y4A135000M
S   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
S   M 50.3;         // FREIGABEKREIS AUF AUSGABEMODUL DURCHGESCHALTET
;       ON  E 2.2;          // NICHT NOT-AUS
ON  M 2.0           // SUMME aller NOT-AUS Möglichkeiten
O   M 12.1          // Tür offen
U   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
R   A 4.0           // Türfreigaberelais K3 ein auf Sicherheitsmodul Y4A135000M
R   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
R   M 50.3;         // FREIGABEKREIS AUF AUSGABEMODUL DURCHGESCHALTET
; Freigaben Antriebe ein:
//  X5:11    E 5.2       // Überwachung von K4 und K5 auf Sicherheitsmodul Y4A135000
//  X5:1     A 4.1       //* Antriebsfreigaberelais K5 ein auf Sicherheitsmodul Y4A135000
NETWORK
TITLE = ALARM 6018 AS Signale und K4 und K5 auf Sicherheitsmodul Y4A135000 abgefallen
U   M 12.1          // Tür offen
UN  A 4.1           //* Antriebsfreigaberelais K5 ein auf Sicherheitsmodul Y4A135000
UN  A 4.0           // Türfreigaberelais K3 ein auf Sicherheitsmodul Y4A135000M
UN(
U   M 2.0           // SUMME aller NOT-AUS Möglichkeiten
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
U   M 51.2;         // ZUSTIMMTASTE GEDRÜCKT
)
L   S5T#2000MS;     // 200MS
SE  T 82;           // STARTEN  ALS EINSCHALTVERZ.
UN  E 5.2           // Überwachung von K4 und K5 auf Sicherheitsmodul Y4A135000
L   S5T#500MS;      // 200MS
SE  T 84;           // STARTEN  ALS EINSCHALTVERZ.
U   T 82;           // STARTEN  ALS EINSCHALTVERZ.
U   T 84;           // STARTEN  ALS EINSCHALTVERZ.
S   DB2.DBX 2.2;    // 6018   K4 oder K5 auf Sicherheitsmodul oder AS-Signale nicht Abgefallen
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 2.2;    // 6018   K4 oder K5 auf Sicherheitsmodul oder AS-Signale nicht Abgefallen
NETWORK
TITLE = Antriebsfreigaberelais K5 einschalten auf Sicherheitsmodul Y4A135000
UN  M 11.0          // HM NOT AUS Test Ersteinschaltung
UN  M 11.4          // HM Tür auf/zu-Test Ersteinschaltung
UN  DB2.DBX 2.2;    // 6018   K4 oder K5 auf Sicherheitsmodul oder AS-Signale nicht Abgefallen
U   M 12.0          // Tür zu und Verriegelt
U   A 4.0           // Türfreigaberelais K3 ein auf Sicherheitsmodul Y4A135000M
O   M 113.0;        // SONDERBETRIEB AKTIV
UN  E 5.2           // Überwachung von K4 und K5 auf Sicherheitsmodul Y4A135000
U   M 131.0;        // UE Modul BETRIEBSBEREIT
S   A 4.1           //* Antriebsfreigaberelais K5 ein auf Sicherheitsmodul Y4A135000
U   A 4.1           //* Antriebsfreigaberelais K5 ein auf Sicherheitsmodul Y4A135000
L   S5T#200MS;      // 200MS
SE  T 83;//200mS EINSCHALTVERZ.Antriebsfreigaberelais K5 ein auf Sicherheitsmodul Y4A135000
O   M 110.1;        // MASCHINENTÜR OFFEN
UN  M 113.0;        // SONDERBETRIEB AKTIV
ON  M 131.0;        // UE Modul BETRIEBSBEREIT
UN  M 131.7;        // ACHSEN IN BEWEGUNG
R   A 4.1           //* Antriebsfreigaberelais K5 ein auf Sicherheitsmodul Y4A135000
END_FUNCTION
FUNCTION FC 65 : VOID
NAME: HANDRAD
BEGIN
NETWORK
TITLE = Handradachse löschen
O     M 89.6;             // Handrad-BA
FN    M 89.4;             // Neg. FM Handrad-BA aus
U     M 89.4;             // Neg. FM Handrad-BA aus
SPBN M001;
=     DB20.DBX 348.5;      // Aktuelle Handradachse löschen
M001:  NOP 0;
NETWORK
TITLE = Funktionen für Handrad
O     DB20.DBX 326.5;     // BA INC1 von Kern
O     DB20.DBX 326.6;     // BA INC10 von Kern
O     DB20.DBX 326.7;     // BA INC100 von Kern
O     DB20.DBX 327.0;     // BA INC1000 von Kern
=     M 89.6;             // Handrad-BA
U(    ;
O(    ;
U     M 89.6;             // Handrad-BA
UN    M 89.7;             // HM Handrad aktiv
)     ;
O     M 89.0;             // Fl. Anwahl INC1
O     M 89.1;             // Fl. Anwahl INC10
O     M 89.2;             // Fl. Anwahl INC100
O     M 89.3;             // Fl. Anwahl INC1000
)     ;
=     DB1.DBX 1440.2;     // Handrad Anwahl
ON    M 89.6;             // Handrad-BA
U     M 89.7;             // HM Handrad aktiv
=     DB1.DBX 1440.3;     // Handrad Abwahl
U     DB1.DBX 1440.2;     // Handrad Anwahl
S     M 89.7;             // HM Handrad aktiv
U     DB1.DBX 1440.3;     // Handrad Abwahl
R     M 89.7;             // HM Handrad aktiv
// INC-Schrittweite bei BA-Wechsel mitführen
U     DB20.DBX 326.5;     // BA INC1 von Kern
FP    M 89.0;             // Fl. Anwahl INC1
UN    M 89.0;
SPB   INC1;
L     +1.0E-06;
T     DB20.DBD 16;        // INC Schrittweite
INC1: NOP   0;
U     DB20.DBX 326.6;     // BA INC10 von Kern
FP    M 89.1;             // Fl. Anwahl INC10
UN    M 89.1;
SPB   INC2;
L     +1.0E-05;
T     DB20.DBD 16;        // INC Schrittweite
INC2: NOP   0;
U     DB20.DBX 326.7;     // BA INC100 von Kern
FP    M 89.2;             // Fl. Anwahl INC100
UN    M 89.2;
SPB   INC3;
L     +1.0E-04;
T     DB20.DBD 16;        // INC Schrittweite
INC3: NOP   0;
U     DB20.DBX 327.0;     // BA INC1000 von Kern
FP    M 89.3;             // Fl. Anwahl INC1000
UN    M 89.3;
SPB   INC4;
L     +1.0E-03;
T     DB20.DBD 16;        // INC Schrittweite
INC4: NOP   0;
END_FUNCTION
FUNCTION FC 34 : VOID
NAME: ZUWEISEN DER EINGÄNGE ACC
BEGIN
// EINGÄNGE:
NETWORK
TITLE = SCHLÜSSELSCHALTER AUF SONDERBETRIEB ZUWEISUNG
U   DB20.DBX 833.0  // RAFI Tastatur X2.1/2 Schlüsselschalter auf Sonderbetrieb
=   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
NETWORK
TITLE = ZUSTIMMTASTE GEDRÜCKT
O   DB20.DBX 834.0  // RAFI Tastatur X6.1/2 Zustimmtaste
O   E 3.2           // Zustimmtaste Handrad
=   M 51.2;         // ZUSTIMMTASTEN GEDRÜCKT
NETWORK
TITLE = ROBOTIK AUX-ON
O   DB1.DBX 1608.0  // USB ROBOTIK X5.1
;       O   E 8.1;          // ROBOTIK AUX-ON
=   M 51.6;         // ROBOTIK AUX-ON
NETWORK
TITLE = ROBOTIK AUTOMATIK/REFERENZBETRIEB UMSCHALTEN
O   DB1.DBX 1608.1  // USB ROBOTIK X5.2
;       O   E 8.2;          // ROBOTIK AUTOMATIK/REFERENZBETRIEB UMSCHALTEN
=   M 51.7;         // ROBOTIK AUTOMATIK/REFERENZBETRIEB UMSCHALTEN
NETWORK
TITLE = ROBOTIK REFERENZPUNKT ANFAHREN STARTEN
O   DB1.DBX 1608.2  // USB ROBOTIK X5.3
;       O   E 8.3;          // ROBOTIK REFERENZPUNKT ANFAHREN STARTEN
=   M 52.0;         // ROBOTIK REFERENZPUNKT ANFAHREN STARTEN
NETWORK
TITLE = ROBOTIK PROGRAMM START
O   DB1.DBX 1608.3  // USB ROBOTIK X5.4
;       O   E 8.4;          // ROBOTIK PROGRAMM START
=   M 52.1;         // ROBOTIK PROGRAMM START
NETWORK
TITLE = ROBOTIK VORSCHUB-STOP
O   DB1.DBX 1609.3  // USB ROBOTIK X5.12
;       O   E 8.5;          // ROBOTIK VORSCHUB-STOP
=   M 52.2;         // ROBOTIK VORSCHUB-STOP
NETWORK
TITLE = ROBOTIK SPANNMITTEL SCHLIEßEN
O   DB1.DBX 1608.5  // USB ROBOTIK X5.6
;       O   E 8.6;          // ROBOTIK SPANNMITTEL SCHLIEßEN
=   M 52.3;         // ROBOTIK SPANNMITTEL SCHLIEßEN
NETWORK
TITLE = ROBOTIK SPANNMITTEL ÖFFNEN
O   DB1.DBX 1608.4  // USB ROBOTIK X5.5
;       O   E 8.7;          // ROBOTIK SPANNMITTEL ÖFFNEN
=   M 52.4;         // ROBOTIK SPANNMITTEL ÖFFNEN
NETWORK
TITLE = ROBOTIK PINOLE VOR
O   DB1.DBX 1608.6  // USB ROBOTIK X5.7
;       O   E 9.0;          // ROBOTIK PINOLE VOR
=   M 52.5;         // ROBOTIK PINOLE VOR
NETWORK
TITLE = ROBOTIK PINOLE ZURÜCK
O   DB1.DBX 1608.7  // USB ROBOTIK X5.8
;       O   E 9.1;          // ROBOTIK PINOLE ZURÜCK
=   M 52.6;         // ROBOTIK PINOLE ZURÜCK
NETWORK
TITLE = ROBOTIK TÜR ÖFFNEN
O   DB1.DBX 1609.0  // USB ROBOTIK X5.9
;       O   E 9.2;          // ROBOTIK TÜR ÖFFNEN
=   M 52.7;         // ROBOTIK TÜR ÖFFNEN
NETWORK
TITLE = ROBOTIK TÜR SCHLIESSEN
O   DB1.DBX 1609.1  // USB ROBOTIK X5.10
;       O   E 9.3;          // ROBOTIK TÜR SCHLIESSEN
=   M 51.5;         // ROBOTIK TÜR SCHLIESSEN
END_FUNCTION
FUNCTION FC 35 : VOID
NAME: ZUWEISEN DER AUSGÄNGE ACC
BEGIN
// ACC  ROBOTIK und Standart ALARMAUSGANGANG
U   M 54.3          // AUSGANGSMERKER ALARMAUSGANGANG
=   A 3.4           // ALARMAUSGANGANG
=   DB1.DBX 1635.0  // USB ROBOTIK X2.9
// ACC  ROBOTIK NOT-AUS AKTIV
U   M 59.3          // AUSGANGSMERKER ROBOTIK NOT-AUS AKTIV
;       =   A 6.5;          // ROBOTIK NOT-AUS AKTIV
=   DB1.DBX 1634.5  // USB ROBOTIK X2.6
// ACC  ROBOTIK MASCHINE BETRIEBSBEREIT
U   M 59.4          // AUSGANGSMERKER ROBOTIK MASCHINE BETRIEBSBEREIT
;       =   A 6.6;          // ROBOTIK MASCHINE BETRIEBSBEREIT
=   DB1.DBX 1634.6  // USB ROBOTIK X2.7
// ACC  ROBOTIK BETRIEBSART REFERENZ
U   M 59.5          // AUSGANGSMERKER ROBOTIK BETRIEBSART REFERENZ
;       =   A 6.7;          // ROBOTIK BETRIEBSART REFERENZ
=   DB1.DBX 1637.0  // USB ROBOTIK X3.9
// ACC  ROBOTIK M0,M1,M2,ODER M30 AKTIV
U   M 59.6          // AUSGANGSMERKER ROBOTIK M0,M1,M2,ODER M30 AKTIV
;       =   A 7.0;          // ROBOTIK M0,M1,M2,ODER M30 AKTIV
=   DB1.DBX 1634.7  // USB ROBOTIK X2.8
// ACC
U   M 59.7          // ROBOTIK ACHSEN STEHEN AUF REF.PKT.
;       =   A 8.1           // ROBOTIK ACHSEN STEHEN AUF REF.PKT.
=   DB1.DBX 1637.1  // USB ROBOTIK X3.10
// ACC  ROBOTIK SPANNMITTEL GESPANNT
U   M 60.0          // AUSGANGSMERKER ROBOTIK SPANNMITTEL GESPANNT
;       =   A 7.1;          // ROBOTIK SPANNMITTEL GESPANNT
=   DB1.DBX 1636.3  // USB ROBOTIK X3.4
// ACC  ROBOTIK SPANNMITTEL OFFEN
U   M 60.1          // AUSGANGSMERKER ROBOTIK SPANNMITTEL OFFEN
;       =   A 7.2;          // ROBOTIK SPANNMITTEL OFFEN
=   DB1.DBX 1636.2  // USB ROBOTIK X3.3
// ACC  ROBOTIK SPANNMITTEL KEIN TEIL GESPANNT
;       U   M 60.2          // AUSGANGSMERKER ROBOTIK SPANNMITTEL KEIN TEIL GESPANNT
;       =   A 7.3;          // ROBOTIK SPANNMITTEL KEIN TEIL GESPANNT
;// ACC  ROBOTIK PINOLE GESPANNT
;
;       U   M 60.3          // AUSGANGSMERKER ROBOTIK PINOLE GESPANNT
;       =   A 7.4;          // ROBOTIK PINOLE GESPANNT
;       =   DB1.DBX 1636.4  // USB ROBOTIK X3.5
;
;// ACC  ROBOTIK PINOLE ENDLAGE HINTEN
;
;       U   M 60.4          // AUSGANGSMERKER ROBOTIK PINOLE ENDLAGE HINTEN
;       =   A 7.5;          // ROBOTIK PINOLE ENDLAGE HINTEN
;       =   DB1.DBX 1636.5  // USB ROBOTIK X3.6
;
;// ACC  ROBOTIK PINOLE KEIN TEIL GESPANNT
;
;       U   M 60.5          // AUSGANGSMERKER ROBOTIK PINOLE KEIN TEIL GESPANNT
;       =   A 7.6;          // ROBOTIK PINOLE KEIN TEIL GESPANNT
// ACC  ROBOTIK MASCHINENTÜR GESCHLOSSEN
U   M 60.6          // AUSGANGSMERKER ROBOTIK MASCHINENTÜR GESCHLOSSEN
;       =   A 7.7;          // ROBOTIK MASCHINENTÜR GESCHLOSSEN
=   DB1.DBX 1636.6  // USB ROBOTIK X3.7
// ACC  ROBOTIK MASCHINENTÜR OFFEN
U   M 60.7          // AUSGANGSMERKER ROBOTIK MASCHINENTÜR OFFEN
;       =   A 8.0;          // ROBOTIK MASCHINENTÜR OFFEN
=   DB1.DBX 1636.7  // USB ROBOTIK X3.8
;
;
;//*************    FREI PROGRAMMIERBARE M_BEFEHLE    ***********************
;
;
;// ACC  M72/M73
;
;       U   M 61.1          // AUSGANGSMERKER M72/M73
;       =   ;          // M72/M73
;
;// ACC  M74/M75
;
;       U   M 61.2          // AUSGANGSMERKER M74/M75
;       =   ;          // M74/M75
;
;// ACC  M76/M77
;
;       U   M 61.3          // AUSGANGSMERKER M76/M77
;       =   A 6.3;          // M76/M77
;
;// ACC  M78/M79
;
;       U   M 61.4          // AUSGANGSMERKER M78/M79
;       =   A 8.2;          // M78/M79
;
;// ACC  M80/M81
;
;       U   M 61.5          // AUSGANGSMERKER M80/M81
;       =   A 8.3;          // M80/M81
;
;// ACC  M82/M83
;
;       U   M 61.6          // AUSGANGSMERKER M82/M83
;       =   A 8.4;          // M82/M83
;
;// ACC  M84/M85
;
;       U   M 61.7          // AUSGANGSMERKER M84/M85
;       =   A 8.5;          // M84/M85
END_FUNCTION
FUNCTION FC  1 : VOID
NAME: SCHMIERMITTEL und PNEUMATIKÖLSTANDÜBERWACHUNG
BEGIN
NETWORK
TITLE = FM WEGIMPULS X-ACHSE
U   DB1.DBX 1422.0; // WEGIMPULS X-ACHSE
ZV  Z 2;            // ZÄHLE VORWÄRTS Z2
NETWORK
TITLE = FM WEGIMPULS Y-ACHSE
U   DB1.DBX 1422.1; // WEGIMPULS Y-ACHSE
ZV  Z 1;            // ZÄHLE VORWÄRTS Z1
NETWORK
TITLE = FM WEGIMPULS Z-ACHSE (2M)
U   DB1.DBX 1422.2; // WEGIMPULS Z-ACHSE
ZV  Z 6;            // ZÄHLE VORWÄRTS Z6
NETWORK
TITLE = WEGINITIALISIERUNG/SCHMIERIMPULS
L   DB10.DBW 10;    // SPS-MSD X-ACHSE ALLE 8 METER EINEN SCHMIERIMPULS
L   Z 2;            // LADE Z2
<=I ;               // VERGLEICH AUF KLEINER GLEICH
O
L   DB10.DBW 12;    // SPS-MSD Y-ACHSE ALLE 8 METER EINEN SCHMIERIMPULS
L   Z 1;            // LADE Z1
<=I ;               // VERGLEICH AUF KLEINER/GLEICH
O
L   DB10.DBW 14;    // SPS-MSD Z-ACHSE ALLE 8 METER EINEN SCHMIERIMPULS
L   Z 6;            // LADE Z6
<=I ;               // VERGLEICH AUF KLEINER GLEICH
;       O   DB1.DBX 1374.4; // TASTE AUX-ON (PC)
O   DB20.DBX 832.6  // TASTE AUX-ON (MSTT)
UN  A 5.2           //* Leistung Schmiermittelpumpe
UN  M 110.1;        // MASCHINENTÜR OFFEN
UN  T 24;           // PAUSENZEIT SCHMIERMITTEL
U   M 110.0;        // AUX-ON
SPBN M3;                 // SPRUNG BEI 0
SET;
R   Z 2;            // RÜCKSETZEN Z2
R   Z 1;            // RÜCKSETZEN Z1
R   Z 6;            // RÜCKSETZEN Z6
S   A 5.2           //* Leistung Schmiermittelpumpe
U   E 3.5           //* Druckschalter Schmiermittel
S   M 20.6;         // HM 7038  DRUCKSCHALTER SCHM. DEFEKT
M3:   NOP  0;
NETWORK
TITLE = DRUCKSCHALTER SCHMIERMITTEL
U   A 5.2           //* Leistung Schmiermittelpumpe
U   E 3.5           //* Druckschalter Schmiermittel
L   S5T#2S;         // 2S
SE  T 22;           // STARTEN T 22 ALS EINSCHALTVERZ.
NETWORK
TITLE = ÜBERWACHUNGSZEIT SCHMIERMITTEL
U   A 5.2           //* Leistung Schmiermittelpumpe
UN  E 3.5           //* Druckschalter Schmiermittel
L   S5T#20S;        // 20S
SE  T 23;           // STARTEN T 23 ALS EINSCHALTVERZ.
NETWORK
TITLE = RÜCKSETZEN SCHMIERMITTEL
O   T 22;           // DRUCKSCHALTER SCHMIERMITTEL
O   T 23;           // ÜBERWACHUNGSZEIT ABGELAUFEN
ON  M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
ON  M 2.0           // SUMME aller NOT-AUS Möglichkeiten
;       ON  E 2.2 ;         // NICHT NOT-AUS
R   A 5.2           //* Leistung Schmiermittelpumpe
NETWORK
TITLE = /FM LEISTUNG SCHMIERMITTEL
U   A 5.2           //* Leistung Schmiermittelpumpe
FP  M 129.1;        // FM LEISTUNG SCHMIERMITTEL
NETWORK
TITLE = PAUSENZEIT SCHMIERMITTEL
U   M 129.1;        // FM LEISTUNG SCHMIERMITTEL
L   S5T#120S;       // 120S
SV  T 24;           // STARTEN T 24 ALS VERL. IMPULS
NETWORK
TITLE = 7039  SCHMIERMITTEL DEFEKT
O   T 23;           // ÜBERWACHUNGSZEIT ABGELAUFEN
S   M 20.7;         // HM 7039  SCHMIERMITTEL DEFEKT
R   A 5.2           //* Leistung Schmiermittelpumpe
R   T 24;           // STARTEN T 24 ALS VERL. IMPULS
U   T 22;           // DRUCKSCHALTER SCHMIERMITTEL
R   M 20.7;         // HM 7039  SCHMIERMITTEL DEFEKT
R   M 20.6;         // HM 7038  DRUCKSCHALTER SCHM. DEFEKT
NETWORK
TITLE = 7063  ÖLSTAND ZENTRALSCHMIERUNG !
UN  E 3.6           // Schwimmerschalter Schmiermittelpumpe
=   M 22.6          // HM 7063 ÖlSTAND ZENTRALSCHMIERUNG
NETWORK
TITLE = SPS/OB-->SCHMIERIMPULS AKTIV
U   A 5.2           //* Leistung Schmiermittelpumpe
=   DB20.DBX 311.4; // SPS/RM-->SCHMIERIMPULS AKTIV
;  NETWORK
;  TITLE = MELDUNG 7019 PNEUMATIKÖLER LEER
;
;; Legende:
;; DB10.DBX 75.4=0-kapazitiver Füllstansschalter hat 0-Signal wenn Öler leer ist
;; DB10.DBX 75.4=1-magnetischer Füllstansschalter hat 1-Signal wenn Öler leer ist
;
;
;       U   DB10.DBX 75.4;  // 1=magn.Füllstansschalter
;       U              // Füllstandsschalter PNEUMATIKÖLER
;       O(
;       UN  DB10.DBX 75.4;  // 0=kapazit.Füllstansschalter
;       UN             // Füllstandsschalter PNEUMATIKÖLER
;       )
;       U   DB10.DBX 77.1;  // SPS-MSD Ölstandsüberwachung aktivieren
;       S   M 18.3          // HM MELDUNG 7019  PNEUMATIKÖLER LEER
;
;       O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
;       O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
;       R   M 18.3;         // HM MELDUNG 7019  PNEUMATIKÖLER LEER
;
NETWORK
TITLE = SCHMIERIMPULSZÄHLER
U   M 129.1;        // FM LEISTUNG SCHMIERMITTEL
SPBN M4;                 // SPRUNG BEI 0
L   DB15.DBD 12;    // SCHMIERIMPULSZÄHLER
L   DW#16#0000_0001;
+D;                 // SCHMIERIMPULSZÄHLER +1
T   DB15.DBD 12;    // SCHMIERIMPULSZÄHLER
M4:   NOP  0;
END_FUNCTION
FUNCTION FC  2 : VOID
NAME: STUECKZAEHLER
BEGIN
NETWORK
TITLE = STÜCKZÄHLER
UN  DB20.DBX 192.2; // M2 dyn
UN  DB20.DBX 195.6; // M30 dyn
O   DB20.DBX 324.2; // DRYRUN AKTIV
SPB   M214;
SET;
L   DB15.DBD 4;     // Zähler
L   DW#16#000F_423F;// 999999 STÜCK
<>D;
SPB   M211;
SET;
L   DW#16#0000_0000;
T   DB15.DBD 4;
M211:  NOP 0;
L   DB15.DBD 4;
L   DW#16#0000_0001;
+D;                 // Stückzähler +1 low
T   DB15.DBD 4;
L   DB15.DBD 8;     // CHARGENZÄHLER
L   DW#16#000F_423F;// 999999 STÜCK
<>D;
SPB   M213;
SET;
L   DW#16#0000_0000;
T   DB15.DBD 8;
M213:  NOP 0;
L   DB15.DBD 8;
L   DW#16#0000_0001;
+D;                 // Stückzähler +1 low
T   DB15.DBD 8;
M214:  L   DB15.DBD 0;     // Sollstückz
L   DW#16#0000_0000;
<>D;
SPB   M202;
SPA   M201;
M202:  NOP 0;
L   DB15.DBD 4;     // Stückz.
L   DB15.DBD 0;     // Sollstückz.
<D;
SPB   M201;
SET;
S   M 21.3;         // HM 7043  SOLLSTÜCKZAHL ERREICHT
L   DW#16#0;
T   DB15.DBD 4;
M201:  NOP 0;
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   M 21.3;         // HM 7043  SOLLSTÜCKZAHL ERREICHT
END_FUNCTION
FUNCTION FC  3 : VOID
NAME: HA STEUERUNG und LEISTUNG SPINDEL0
BEGIN
//  X3:1  Stromistwert HAUPTSPINDEL
//  X3:2  GND
//  X3:5  Led H2   E 0.3  =  Hauptantrieb SERVO-READY
//  X3:11 Led H1   A 0.3  =  Hauptantrieb Reglerfreigabe
//  X3:12 Led H3   A 11.5 =  Parametersatz Umschaltung 1.Eingang (Bit0 9V=3000Upm)
//  X3:13 Led H4   A 11.6
//  X5:2     E 4.1 //*? Überwachung AS Signal HS/Z
//  X5:4     A 4.3 // Impulsfreigabe Hauptantrieb und Z-Achse
NETWORK
TITLE = 7026 Störung Lüfter Hauptmotor
UN  E 4.5           // Motorschutz Lüfter HA
S   M 19.2;         // HM  7026 Störung Lüfter Hauptmotor
U   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   M 19.2;         // HM  7026 Störung Lüfter Hauptmotor
;  NETWORK
;  TITLE = Alarm 6068 Hauptmotor Übertemperatur
;
;       U   M 51.0;         // Thermo Lenze Hauptmotor CT250TC
;       UN  DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
;       S   DB2.DBX 8.4;    // 6068 Hauptmotor Übertemperatur
;
;       U   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
;       R   DB2.DBX 8.4;    // 6068 Hauptmotor Übertemperatur
;
NETWORK
TITLE = M52 RUNDACHSBETRIEB
;       O   DB1.DBX 1402.0  // SINUMERIK 810/820
;       O   DB1.DBX 1402.1  // FANUC 0
;       O   DB1.DBX 1403.0  // FANUC 21
;       U   DB20.DBX 198.4; // M52 dyn C-ACHSE EINSCHALTEN
;    SPBN   M001
;       SET;
;       =   DB20.DBX 3.5;   // Umschaltung von Spindel auf Rundachsbetrieb oder umgekehrt
;       =   DB20.DBX 823.0; // 1=Rundachsbetrieb 0=Spindelbetrieb
;       L   0
;       T   DB20.DBB 822;   // Spindelnummer welche umgeschaltet wird
;M001:  NOP 0
;  NETWORK
;  TITLE = M53 SPINDELBETRIEB
;
;       O   DB1.DBX 1402.0  // SINUMERIK 810/820
;       O   DB1.DBX 1402.1  // FANUC 0
;       O   DB1.DBX 1403.0  // FANUC 21
;       U   DB20.DBX 198.5; // M53 dyn C-ACHSE AUSSCHALTEN
;       O   DB20.DBX 192.2; // M2 DYN.
;       O   DB20.DBX 195.6; // M30 DYN.
;    SPBN   M002
;       =   DB20.DBX 3.5;   // Umschaltung von Spindel auf Rundachsbetrieb oder umgekehrt
;       R   DB20.DBX 823.0; // 1=Rundachsbetrieb 0=Spindelbetrieb
;       L   0
;       T   DB20.DBB 822;   // Spindelnummer welche umgeschaltet wird
;M002:  NOP 0
;
NETWORK 1
TITLE = Spindelauswahl für Rundachsbetrieb Kanal 0
O   DB20.DBX 3.5;   // Umschaltung von Spindel auf Rundachsbetrieb oder umgekehrt
SPBN  M1002
L   0               // UMSCHALTUNG SPINDEL0
L   DB20.DBB 822;   // Spindelnummer welche umgeschaltet wird
==I
SPBN  M1002
S   M 80.2;         // AFG von Achsumschaltung Spindel0
O   DB20.DBX 823.0; // 1=Rundachsbetrieb 0=Spindelbetrieb
=   M 80.1;         // Spindel0 1=Rundachsbetrieb 0=Spindelbetrieb
M1002:  NOP 0
NETWORK 1
TITLE = Spindelauswahl für Rundachsbetrieb Kanal 1
O   DB25.DBX 3.5;   // Umschaltung von Spindel auf Rundachsbetrieb oder umgekehrt
SPBN  M1102
L   0               // UMSCHALTUNG SPINDEL0
L   DB25.DBB 822;   // Spindelnummer welche umgeschaltet wird
==I
SPBN  M1102
S   M 80.2;         // AFG von Achsumschaltung Spindel0
O   DB25.DBX 823.0; // 1=Rundachsbetrieb 0=Spindelbetrieb
=   M 80.1;         // Spindel0 1=Rundachsbetrieb 0=Spindelbetrieb
M1102:  NOP 0
NETWORK 1
TITLE = AFG von Achsumschaltung
U   M 80.1;         // Spindel0 1=Rundachsbetrieb 0=Spindelbetrieb
U   M 80.5;         // RUNDACHSBETRIEB
O(
UN  M 80.1;         // Spindel0 1=Rundachsbetrieb 0=Spindelbetrieb
U   M 80.4;         // SPINDEL0 SPINDELBETRIEB
)
R   M 80.2;         // AFG EFG
NETWORK 1
TITLE = UMSCHALTEN auf Rundachsbetrieb
U   M 80.1;         // Spindel0 1=Rundachsbetrieb 0=Spindelbetrieb
U   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
UN  M 80.4;         // SPINDEL0 SPINDELBETRIEB
UN  M 80.5;         // RUNDACHSBETRIEB
L   S5TIME#300MS
SE  T 89
U   T 89
S   M 80.5;         // RUNDACHSBETRIEB
U   M 80.1;         // Spindel0 1=Rundachsbetrieb 0=Spindelbetrieb
U   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
R   M 80.4;         // SPINDEL0 SPINDELBETRIEB
NETWORK 1
TITLE = UMSCHALTEN auf Spindelbetrieb
UN  M 80.1;         // Spindel0 1=Rundachsbetrieb 0=Spindelbetrieb
UN  M 80.4;         // SPINDEL0 SPINDELBETRIEB
UN  M 80.5;         // RUNDACHSBETRIEB
L   S5TIME#300MS
SE  T 88
U   T 88
S   M 80.4;         // SPINDEL0 SPINDELBETRIEB
NETWORK 1
TITLE = RESET RUNDACHSBETRIEB
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
U   M 80.5;         // RUNDACHSBETRIEB
S   M 80.0;         // Anforderung Rundachsbetrieb beenden
U   M 80.0;         // Anforderung Rundachsbetrieb beenden
UN  DB20.DBX 862.0; // Transmit oder Trancyl aktiv
SPBN   M1001
S   DB20.DBX 3.5;   // Umschaltung von Spindel auf Rundachsbetrieb oder umgekehrt
R   DB20.DBX 823.0; // 1=Rundachsbetrieb 0=Spindelbetrieb
L   0
T   DB20.DBB 822    // UMSCHALTUNG SPINDEL0
R   M 80.0;         // Anforderung Rundachsbetrieb beenden
R   M 80.1;         // Spindel0 1=Rundachsbetrieb 0=Spindelbetrieb
R   M 80.5;         // RUNDACHSBETRIEB
R   M 115.3         // Nachführbetrieb der Haupspindel vorbereiten
R   M 115.4;        // SPINDELFREIGABE Rundachsbetrieb K0
=   DB20.DBX 262.5  // M53 SPINDELBETRIEB RÜCKMELDEN
M1001:  NOP 0
NETWORK 4
TITLE = SPINDELBETRIEB (EINSCHALTZUSTAND)
UN  M 80.1;         // Spindel0 1=Rundachsbetrieb 0=Spindelbetrieb
O   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
S   M 80.4;         // SPINDEL0 SPINDELBETRIEB
R   M 80.5;         // RUNDACHSBETRIEB
FP  M 80.7          // FM UMSCHALTEN AUF SPINDELBETRIEB
NETWORK
TITLE = Impulsfreigabe Hauptantrieb und Z-Achse
U   M 110.0;        // AUX-ON
U(
O   M 50.0;         // Tür zu und Verriegelt und Türfreigaberelais K3 ein
O   M 113.0;        // SONDERBETRIEB AKTIV
)
S   A 4.3           // Impulsfreigabe Hauptantrieb und Z-Achse
UN  M 50.0;         // Tür zu und Verriegelt und Türfreigaberelais K3 ein
UN  M 113.0;        // SONDERBETRIEB AKTIV
ON  M 110.0;        // AUX-ON
O   M 113.7;        // /FM SONDERBETRIEB AKTIV
R   A 4.3           // Impulsfreigabe Hauptantrieb und Z-Achse
NETWORK
TITLE = A6083 AS-Signalüberwachung Hauptspindel / Z-Achse
U   A 4.3           // Impulsfreigabe Hauptantrieb und Z-Achse
U   E 4.1           //*? Überwachung AS Signal HS/Z
O(
UN  A 4.3           // Impulsfreigabe Hauptantrieb und Z-Achse
UN  E 4.1           //*? Überwachung AS Signal HS/Z
)
L   S5TIME#300MS
SE  T 19
U   T 19
S   DB2.DBX 10.3;   // A6083 AS-Signalüberwachung Hauptspindel / Z-Achse
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 10.3;   // A6083 AS-Signalüberwachung Hauptspindel / Z-Achse
NETWORK
TITLE = REGLERFREIGABE SIEMENS HAUPTANTRIEB
O   DB20.DBX 340.0; // SPINDEL EIN IM UHRZEIGER
O   DB20.DBX 340.1; // SPINDEL EIN GEGEN UHRZEIGER
U   M 80.4;         // SPINDEL0 SPINDELBETRIEB
S   M 145.2;        // HM REGLERFREIGABE SIEMENS HAUPTANTRIEB
U   M 145.2;        // HM REGLERFREIGABE SIEMENS HAUPTANTRIEB
U   M 104.2;        // SUM  SFG
O(
UN  M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
U   M 145.7         // HM REGLERFREIGABE FÜR HAUPTANTRIEB
)
O(
O   M 115.4;        // SPINDELFREIGABE Rundachsbetrieb K0 8;;;;;
O   M 80.5;         // RUNDACHSBETRIEB
O   M 116.1         // OFFSET VON HAUPTANTRIEB ANSCHAUEN
U   M 104.2;        // SUM  SFG
)
U   A 9.1           //* NOT AUS für NE Modul Klemme 64
U   M 2.0           // SUMME aller NOT-AUS Möglichkeiten
=   A 0.3           // Hauptantrieb Reglerfreigabe
S   M 145.7         // HM REGLERFREIGABE FÜR HAUPTANTRIEB
R   M 145.4
NETWORK
TITLE = SPINDELFREIGABE an ACC FÜR HAUPTANTRIEB SPINDEL 0
U   A 0.3           // Hauptantrieb Reglerfreigabe
U   E 0.3;          // Hauptantrieb SERVO-READY
U   M 80.4;         // SPINDEL0 SPINDELBETRIEB
U   M 145.7         // HM REGLERFREIGABE FÜR HAUPTANTRIEB
=   M 145.3;        // SPINDELFREIGABE an ACC FÜR HAUPTANTRIEB SPINDEL 0
NETWORK
TITLE = Parametersatz Umschaltung für Rundachsbetrieb
U   M 80.5;         // RUNDACHSBETRIEB
=   A 11.5          // Parametersatz Umschaltung 1.Eingang (Bit0 9V=3000Upm)
NETWORK
TITLE = ÜBERWACHUNG HA SCHARF
U   A 0.3           // Hauptantrieb Reglerfreigabe
L   S5T#2S;         // 25X0,1S
SE  T 85;           // STARTEN T8 ALS EINSCHALTVERZ.
NETWORK
TITLE = 6013  HAUPTANTRIEB NICHT BEREIT
U   T 85;           // HA ÜBERWACHUNG SCHARF
UN  E 0.3;          // Hauptantrieb SERVO-READY
S   DB2.DBX 1.5;    // 6013  HAUPTANTRIEB NICHT BEREIT
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 1.5;    // 6013  HAUPTANTRIEB NICHT BEREIT
NETWORK
TITLE = Reglerfreigabe Hauptantrieb BETRIEBSBEREIT
U   E 0.3;          // Hauptantrieb SERVO-READY
=   DB1.DBX 1460.3  // REGLERFREIGABE ABSOLUTE ACHSE 3
NETWORK
TITLE = ANWAHL HAUPTANTRIEB M3
UN  DB20.DBX 224.3; // M3 STAT.
U   M 104.2;        // SUM  SFG
U(  ;
O   DB20.DBX 192.3; // M3 DYN.
O(  ;
U   M 148.2;        // HM M3 GESPEICHERT M0/M1
U   DB20.DBX 0.2;   // FC NC-START
)   ;
O(  ;
U   T 43;           // ÜBERWACHUNGSZEIT FÜR HA/M3
U   M 132.3;        // /FM TASTE SPINDEL START
U   M 104.5;        // HM BETRIEBSART JOG UND INC
UN  DB20.DBX 324.0; // PROGRAMM LÄUFT
)   ;
)   ;
S   M 145.0         // HM ANWAHL HAUPTANTRIEB M3
O   M 145.0         // HM ANWAHL HAUPTANTRIEB M3
U   M 80.4;         // SPINDEL0 SPINDELBETRIEB
=   DB20.DBX 256.3; // M3 RÜCKMELDEN
S   DB20.DBX 340.0; // SPINDEL EIN IM UHRZEIGER
R   DB20.DBX 340.1; // SPINDEL EIN GEGEN UHRZEIGER
R   M 148.2;        // HM M3 GESPEICHERT M0/M1
R   M 145.1         // HM ANWAHL HAUPTANTRIEB M4
NETWORK
TITLE = ANWAHL HAUPTANTRIEB   M4
UN  DB20.DBX 224.4; // M4 STAT.
U   M 104.2;        // SUM  SFG
U(  ;
O   DB20.DBX 192.4; // M4 DYN.
O(  ;
U   M 148.3;        // HM M4 GESPEICHERT M0/M1
U   DB20.DBX 0.2;   // FC NC-START
)   ;
O(  ;
UN  T 43;           // ÜBERWACHUNGSZEIT FÜR HA/M4
U   M 132.3;        // /FM TASTE SPINDEL START
U   M 104.5;        // HM BETRIEBSART JOG UND INC
UN  DB20.DBX 324.0; // PROGRAMM LÄUFT
)   ;
)   ;
S   M 145.1         // HM ANWAHL HAUPTANTRIEB M4
O   M 145.1         // HM ANWAHL HAUPTANTRIEB M4
U   M 80.4;         // SPINDEL0 SPINDELBETRIEB
=   DB20.DBX 256.4; // M4 RÜCKMELDEN
S   DB20.DBX 340.1; // SPINDEL EIN GEGEN UHRZEIGER
R   DB20.DBX 340.0; // SPINDEL EIN IM UHRZEIGER
R   M 148.3;        // HM M4 GESPEICHERT M0/M1
R   M 145.0         // HM ANWAHL HAUPTANTRIEB M3
NETWORK
TITLE = M3,M4 GESPEICHERT VON M0,M1
U   M 34.1;         // HM M0/M1 DYNAMISCH
U   DB20.DBX 224.3; // M3 STAT.
UN  M 148.4;        // HM M3/M4 GESPEICHERT M0/M1
S   M 148.2;        // HM M3 GESPEICHERT M0/M1
U   M 34.1;         // HM M0/M1 DYNAMISCH
U   DB20.DBX 224.4; // M4 STAT.
UN  M 148.4;        // HM M3/M4 GESPEICHERT M0/M1
S   M 148.3;        // HM M4 GESPEICHERT M0/M1
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   M 148.2;        // HM M3 GESPEICHERT M0/M1
R   M 148.3;        // HM M4 GESPEICHERT M0/M1
R   M 116.1         // OFFSET VON HAUPTANTRIEB ANSCHAUEN
NETWORK
TITLE = ANWAHL HAUPTANTRIEB    M5
U   M 34.1;         // HM M0/M1 DYNAMISCH
UN  M 148.4;        // HM M3/M4 GESPEICHERT M0/M1
O;
U   M    132.7;     // FM SPINDEL HALT
U   M    104.5;     // HM BETRIEBSART JOG UND INC
UN  DB20.DBX 324.0; // PROGRAMM LÄUFT
O;
U   M    132.7;     // FM SPINDEL HALT
U   M    104.5;     // HM BETRIEBSART JOG UND INC
UN  DB20.DBX 324.0; // PROGRAMM LÄUFT
O   DB20.DBX 192.5; // M5 dynamisch
O   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB20.DBX 324.2; // DRYRUN AKTIV
O   DB20.DBX 192.2; // M2 dyn
O   DB20.DBX 195.6; // M30 dyn
O   M 167.4;        // Spindel drehen aus
O   M 109.5;        // /FM AUX-ON
O   DB20.DBX 328.0; // Spindel 0 soll auf Rundachse geschaltet werden
=   DB20.DBX 256.5; // M5 RÜCKMELDEN
R   DB20.DBX 340.0; // SPINDEL EIN IM UHRZEIGER
R   DB20.DBX 340.1; // SPINDEL EIN GEGEN UHRZEIGER
R   M 145.0         // HM ANWAHL HAUPTANTRIEB M3
R   M 145.1         // HM ANWAHL HAUPTANTRIEB M4
R   M 145.2
R   M 145.4
R   M 167.4;        // Spindel drehen aus
NETWORK
TITLE = HM M3/M4 GESPEICHERT M0/M1
O   M 148.2;        // HM M3 GESPEICHERT M0/M1
O   M 148.3;        // HM M4 GESPEICHERT M0/M1
S   M 148.4;        // HM M3/M4 GESPEICHERT M0/M1
UN  M 34.1;         // HM M0/M1 DYNAMISCH
R   M 148.4;        // HM M3/M4 GESPEICHERT M0/M1
NETWORK
TITLE = ISTDREHZAHL KLEINER 20U/MIN
O   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
O   DB1.DBX 1356.3; // HAUPTANTRIEB STILLSTAND
L   S5T#100MS;      // 25X0,1S
SE  T 73
U   T 73
=   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
R   M 145.7         // HM REGLERFREIGABE FÜR HAUPTANTRIEB
R   DB20.DBX 328.0; // Spindel 0 soll auf Rundachse geschaltet werden
NETWORK
TITLE = HA LAUFT AUS  AFG/EFG
U   DB20.DBX 224.5; // M5 STAT.
UN  M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
U   M 80.4;         // SPINDEL0 SPINDELBETRIEB
=   M 90.6;         // HA LAUFT AUS  AFG/EFG
NETWORK
TITLE = 6014  KEINE HAUPTSPINDELDREHZAHL
O   DB20.DBX 340.0; // SPINDEL EIN IM UHRZEIGER
O   DB20.DBX 340.1; // SPINDEL EIN GEGEN UHRZEIGER
UN  M 132.1;        // SPINDEL HALT AKTIV
UN  M 115.4;        // SPINDELFREIGABE Rundachsbetrieb K0
UN  M 80.1;         // Spindel0 1=Rundachsbetrieb 0=Spindelbetrieb
L   S5T#5S;         // 5 S
SE  T 27;           // STARTEN T 27 ALS EINSCHALTVERZ.
U   T 27;           // M3/M4-VERZÖGERUNGSZ. ABGELAUFEN
U   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
L   S5T#1S;         // 5 S
SE  T 10;           // STARTEN T 10 ALS EINSCHALTVERZ.
U   T 10;           // M3/M4-VERZÖGERUNGSZ. ABGELAUFEN
S   DB2.DBX 1.6;    // 6014  KEINE HAUPTSPINDELDREHZAHL    8+++++++++
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 1.6;    // 6014  KEINE HAUPTSPINDELDREHZAHL
NETWORK
TITLE = Leistungsanzeige HAUPTSPINDEL
L   DB1.DBW 1508    // HAUPTSPINDEL
DTR;                // GANZZAHL (32BIT) IN GLEITPUNKTZAHL (FLOAT) UMWANDELN
T   DB1.DBD 1536    // Schnittstellendaten für Abfrage im NC-Programm
END_FUNCTION
FUNCTION FC  4 : VOID
NAME: X,Y,Z-ACHSE, Bremssteuerung, Imp.und Reglerfreigabe
BEGIN
//  -A111 X-ACHSE
//  X3:5  Led H2   E 0.0  // X-ACHSE SERVO-READY
//  X3:11 Led H1   A 0.0  // X-ACHSE Reglerfreigabe
//  X3:12 Led H3   A 11.0
//  X3:13 Led H4   A 11.1
//
//  -A112 Y-ACHSE
//
//  X3:5  Led H2   E 0.1  =  Y-ACHSE SERVO-READY
//  X3:11 Led H1   A 0.1  =  Y-ACHSE Reglerfreigabe
//  X3:12 Led H3   A 12.1
//  X3:13 Led H4   A 11.2
//
//  -A113 Z-ACHSE
//  X3:5  Led H2   E 0.2  // Z-ACHSE SERVO-READY
//  X3:11 Led H1   A 0.2  // Z-ACHSE Reglerfreigabe
//  X3:12 Led H3   A 11.3
//  X3:13 Led H4   A 11.4
//  X6:3     E 5.5    // Bremse öffnen von Z-Achsreglerbaugruppe
//  X421:7   A 9.3    //*? Bremse Z-Motor öffnen
//  X421:4   A 9.0    //*? Impulsfreigabe X/Y
//  X420:4   A 10.0   //*? Impulsfreigabe X/Y
//  X5:1     E 4.0       //*? Überwachung AS Signal X/Y
//  X5:3     E 4.2       //*? Überwachung AS Signal U/E
NETWORK
TITLE = Offsetabgleich für alle analogen Linear- und Rundachsen
U   DB20.DBX 831.6  // TASTE VORSCHUB HALT  (MSTT)
S   M 116.5         // Offsetabgleich von MSTT verhindern (nur von PC-Tastatur)
UN  DB20.DBX 295.1  // CTRL 4  // Offsetabgleich für alle analogen Linear- und Rundachsen
UN  DB20.DBX 831.6  // TASTE VORSCHUB HALT  (MSTT)
R   M 116.5         // Offsetabgleich von MSTT verhindern (nur von PC-Tastatur)
U   DB20.DBX 295.1  // CTRL 4  // Offsetabgleich für alle analogen Linear- und Rundachsen
UN  M 116.5         // Offsetabgleich von MSTT verhindern (nur von PC-Tastatur)
U   M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
U   M 111.0;        // ACHSEN BETRIEBSBEREIT
S   DB1.DBX 1466.0  // Offsetabgleich für alle analogen Linear- und Rundachsen
U   DB1.DBX 1466.0  // Offsetabgleich für alle analogen Linear- und Rundachsen
L   S5T#5S;         // Dauer autom. Abgleich
SE  T 17;           // Dauer autom. Abgleich
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   T 17;           // Dauer autom. Abgleich
R   DB1.DBX 1466.0  // Offsetabgleich für alle analogen Linear- und Rundachsen
NETWORK
TITLE = Impuls und Reglerfreigabe X/Y/A Achse
U   M 2.0           // SUMME aller NOT-AUS Möglichkeiten
U   M 110.0;        // AUX-ON
U   M 131.0;        // UE Modul BETRIEBSBEREIT
S   A 9.0           //*? Impulsfreigabe X/Y
S   A 10.0          //*? Impulsfreigabe X/Y
S   A 0.0           // X-ACHSE Reglerfreigabe
S   A 0.1           // Y-ACHSE Reglerfreigabe
U   A 0.1           // Y-ACHSE Reglerfreigabe
UN  E 0.4;          // Walter Rundachse  SERVO-READY
U(
O   DB10.DBX 100.1  // 100mm Walter Rundachse aktivieren
O   DB10.DBX 100.3  // 80mm Walter Rundachse aktivieren
)
S   A 0.4           // A-ACHSE Reglerfreigabe
O   M 110.2         // HM AUX OFF schalten
UN  M 131.7;        // ACHSEN IN BEWEGUNG
ON  M 2.0           // SUMME aller NOT-AUS Möglichkeiten
R   A 0.0           // X-ACHSE Reglerfreigabe
R   A 0.1           // Y-ACHSE Reglerfreigabe
R   A 0.4           // A-ACHSE Reglerfreigabe
UN  M 110.0;        // AUX-ON
R   A 9.0           //*? Impulsfreigabe X/Y
R   A 10.0          //*? Impulsfreigabe X/Y
U   A 6.1;          // RUNDACHSE KLEMMEN
U   E 6.0           // DRUCKSCHALTER RUNDACHSE
R   A 0.4           // A-ACHSE Reglerfreigabe
NETWORK
TITLE = A6082 AS-Signalüberwachung X/Y-Achse
U   A 9.0           //  (ist gleich A 10.0) Impulsfreigabe X/Y
U   E 4.0           // Überwachung AS Signal X/Y-Achse
O(
UN  A 9.0           //  (ist gleich A 10.0) Impulsfreigabe X/Y
UN  E 4.0           // Überwachung AS Signal X/Y-Achse
)
L   S5TIME#300MS
SE  T 18
U   T 18
S   DB2.DBX 10.2;   // A6082 AS-Signalüberwachung X/Y-Achse
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 10.2;   // A6082 AS-Signalüberwachung X/Y-Achse
NETWORK
TITLE = Bremse Z-Achse öffnen
U   E 5.5           // Bremse öffnen von Z-Achsreglerbaugruppe
UN  DB2.DBX 0.0;    // 6000  NOT-AUS
=   A 9.3;          // Bremse Z-Achse öffnen
NETWORK
TITLE = Achsreglerfreigaben bei TÜR ZU oder Sonderbetrieb einschalten
O   M 113.0;        // SONDERBETRIEB AKTIV
O   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
U   M 110.0;        // AUX-ON
U   T 83;//200mS EINSCHALTVERZ.Antriebsfreigaberelais K5 ein auf Sicherheitsmodul Y4A135000
UN  M 110.2         // HM AUX OFF schalten
S   A 0.2           // Z-ACHSE Reglerfreigabe
NETWORK
TITLE = Achsreglerfreigaben ausschalten
O   M 110.2         // HM AUX OFF schalten
O(
U   M 143.4;        // HM TÜR AUF STAT.
U   DB10.DBX 75.0;  // SPS-MSD /TÜRAUTOMATIK
U   M 55.7;         // FREIGABE ZUM TÜRENTRIEGELN
UN  M 113.0;        // SONDERBETRIEB AKTIV
)
O(
U   M 51.2;         // ZUSTIMMTASTEN GEDRÜCKT
U   M 55.7;         // FREIGABE ZUM TÜRENTRIEGELN
UN  M 113.0;        // SONDERBETRIEB AKTIV
UN  DB10.DBX 75.0;  // SPS-MSD /TÜRAUTOMATIK
)
O   M 113.1;        // Anwahl Sonderbetrieb beenden
UN  M 131.7;        // ACHSEN IN BEWEGUNG
ON  M 2.0           // SUMME aller NOT-AUS Möglichkeiten
R   A 0.2           // Z-ACHSE Reglerfreigabe
NETWORK
TITLE = ACHSEN BETRIEBSBEREIT
U   E 0.0;          // X-ACHSE SERVO-READY
U   E 0.1;          // Y-ACHSE SERVO-READY
U   E 0.2;          // Z-ACHSE SERVO-READY
=   M 111.0;        // ACHSEN BETRIEBSBEREIT
NETWORK
TITLE = Reglerfreigabe ACHSEN BETRIEBSBEREIT
U   E 0.0;          // X-ACHSE SERVO-READY
=   DB1.DBX 1460.0  // REGLERFREIGABE ABSOLUTE ACHSE 0
U   E 0.1;          // Y-ACHSE SERVO-READY
=   DB1.DBX 1460.1  // REGLERFREIGABE ABSOLUTE ACHSE 1
U   E 0.2;          // Z-ACHSE SERVO-READY
=   DB1.DBX 1460.2  // REGLERFREIGABE ABSOLUTE ACHSE 2
NETWORK
TITLE = ACHSEN BETRIEBSBEREIT  AFG/EFG
UN  M 111.0;        // ACHSEN BETRIEBSBEREIT
=   M 90.1;         // ACHSEN BETRIEBSBEREIT  AFG/EFG
NETWORK
TITLE = ÜBERWACHUNG ACHSANTRIEBE X,Y SCHARF
U   A 0.0           // X-ACHSE Reglerfreigabe
U   A 0.1           // Y-ACHSE Reglerfreigabe
L   S5T#3S;         // 3 SEKUNDEN
SE  T 3;            // STARTEN T 4 ALS EINSCHALTVERZ.
NETWORK
TITLE = ÜBERWACHUNG ACHSANTRIEBE Z SCHARF
U   A 0.2           // Z-ACHSE Reglerfreigabe
L   S5T#3S;         // 3 SEKUNDEN
SE  T 4;            // STARTEN T 4 ALS EINSCHALTVERZ.
NETWORK
TITLE = 6010  ANTRIEB X-ACHSE N. BEREIT
U   T 3;            // ACHSÜBERWACHUNG SCHARF
UN  E 0.0;          // X-ACHSE SERVO-READY
L   S5T#1S;    // 2X0,1S
SE  T 62;           // STARTEN T 62 ALS EINSCHALTVERZ.
U   T 62;
S   DB2.DBX 1.2;    // 6010  ANTRIEB X-ACHSE N. BEREIT
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 1.2;    // 6010  ANTRIEB X-ACHSE N. BEREIT
NETWORK
TITLE = 6076  ANTRIEB Y-ACHSE N. BEREIT
U   T 3;            // ACHSÜBERWACHUNG SCHARF
UN  E 0.1;          // Y-ACHSE SERVO-READY
L   S5T#1S;    // 2X0,1S
SE  T 63;           // STARTEN T 63 ALS EINSCHALTVERZ.
U   T 63;
S   DB2.DBX 9.4;    // 6076  ANTRIEB Y-ACHSE N. BEREIT
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 9.4;    // 6076  ANTRIEB Y-ACHSE N. BEREIT
NETWORK
TITLE = 6012  ANTRIEB Z-ACHSE N. BEREIT
U   T 4;            // ACHSÜBERWACHUNG SCHARF
UN  E 0.2;          // Z-ACHSE SERVO-READY
L   S5T#1S;    // 2X0,1S
SE  T 64;           // STARTEN T 64 ALS EINSCHALTVERZ.
U   T 64;
S   DB2.DBX 1.4;    // 6012  ANTRIEB Z-ACHSE N. BEREIT
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 1.4;    // 6012  ANTRIEB Z-ACHSE N. BEREIT
NETWORK
TITLE = HM ANTRIEB ACHSEN NICHT BEREIT
O   DB2.DBX 1.2;    // 6010  ANTRIEB X-ACHSE N. BEREIT
O   DB2.DBX 9.4;    // 6076  ANTRIEB Y-ACHSE N. BEREIT
O   DB2.DBX 1.4;    // 6012  ANTRIEB Z-ACHSE N. BEREIT
=   M 111.1;        // HM ANTRIEB XYZ-ACHSEN NICHT BEREIT
NETWORK
TITLE
NETWORK
TITLE
END_FUNCTION
FUNCTION FC  5 : VOID
NAME: JOG ACHSEN
BEGIN
NETWORK
TITLE = JOG-ACHSEN FREIGABE
U   M 102.0;        // SUM ACHSFREIGABE
U(    ;
O   DB20.DBX 324.4; // REFERENZPUNKT AKTIV
O   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
)     ;
U(
O   M 149.0         // FREIGABE VON SICHERHEITSEINRICHTUNGEN
O(
U   M 50.3;         // FREIGABEKREIS AUF AUSGABEMODUL DURCHGESCHALTET
U   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
)
)
=   M 112.0;        // JOG-ACHSEN FREIGABE
NETWORK
TITLE = JOG-ACHSEN/STARTVERR.
U   DB20.DBX 1.2;   // FC JOG-ACHSEN
UN  M 112.0;        // JOG-ACHSEN FREIGABE
O   M 149.1         // NEGATIV FLANKENMERKER AUS FREIGABE VON SICHERHEITSEINRICHTUNGEN
SPBN   M051
=   DB20.DBX 1.2;   // JOG-ANFORDERUNG
L   DW#16#0000_0000;// LADE 0
T   DB20.DBD 10;    // JOG IN ALLEN ACHSEN AUS
M051: NOP 0
NETWORK
TITLE = JOG-ACHSEN mit Eilgang
;DB20.DBX 295.7      // Eilgangüberlagerungtaste
;DB20.DBX 827.6;     // Eilgangüberlagerungtaste
;       U   DB20.DBX 295.7  // Eilgangüberlagerungtaste
U   DB20.DBX 827.6  // Eilgangüberlagerungtaste
U   M 12.0          // Tür zu und Verriegelt
U   M 112.0;        // JOG-ACHSEN FREIGABE
U   M 131.7;        // ACHSEN IN BEWEGUNG
U   DB20.DBX 324.4; // REFERENZPUNKT AKTIV
=   DB20.DBX 824.0; // Eilgangueberlagerung Kanal 0
END_FUNCTION
FUNCTION FC  6 : VOID
NAME: ACHSEN_REFERENZIEREN
BEGIN
NETWORK
TITLE = AUTOMATISCH IN BA REF UMSCHALTEN
U   DB1.DBX 1366.0; // DNC-BETRIEB AKTIV
SPB M0014
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
S   DB100.DBX500.0; // BIT FRÜR BA JOG
L   DB20.DBW 288    // BA ANWAHL
L   0
==I
SPB M0011
L   DB20.DBW 288    // BA ANWAHL
T   DB100.DBW 500
M0011: NOP 0
UN  DB20.DBX 324.4; // REFERENZPUNKT AKTIV
;       UN  DB1.DBX 1374.4  // AUX ON TASTE
UN  DB20.DBX 832.6  // TASTE AUX-ON (MSTT)
U   M 110.0;        // AUX-ON
UN  M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB 8++++++++++
UN  M 21.2;         // HM MELDUNG 7042  MASCHINENTÜR INIT. 8++++++++++
SPBN M0012
L   W#16#0400       // BIT BA REF SETZEN
T   DB20.DBW 288    // BA ANWAHL
SPA M0013
M0012: NOP 0
L   DB100.DBW 500
T   DB20.DBW 288    // BA ANWAHL
M0013: NOP 0
L   DB20.DBW 326
L   DB20.DBW 288
<>I
=   DB20.DBX 0.0    // BA WECHSELANFORDERUNG
M0014: NOP 0
NETWORK
TITLE = FREIGABE REFERENZIEREN
U   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
U   M 102.0;        // SUM ACHSFREIGABE
UN  M 100.2;        // VORSCHUBSCHALTER AUF 0%
;       UN  M 131.7;        // ACHSEN IN BEWEGUNG
UN  M 91.1;         // SUM ALARME  AFG/EFG
UN  DB3.DBX 2.6;    // 7022  WERKZEUGWENDER INITIALISIEREN
//// Designed by WIMMER:
U(
ON  M 94.7          // SFG Werkzeugwender nicht in Grundstellung
O   M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
)
////
=   M 120.1;        // REF ACHSEN FREIGABE
NETWORK
TITLE = REFERENZPUNKT ANFAHREN ABBRECHEN(RESET AUSLÖSEN)
U   DB20.DBX 327.2; // BETRIEBSART REFERENZ
U   M 110.1;        // MASCHINENTÜR OFFEN
FP  M 122.4         // FLANKENMERKER RESET AUSLÖSEN
O   M 122.4         // FLANKENMERKER RESET AUSLÖSEN
O   DB1.DBX 1440.0  // NC-RESET AUSLÖSEN
=   DB1.DBX 1440.0  // NC-RESET AUSLÖSEN
NETWORK
TITLE = REFPKT.X,Y,Z, VON MST ZUGLEICH ANFAHREN
UN  M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   DB20.DBX 830.4; // VON MST ALLE ACHSEN REFERENZIEREN
U   DB20.DBX 327.2; // BETRIEBSART REFERENZ
FP  M 121.0         // FM REFPKT.X,Y,Z, VON MST ZUGLEICH ANFAHREN
U   M 121.0         // FM REFPKT.X,Y,Z, VON MST ZUGLEICH ANFAHREN
U   M 120.1;        // REF ACHSEN FREIGABE
SPBN M1701;
SET;
=   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
=   DB20.DBX 8.0;   // REFERENZIEREN X-ACHSE
=   DB20.DBX 8.1;   // REFERENZIEREN Y-ACHSE
=   DB20.DBX 8.2;   // REFERENZIEREN Z-ACHSE
UN  M 124.1         // Werkzeugmagazin referenziert
=   M 124.3         // Referenzieren Werkzeugmagazin nach rechts schwenken
M1701: NOP 0;
NETWORK
TITLE = REFPKT.X,Y,Z,4,ZUGLEICH ANFAHREN AUSGELÖST VON PC-TASTE NUM 5 ODER MST-TASTE S228/156
UN  M 100.2;        // VORSCHUBSCHALTER AUF 0%
UN  M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.1;        // REF ACHSEN FREIGABE
U   DB20.DBX 8.0;   // REFERENZIEREN X-ACHSE
U   DB20.DBX 8.1;   // REFERENZIEREN Y-ACHSE
U   DB20.DBX 8.2;   // REFERENZIEREN Z-ACHSE
S   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
FP  M 121.5;        // FM REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESET TASTE GEDRÜCKT
R   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
R   M 124.3         // Referenzieren Werkzeugmagazin nach rechts schwenken
R   M 119.1         // HM REFERENZIEREN VON ACHSE A ERWÜNSCHT
R   M 119.6         // EINLESEFREIGABE WEGNEHMEN (VON RUNDACHSE)
NETWORK
TITLE = ALARME/REFERENZPUNKTVERLUST
O   M 121.5;        // FM REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
FP  M 104.6;
U   M 104.6;
=   DB20.DBX 348.0; // REFERENZPUNKT DEAKTIVIEREN
NETWORK
TITLE = REFPKT.ANFAHREN SPERREN
U   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN
UN  M 120.1;        // REF ACHSEN FREIGABE
R   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN
S   DB20.DBX 318.7; // NEG.QUITTUNG DNC REFPKT.ANFAHREN
NETWORK
TITLE = Z-ACHSE REFERENZIEREN (FC-CODE)
U   M 121.5;        // FM REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   DB20.DBX 8.2;   // REFERENZIEREN Z-ACHSE
O(;
UN  M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.1;        // REF ACHSEN FREIGABE
U   DB20.DBX 8.2;   // REFERENZIEREN Z-ACHSE
);
=   DB20.DBX 8.2;   // REFERENZIEREN Z-ACHSE
R   DB20.DBX 8.0;   // REFERENZIEREN X-ACHSE
R   DB20.DBX 8.1;   // REFERENZIEREN Y-ACHSE
NETWORK
TITLE = FM REFERENZPUNKT Z-ACHSE AKTIV
U   DB1.DBX 134.2;  // REFERENZPUNKT Z-ACHSE AKTIV
FP  M 120.7;        // FM REFERENZPUNKT Z-ACHSE AKTIV
NETWORK
TITLE = X-ACHSE REFERENZIEREN (FC-CODE)
U   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.7;        // FM REFERENZPUNKT Z-ACHSE AKTIV
O(;
UN  M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.1;        // REF ACHSEN FREIGABE
U   DB20.DBX 8.0;   // REFERENZIEREN X-ACHSE
);
=   DB20.DBX 8.0;   // REFERENZIEREN X-ACHSE
SPBN   M702;
SET;
=   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
M702:  NOP 0;
NETWORK
TITLE = Y-ACHSE REFERENZIEREN (FC-CODE)
U   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.7;        // FM REFERENZPUNKT Z-ACHSE AKTIV
O(;
UN  M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.1;        // REF ACHSEN FREIGABE
U   DB20.DBX 8.1;   // REFERENZIEREN Y-ACHSE
);
=   DB20.DBX 8.1;   // REFERENZIEREN Y-ACHSE
SPBN   M703;
SET;
=   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
M703:  NOP 0;
NETWORK
TITLE = FM REFERENZPUNKT AKTIV
U   DB20.DBX 324.4; // REFERENZPUNKT AKTIV
FP  M 122.5;        // FM REFERENZPUNKT AKTIV
U   M 122.5;        // FM REFERENZPUNKT AKTIV
R   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
NETWORK
TITLE = 7017  REFERENZPUNKT ANFAHREN
U   M 110.0;        // AUX-ON
UN  DB20.DBX 324.4; // REFERENZPUNKT AKTIV
U   M 111.0;        // ACHSEN BETRIEBSBEREIT
=   M 18.1;         // HM 7017  REFERENZPUNKT ANFAHREN
END_FUNCTION
FUNCTION FC  7 : VOID
NAME: AUX_ON
BEGIN
NETWORK
TITLE = 6024 ALARM MASCHINENTÜR OFFEN
;       UN  M 55.7;         // FREIGABE ZUM TÜRENTRIEGELN
;       U   M 110.1;        // MASCHINENTÜR OFFEN
;;       UN  M 113.0;        // SONDERBETRIEB AKTIV
;       UN  DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
;       UN  T 25;           // SONDERBETRIEB AKTIV 800MS AUSSCHALTVERZ.
;       U   M 110.0;        // AUX-ON
;       S   DB2.DBX 3.0;    // 6024 ALARM MASCHINENTÜR OFFEN
;
;       O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (PC/MSTT)
;       O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
;       R   DB2.DBX 3.0;    // 6024  MASCHINENTÜR OFFEN
NETWORK
TITLE = SUM ALARME 1
L   DB2.DBW 0;      // WORT/ALARME 1
L   W#16#0;         // LADE KONSTANTE HEX 0
<>I;                // VERGLEICH AUF UNGLEICH
=   M 105.0;        // SUM ALARME 1
NETWORK
TITLE = SUM ALARME 2
L   DB2.DBW 2;      // WORT/ALARME 2
L   W#16#0;         // LADE KONSTANTE HEX 0
<>I;                // VERGLEICH AUF UNGLEICH
=   M 105.1;        // SUM ALARME 2
NETWORK
TITLE = SUM ALARME 3/THERM. ALARME
L   DB2.DBW 4;      // BYTE/ALARME 3
L   W#16#0;         // LADE KONSTANTE HEX 0
<>I;                // VERGLEICH AUF UNGLEICH
=   M 105.2;        // SUM ALARME 3/THERM. ALARME
NETWORK
TITLE = SUM ALARME 5
L   DB2.DBW 6;      // WORT/ALARME 5
L   W#16#0;         // LADE KONSTANTE HEX 0
<>I;                // VERGLEICH AUF UNGLEICH
=   M 105.3;        // SUM ALARME 5
NETWORK
TITLE = SUM ALARME 6
L   DB2.DBW 8;      // WORT/ALARME 6
L   W#16#0;         // LADE KONSTANTE HEX 0
<>I ;               // VERGLEICH AUF UNGLEICH
=   M 105.4;        // SUM ALARME 6
NETWORK
TITLE = SUM ALARME 7
L   DB2.DBW 10;      // WORT/ALARME 7
L   W#16#0;         // LADE KONSTANTE HEX 0
<>I ;               // VERGLEICH AUF UNGLEICH
=   M 105.5;        // SUM ALARME 7
NETWORK
TITLE = FM TASTE AUX-ON (MSTT/PC)
;       U   DB1.DBX 1374.4; // TASTE AUX-ON (PC)
;       UN  DB1.DBX 1374.3; // TASTE AUX-OFF (PC)
U   DB20.DBX 832.6  // TASTE AUX-ON (MSTT)
UN  DB20.DBX 832.4  // TASTE AUX-OFF (MSTT)
FP  M 110.5;        // FM TASTE AUX-ON
NETWORK
TITLE = FM TASTE AUX-OFF (MSTT/PC)
;       U   DB1.DBX 1374.3; // TASTE AUX-OFF (PC)
;       UN  DB1.DBX 1374.4; // TASTE AUX-ON (PC)
U   DB20.DBX 832.4  // TASTE AUX-OFF (MSTT)
UN  DB20.DBX 832.6  // TASTE AUX-ON (MSTT)
FP  M 110.7;        // FM TASTE AUX-OFF
NETWORK
TITLE = ANWAHL AUX-ON
;       U   DB1.DBX 1374.4; // TASTE AUX-ON (PC)
;       UN  DB1.DBX 1374.3; // TASTE AUX-OFF (PC)
U   DB20.DBX 832.6  // TASTE AUX-ON (MSTT)
UN  DB20.DBX 832.4  // TASTE AUX-OFF (MSTT)
O   M 168.1;        // ROBOTIK/FM AUX-ON
O   DB1.DBX 1382.4; // DNC/AUX-ON
UN  M 91.1;         // SUM ALARME  AFG/EFG
;       U   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
UN  M 11.0          // HM NOT AUS Test Ersteinschaltung
UN  M 11.4          // HM Tür auf/zu-Test Ersteinschaltung
UN  M 110.2          // HM AUX OFF schalten
UN  M 11.5          // Warten auf USBSPS nicht NOT-AUS
S   M 110.0;        // AUX-ON
NETWORK
TITLE = FM AUX-ON
U   M 110.0;        // AUX-ON
FP  M 110.3;        // FM AUX-ON
NETWORK
TITLE = ABWAHL AUX-ON
UN  DB20.DBX 324.0; // PROGRAMM LÄUFT
UN  M 20.0;         // HAUPTANTRIEB LÄUFT (AUSGANG BEDIENMODUL X1:9)
UN  M 131.7;        // ACHSEN IN BEWEGUNG
UN  M 152.0         // WERKZEUGWENDEN AKTIV
U(  ;
O   M 110.7;        // FM TASTE AUX-OFF
O   DB1.DBX 1382.3; // DNC/ AUX-OFF
)   ;
O   DB2.DBX 0.0;    // 6000  NOT-AUS
O   DB1.DBX 1370.1; // FATALER FEHLER AUFGETRETEN
;       O   DB2.DBX 0.6;    // 6006  NOT AUS-Relais K1 auf Sicherheitsmodul nicht Abgefallen
;       O   DB2.DBX 1.1;    // 6009  AUSGABEMODUL NICHT DURCHGESCHALTET
;       O   DB2.DBX 3.0;    // 6024  ALARM MASCHINENTÜR OFFEN
O   DB2.DBX 8.3;    // 6067 KEINE DRUCKLUFT
O   DB2.DBX 8.4;    // 6068 Hauptmotor Übertemperatur
O   DB2.DBX 0.5;    // 6005 K2 oder K3 auf Sicherheitsmodul nicht Abgefallen
O   DB2.DBX 0.7;    // 6007 VERSORGUNGSSCHÜTZE NICHT ABGEFALLEN
O   DB2.DBX 1.2;    // 6010 ANTRIEB X-ACHSE N. BEREIT
O   DB2.DBX 9.4;    // 6076 ANTRIEB Y-ACHSE N. BEREIT
O   DB2.DBX 1.4;    // 6012 ANTRIEB Z-ACHSE N. BEREIT
O   DB2.DBX 1.5;    // 6013 HAUPTANTRIEB NICHT BEREIT
O   DB2.DBX 2.2;    // 6018 K4 oder K5 auf Sicherheitsmodul oder AS-Signale nicht Abgefallen
O   DB2.DBX 2.3;    // 6019 UE Modul nicht betriebsbereit
O   DB2.DBX 3.2;    // 6026 Störung Kühlmittelpumpe
O   DB2.DBX 9.5;    // 6077 SCHRAUBSTOCK NICHT BEREIT
O   DB2.DBX 9.6;    // 6078 Schutzschalter Werkzeugmagazin ausgelöst
O   DB2.DBX 9.7;    // 6079 Schutzschalter Werkzeuwechsler ausgelöst
O   DB2.DBX 10.0;   // 6080 Druckschalter Klemmung TANI fehlt
O   DB2.DBX 10.1;   // 6081 Klemmung TANI nicht offen
O   DB2.DBX 10.2;   // 6082 AS-Signalüberwachung X/Y-Achse
O   DB2.DBX 10.3;   // 6083 AS-Signalüberwachung Hauptspindel / Z-Achse
O   DB2.DBX 10.4;   // 6084 Störung AS/Signal UE-Modul
O   DB2.DBX 10.7;   // 6087 ANTRIEB A-ACHSE N. BEREIT
O   DB2.DBX 11.0;   // 6088 Schutzschalter Türsteuergerät ausgelöst
O   DB2.DBX 11.1;   // 6089 ANTRIEB B-ACHSE N. BEREIT
O   DB2.DBX 11.2;   // 6090 Spänefördererschütz nicht abgefallen
O   DB2.DBX 11.3;   // 6091 Türautomatikschütz nicht abgefallen
O   DB2.DBX 11.5;   // 6093 AS-Signalüberwachung A-Achse
S   M 110.2         // HM AUX OFF schalten
UN  E 0.0;          // X-ACHSE SERVO-READY
UN  E 0.1;          // Y-ACHSE SERVO-READY
UN  E 0.2;          // Z-ACHSE SERVO-READY
UN  E 5.5           // Bremse öffnen von Z-Achsreglerbaugruppe
UN  A 0.0           // X-ACHSE Reglerfreigabe
UN  A 0.1           // Y-ACHSE Reglerfreigabe
UN  A 0.2           // Z-ACHSE Reglerfreigabe
U   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
U   M 110.2         // HM AUX OFF schalten
R   M 110.0;        // AUX-ON
R   M 110.2         // HM AUX OFF schalten
NETWORK
TITLE = Deaktivierung der stehenden Achsen
UN  M 110.0;        // AUX-ON
=   DB1.DBX 1734.0  // Deaktivierung absolute Achse X
=   DB1.DBX 1734.1  // Deaktivierung absolute Achse Y
=   DB1.DBX 1734.2  // Deaktivierung absolute Achse Z
=   DB1.DBX 1734.4  // Deaktivierung absolute Achse 4 (A-Rundachse)
NETWORK
TITLE = AUX-ON  SFG
ON  M 110.0;        // AUX-ON
O   M 110.1;        // MASCHINENTÜR OFFEN
=   M 94.0;         // AUX-ON / MASCHINENTÜR OFFEN  SFG
NETWORK
TITLE = AUX-ON   AFG/EFG
ON  M 110.0;        // AUX-ON
;       ON  M 50.3;         // FREIGABEKREIS AUF AUSGABEMODUL DURCHGESCHALTET
=   M 90.0;         // AUX-ON   AFG/EFG
NETWORK
TITLE = MELDUNG 7016  HILFSANTRIEBE EINSCHALTEN
UN  M 110.0;        // AUX-ON
=   M 18.0          // HM MELDUNG 7016  HILFSANTRIEBE EINSCHALTEN
NETWORK
TITLE = SPS/OB-->NOT-AUS AKTIV
U   DB2.DBX 0.0;    // 6000  NOT-AUS
=   DB1.DBX 1390.5; // SPS/RM-->NOT-AUS AKTIV
NETWORK
TITLE = SPS/OB-->MASCHINENTÜR GESCHL.
U   M 12.0          // Tür zu und Verriegelt
=   DB1.DBX 1390.1; // SPS/RM-->MASCHINENTÜR GESCHL.
NETWORK
TITLE = SPS/OB-->AUX-ON
U   M 110.0;        // AUX-ON
=   DB1.DBX 1390.2; // SPS/RM-->AUX-ON
NETWORK
TITLE = AUX-ON/NEG.DNC-QUITTUNG
U   DB1.DBX 1366.0; // DNC-BETRIEB AKTIV
U(  ;
U   DB1.DBX 1382.4; // DNC/AUX-ON
UN  M 110.0;        // AUX-ON
O   ;
U   DB1.DBX 1382.3; // DNC/AUX-OFF
U   M 110.0;        // AUX-ON
)   ;
S   DB1.DBX 1398.1; // AUX-ON/NEG.DNC-QUITTUNG
END_FUNCTION
FUNCTION FC  8 : VOID
NAME: NC_STARTVERR.UND BEDIENTASTEN
BEGIN
NETWORK
TITLE = AFG/EFG/SFG/ALARM AKTIV
U   M 104.2;        // SUM  SFG
U   DB20.DBX 340.4; // ACHSFREIGABE
U   DB20.DBX 340.3; // EINLESEFREIGABE
UN  M 138.4;        // SCHRAUBSTOCK OFFEN
=   M 103.1;        // NC-START FREIGABE VON AFG,EFG,SFG
ON  M 103.1;        // NC-START FREIGABE VON AFG,EFG,SFG
O   DB1.DBX 1366.3; // MASCHINENALARM AKTIV
=   M 96.0;         // NC-STARTVERRIEGELUNG
NETWORK
TITLE = SCHLÜSSELSCHALTER BETRIEBSART AUTOMATIK/HAND
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
=   M 96.1;         // NC-STARTVERRIEGELUNG
NETWORK
TITLE = PNEUMATIKÖLER LEER
U   M 18.3          // HM MELDUNG 7019  PNEUMATIKÖLER LEER
=   M 96.2;         // NC-STARTVERRIEGELUNG
NETWORK
TITLE = SOLLSTÜCKZAHL ERREICHT
U   M 21.3;         // HM 7043  SOLLSTÜCKZAHL ERREICHT
=   M 96.3;         // NC-STARTVERRIEGELUNG
NETWORK
TITLE = AUFFANGSCHALEÜBERWACHUNG
U   M 18.6;         // HM MELDUNG 7022  AUFFANGSCHALEÜBERWACHUNG
=   M 96.4;         // NC-STARTVERRIEGELUNG
NETWORK
TITLE = SCHMIERMITTEL UND LÜFTER HA
O   M 19.2;         // HM 7026 Störung Lüfter Hauptmotor
O   M 20.6;         // HM 7038 DRUCKSCHALTER SCHM. DEFEKT
O   M 20.7;         // HM 7039 SCHMIERMITTEL DEFEKT
O   M 23.5          // HM 7082 Schutzschalter Späneförderer ausgelöst
=   M 96.5;         // NC-STARTVERRIEGELUNG
NETWORK
TITLE = REFERENZPUNKT NICHT AKTIV
UN  DB20.DBX 324.4; // REFERENZPUNKT AKTIV
=   M 96.6;         // NC-STARTVERRIEGELUNG
;      M 97.0;         // NC-START VERR.Werkzeugwender nicht in Grundstellung
NETWORK
TITLE = SUM NC-START VERR.
L   MW 96;          // WORT/NC-STARTVERR.
L   W#16#0;         // LADE KONSTANTE HEX 0
<>I ;               // VERGEICH AUF UNGEICH
=   M 104.3;        // SUM NC-STARTVERR.
NETWORK
TITLE = 7018  SCHLÜSSEL UMSCHALTEN
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
=   M 18.2;         // HM 7018  SCHLÜSSEL UMSCHALTEN
NETWORK
TITLE = FM CYCLE-START TASTE VON BEDIENPULT UND RAFITASTE
O   DB20.DBX 831.5  // CYCLE-START TASTE VON BEDIENPULT
O   DB20.DBX 833.3  // RAFI Tastatur X3.1/2 CYCLE START Taste
FP  M 106.5;        // FM CYCLE-START TASTE VON BEDIENPULT
NETWORK
TITLE = CYCLE-START VON TASTEN,ROBOTIK UND NACH TÜR ZU
O   M 106.5;        // FM CYCLE-START TASTE VON BEDIENPULT
O   M 146.3         // CYCLE START NACH TÜR ZU
O   M 168.7;        // ROBOTIK/FM NC-START
UN  M 104.3;        // SUM  NC-STARTVERR.
=   DB20.DBX 3.6;   // NC-START ANFORDERUNG
R   T 72
NETWORK
TITLE = FM NC-STOPP TASTE VON BEDIENPULT oder HM M0/M1 DYNAMISCH
O   DB20.DBX 831.4  // NC-STOPP TASTE VON BEDIENPULT
FP  M 105.6;        // FM NC-STOPP TASTE VON BEDIENPULT
O   M 105.6;        // FM NC-STOPP TASTE VON BEDIENPULT
O   M 34.1;         // HM M0/M1 DYNAMISCH
O   DB20.DBX 0.1;   // NC-STOPP ANFORDERUNG
=   DB20.DBX 0.1;   // NC-STOPP ANFORDERUNG
NETWORK
TITLE = FM RESET TASTE VON BEDIENPULT
O   DB20.DBX 828.6  // RESET TASTE VON BEDIENPULT
FP  M 105.7;        // FM RESET TASTE VON BEDIENPULT
O   M 105.7;        // FM RESET TASTE VON BEDIENPULT
O   DB20.DBX 4.0;   // RESET ANFORDERUNG
=   DB20.DBX 4.0;   // RESET ANFORDERUNG
NETWORK
TITLE = NC-START/STARTVERR.
O   DB20.DBX 0.2;   // FC NC-START
U(
O   DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
O   DB20.DBX 326.2; // BETRIEBSART MDI
)
UN  M 104.3;        // SUM  NC-STARTVERR.
=   DB20.DBX 0.2;   // FC NC-START
NETWORK
TITLE = NC-START/NEG.DNC-QUITTUNG
U   DB1.DBX 1366.0; // DNC-BETRIEB AKTIV
U   DB20.DBX 0.2;   // FC NC-START
U   M 104.3;        // SUM  NC-STARTVERR.
S   DB20.DBX 319.0; // NC-START/NEG.DNC-QUITTUNG
END_FUNCTION
FUNCTION FC  9 : VOID
NAME: RESET
BEGIN
NETWORK
TITLE = /FM AUX-ON
U   M 110.0;        // AUX-ON
FN  M 109.5;        // /FM AUX-ON
NETWORK
TITLE = /FM AUX-ON/WORT RESET
U   M 109.5;        // /FM AUX-ON
=   M 98.1;         // /FM AUX-ON/WORT RESET
NETWORK
TITLE = /FM SONDERBETRIEB AKTIV
U   M 113.0;        // SONDERBETRIEB AKTIV
FN  M 113.7;        // /FM SONDERBETRIEB AKTIV
NETWORK
TITLE = /FM SONDERBETRIEB/WORT RESET
U   M 113.7;        // /FM SONDERBETRIEB AKTIV
=   M 98.3;         // /FM SONDERBETRIEB/WORT RESET
NETWORK
TITLE = SUM RESET
L   MW 98;          // LADE WORT RESET
L   W#16#0;         // LADE KONSTANTE HEX 0
<>I ;               // VERGLEICH AUF UNGLEICH
=   M 104.4;        // SUM RESET
NETWORK
TITLE = FC RESET/SPS
O   M 104.4;        // SUM RESET
S   DB1.DBX 1440.0; // FC RESET/SPS
END_FUNCTION
FUNCTION FC 10 : VOID
NAME: BEI_PROGRAMM_ENDE_RESET_ODER_NEUSTART
BEGIN
NETWORK 1
TITLE = DAUERLAUF BIT FREISCHALTEN
U   DB10.DBX 1.0;   // SPS-MSD DAUERLAUF-BIT
UN  DB10.DBX 77.2;  // LADEMAGAZIN AKTIVIEREN
=   M 10.2          // NEUSTART NACH PROGRAMMENDE
NETWORK 2
TITLE = LED 1x TASTE AKTIVIEREN
U   DB10.DBX 77.2;  // LADEMAGAZIN UND 1x TASTE AKTIVIEREN
SPB   M1001
=   M 8.2           // 1X AKTIV
M1001:  NOP 0;
NETWORK 3
TITLE = NEUSTART VON 1X TASTE UND VON M30,M32
// M30 UND M32 SIND NICHT IM SELBEN PLC-CYCLUS AKTIV !!!!!!!!
U   DB20.DBX 195.6; // M30 DYN.
U   DB10.DBX 77.2;  // LADEMAGAZIN AKTIVIEREN
UN  M 8.2           // 1X AKTIV
S   M 10.6          // HM M30 DYN.
U   DB20.DBX 196.0; // M32 DYN.
U   DB10.DBX 77.2;  // LADEMAGAZIN AKTIVIEREN
UN  M 8.2           // 1X AKTIV
S   M 10.7          // HM M32 DYN.
U   M 10.6          // HM M30 DYN.
U   M 10.7          // HM M32 DYN.
S   M 10.2          // NEUSTART NACH PROGRAMMENDE
NETWORK 4
TITLE = PROGRAMM ENDE AN NC
U   M 10.1;         // HM
UN  M 10.2          // NEUSTART NACH PROGRAMMENDE
=   DB20.DBX 348.1; // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT
R   M 10.1;         // HM
R   M 10.6          // HM M30 DYN.
R   M 10.7          // HM M32 DYN.
NETWORK 5
TITLE = PROGRAMM ENDE UND NEUSTART (DAUERLAUF) AN NC
U   M 10.1;         // HM
U   M 10.2          // NEUSTART NACH PROGRAMMENDE
UN  M 104.3;        // SUM  NC-STARTVERR.
U   DB20.DBX 340.3; // EINLESEFREIGABE
R   M 10.1;         // HM
=   DB20.DBX 348.2; // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT UND GESTARTET
R   M 10.2          // NEUSTART NACH PROGRAMMENDE
R   M 10.6          // HM M30 DYN.
R   M 10.7          // HM M32 DYN.
NETWORK 6
TITLE = ISTDREHZAHL KLEINER 20U/MIN
O   DB20.DBX 192.2; // M2 DYN.
O   DB20.DBX 195.6; // M30 DYN.
S   M 10.0;         // HM
U   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
UN  M 155.3         // SOLLZÄHLER mit neuem T-WORT geladen
U   M 10.0;         // HM ENDE ODER NEUSTART
S   M 10.1;         // HM
R   M 10.0;         // HM ENDE ODER NEUSTART
NETWORK 7
TITLE = M2 M30 und M32 RÜCKMELDEN VON M2,M30,M32 UND RESET
O   DB20.DBX 348.1; // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT
O   DB20.DBX 348.2; // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT UND GESTARTET
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUSGELÖST
R   M 10.0;         // HM ENDE ODER NEUSTART
R   M 10.1;         // HM
R   M 10.2          // NEUSTART NACH PROGRAMMENDE
R   M 10.6          // HM M30 DYN.
R   M 10.7          // HM M32 DYN.
=   DB20.DBX 259.6; // M30 RÜCKMELDEN
=   DB20.DBX 260.0; // M32 RÜCKMELDEN
=   DB20.DBX 256.2; // M2 RÜCKMELDEN
NETWORK
TITLE =Signale mit M96 H0 von Teileprogramm an PLC (M96 VON CYCLUS L700)
M003:  ON  DB20.DBX 204.0;// M96
SPB M1002;
NETWORK
TITLE =BM7043 Sollstückzahl erreicht
L   DB20.DBW 186;  // H-Wort (word)
L   0;
==I ;
S   M 21.3;         // HM 7043  SOLLSTÜCKZAHL ERREICHT
M1002: NOP 0
END_FUNCTION
FUNCTION FC 14: VOID
NAME: PLC NC-ÜBERGABE UND AFG/EFG/SFG
BEGIN
NETWORK
TITLE = SUM MELDUNGEN  AFG/EFG
;//  BELEGUNG VON MELDUNGSWORT DB3.DBW 0:
;//  DB3.DBX 0.0;  7000  FALSCHES T-WORT
L   DB3.DBW 0;      // WORT/MELDUNGEN  AFG/EFG
L   W#16#0;         // LADE KONSTANTE HEX 0
<>I;                // VERGLEICH AUF UNGLEICH 0
=   M 91.0;         // SUM MELDUNGEN  AFG/EFG
NETWORK
TITLE = SUM MELDUNGEN  AFG/EFG
O   M 105.0;        // SUM ALARME 1
O   M 105.1;        // SUM ALARME 2
O   M 105.2;        // SUM ALARME 3/THERM. ALARME
O   M 105.3;        // SUM ALARME 5
O   M 105.4;        // SUM ALARME 6
O   M 105.5;        // SUM ALARME 7
=   M 91.1;         // SUM ALARME  AFG/EFG
NETWORK
TITLE = AFG/EFG VON SICHERHEITSEINRICHTUNGEN
UN  M 149.0;        // FREIGABE VON SICHERHEITSEINRICHTUNGEN
=   M 92.0;         // AFG/EFG VON SICHERHEITSEINRICHTUNGEN
NETWORK
TITLE = ACHSFREIGABE
UN  M 90.0;         // AUX-ON   AFG/EFG
UN  M 90.1;         // ACHSEN BETRIEBSBEREIT  AFG/EFG
UN  M 90.3;         // WZW  AFG/EFG
UN  M 90.4;         // AWZ EINKUPPELANFORDERUNG
UN  M 90.5;         // AWZ LAUFT AUS  AFG/EFG
UN  M 90.6;         // HA LAUFT AUS  AFG/EFG
UN  M 90.7          // Werkzeugwenden aktiv
UN  M 91.0;         // SUM MELDUNGEN  AFG/EFG
UN  M 91.1;         // SUM ALARME  AFG/EFG
UN  M 91.2;         // ROBOTIK VORSCHUB-STOP AFG/EFG
UN  M 91.3;         // TÜRBEWEGUNG AKTIV  AFG/EFG
UN  M 91.4;         // /Normaler/ ACHSFREIGABE/EINLESEFREIGABE    K0
UN  M 91.6;         // VORSCHUB HALT AKTIV  AFG/EFG
//// Designed by WIMMER:
U(
ON  M 91.7;         // SPINDEL HALT AKTIV  AFG/EFG
O   M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
)
////
UN  M 92.0;         // AFG/EFG VON SICHERHEITSEINRICHTUNGEN
UN  M 80.2;         // AFG EFG von Achsumschaltung Spindel0
=   M 102.0;        // SUM ACHSFREIGABE
U   M 102.0;        // SUM ACHSFREIGABE
=   DB20.DBX 340.4; // ACHSFREIGABE
NETWORK
TITLE = EINLESEFREIGABE
UN  M 90.0;         // AUX-ON   AFG/EFG
UN  M 90.2;         // EINLESEFREIGABE WEGNEHMEN (VON RUNDACHSE)
UN  M 90.1;         // ACHSEN BETRIEBSBEREIT  AFG/EFG
UN  M 90.3;         // WZW  AFG/EFG
UN  M 90.4;         // AWZ EINKUPPELANFORDERUNG
UN  M 90.5;         // AWZ LAUFT AUS  AFG/EFG
UN  M 90.6;         // HA LAUFT AUS  AFG/EFG
UN  M 90.7          // Werkzeugwenden aktiv
UN  M 91.0;         // SUM MELDUNGEN  AFG/EFG
UN  M 91.1;         // SUM ALARME  AFG/EFG
UN  M 91.3;         // TÜRBEWEGUNG AKTIV  AFG/EFG
UN  M 91.4;         // /Normaler/ ACHSFREIGABE/EINLESEFREIGABE    K0
UN  M 91.5;         // SCHRAUBSTOCKBEW. AKTIV  AFG/EFG
UN  M 91.7;         // SPINDEL HALT AKTIV  AFG/EFG
UN  M 92.0;         // AFG/EFG VON SICHERHEITSEINRICHTUNGEN
UN  M 80.2;         // AFG EFG von Achsumschaltung Spindel0
=   M 104.0;        // SUM EINLESEFREIGABE
U   M 104.0;        // SUM EINLESEFREIGABE
=   DB20.DBX 340.3; // EINLESEFREIGABE
NETWORK
TITLE = Freigaben für Betriebsart Repos
U   DB20.DBX 324.0; // PROGRAMM LÄUFT
U   DB20.DBX 324.1; // PROGRAMM ANGEHALTEN
U   DB20.DBX 326.3; // BETRIEBSART REPOS
SPBN     M1402;
=   DB25.DBX 340.3; // EINLESEFREIGABE K1
=   DB25.DBX 340.4; // ACHSFREIGABE    K1
M1402: NOP   0;
NETWORK
TITLE = SUM  SFG
;//    BELEGUNG VON MW 94 (SFG) :     8*************
;//    M 94.0;+++      MASCHINENTÜR OFFEN oder AUX-OFF
;//    M 94.1;+++      UE Modul BETRIEBSBEREIT
;//    M 94.2;+++      Werkzeug ausgestossen
;//    M 94.3;???      SPANNMITTEL KEIN TEIL GESPANNT
;//    M 94.4;???      SPANNMITTEL OFFEN
;//    M 94.5;+++      FALSCHES T-WORT
;//    M 94.7          Werkzeugwender nicht in Grundstellung
;//    M 95.0
;//    M 95.7;      SPINDEL HALT AKTIV
;//    M 80.4;      SPINDEL0 SPINDELBETRIEB
;//    M 115.4;     SPINDELFREIGABE nur für den ACC wegnehmen im Werkzeugwenderbetrieb
L   MW 94;          // WORT  SFG
L   W#16#0;         // LADE KONSTANTE HEX 0
==I;                // VERGLEICH AUF GLEICH
=   M 104.2;        // SUM  SFG
NETWORK
TITLE = SPINDELFREIGABE SPINDEL 0
U   M 104.2;        // SUM  SFG
U   M 145.3         // REGLERFREIGABE FÜR HAUPTANTRIEB
U   M 80.4;         // SPINDEL0 SPINDELBETRIEB
L   S5T#200MS;      //
SE  T 55
U   T 55
UN  DB20.DBX 324.2; // DRYRUN AKTIV
UN  M 115.4;        // SPINDELFREIGABE Rundachsbetrieb K0
=   DB20.DBX 340.5; // SPINDELFREIGABE SPINDEL 0
NETWORK
TITLE = SPINDELFREIGABE Kanal1 SPINDEL 0
CLR
=   DB25.DBX 340.5; // SPINDELFREIGABE SPINDEL 0
NETWORK
TITLE = SPS/OB-->WERKZEUGW. AKTIV
U   DB10.DBX 75.5;  // SPS-MSD /WERKZEUGWENDER
=   DB20.DBX 382.0; // SPS/RM-->WERKZEUGW. AKTIV
NETWORK
TITLE = SPS/OB-->TÜRAUTOMATIK AKTIV
U   DB10.DBX 75.0;  // SPS-MSD /TÜRAUTOMATIK
=   DB1.DBX 1418.0; // SPS/RM-->TÜRAUTOMATIK AKTIV
NETWORK
TITLE = SPS/OB-->PNEUM. SCHRAUBSTOCK AKTIVIEREN
U   DB10.DBX 75.1   // PNEUM. SCHRAUBSTOCK AKTIVIEREN
=   DB20.DBX 382.1; // SPS/RM--> PNEUM. SCHRAUBSTOCK AKTIVIEREN
NETWORK
TITLE = SPS/OB-->AUSBLASEINR. AKTIV
U   DB10.DBX 75.3;  // SPS-MSD /AUSBLASEN
=   DB20.DBX 382.4; // SPS/RM-->AUSBLASEINR. AKTIV
NETWORK
TITLE = SPS/OB-->KÜHLMITTEL AKTIV
SET;
=   DB20.DBX 382.3; // SPS/RM-->KÜHLMITTEL AKTIV
NETWORK
TITLE = SUM SPINDEL EIN/AUS
UN  DB20.DBX 326.1; // BETRIEBSART EDIT
UN  DB20.DBX 326.2; // BETRIEBSART MDI
UN  DB20.DBX 326.3; // BETRIEBSART REPOS
UN  DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
UN  DB20.DBX 327.2; // BETRIEBSART REFERENZ
UN  DB20.DBX 327.4; // BETRIEBSART PRESET
=   M 104.5;        // HM BETRIEBSART JOG UND INC
END_FUNCTION
FUNCTION FC 12: VOID
NAME: SONDERBETRIEB
BEGIN
//  DB20.DBX 833.0  = M 50.1  RAFI Tastatur X2.1/2 Schlüsselschalter auf Sonderbetrieb
//  DB20.DBX 834.0  = M 51.2  RAFI Tastatur X6.1/2 Zustimmtaste
//  X422:2   E 3.2  = M 51.2  Zustimmtaste Handrad
NETWORK
TITLE = FG SONDERBETRIEB/SICHERHEITSSCH.
U   M 110.0;        // AUX-ON
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
U   M 51.2;         // ZUSTIMMTASTE GEDRÜCKT
U   M 110.1;        // MASCHINENTÜR OFFEN
UN  DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
UN  E 2.0;          // TÜR geschlossen von Türsicherheitsverriegelung ÜK
S   M 113.0;        // SONDERBETRIEB AKTIV
NETWORK
TITLE = Sonderbetrieb nach loslassen der Zustimmtaste oder Schlüsselschalter beenden
ON  M 51.2;         // ZUSTIMMTASTE GEDRÜCKT
ON  M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
U   M 113.0;        // SONDERBETRIEB AKTIV
=   M 113.1;        // Anwahl Sonderbetrieb beenden
;       UN  E 0.0;          // X-ACHSE SERVO-READY
;       UN  E 0.1;          // Y-ACHSE SERVO-READY
UN  E 0.2;          // Z-ACHSE SERVO-READY
UN  E 5.5           // Bremse öffnen von Z-Achsreglerbaugruppe
U   M 113.1;        // Anwahl Sonderbetrieb beenden
R   M 113.0;        // SONDERBETRIEB AKTIV
NETWORK
TITLE = MELDUNG 7020  SONDERBETRIEB AKTIV
U   M 113.0;        // SONDERBETRIEB AKTIV
=   M 18.4;         // HM MELDUNG 7020  SONDERBETRIEB AKTIV
NETWORK
TITLE = SONDERBETRIEB AKTIV
U   M 113.0;        // SONDERBETRIEB AKTIV
L   S5T#800MS;      // 8X0,1S
SA  T 25;           // SONDERBETRIEB AKTIV 800MS AUSSCHALTVERZ.
NETWORK
TITLE = SB AKTIV/VORSCHUBBEGR. 2M/MIN
U   M 113.0;        // SONDERBETRIEB AKTIV
=   DB20.DBX 340.2; // SB AKTIV/VORSCHUBBEGR.FÜR AC 2M/MIN
NETWORK
TITLE = SPS/OB-->HANDBETRIEB EIN
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
=   DB1.DBX 1390.6; // SPS/RM-SCHLÜSSELSCHALTER AUF SONDERBETRIEB
END_FUNCTION
FUNCTION FC 13: VOID
NAME: INITIALISIERUNGEN ÜBERWACHUNGEN UND ALARME
BEGIN
;NETWORK
;TITLE = Abbruch bei nicht getestete Oberflaechen  ;8+++++++++
;      O     DB1.DBX 1402.2;     // Emcotronic TM02
;      O     DB1.DBX 1402.3;     // NUM
;      O     DB1.DBX 1402.4;     // PAL
;      O     DB1.DBX 1402.5;     // Heidenhain TNC355
;      O     DB1.DBX 1403.1;     // Techsoft
;      S     DB2.DBX 0.2;        // FM6002 SPS kein Programm geladen
;      BEB   ;                   // keine PLC-Bearbeitung
;
;      O     DB1.DBX 1402.0;     // Steuerungskennung SINUMERIK 810/820
;      O     DB1.DBX 1402.1;     // Fanuc 0TB
;      O     DB1.DBX 1403.0;     // Fanuc 21TB
;      =     M 114.0;            // 810T / 0TB / 21TB
NETWORK
TITLE = Remanente Bereiche aus SE/Daten laden
U     DB1.DBX 1370.0;     // 1. PLC-Durchlauf
SPBN     M1301;
L     DB15.DBW 20;        // Maschinenkonfig. in SE-Daten
T     MW 28;               // akt. Maschinenkonfiguration
M1301: NOP   0;
NETWORK
TITLE = SCHLÜSSELSCHALTER VORHANDEN
U   DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
S   DB1.DBX 1418.2; // SCHLÜSSELSCHALTER VORHANDEN
NETWORK
TITLE = M0,M1 AUSLÖSUNG
O   DB20.DBX 192.0; // M0 DYNAMISCH
=   DB20.DBX 256.0; // M0 RÜCKMELDEN
U   DB20.DBX 192.1; // M1 DYNAMISCH
U   DB20.DBX 324.5; // BEDINGTER HALT AKTIV
=   DB20.DBX 256.1; // M1 RÜCKMELDEN
U   DB20.DBX 192.1; // M1 DYNAMISCH
U   DB20.DBX 324.5; // BEDINGTER HALT AKTIV
O   DB20.DBX 192.0; // M0 DYNAMISCH
=   M 34.1;         // HM M0/M1 DYNAMISCH
NETWORK
TITLE = SPS/OB-->EINZELSTÜCK AKTIV
UN  DB10.DBX 1.0;   // SPS-MSD DAUERLAUF AKTIV
=   DB1.DBX 1390.3; // SPS/RM-->EINZELSTÜCK AKTIV
NETWORK
TITLE = VORSCHUBSCHALTER AUF 0%
L   DB20.DBW 358;   // FEED-OVERRIDE-ISTWERT
L   W#16#0000;      // LADE 0
==I;                // VERGLEICH AUF GLEICH
=   M 100.2;        // VORSCHUBSCHALTER AUF 0%
NETWORK
TITLE = VORSCHUBSCHALTER AUF >=3%
L   DB20.DBW 358;   // FEED-OVERRIDE-ISTWERT
L   30              // LADE 0
>=I;                // VERGLEICH AUF GRÖSSER/GLEICH
FP  M 100.3         // FM VORSCHUBSCHALTER GRÖSSER/GLEICH 3%
NETWORK
TITLE = VORSCHUBSCHALTER AUF >4%
L   DB20.DBW 358;   // FEED-OVERRIDE-ISTWERT
L   40              // LADE 0
>I;                 // VERGLEICH AUF GRÖSSER
=   M 100.4         // VORSCHUBSCHALTER GRÖSSER 4%
NETWORK
TITLE = ALARMAUSGANG
U   DB1.DBX 1366.3; // MASCHINENALARM AKTIV
=   M 54.3;         // AUSGANGSMERKER ALARMAUSGANGANG
NETWORK
TITLE = HAUPTANTRIEB LÄUFT
UN  M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
=   M 20.0;         // HAUPTANTRIEB LÄUFT (AUSGANG BEDIENMODUL X1:9)
NETWORK
TITLE = FREIGABE VON SICHERHEITSEINRICHTUNGEN
U   M 50.3;         // FREIGABEKREIS AUF AUSGABEMODUL DURCHGESCHALTET
U   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
UN  M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
O
UN  M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
U   M 51.2;         // ZUSTIMMTASTE GEDRÜCKT
U   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
=   M 149.0         // FREIGABE VON SICHERHEITSEINRICHTUNGEN
FN  M 149.1         // NEGATIV FLANKENMERKER AUS FREIGABE VON SICHERHEITSEINRICHTUNGEN
NETWORK
TITLE = ACHSEN IN BEWEGUNG
L   DB1.DBB 1358;   // ACHSEN IN BEWEGUNG
L   B#16#0;         // LADE KONSTANTE HEX 0
<>I;                // VERGLEICH AUF UNGLEICH
=   M 131.7;        // ACHSEN IN BEWEGUNG
NETWORK
TITLE = ALARM 6026 Störung Kühlmittelpumpe
UN  E 4.6           // Motorschutz Kühlmittel
S   DB2.DBX 3.2;    // 6026 Störung Kühlmittelpumpe
U   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 3.2;    // 6026 Störung Kühlmittelpumpe
END_FUNCTION
FUNCTION FC 16: VOID
NAME: INC_ACHSEN
BEGIN
NETWORK
TITLE = ACHSEN INC FREIGABE
U   M 102.0;        // SUM ACHSFREIGABE
=   M 106.0;        // ACHSEN INC FREIGABE
NETWORK
TITLE = ACHSEN INC/STARTVERR.
U   DB20.DBX 1.3;   // FC INC ANFORDERUNG
UN  M 106.0;        // ACHSEN INC FREIGABE
R   DB20.DBX 1.3;   // FC INC ANFORDERUNG
END_FUNCTION
FUNCTION FC 17: VOID
NAME: UE Modul Netzeinspeisung
BEGIN
//           A 5.1 // AUX ON Schütz
//  X5:3     A 4.2 // Impulsfreigabe UE-Modul und LEISTUNG Zwischenkreis
//  X5:5     E 4.4       // Rückmeldung Netzschütz UE-Modul abgefallen
//  X6:3     E 4.3       //* UE-Modul betriebsbereit
//  X5:3     E 4.2       // Überwachung AS Signal U/E
NETWORK
TITLE = ALARM 6007 VERSORGUNGSSCHÜTZE NICHT ABGEFALLEN
U   A 4.2           // Impulsfreigabe UE-Modul und LEISTUNG Zwischenkreis
U   E 4.4           // Rückmeldung Netzschütz UE-Modul abgefallen
O(
UN  A 4.2           // Impulsfreigabe UE-Modul und LEISTUNG Zwischenkreis
UN  E 4.4           // Rückmeldung Netzschütz UE-Modul abgefallen
)
L   S5TIME#1500MS
SE  T 26
U   T 26
S   DB2.DBX 0.7;    // ALARM 6007 VERSORGUNGSSCHÜTZE NICHT ABGEFALLEN
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 0.7;    // ALARM 6007 VERSORGUNGSSCHÜTZE NICHT ABGEFALLEN
NETWORK
TITLE = UE Modul
U   M 110.0;        // AUX-ON
U   E 4.4;          // Rückmeldung Netzschütz UE-Modul abgefallen
S   A 5.1           // AUX ON Schütz
S   A 4.2           // Impulsfreigabe UE-Modul und LEISTUNG Zwischenkreis
UN  M 110.0;        // AUX-ON
R   A 5.1           // AUX ON Schütz
R   A 4.2           // Impulsfreigabe UE-Modul und LEISTUNG Zwischenkreis
NETWORK
TITLE = A6084 AS-Signalüberwachung UE Modul
U   A 4.2           // Impulsfreigabe UE-Modul und LEISTUNG Zwischenkreis
U   E 4.2           // Überwachung AS Signal U/E
O(
UN  A 4.2           // Impulsfreigabe UE-Modul und LEISTUNG Zwischenkreis
UN  E 4.2           // Überwachung AS Signal U/E
)
L   S5TIME#300MS
SE  T 38
U   T 38
S   DB2.DBX 10.4;   // A6084 AS-Signalüberwachung UE Modul
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 10.4;   // A6084 AS-Signalüberwachung UE Modul
NETWORK
TITLE = UE Modul BETRIEBSBEREIT
U   E 4.3           //* UE-Modul betriebsbereit
U   A 5.1           // AUX ON Schütz
U   A 4.2           // Impulsfreigabe UE-Modul und LEISTUNG Zwischenkreis
=   M 131.0;        // UE Modul BETRIEBSBEREIT
NETWORK
TITLE = SFG VON UE Modul BETRIEBSBEREIT
UN  M 131.0;        // UE Modul BETRIEBSBEREIT
=   M 94.1;         // UE Modul BETRIEBSBEREIT
NETWORK
TITLE = UE Modul betriebsbereit
U   A 5.1           // AUX ON Schütz
U   A 4.2           // Impulsfreigabe UE-Modul und LEISTUNG Zwischenkreis
L   S5T#4S;         // 25X0,1S
SE  T 8;            // STARTEN T8 ALS EINSCHALTVERZ.
NETWORK
TITLE = ALARM 6019 UE Modul nicht betriebsbereit
UN  E 4.3           //* UE-Modul betriebsbereit
L   S5T#200MS;      // 2X0,1S
SE  T 9;            // STARTEN T9 ALS EINSCHALTVERZ.
U   T 8;            // HA ÜBERWACHUNG SCHARF
U   T 9;            // HA/SERVO-READY (0,2S)
U   M 2.0           // SUMME aller NOT-AUS Möglichkeiten
U   A 9.1           //* NOT AUS für NE Modul Klemme 64
S   DB2.DBX 2.3;    // 6019 UE Modul nicht betriebsbereit
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 2.3;    // 6019 UE MODUL NICHT BETRIEBSBEREIT
END_FUNCTION
FUNCTION FC 18: VOID
NAME: KÜHLMITTEL_M8_M9
BEGIN
NETWORK
TITLE = FM TASTE KÜHLMITTEL (MSTT/PC)
U   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
;       U   DB20.DBX 294.6; // TASTE KÜHLMITTEL (PC)
U   DB20.DBX 831.1  // TASTE KÜHLMITTEL (MSTT)
FP  M 107.7;        // FM TASTE KÜHLMITTEL
NETWORK
TITLE = ANWAHL KÜHLMITTEL EIN (M8)
UN  M 107.1;        // KÜHLMITTEL EIN
U   M 107.7;        // FM TASTE KÜHLMITTEL
O   ;
U   DB20.DBX 225.1; // M9 STATISCH
U   DB20.DBX 193.0; // M8 DYN.
UN  DB20.DBX 192.7; // M7 DYN.
O   DB20.DBX 302.6; // DNC/KÜHLMITTEL EIN
S   M 107.1;        // KÜHLMITTEL EIN
S   M 117.1;        // M7 MINIMALSCHMIERUNG EIN
=   M 107.2;        // HM KÜHLMITTEL EIN
=   DB20.DBX 257.0; // M8 RÜCKMELDEN
NETWORK
TITLE = ANWAHL MINIMALSCHMIERUNG EIN
U   DB20.DBX 225.1; // M9 STATISCH
U   DB20.DBX 192.7; // M7 DYN.
UN  DB20.DBX 193.0; // M8 DYN.
S   M 117.1;        // M7 MINIMALSCHMIERUNG EIN
=   DB20.DBX 256.7; // M7 AUSLÖSEN
NETWORK
TITLE = KÜHLMITTEL PROGRAMMIERT
U   DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
U   DB20.DBX 257.0; // M8 RÜCKMELDEN
U   DB20.DBX 193.0; // M8 DYN.
S   M 107.3;        // KÜHLMITTEL PROGRAMMIERT
NETWORK
TITLE = ABWAHL KÜHLMITTEL EIN (M9)
UN  M 107.3;        // KÜHLMITTEL PROGRAMMIERT
U   M 107.1;        // KÜHLMITTEL EIN
UN  M 107.2;        // HM KÜHLMITTEL EIN
U   M 107.7;        // FM TASTE KÜHLMITTEL
O   ;
O   DB20.DBX 193.1; // M9 DYN.
O   DB20.DBX 302.5; // DNC/KÜHLMITTEL AUS
O   ;
U   M 107.4;        // KÜHLMITTEL UNTERBRECHUNG
UN  DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
O   DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
ON  M 110.0;        // AUX-ON
O   DB20.DBX 192.2; // M2 DYN.
O   DB20.DBX 195.6; // M30 DYN.
=   DB20.DBX 257.1; // M9 RÜCKMELDEN
R   M 107.1;        // KÜHLMITTEL EIN
R   M 117.1;        // M7 MINIMALSCHMIERUNG EIN
R   M 107.3;        // KÜHLMITTEL PROGRAMMIERT
NETWORK
TITLE = KÜHLMITTEL UNTERBRECHUNG
U   M 107.3;        // KÜHLMITTEL PROGRAMMIERT
UN  M 107.4;        // KÜHLMITTEL UNTERBRECHUNG
U   M 107.7;        // FM TASTE KÜHLMITTEL
S   M 107.4;        // KÜHLMITTEL UNTERBRECHUNG
=   M 107.5;        // HM KÜHLMITTEL UNTERBRECHUNG
U   M 107.4;        // KÜHLMITTEL UNTERBRECHUNG
UN  M 107.5;        // HM KÜHLMITTEL UNTERBRECHUNG
U   M 107.7;        // FM TASTE KÜHLMITTEL
ON  M 107.1;        // KÜHLMITTEL EIN
R   M 107.4;        // KÜHLMITTEL UNTERBRECHUNG
NETWORK
TITLE = KÜHLMITTEL FREIGABE
U(  ;
U   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
;       O   DB20.DBX 294.6; // TASTE KÜHLMITTEL (PC)
O   DB20.DBX 831.1  // TASTE KÜHLMITTEL (MSTT)
)   ;
UN  M 107.4;        // KÜHLMITTEL UNTERBROCHEN
U(  ;
ON  DB20.DBX 324.1; // PROGRAMM ANGEHALTEN
O   M 20.0;         // HAUPTANTRIEB LÄUFT (AUSGANG BEDIENMODUL X1:9)
)   ;
UN  M 34.1;         // HM M0/M1 DYNAMISCH
UN  DB20.DBX 324.2; // DRYRUN AKTIV
UN  M 152.0;        // WERKZEUGWENDEN AKTIV
=   M 107.0;        // KÜHLMITTEL FREIGABE
NETWORK
TITLE = KÜHLMITTEL LEISTUNG
U   M 107.1;        // KÜHLMITTEL EIN
U   M 107.0;        // KÜHLMITTEL FREIGABE
=   A 10.7          // Kühlmittelpumpe
NETWORK
TITLE = MINIMALSCHMIERUNG LEISTUNG
U   M 117.1;        // M7 MINIMALSCHMIERUNG EIN
U   M 107.0;        // KÜHLMITTEL FREIGABE
=   A 10.6;         // AUSGANGSMERKER MINIMALSCHMIERUNG
NETWORK
TITLE = SPS/OB-->KÜHLMITTEL EIN
U   M 107.1;        // KÜHLMITTEL EIN
=   DB20.DBX 310.5; // SPS/OB-->KÜHLMITTEL EIN
NETWORK
TITLE = SPS/OB-->KÜHLMITTEL SPRITZT
U   A 10.7          // Kühlmittelpumpe
=   DB20.DBX 310.6; // SPS/RM-->KÜHLMITTEL SPRITZT
NETWORK
TITLE = KÜHLMITTEL/NEG.DNC-QUITTUNG
U   DB1.DBX 1366.0; // DNC-BETRIEB AKTIV
U(  ;
U   DB20.DBX 302.6; // DNC/KÜHLMITTEL EIN
UN  DB20.DBX 225.0; // M8 STAT.
O   ;
U   DB20.DBX 302.5; // DNC/KÜHLMITTEL AUS
UN  DB20.DBX 225.1; // M9 STAT.
)   ;
S   DB1.DBX 1398.1; // KÜHLMITTEL/NEG.DNC-QUITTUNG
END_FUNCTION
FUNCTION FC 19: VOID
NAME: SPINDEL_HALT_START
BEGIN
NETWORK
TITLE = FM TASTE SPINDEL START (MSTT/PC)
U   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
;       U   DB20.DBX 295.4; // TASTE SPINDEL START (PC)
;       UN  DB20.DBX 295.3; // TASTE SPINDEL HALT (PC)
U   DB20.DBX 832.3  // TASTE SPINDEL START (MSTT)
UN  DB20.DBX 832.1  // TASTE SPINDEL HALT  (MSTT)
FP  M 132.5;        // FM TASTE SPINDEL START
NETWORK
TITLE = FM TASTE SPINDEL HALT (MSTT/PC)
;       U   DB20.DBX 295.3; // TASTE SPINDEL HALT (PC)
;       UN  DB20.DBX 295.4; // TASTE SPINDEL START (PC)
U   DB20.DBX 832.1  // TASTE SPINDEL HALT  (MSTT)
UN  DB20.DBX 832.3  // TASTE SPINDEL START (MSTT)
U   A 0.3           // Hauptantrieb Reglerfreigabe
FP  M 132.7;        // FM TASTE SPINDEL HALT
NETWORK
TITLE = SPINDEL HALT/START FREIGABE
U   M 104.2; // SUM  SFG
UN  M 131.7;        // ACHSEN IN BEWEGUNG
UN  M 152.0         // WERKZEUGWENDEN AKTIV
=   M 132.0;        // SPINDEL HALT/START FREIGABE
NETWORK
TITLE = SPINDEL HALT AKTIV
U   M 132.0;        // SPINDEL HALT/START FREIGABE
U   M 132.7;        // FM TASTE SPINDEL HALT
U(  ;
ON  M 104.5;        // HM BETRIEBSART JOG UND INC
O   DB20.DBX 324.0; // PROGRAMM LÄUFT
)   ;
U   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
R   M 145.4
S   M 132.1;        // SPINDEL HALT AKTIV
U   M 132.5;        // FM TASTE SPINDEL START
U   M 132.1;        // SPINDEL HALT AKTIV
U(  ;
ON  M 104.5;        // HM BETRIEBSART JOG UND INC
O   DB20.DBX 324.0; // PROGRAMM LÄUFT
)   ;
U   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
S   M 145.4
U   M 145.4
L   S5T#200MS;      // 200MS
SE  T 5;            // STARTEN T 1 ALS EINSCHALTVERZ.
U   T 5;            // STARTEN T 1 ALS EINSCHALTVERZ.
R   M 132.1;        // SPINDEL HALT AKTIV
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   M 132.1;        // SPINDEL HALT AKTIV
R   M 145.4
NETWORK
TITLE = SPINDEL HALT AKTIV  SFG/ AFG/EFG
U   M 132.1;        // SPINDEL HALT AKTIV
;       U(
;       O   M 80.4;         // SPINDEL0 SPINDELBETRIEB
;       ON  M 25.5;         // SPINDEL1 1=Rundachsbetrieb 0=Spindelbetrieb
;       )
=   M 95.7;         // SPINDEL HALT AKTIV  SFG
=   M 91.7;         // SPINDEL HALT AKTIV  AFG/EFG
NETWORK
TITLE = ÜBERWACHUNGSZ. FÜR HA M3/M4
U   M 132.5;        // FM TASTE SPINDEL START
U   M 104.5;        // HM BETRIEBSART JOG UND INC
UN  DB20.DBX 324.0; // PROGRAMM LÄUFT
L   S5T#1S;         // 10X0,1S
SV  T 43;           // STARTEN T 43 ALS VERL. IMPULS
NETWORK
TITLE = /FM TASTE SPINDEL START (MT/PC)
;       U   DB20.DBX 295.4; // TASTE SPINDEL START (PC)
;       UN  DB20.DBX 295.3; // TASTE SPINDEL HALT (PC)
U   DB20.DBX 832.3  // TASTE SPINDEL START (MSTT)
UN  DB20.DBX 832.1  // TASTE SPINDEL HALT  (MSTT)
FN  M 132.3;        // FM TASTE SPINDEL START
END_FUNCTION
FUNCTION FC 20: VOID
NAME: VORSCHUB_HALT_START
BEGIN
NETWORK
TITLE = FM TASTE VORSCHUB START(MSTT/PC)
;       U   DB20.DBX 295.2; // TASTE VORSCHUB START (PC)
;       UN  DB20.DBX 295.1; // TASTE VORSCHUB HALT (PC)
U   DB20.DBX 832.0  // TASTE VORSCHUB START (MSTT)
UN  DB20.DBX 831.6  // TASTE VORSCHUB HALT  (MSTT)
FP  M 133.5;        // FM TASTE VORSCHUB START
NETWORK
TITLE = FM TASTE VORSCHUB HALT (MSTT/PC)
;       U   DB20.DBX 295.1; // TASTE VORSCHUB HALT (PC)
;       UN  DB20.DBX 295.2; // TASTE VORSCHUB START (PC)
U   DB20.DBX 831.6  // TASTE VORSCHUB HALT  (MSTT)
UN  DB20.DBX 832.0  // TASTE VORSCHUB START (MSTT)
FP  M 133.7;        // FM TASTE VORSCHUB HALT
NETWORK
TITLE = VORSCHUB HALT/START FREIGABE
U   M 102.0;        // SUM ACHSFREIGABE
UN  M 152.0         // WERKZEUGWENDEN AKTIV
=   M 133.0;        // VORSCHUB HALT/START FREIGABE
NETWORK
TITLE = VORSCHUB HALT AKTIV
O   DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
O   DB20.DBX 326.2; // BETRIEBSART MDI
O   DB20.DBX 327.2; // BETRIEBSART REFERENZ
U(
U   M 133.0;        // VORSCHUB HALT/START FREIGABE
U   M 133.7;        // FM TASTE VORSCHUB HALT
U   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
U   M 131.7         // ACHSEN IN BEWEGUNG
)
S   M 133.1;        // VORSCHUB HALT AKTIV
U   M 133.5;        // FM TASTE VORSCHUB START
UN  M 132.1;        // SPINDEL HALT AKTIV
U   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   M 133.1;        // VORSCHUB HALT AKTIV
NETWORK
TITLE = VORSCHUB HALT AKTIV  AFG/EFG
O   M 133.1;        // VORSCHUB HALT AKTIV
=   M 91.6;         // VORSCHUB HALT AKTIV  AFG/EFG
END_FUNCTION
FUNCTION FC 21: VOID
NAME: FUSZTASTER FÜR PNEUMATISCHES SPANNMITTEL
BEGIN
NETWORK
TITLE = FUSZTASTER FÜR PNEUMATISCHES SPANNMITTEL SCHLIESZEN
U   E 7.5;         // Fußtaster Spannmittel zu
UN  E 7.6;         // Fußtaster Spannmittel auf
FP  M 138.1;       // FM FUSZTASTER SPANNMITTEL ZU
NETWORK
TITLE = FUSZTASTER FÜR PNEUMATISCHES SPANNMITTEL ÖFFNEN
U   E 7.6;         // Fußtaster Spannmittel auf
UN  E 7.5;         // Fußtaster Spannmittel zu
FP  M 138.2;       // FM FUSZTASTER SPANNMITTEL AUF
END_FUNCTION
FUNCTION FC 25: VOID
NAME: AUSBLASEINRICHTUNG
BEGIN
NETWORK
TITLE = AUSBLASEN EIN (M71)
O   DB20.DBX 200.7; // M71 DYN.
O   DB20.DBX 303.0; // DNC/AUSBLASEN EIN
S   M 140.1;        // AUSBLASEN EIN
=   DB20.DBX 264.7; // M71 RÜCKMELDEN
NETWORK
TITLE = AUSBLASEN AUS (M72)
O   DB20.DBX 201.0; // M72 DYN.
O   DB20.DBX 302.7; // DNC/AUSBLASEN AUS
ON  M 110.0;        // AUX-ON
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB20.DBX 324.2; // DRYRUN AKTIV
O   M 34.1;         // HM M0/M1 DYNAMISCH
O   DB20.DBX 192.2; // M2 DYN.
O   DB20.DBX 195.6; // M30 DYN.
R   M 140.1;        // AUSBLASEN EIN
=   DB20.DBX 265.0; // M72 RÜCKMELDEN
NETWORK
TITLE = LEISTUNG AUSBLASEN
U   M 140.1;        // AUSBLASEN EIN
=   A 6.0           // Ausblasventil Schraubstock
NETWORK
TITLE = SPS/OB-->AUSBLASEN AKTIV
U   M 140.1;        // AUSBLASEN EIN
=   DB20.DBX 310.7; // SPS/RM-->AUSBLASEN AKTIV
NETWORK
TITLE = AUSBLASEN/NEG.DNC-QUITTUNG
U   DB20.DBX 303.0; // DNC/AUSBLASEN EIN
UN  DB20.DBX 264.7; // M71 RÜCKMELDEN
O   ;
U   DB20.DBX 302.7; // DNC/AUSBLASEN AUS
UN  DB20.DBX 265.0; // M72 RÜCKMELDEN
U   DB1.DBX 1366.0; // DNC-BETRIEB AKTIV
S   DB20.DBX 318.4; // AUSBLASEN/NEG.DNC-QUITTUNG
END_FUNCTION
FUNCTION FC 27: VOID
NAME: TUERAUTOMATIK
BEGIN
//  X6:5     E 6.2       // Türautomatik Sicherheitstürschaltleiste ausgelöst
//  X6:6     E 6.3       // Türautomatik Endschalter Tür offen
//  X6:7     E 6.4       // Störung Türsteuergerät Langer&Laumann
//  X6:11    E 6.5       // Schutzschalter Türsteuergerät
//  X5:7     E 7.4       // Türautomatik Motorschützüberwachung
//
//  X6:10    A 6.3       // LEISTUNG TÜR ZU
//  X6:9     A 3.5       // LEISTUNG TÜR AUF
//  X421:6   A 9.2       // Verriegelunsmagnet Maschinentür entriegeln ohne Türautomatik
//  X421:11  A 9.7       // Türmotorschütze ein
// DNC Betrieb:
// DB1.DBX 1382.0  Maschinentüre auf
// DB1.DBX 1382.1  Maschinentüre zu
// DB1.DBX 1382.2  Maschinentüre Stop
NETWORK
TITLE = ALARM 6091 Türautomatikschütz nicht abgefallen
UN  E 6.2           // Türautomatik Sicherheitstürschaltleiste ausgelöst
UN  E 7.4           // Türautomatik Motorschützüberwachung
L   S5T#300MS       //
SE  T 46            //
U   T 46            //
S   DB2.DBX 11.3;   // 6091 Türautomatikschütz nicht abgefallen
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 11.3;   // 6091 Türautomatikschütz nicht abgefallen
NETWORK
TITLE = ALARM 6088 Schutzschalter Türsteuergerät ausgelöst
UN  E 6.5           // Schutzschalter Türsteuergerät
S   DB2.DBX 11.0;   // 6088 Schutzschalter Türsteuergerät ausgelöst
U   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
U   E 6.5           // Schutzschalter Türsteuergerät
R   DB2.DBX 11.0;   // 6088 Schutzschalter Türsteuergerät ausgelöst
NETWORK  1
TITLE = /FM MASCHINENTÜR OFFEN
U   M 110.1;        // MASCHINENTÜR OFFEN
FN  M 144.5;        // /FM MASCHINENTÜR OFFEN
NETWORK  2
TITLE = FM ENDSCH. TÜR OFFEN
U   E 6.3;          // Türautomatik Endschalter Tür offen
FP  M 144.3;        // FM ENDSCH. TÜR OFFEN
NETWORK
TITLE = FM Türautomatik Sicherheitstürschaltleiste ausgelöst
U   E 6.2           // Türautomatik Sicherheitstürschaltleiste ausgelöst
FP  M 143.1;        // FM Türautomatik Sicherheitstürschaltleiste ausgelöst
NETWORK
TITLE = FM Störung Türsteuergerät Langer&Laumann
U   E 6.4           // Störung Türsteuergerät Langer&Laumann
FP  M 144.1;        // FM Störung Türsteuergerät Langer&Laumann
NETWORK
TITLE = Türmotorschütze einschalten
U   M 2.0           // SUMME aller NOT-AUS Möglichkeiten
=   A 9.7           // Türmotorschütze ein
NETWORK  3
TITLE = FREIGABE TÜR ZU/AUF
UN  DB2.DBX 0.0;    // 6000  NOT-AUS
U   M 55.7;         // FREIGABE ZUM TÜRENTRIEGELN
U   E 6.2           // Türautomatik Sicherheitstürschaltleiste ausgelöst
=   M 143.0;        // FREIGABE TÜR ZU/AUF
NETWORK  6
TITLE = HM TIPPBETRIEB TÜR ZU bei AUX OFF
U   DB1.DBX 1374.1; // TASTE TÜR ZU  (MSTT)
UN  DB1.DBX 1374.0; // TASTE TÜR AUF (MSTT)
UN  M 110.0;        // AUX-ON
U   M 143.0;        // FREIGABE TÜR ZU/AUF
=   M 143.7;        // HM TIPPBETRIEB TÜR ZU
R   M 143.6;        // HM TIPPBETRIEB TÜR AUF
R   M 143.4;        // HM TÜR AUF STAT.
NETWORK  7
TITLE = HM TIPPBETRIEB TÜR AUF bei AUX OFF
U   DB1.DBX 1374.0; // TASTE TÜR AUF (MSTT)
UN  DB1.DBX 1374.1; // TASTE TÜR ZU  (MSTT)
UN   M 110.0;       // AUX-ON
U   M 143.0;        // FREIGABE TÜR ZU/AUF
=   M 143.6;        // HM TIPPBETRIEB TÜR AUF
R   M 143.7;        // HM TIPPBETRIEB TÜR ZU
R   M 143.5;        // HM TÜR ZU STAT.
NETWORK  8
TITLE = HM TÜR ZU STAT.
U   DB1.DBX 1374.1; // TASTE TÜR ZU  (MSTT)
UN  DB1.DBX 1374.0; // TASTE TÜR AUF (MSTT)
U   M 110.0;        // AUX-ON
U   M 143.0;        // FREIGABE TÜR ZU/AUF
O(  ;
O   DB1.DBX 1382.1; // DNC/TÜR ZU
O   M 170.4;        // ROBOTIK/FM TÜR ZU
O   M 146.2;        // Anforderung Tür zu von Dauerlauf
U   M 143.0;        // FREIGABE TÜR ZU/AUF
)   ;
O(
U   M 12.0          // Tür zu und Verriegelt
U   M 143.0;        // FREIGABE TÜR ZU/AUF
U   A 6.3 ;         // LEISTUNG TÜR ZU
UN  M 143.4;        // HM TÜR AUF STAT.
UN   M 143.6;       // HM TIPPBETRIEB TÜR AUF
)
O(
U   DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
U   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
)
S   M 143.5;        // HM TÜR ZU STAT.
R   M 143.4;        // HM TÜR AUF STAT.
R   M 143.6;        // HM TIPPBETRIEB TÜR AUF
NETWORK  9
TITLE = HM TÜR AUF STAT. (M0/M1/M2/M30)
O   DB20.DBX 348.1; // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT (M2,M30)
O   M 34.1;         // HM M0/M1 DYNAMISCH
S   M 144.0;        // HM TÜR AUF STAT. (M0/M1/M2/M30)
NETWORK 10
TITLE = HM TÜR AUF STAT.
U   DB1.DBX 1374.0; // TASTE TÜR AUF (MSTT)
UN  DB1.DBX 1374.1; // TASTE TÜR ZU  (MSTT)
U   M 110.0;        // AUX-ON
U   M 143.0;        // FREIGABE TÜR ZU/AUF
;       O   M 144.3;        // FM ENDSCH. TÜR OFFEN
O(  ;
O   DB1.DBX 1382.0; // DNC/TÜR AUF
O   M 170.3;        // ROBOTIK/FM TÜR AUF
U   M 143.0;        // FREIGABE TÜR ZU/AUF
UN  E 6.3;          // Türautomatik Endschalter Tür offen
)   ;
O(   ;
U   M 143.0;        // FREIGABE TÜR ZU/AUF
U   M 144.0;        // HM TÜR AUF STAT. (M0/M1/M2/M30)
)
O(   ;
U   DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
U   E 6.3;          // Türautomatik Enschalter Tür offen
)
S   M 143.4;        // HM TÜR AUF STAT.
R   A 6.3 ;         // LEISTUNG TÜR ZU
R   M 143.5;        // HM TÜR ZU STAT.
R   M 143.7;        // HM TIPPBETRIEB TÜR ZU
R   M 144.0;        // HM TÜR AUF STAT. (M0/M1/M2/M30)
NETWORK 12
TITLE = ABBRUCH TÜRBEWEGUNG
O   DB2.DBX 0.0;    // 6000  NOT-AUS
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // TASTE RESET
O   DB1.DBX 1382.2; // DNC/TÜR STOP
O   M 143.1;        // FM Türautomatik Sicherheitstürschaltleiste ausgelöst
ON  E 6.2           // Türautomatik Sicherheitstürschaltleiste ausgelöst
O   M 144.1;        // FM Störung Türsteuergerät Langer&Laumann
U   M 91.3;         // TÜRBEWEGUNG AKTIV  AFG/EFG
R   M 143.5;        // HM TÜR ZU STAT.
R   M 143.4;        // HM TÜR AUF STAT.
R   M 144.0;        // HM TÜR AUF STAT. (M0/M1/M2/M30)
NETWORK 14
TITLE = LEISTUNG TÜR ZU
O   M 143.7;        // HM TIPPBETRIEB TÜR ZU
O   M 143.5;        // HM TÜR ZU STAT.
=   A 6.3 ;         // LEISTUNG TÜR ZU
FP  M 144.6;        // FM TÜR ZU
U   M 144.6;        // FM TÜR ZU
S   M 144.7;        // HM TÜR ZU
NETWORK 15
TITLE = LEISTUNG TÜR AUF
O   M 143.4;        // HM TÜR AUF STAT.
O   M 143.6;        // HM TIPPBETRIEB TÜR AUF
;       UN  E 0.0;          // X-ACHSE SERVO-READY
;       UN  E 0.1;          // Y-ACHSE SERVO-READY
UN  E 0.2;          // Z-ACHSE SERVO-READY
UN  E 5.5           // Bremse öffnen von Z-Achsreglerbaugruppe
=   A 3.5 ;         // LEISTUNG TÜR AUF
NETWORK 16
TITLE = TÜR OFFEN
U   A 3.5 ;         // LEISTUNG TÜR AUF
U   E 6.3;          // Türautomatik Enschalter Tür offen
=   M 143.2;        // TÜR OFFEN
NETWORK
TITLE = HM TÜR ZU LÖSCHEN
U   M 144.7;        // HM TÜR ZU
U   A 6.3 ;         // LEISTUNG TÜR ZU
U   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
R   M 144.7;        // HM TÜR ZU
NETWORK 17
TITLE = TÜRBEWEGUNG AKTIV  AFG/EFG
U   M 144.7;        // HM TÜR ZU
U   A 6.3 ;         // LEISTUNG TÜR ZU
U   M 110.1;        // MASCHINENTÜR OFFEN
O   ;
U   A 3.5 ;         // LEISTUNG TÜR AUF
UN  E 6.3;          // Türautomatik Enschalter Tür offen
=   M 91.3;         // TÜRBEWEGUNG AKTIV  AFG/EFG
NETWORK 18
TITLE = 6064  TÜR NICHT BEREIT
U   M 91.3;         // TÜRBEWEGUNG AKTIV  AFG/EFG
L   S5T#10S;        //
SE  T 37;           // STARTEN T 37 ALS EINSCHALTVERZ.
U   T 37;           // TÜR AUF/ZEITÜBERWACHUNG
S   DB2.DBX 8.0;    // 6064  TÜR NICHT BEREIT
R   A 6.3 ;         // LEISTUNG TÜR ZU
R   A 3.5 ;         // LEISTUNG TÜR AUF
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 8.0;    // 6064  TÜR NICHT BEREIT
NETWORK 19
TITLE = SPS/OB-->TÜR OFFEN
U   M 143.2;        // TÜR OFFEN
=   DB1.DBX 1390.0; // SPS/RM-->TÜR OFFEN
NETWORK 20
TITLE = TÜR/FC DNC-QUITTUNG
U   DB1.DBX 1366.0; // DNC-BETRIEB AKTIV
U(  ;
U   DB1.DBX 1382.1; // DNC/TÜR ZU
UN  M 143.5;        // HM TÜR ZU STAT.
O   ;
U   DB1.DBX 1382.0; // DNC/TÜR AUF
UN  M 143.4;        // HM TÜR AUF STAT.
O   ;
U   DB1.DBX 1382.2; // DNC/TÜR STOP
U(  ;
O   M 143.7;        // HM TIPPBETRIEB TÜR ZU
O   M 143.6;        // HM TIPPBETRIEB TÜR AUF
O   M 143.5;        // HM TÜR ZU STAT.
O   M 143.4;        // HM TÜR AUF STAT.
)   ;
)   ;
S   DB1.DBX 1398.1; // TÜR/FC DNC-QUITTUNG
NETWORK 21
TITLE = Automatischer Cycle-Start nach Tür zu
U   DB10.DBX 77.4;  // SPS-MSD Automatischer Cycle-Start nach Tür zu
U   M 50.3;         // FREIGABEKREIS AUF AUSGABEMODUL DURCHGESCHALTET
U   M 50.0          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
U   A 6.3 ;         // LEISTUNG TÜR ZU
FP  M 146.0;        // FM NC-Startanforderung nach Tür zu
U   M 146.0;        // FM NC-Startanforderung nach Tür zu
U   DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
U(
U   DB20.DBX 224.1; // M1 STATISCH
U   DB20.DBX 324.5; // BEDINGTER HALT AKTIV
O   DB20.DBX 224.0; // M0 STATISCH
O   DB20.DBX 224.2; // M2 STATISCH
O   DB20.DBX 227.6; // M30 STATISCH
)
L   S5T#1S;         // 1S
SV  T 72            // IMPULS (VERLÄNGERT)
U   T 72            // IMPULS (VERLÄNGERT)
=   M 146.3         // CYCLE START NACH TÜR ZU
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
O   DB1.DBX 1440.0  // RESET AUSGELÖST
ON  DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
R   T 72
NETWORK 22
TITLE = Automatisches Tür schliessen im Dauerlauf
U   DB10.DBX 77.4;  // SPS-MSD Automatischer Cycle-Start nach Tür zu
U   E 6.3;          // Türautomatik Enschalter Tür offen
U   A 3.5           // LEISTUNG TÜR AUF
FP  M 146.1;        // FM Anforderung Tür zu (Dauerlauf)
U   M 146.1;        // FM Anforderung Tür zu (Dauerlauf)
U   M 200.7
U   DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
L   S5T#1S;         // 1S
SV  T 29            // IMPULS (VERLÄNGERT)
U   T 29            // IMPULS (VERLÄNGERT)
=   M 146.2;        // Anforderung Tür zu von Dauerlauf
END_FUNCTION
FUNCTION FC 28: VOID
NAME: WARNLEUCHTE
BEGIN
NETWORK
TITLE = 1S IMPULSDAUER FÜR HS PENDELN
UN  T 58;           // PAUSENDAUER
L   S5T#1S;         // 1S
SV  T 57;           // STARTEN ALS VERLÄNGERTER IMPULS
NETWORK
TITLE = 1S PAUSENDAUER FÜR HS PENDELN
UN  T 57;           // 2HZ FREQUENZ
L   S5T#1S;         // 1S
SV  T 58;           // STARTEN ALS VERLÄNGERTER IMPULS
NETWORK
TITLE = BLINKLICHT IMPULSDAUER
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
SPBN   M001
L   DB10.DBW 20;    // LADE ZEIT AUS SPS-MSD
ITB;                // UMWANDELN EINER GANZZAHL IN EINEN BCD-WERT
T   MW 30;          // TRANSFERIERE IN MW30
SET;
R   M 31.4;         // ZEITRASTER 0,01S EINSTELLEN
R   M 31.5;         // ZEITRASTER 0,01S EINSTELLEN
M001:  NOP 0
UN  T 48;           // PAUSENDAUER
L   MW 30;          // LADE MW30
SV  T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
NETWORK
TITLE = BLINKLICHT PAUSENDAUER
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
SPBN   M002
L   DB10.DBW 22;    // LADE ZEIT AUS SPS-MSD
ITB;                // UMWANDELN EINER GANZZAHL IN EINEN BCD-WERT
T   MW 32;          // TRANSFERIERE IN MW32 (ZEITRASTER IST  0,01S)
SET;
R   M 33.4;         // ZEITRASTER 0,01S EINSTELLEN
R   M 33.5;         // ZEITRASTER 0,01S EINSTELLEN
M002:  NOP 0
UN  T 47;           // IMPULSDAUER
L   MW 32;          // LADE MW30
SV  T 48;           // STARTEN ALS VERLÄNGERTER IMPULS
NETWORK
TITLE =  ANSTEUERUNG FÜR DIE GRÜNE LAMPE AUTOMATIKANZEIGE
U   DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
U   T 47;           // IMPULSDAUER
UN  DB1.DBX 1366.3; // MASCHINENALARM AKTIV
O
U   DB20.DBX 324.0; // PROGRAMM LÄUFT
UN  DB20.DBX 324.1; // STOP-ZUSTAND
UN  DB20.DBX 325.1; // STOP-ZUSTAND DURCH SINGLE
UN  DB1.DBX 1366.3; // MASCHINENALARM AKTIV
=   A 5.4           // GRÜNE WARNLEUCHTE AUTOMATIK
;  NETWORK
;  TITLE =  ANSTEUERUNG FÜR DIE WEISE LAMPE
;
;       UN  M 8.2           // 1X AKTIV
;8(((((((       =   A 5.3;           // WEISSE WARNLEUCHTE LADEMAGAZIN STEHT
NETWORK
TITLE =  ANSTEUERUNG FÜR DIE ROTE LAMPE ALARMANZEIGE
U   DB1.DBX 1366.3; // MASCHINENALARM AKTIV
U   T 47;           // IMPULSDAUER
=   A 5.5           // ROTE WARNLEUCHTE ALARM
END_FUNCTION
FUNCTION FC 40: VOID
NAME: ROBOTIK_INTERFACE
BEGIN
NETWORK
TITLE = Robotik Achsen stehen auf Referenzpunkt
L   DB1.DBD 70;     // REF-POSITION X
L   -0.5E-03;       // 0,5MM
+R  ;
L   DB1.DBD 0;      // IST POSITION X
<R  ;
=   M 168.0
L   DB1.DBD 74;     // REF-POSITION Y
L   -0.5E-03;       // 0,5MM
+R  ;
L   DB1.DBD 4;      // IST POSITION Y
<R  ;
=   M 168.1
L   DB1.DBD 78;     // REF-POSITION Z
L   -0.5E-03;       // 0,5MM
+R  ;
L   DB1.DBD 8;      // IST POSITION Z
<R  ;
=   M 168.2
U   M 168.0
U   M 168.1
U   M 168.2
U   DB1.DBX 134.0;  // REFERENZPUNKT X-ACHSE AKTIV
U   DB1.DBX 134.1;  // REFERENZPUNKT Y-ACHSE AKTIV
U   DB1.DBX 134.2;  // REFERENZPUNKT Z-ACHSE AKTIV
=   M 59.7          // ROBOTIK ACHSEN STEHEN AUF REF.PKT.
NETWORK
TITLE = VORSCHUB HALT AKTIV  AFG/EFG
O   M 52.2;         // ROBOTIK VORSCHUB-STOP
=   M 91.2;         // ROBOTIK VORSCHUB-STOP AFG/EFG
NETWORK
TITLE = ROBOTIK/FM AUX-ON
U   M 51.6;         // ROBOTIK AUX-ON
FP  M 168.1;        // ROBOTIK/FM AUX-ON
NETWORK
TITLE = ROBOTIK/FM UMSCH. BA REF/AUTO
U   M 51.7;         // ROBOTIK AUTOMATIK/REFERENZBETRIEB UMSCHALTEN
FP  M 168.3;        // ROBOTIK/FM UMSCH. BA REF/AUTO
NETWORK
TITLE = REFERENZPUNKT ANFAHREN
U   M 52.0;         // ROBOTIK REFERENZPUNKT ANFAHREN STARTEN
FP  M 168.5;        // ROBOTIK/FM REF ACHSEN
U   M 168.5;        // ROBOTIK/REF ACHSEN
SPBN   M001;
S   DB20.DBX 289.2; // BETRIEBSART REFERENZPUNKT ANFAHREN
S   DB20.DBX 0.0;   // BETRIEBSARTUMSCHALTUNG AKTIVIEREN
L   W#16#7;         // LADE HEX 5 (X,Y UND Z-ACHSE REFERENZIEREN)
T   DB20.DBB 8;     // REFERENZIEREN DER KANALACHSEN
S   DB20.DBX 1.1;   // ANFORDERUNG REFERENZPKT.ANFAHREN
M001:  NOP 0;
NETWORK
TITLE = ROBOTIK/FM NC-START
U   M 52.1;         // ROBOTIK PROGRAMM START
FP  M 168.7;        // ROBOTIK/FM NC-START
NETWORK
TITLE = ROBOTIK/FM SPM ZU
U   M 52.3;         // ROBOTIK SPANNMITTEL SCHLIEßEN
UN  M 52.4;         // ROBOTIK SPANNMITTEL ÖFFNEN
FP  M 169.1;        // ROBOTIK/FM SPM ZU
NETWORK
TITLE = ROBOTIK/FM SPM AUF
U   M 52.4;         // ROBOTIK SPANNMITTEL ÖFFNEN
UN  M 52.3;         // ROBOTIK SPANNMITTEL SCHLIEßEN
FP  M 169.3;        // ROBOTIK/FM SPM AUF
NETWORK
TITLE = ROBOTIK/FM PINOLE VOR
U   M 52.5;         // ROBOTIK PINOLE VOR
UN  M 52.6;         // ROBOTIK PINOLE ZURÜCK
FP  M 169.5;        // ROBOTIK/FM PINOLE VOR
NETWORK
TITLE = ROBOTIK/FM PINOLE ZURÜCK
U   M 52.6;         // ROBOTIK PINOLE ZURÜCK
UN  M 52.5;         // ROBOTIK PINOLE VOR
FP  M 169.7;        // ROBOTIK/FM PINOLE ZURÜCK
NETWORK
TITLE = ROBOTIK/TÜR AUF
U   M 52.7;         // ROBOTIK TÜR ÖFFNEN
UN  M 51.2;         // ZUSTIMMTASTE GEDRÜCKT
FP  M 170.3;        // ROBOTIK/FM TÜR AUF
NETWORK
TITLE = ROBOTIK/TÜR ZU
U   M 51.5;         // ROBOTIK TÜR SCHLIESSEN
UN  M 51.2;         // ZUSTIMMTASTE GEDRÜCKT
FP  M 170.4;        // ROBOTIK/FM TÜR ZU
NETWORK
TITLE = ROBOTIK/NOT-AUS AKTIV
U   DB2.DBX 0.0;    // 6000  NOT-AUS AKTIV
=   M 59.3;         // AUSGANGSMERKER ROBOTIK NOT-AUS AKTIV
NETWORK
TITLE = ROBOTIK/MASCHINE READY
U   M 110.0;        // AUX-ON
;       U   M 110.1;        // MASCHINENTÜR OFFEN
U(
O   E 6.3;          // Türautomatik Endschalter Tür offen
)
U   DB20.DBX 324.4; // REFERENZPUNKT AKTIV
U(
O   M 138.4;        // SCHRAUBSTOCK OFFEN
ON  DB10.DBX 75.1   // PNEUM. SCHRAUBSTOCK AKTIVIEREN
)
U   DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
=   M 59.4;         // AUSGANGSMERKER ROBOTIK MASCHINE BETRIEBSBEREIT
NETWORK
TITLE = ROBOTIK BETRIEBSART REFERENZ
U   DB20.DBX 327.2; // BETRIEBSART REFERENZ
=   M 59.5;         // AUSGANGSMERKER ROBOTIK BETRIEBSART REFERENZ
NETWORK
TITLE = ROBOTIK M0,M1,M2,ODER M30 AKTIV
U   DB20.DBX 224.1; // M1 STATISCH
U   DB20.DBX 324.5; // BEDINGTER HALT AKTIV
O   DB20.DBX 224.0; // M0 STATISCH
O   DB20.DBX 224.2; // M2 STATISCH
O   DB20.DBX 227.6; // M30 STATISCH
=   M 59.6;         // AUSGANGSMERKER ROBOTIK M0,M1,M2,ODER M30 AKTIV
NETWORK
TITLE = ROBOTIK SPANNMITTEL GESPANNT
U   M 138.5;        // SCHRAUBSTOCK GESPANNT
=   M 60.0;         // AUSGANGSMERKER ROBOTIK SPANNMITTEL GESPANNT
NETWORK
TITLE = ROBOTIK SPANNMITTEL OFFEN
U   M 138.4;        // SCHRAUBSTOCK OFFEN
=   M 60.1;         // AUSGANGSMERKER ROBOTIK SPANNMITTEL OFFEN
NETWORK
TITLE = ROBOTIK SPANNMITTEL KEIN TEIL GESPANNT
U   M 139.0         // SPANNMITTEL KEIN TEIL GESPANNT
=   M 60.2;         // AUSGANGSMERKER ROBOTIK SPANNMITTEL KEIN TEIL GESPANNT
NETWORK
TITLE = ROBOTIK PINOLE GESPANNT
U   M 141.3;        // PINOLE GESPANNT
=   M 60.3;         // AUSGANGSMERKER ROBOTIK PINOLE GESPANNT
NETWORK
TITLE = ROBOTIK PINOLE ENDLAGE HINTEN
U   M 141.2;        // PINOLE ENDLAGE HINTEN
=   M 60.4;         // AUSGANGSMERKER ROBOTIK PINOLE ENDLAGE HINTEN
NETWORK
TITLE = ROBOTIK PINOLE KEIN TEIL GESPANNT
U   M 141.1;        // HM PINOLE KEIN TEIL GESPANNT
=   M 60.5;         // AUSGANGSMERKER ROBOTIK PINOLE KEIN TEIL GESPANNT
NETWORK
TITLE = ROBOTIK MASCHINENTÜR GESCHLOSSEN
U   M 12.0          // Tür zu und Verriegelt
=   M 60.6;         // AUSGANGSMERKER ROBOTIK MASCHINENTÜR GESCHLOSSEN
NETWORK
TITLE = ROBOTIK MASCHINENTÜR OFFEN
U   M 143.2;        // TÜR OFFEN
=   M 60.7;         // AUSGANGSMERKER ROBOTIK MASCHINENTÜR OFFEN
NETWORK
TITLE = UMSCH. BA REF/AUTO
UN  M 168.3;        // ROBOTIK/FM UMSCH. BA REF/AUTO
BEB ;
UN  M 59.5;         // AUSGANGSMERKER ROBOTIK BETRIEBSART REFERENZ
S   DB20.DBX 289.2; // FC BETRIEBSART REFERENZ
S   DB20.DBX 0.0;   // BETRIEBSARTENUMSCHALTUNG
BEB ;
U   M 59.5;         // AUSGANGSMERKER ROBOTIK BETRIEBSART REFERENZ
S   DB20.DBX 288.4; // FC BETRIEBSART AUTOMATIK
S   DB20.DBX 0.0;   // BETRIEBSARTENUMSCHALTUNG
END_FUNCTION
FUNCTION FC 15: VOID
NAME: MELDUNGEN
BEGIN
;HILFSMERKER ZUORDNUNG:
;TITLE = 7000  FALSCHES T-WORT                M 17.0
;TITLE = 7016  HILFSANTRIEBE EINSCHALTEN      M 18.0
;TITLE = 7017  REFERENZPUNKT ANFAHREN         M 18.1
;TITLE = 7018  SCHLÜSSEL UMSCHALTEN           M 18.2
;TITLE = 7019  PNEUMATIKÖLER LEER             M 18.3
;TITLE = 7020  SONDERBETRIEB AKTIV            M 18.4
;TITLE = 7021  WERKZEUGWENDER FREIFAHREN      M 18.5
;TITLE = 7022  AUFFANGSCHALEÜBERWACHUNG       M 18.6
;TITLE = 7023  DRUCKSCHALTER EINSTELLEN       M 18.7
;TITLE = 7024  SPANNMITTELBERO EINSTELLEN     M 19.0
;TITLE = 7025  WARTEZEIT HAUPTANTRIEB         M 19.1
;TITLE = 7026  Störung Lüfter Hauptmotor      M 19.2
;TITLE = 7038  DRUCKSCH.SCHMIERMITTEL DEFEKT  M 20.6
;TITLE = 7039  SCHMIERMITTEL DEFEKT           M 20.7
;TITLE = 7040  MASCHINENTÜR OFFEN             M 21.0
;TITLE = 7042  MASCHINENTÜR INIT.             M 21.2
;TITLE = 7043  SOLLSTÜCKZAHL ERREICHT         M 21.3
;TITLE = 7055  SPANNMITTEL OFFEN              M 22.0
;TITLE = 7054  SPANNMITTEL KEIN TEIL GESPANNT M 22.1
;TITLE = 7050  ZANGE SCHLIESSEN               M 22.2
;TITLE = 7051  ZANGE/KEIN TEIL GESPANNT       M 22.3
;TITLE = 7052  PINOLE IN ZWISCHENSTELLUNG     M 22.4
;TITLE = 7053  PINOLE/KEIN TEIL GESPANNT      M 22.5
;TITLE = 7063  ÖLSTAND ZENTRALSCHMIERUNG      M 22.6
;TITLE = 7075  Offsetabgleich aktiv
;TITLE = 7077  Werkzeugwender einstellen                 M 23.0  NEU CM250
;TITLE = 7078  Werkzeugköcher nicht zurückgeschwenkt     M 23.1  NEU CM250
;TITLE = 7079  Werkzeugwechselarm nicht in Grundstellung M 23.2  NEU CM250
;TITLE = 7080  Werkzeug nicht richtig eingespannt!       M 23.3  NEU CM250
;TITLE = 7081  Geht mit weniger Grad!                    M 23.4  NEU CM250
;TITLE = 7082  Schutzschalter Späneförderer ausgelöst    M 23.5  NEU CM250
;TITLE = 7083  Magazinieren aktiv                        M 23.6  NEU CM250
;TITLE = 7084  Schraubstock offen                        M 23.7  NEU CM250
;TITLE = 7085  Rundachse A auf 0° fahren                 M 25.0  NEU CM250
NETWORK
TITLE = ALLE MELDUNGEN LÖSCHEN
L   0
T   DB3.DBD 0       // 7000 BIS 7031
T   DB3.DBD 4       // 7032 BIS 7063
T   DB3.DBD 8       // 7064 BIS 7095
NETWORK
TITLE = 7085  Rundachse A auf 0° fahren !
U   M 25.0;         // HM 7085  Rundachse A auf 0° fahren !
=   DB3.DBX 10.5;   // 7085  Rundachse A auf 0° fahren !
BEB
NETWORK
TITLE = Warten auf USBSPS nicht NOT-AUS
U   M 11.5          // Warten auf USBSPS nicht NOT-AUS
BEB
NETWORK
TITLE = MELDUNG 7075 Offsetabgleich aktiv
U   DB1.DBX 1466.0  // Offsetabgleich für alle analogen Linear- und Rundachsen
=   DB3.DBX 9.3;    // 7075 Offsetabgleich aktiv
BEB
NETWORK
TITLE = MELDUNG 7081 Geht mit weniger Grad!
U   M 23.4          // HM 7081 Geht mit weniger Grad!
=   DB3.DBX 10.1    // 7081 Geht mit weniger Grad!
BEB
NETWORK
TITLE = MELDUNG 7082 Schutzschalter Späneförderer ausgelöst
U   M 23.5          // HM 7082 Schutzschalter Späneförderer ausgelöst
=   DB3.DBX 10.2    // MELDUNG 7082 Schutzschalter Späneförderer ausgelöst
BEB
NETWORK
TITLE = MELDUNG 7083 Magazinieren aktiv
U   M 23.6          // HM 7083 MELDUNG 7083 Magazinieren aktiv
=   DB3.DBX 10.3    // MELDUNG 7083 Magazinieren aktiv
BEB
NETWORK
TITLE = MELDUNG 7026 Schutzschalter Hauptmotorlüfter ausgelöst
U   M 19.2;         // HM  7026 Störung Lüfter Hauptmotor
=   DB3.DBX 3.2;    // MELDUNG 7026 Störung Lüfter Hauptmotor
BEB
NETWORK
TITLE = 7042  MASCHINENTÜR INIT.
U   M 21.2          // HM 7042  MASCHINENTÜR INIT.
=   DB3.DBX 5.2;    // 7042  MASCHINENTÜR INIT.
BEB
NETWORK
TITLE = 7016  HILFSANTRIEBE EINSCHALTEN
U   M 18.0          // HM 7016  HILFSANTRIEBE EINSCHALTEN
=   DB3.DBX 2.0;    // 7016  HILFSANTRIEBE EINSCHALTEN
BEB
NETWORK
TITLE = 7021  WERKZEUGWENDER FREIFAHREN
U   M 18.5;         // HM 7021  WERKZEUGWENDER FREIFAHREN
=   DB3.DBX 2.5;    // MELDUNG 7021  WERKZEUGWENDER FREIFAHREN
BEB
NETWORK
TITLE = Meldung 7080 Werkzeug nicht richtig eingespannt!
U   M 23.3          // HM 7080 Werkzeug nicht richtig eingespannt!
=   DB3.DBX 10.0    // 7080 Werkzeug nicht richtig eingespannt!
BEB
NETWORK
TITLE = Meldung 7078 Werkzeugköcher nicht zurückgeschwenkt
U   M 23.1          // HM 7078 Werkzeugköcher nicht zurückgeschwenkt
=   DB3.DBX 9.6     // 7078   Werkzeugköcher nicht zurückgeschwenkt
BEB
NETWORK
TITLE = Meldung 7079 Werkzeugwechselarm nicht in Grundstellung
U   M 23.2          // HM 7079 Werkzeugwechselarm nicht in Grundstellung
=   DB3.DBX 9.7     // 7079 Werkzeugwechselarm nicht in Grundstellung
BEB
NETWORK
TITLE = 7019  PNEUMATIKÖLER LEER
U   M 18.3          // HM 7019  PNEUMATIKÖLER LEER
=   DB3.DBX 2.3;    // 7019  PNEUMATIKÖLER LEER
BEB
NETWORK
TITLE = MELDUNG 7022  AUFFANGSCHALEÜBERWACHUNG
U   M 18.6;         // HM 7022  AUFFANGSCHALEÜBERWACHUNG
=   DB3.DBX 2.6;    // MELDUNG 7022  AUFFANGSCHALEÜBERWACHUNG
BEB
NETWORK
TITLE = MELDUNG 7024  SPANNMITTELENDSCHALTER EINSTELLEN
U   M 19.0;         // HM 7024  SPANNMITTELENDSCHALTER EINSTELLEN
=   DB3.DBX 3.0;    // MELDUNG 7024  SPANNMITTELENDSCHALTER EINSTELLEN
BEB
NETWORK
TITLE = 7077 Werkzeugwender einstellen
U   M 23.0          // HM 7077 Meldung Werkzeugwender einstellen
=   DB3.DBX 9.5;    // 7077 Werkzeugwender einstellen
BEB
NETWORK
TITLE = 7020  SONDERBETRIEB AKTIV
U   M 18.4;         // HM 7020  SONDERBETRIEB AKTIV
=   DB3.DBX 2.4;    // 7020  SONDERBETRIEB AKTIV
BEB
NETWORK
TITLE = 7040  MASCHINENTÜR OFFEN
U   M 21.0          // HM 7040  MASCHINENTÜR OFFEN
=   DB3.DBX 5.0;    // 7040  MASCHINENTÜR OFFEN
BEB
NETWORK
TITLE = 7018  SCHLÜSSEL UMSCHALTEN
U   M 18.2;         // HM 7018  SCHLÜSSEL UMSCHALTEN
=   DB3.DBX 2.2;    // 7018  SCHLÜSSEL UMSCHALTEN
BEB
NETWORK
TITLE = 7084  Schraubstock offen !
U   M 110.0;        // AUX-ON
U   DB20.DBX 324.4; // REFERENZPUNKT AKTIV
U   M 23.7;         // HM 7084  Schraubstock offen !
=   DB3.DBX 10.4;   // 7084  Schraubstock offen !
BEB
NETWORK
TITLE = 7054 SPANNMITTEL KEIN TEIL GESPANNT
U   M 110.0;        // AUX-ON
U   DB20.DBX 324.4; // REFERENZPUNKT AKTIV
U   M 22.1;         // HM 7054 SPANNMITTEL KEIN TEIL GESPANNT
=   DB3.DBX 6.6;    // 7054 SPANNMITTEL KEIN TEIL GESPANNT
BEB
NETWORK
TITLE = 7050  ZANGE SCHLIESSEN
U   M 110.0;        // AUX-ON
U   DB20.DBX 324.4; // REFERENZPUNKT AKTIV
U   M 22.2;         // HM 7050  ZANGE SCHLIESSEN
=   DB3.DBX 6.2;    // 7050  ZANGE SCHLIESSEN
BEB
NETWORK
TITLE = 7051  ZANGE/KEIN TEIL GESPANNT
U   M 110.0;        // AUX-ON
U   DB20.DBX 324.4; // REFERENZPUNKT AKTIV
U   M 22.3;         // HM 7051  ZANGE/KEIN TEIL GESPANNT
=   DB3.DBX 6.3;    // 7051  ZANGE/KEIN TEIL GESPANNT
BEB
NETWORK 25
TITLE = 7053  PINOLE/KEIN TEIL GESPANNT
U   M 22.5;         // HM 7053  PINOLE/KEIN TEIL GESPANNT
=   DB3.DBX 6.5;    // 7053  PINOLE/KEIN TEIL GESPANNT
BEB
NETWORK
TITLE = 7016  HILFSANTRIEBE EINSCHALTEN
U   DB20.DBX 324.4; // REFERENZPUNKT AKTIV
U   M 18.0          // HM 7016  HILFSANTRIEBE EINSCHALTEN
=   DB3.DBX 2.0;    // 7016  HILFSANTRIEBE EINSCHALTEN
BEB
NETWORK
TITLE = 7040  MASCHINENTÜR OFFEN
U   M 21.0          // HM 7040  MASCHINENTÜR OFFEN
=   DB3.DBX 5.0;    // 7040  MASCHINENTÜR OFFEN
BEB
NETWORK
TITLE = 7016  HILFSANTRIEBE EINSCHALTEN
U   M 18.0          // HM 7016  HILFSANTRIEBE EINSCHALTEN
=   DB3.DBX 2.0;    // 7016  HILFSANTRIEBE EINSCHALTEN
BEB
NETWORK
TITLE = 7077 Werkzeugwender einstellen
U   M 23.0          // HM 7077 Meldung Werkzeugwender einstellen
=   DB3.DBX 9.5;    // 7077 Werkzeugwender einstellen
BEB
NETWORK
TITLE = 7063  ÖLSTAND ZENTRALSCHMIERUNG
O   M 22.6          // HM 7063 ÖLSTAND ZENTRALSCHMIERUNG
=   DB3.DBX 7.7;    // 7063 ÖLSTAND ZENTRALSCHMIERUNG !
BEB
NETWORK
TITLE = 7038  DRUCKSCHALTER SCHMIERMITTEL DEFEKT
U   M 20.6;         // HM 7038  DRUCKSCHALTER SCHM. DEFEKT
=   DB3.DBX 4.6;    // 7038  DRUCKSCHALTER SCHM. DEFEKT
BEB
NETWORK
TITLE = 7039  SCHMIERMITTEL DEFEKT
U   M 20.7;         // HM 7039  SCHMIERMITTEL DEFEKT
=   DB3.DBX 4.7;    // 7039  SCHMIERMITTEL DEFEKT
BEB
NETWORK 24
TITLE = 7052  PINOLE IN ZWISCHENSTELLUNG
U   M 22.4;         // HM 7052  PINOLE IN ZWISCHENSTELLUNG
=   DB3.DBX 6.4;    // 7052  PINOLE IN ZWISCHENSTELLUNG
BEB
NETWORK
TITLE = 7017  REFERENZPUNKT ANFAHREN
U   M 18.1          // HM 7017  REFERENZPUNKT ANFAHREN
=   DB3.DBX 2.1;    // 7017  REFERENZPUNKT ANFAHREN
BEB
NETWORK
TITLE = 7000  FALSCHES T-WORT
U   M 17.0;         // HM 7000  FALSCHES T-WORT
=   DB3.DBX 0.0;    // 7000  FALSCHES T-WORT
BEB
NETWORK
TITLE = 7043  SOLLSTÜCKZAHL ERREICHT
U   M 21.3;         // HM 7043  SOLLSTÜCKZAHL ERREICHT
=   DB3.DBX 5.3;    // 7043  SOLLSTÜCKZAHL ERREICHT
BEB
NETWORK
TITLE = 7018  SCHLÜSSEL UMSCHALTEN
U   M 18.2;         // HM 7018  SCHLÜSSEL UMSCHALTEN
=   DB3.DBX 2.2;    // 7018  SCHLÜSSEL UMSCHALTEN
BEB
END_FUNCTION
FUNCTION FC 32: VOID
NAME: LED ANSTEUERUNG
BEGIN
NETWORK
TITLE = LAMPENTEST
;       U   DB20.DBX 294.7  // 8*************
;       S   A 8.1
;       S   DB20.DBX 846.0  // SKIP
;       S   DB20.DBX 846.1  // DRYRUN
;       S   DB20.DBX 847.0  // TÜR ÖFFNEN
;       S   DB20.DBX 847.1  // TÜR SCHLIESSEN
;       S   DB20.DBX 847.3  // 1X TASTE
;       S   DB20.DBX 847.4  // OPT.STOP (M1)
;       S   DB20.DBX 848.4  // WERKZEUGWENDEN
;       S   DB20.DBX 848.6  // RESET
;       S   DB20.DBX 848.7  // EINZELSATZ
;       S   DB20.DBX 849.6  // PINOLE ZURÜCK
;       S   DB20.DBX 849.7  // PINOLE VOR
;       S   DB20.DBX 850.0
;       S   DB20.DBX 850.4  // REFERENZPUNKT ANFAHREN
;       S   DB20.DBX 851.1  // KÜHLMITTEL
;       S   DB20.DBX 851.4  // PROGRAMM HALT
;       S   DB20.DBX 851.5  // PROGRAMM START
;       S   DB20.DBX 851.6  // VORSCHUB HALT
;       S   DB20.DBX 852.0  // VORSCHUB START
;       S   DB20.DBX 852.1  // SPINDEL STOPP
;       S   DB20.DBX 852.3  // SPINDEL START
;       S   DB20.DBX 852.4  // AUX OFF
;       S   DB20.DBX 852.6  // AUX ON
;
;       R   DB20.DBX 846.0  // SKIP
;       R   DB20.DBX 846.1  // DRYRUN
;       R   DB20.DBX 847.0  // TÜR ÖFFNEN
;       R   DB20.DBX 847.1  // TÜR SCHLIESSEN
;       R   DB20.DBX 847.3  // 1X TASTE
;       R   DB20.DBX 847.4  // OPT.STOP (M1)
;       R   DB20.DBX 848.4  // WERKZEUGWENDEN
;       R   DB20.DBX 848.6  // RESET
;       R   DB20.DBX 848.7  // EINZELSATZ
;       R   DB20.DBX 849.6  // PINOLE ZURÜCK
;       R   DB20.DBX 849.7  // PINOLE VOR
;       R   DB20.DBX 850.0
;       R   DB20.DBX 850.4  // REFERENZPUNKT ANFAHREN
;       R   DB20.DBX 851.1  // KÜHLMITTEL
;       R   DB20.DBX 851.4  // PROGRAMM HALT
;       R   DB20.DBX 851.5  // PROGRAMM START
;       R   DB20.DBX 851.6  // VORSCHUB HALT
;       R   DB20.DBX 852.0  // VORSCHUB START
;       R   DB20.DBX 852.1  // SPINDEL STOPP
;       R   DB20.DBX 852.3  // SPINDEL START
;       R   DB20.DBX 852.4  // AUX OFF
;       R   DB20.DBX 852.6  // AUX ON
;       U   T 47;           // BLINKIMPULS
;       =   DB20.DBX 851.0
;
;
;       U   T 48;           // STARTEN ALS VERLÄNGERTER IMPULS
;       =   DB20.DBX 850.6
NETWORK
TITLE = LED SPANNMITTEL
O   DB3.DBX 10.4;   // 7084  Schraubstock offen !
ON  M 138.5;        // SCHRAUBSTOCK GESPANNT
U   T 47;           // BLINKIMPULS
O   M 138.5;        // SCHRAUBSTOCK GESPANNT
U(
O   DB10.DBX 75.2;  // SPS-MSD /PNEUM. SPM
O   DB10.DBX 75.6;  // EMCO-SPANNMITTEL AKTIVIEREN ET325II (ohne Bit 2 aktiv)
)
=   M 58.7;         // AUSGANGSMERKER LAMPE SPANNMITTELTASTE
=   DB20.DBX 848.4
NETWORK
TITLE = LED M 200.0 WERKZEUGWENDER EINSTELLARBEITEN
U   M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
U   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
=   DB20.DBX 851.3  // LED WERKZEUGWENDER EINSTELLARBEITEN
//// Designed by WIMMER:
NETWORK
TITLE = LED M 200.3 SPANNMITTEL BEI TUER ZU
U   M 200.3         // WERKZEUGWENDER EINSTELLARBEITEN
U   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
=   DB20.DBX 851.2  // LED SPANNMITTEL BEI TUER ZU
////
NETWORK
TITLE = LED KÜHLMITTEL
U   A 10.7          // Kühlmittelpumpe
=   DB20.DBX 851.1  // LED KÜHLMITTEL
NETWORK
TITLE = LED DRYRUN AKTIV
U   DB20.DBX 324.2  // DRYRUN aktiv
=   DB20.DBX 846.1  // DRYRUN
NETWORK
TITLE = LED AUSBLENDSATZ SKIP
U   DB20.DBX 324.6  // Ausblendsatz (SKIP) aktiv
=   DB20.DBX 846.0  // SKIP
NETWORK
TITLE = LED Optionaler Halt M1 aktiv
U   DB20.DBX 324.5  // Optionaler Halt M1 aktiv
=   DB20.DBX 847.4  // OPT.STOP (M1)
NETWORK
TITLE = LED 1X AKTIV
U   M 8.2           // 1X AKTIV
=   DB20.DBX 847.3  // 1X TASTE
NETWORK
TITLE = LED EINZELSATZ
U   DB20.DBX 324.7  // Einzelsatz (SINGLE) aktiv
=   DB20.DBX 848.7  // EINZELSATZ
NETWORK
TITLE = LED AUX ON LEUCHTEN, AUX OFF BLINKEN
U   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
O   M 110.0;        // AUX-ON
UN  M 11.0          // HM NOT AUS Test Ersteinschaltung
UN  M 11.4          // HM Tür auf/zu-Test Ersteinschaltung
UN  M 11.5          // Warten auf USBSPS nicht NOT-AUS
=   DB20.DBX 852.6  // AUX ON
NETWORK
TITLE = LED REFERENZPUNKT ANFAHREN
U   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
U   M 110.0;        // AUX-ON
O   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
UN  DB20.DBX 324.4; // REFERENZPUNKT AKTIV
UN  M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
UN  M 21.0          // HM 7040  MASCHINENTÜR OFFEN
U   M 111.0;        // ACHSEN BETRIEBSBEREIT
UN  DB3.DBX 9.3;    // 7075 Offsetabgleich aktiv
U   M 120.1;        // REF ACHSEN FREIGABE
=   DB20.DBX 850.4  // REFERENZPUNKT ANFAHREN
NETWORK
TITLE = LED WERKZEUGWENDEN AKTIV
O   M 152.0;        // WERKZEUGWENDEN AKTIV
=   DB20.DBX 848.4  // WERKZEUGWENDEN
NETWORK
TITLE = LED SPINDEL HALT AKTIV
U   M 132.1;        // SPINDEL HALT AKTIV
U   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
=   DB20.DBX 852.3  // SPINDEL START
NETWORK
TITLE = LED VORSCHUB HALT AKTIV
U   M 133.1;        // VORSCHUB HALT AKTIV
U   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
=   DB20.DBX 852.0  // VORSCHUB START
NETWORK
TITLE = LED PROGRAMM HALT AKTIV
U   DB20.DBX 324.0; // Programm läuft
U   DB20.DBX 324.1; // STOP-ZUSTAND
U   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
=   DB20.DBX 851.5  // PROGRAMM START
NETWORK
TITLE = SCHRAUBSTOCK GESPANNT
O   M 138.5;        // SCHRAUBSTOCK GESPANNT
O   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
U   DB10.DBX 75.1   // PNEUM. SCHRAUBSTOCK AKTIVIEREN
=   DB20.DBX 849.6  // LED SCHRAUBSTOCK GESPANNT
NETWORK
TITLE = TÜR GESCHLOSSEN TÜRAUTOMATIK
O   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
O   M 50.0;          // TÜR ZU VON TÜRMODUL Y4A043000 X5:4 FG1
U   DB10.DBX 75.0;  // TÜRAUTOMATIK AKTIVIEREN
=   DB20.DBX 847.1  // TÜR SCHLIESSEN
NETWORK
TITLE = LED Taste Späneförderer
U   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
U   A 10.5          // Späneförderer rückwärts
O   A 10.4          // Späneförderer vorwärts
=   DB20.DBX 847.2  // LED Taste Späneförderer
NETWORK
TITLE =  PROGRAMM BEENDEN UND LED LÖSCHEN
O   M 110.0;        // AUX-ON
O   M 6.2           // A-Achse steht nicht innerhalb +-5°
U   DB1.DBX 1440.1; // PROGRAMM BEENDEN
R   DB1.DBX 1440.1; // PROGRAMM BEENDEN
U   DB1.DBX 1440.1; // PROGRAMM BEENDEN
UN  M 110.0;        // AUX-ON
SPBN   M001
L   W#16#0;         // LADE KONSTANTE HEX 0
T   DB20.DBW 846    // LED IM BEDIENPULT LÖSCHEN
T   DB20.DBW 848    // LED IM BEDIENPULT LÖSCHEN
T   DB20.DBW 850    // LED IM BEDIENPULT LÖSCHEN
T   DB20.DBW 852    // LED IM BEDIENPULT LÖSCHEN
M001:  NOP 0
END_FUNCTION
FUNCTION FC 60: VOID
NAME: FREI PROGRAMMIERBARE M-BEFEHLE
BEGIN
UN  E 9.7;          //* NOT AUS EINGANG FÜR FREI PROGRAMMIERBARE AUSGÄNGE
L   S5T#500MS;      // 500MS
SE  T 61;           // STARTEN T 61 ALS EINSCHALTVERZ.
U   T 61;
FP  M 46.0
ON  M 110.0;        // AUX-ON
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
U   E 9.7;          //* NOT AUS EINGANG FÜR FREI PROGRAMMIERBARE AUSGÄNGE
O   M 46.0
SPBN   M001
L   0
T   MB 61
M001:  NOP 0
U   DB20.DBX 201.0; // M72 dyn
UN  DB20.DBX 201.1; // M73 dyn
S   M 61.1;         // AUSGANGSMERKER M72/M73
U   DB20.DBX 201.1; // M73 dyn
R   M 61.1;         // AUSGANGSMERKER M72/M73
U   DB20.DBX 201.2; // M74 dyn
UN  DB20.DBX 201.3; // M75 dyn
S   M 61.2;         // AUSGANGSMERKER M74/M75
U   DB20.DBX 201.3; // M75 dyn
R   M 61.2;         // AUSGANGSMERKER M74/M75
U   DB20.DBX 201.4; // M76 dyn
UN  DB20.DBX 201.5; // M77 dyn
S   M 61.3;         // AUSGANGSMERKER M76/M77
U   DB20.DBX 201.5; // M77 dyn
R   M 61.3;         // AUSGANGSMERKER M76/M77
U   DB20.DBX 201.6; // M78 dyn
UN  DB20.DBX 201.7; // M79 dyn
S   M 61.4;         // AUSGANGSMERKER M78/M79
U   DB20.DBX 201.7; // M79 dyn
R   M 61.4;         // AUSGANGSMERKER M78/M79
U   DB20.DBX 202.0; // M80 dyn
UN  DB20.DBX 202.1; // M81 dyn
S   M 61.5;         // AUSGANGSMERKER M80/M81
U   DB20.DBX 202.1; // M81 dyn
R   M 61.5;         // AUSGANGSMERKER M80/M81
U   DB20.DBX 202.2; // M82 dyn
UN  DB20.DBX 202.3; // M83 dyn
S   M 61.6;         // AUSGANGSMERKER M82/M83
U   DB20.DBX 202.3; // M83 dyn
R   M 61.6;         // AUSGANGSMERKER M82/M83
U   DB20.DBX 202.4; // M84 dyn
UN  DB20.DBX 202.5; // M85 dyn
S   M 61.7;         // AUSGANGSMERKER M84/M85
U   DB20.DBX 202.5; // M85 dyn
R   M 61.7;         // AUSGANGSMERKER M84/M85
END_FUNCTION
FUNCTION FC 37 : VOID
NAME: RUNDACHSE WALTER
BEGIN
//  -A115 (res. für Walter Rundachse)
//
//  X3:5  Led H2   E 0.4
//  X3:11 Led H1   A 0.4
//  X3:12 Led H3   A 11.7
//  X3:13 Led H4   A 12.0
//  E 6.0   // Druckschalter Klemmung Rundachse
//  A 6.1   // RUNDACHSE KLEMMEN
//  A 6.2   // RUNDACHSE FREIGEBEN
NETWORK
TITLE = A6093 AS-Signalüberwachung A-Achse
U   A 4.3           // Impulsfreigabe Hauptantrieb und Z-Achse
U   E 6.1           // Überwachung AS Signal A-Achse
O(
UN  A 4.3           // Impulsfreigabe Hauptantrieb und Z-Achse
UN  E 6.1           // Überwachung AS Signal A-Achse
)
L   S5TIME#300MS
SE  T 49
U   T 49
S   DB2.DBX 11.5;   // 6093 AS-Signalüberwachung A-Achse
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 11.5;   // 6093 AS-Signalüberwachung A-Achse
NETWORK
TITLE = ÜBERWACHUNG RUNDACHSE BETRIEBSBEREIT
U   A 0.4           // A-ACHSE Reglerfreigabe
L   S5T#3S;         // 3 SEKUNDEN
SE  T 45;            // STARTEN T 45 ALS EINSCHALTVERZ.
NETWORK
TITLE = 6087 ANTRIEB A-ACHSE N. BEREIT
U   T 45;           // ACHSÜBERWACHUNG SCHARF
UN  E 0.4;          // A-Achse  SERVO-READY
L   S5T#1S;         // 1S
SE  T 28;           // STARTEN T 28 ALS EINSCHALTVERZ.
U   T 28;
S   DB2.DBX 10.7;    // 6087 ANTRIEB A-ACHSE N. BEREIT
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 10.7;    // 6087 ANTRIEB A-ACHSE N. BEREIT
NETWORK
TITLE = REGLERFREIGABE ABSOLUTE ACHSE 4
U   E 0.4;          // Walter Rundachse  SERVO-READY
FP  M 120.2         // FM Walter Rundachse  SERVO-READY
U   M 120.2         // FM Walter Rundachse  SERVO-READY
S   M 120.3         // HM REGLERFREIGABE ABSOLUTE ACHSE 4
U   A 0.4           // A-ACHSE Reglerfreigabe
FN  M 120.4         // FM A-ACHSE Reglerfreigabe
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   M 120.4         // FM A-ACHSE Reglerfreigabe
R   M 120.3         // HM REGLERFREIGABE ABSOLUTE ACHSE 4
U   M 120.3         // HM REGLERFREIGABE ABSOLUTE ACHSE 4
=   DB1.DBX 1460.4  // REGLERFREIGABE ABSOLUTE ACHSE 4
NETWORK
TITLE = ANFORDERUNG REFPKT A,X,Y,Z,ZUGLEICH ANFAHREN
U   M 121.3;        // REFPKT X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.1;        // REF ACHSEN FREIGABE
S   M 119.1         // HM REFERENZIEREN VON ACHSE A ERWÜNSCHT
R   DB20.DBX 9.2;   // REFERENZIEREN VON KANALACHSE A
NETWORK
TITLE = DURCHFÜHRUNG REFPKT A,X,Y,Z,ZUGLEICH ANFAHREN
U   M 119.1         // HM REFERENZIEREN VON ACHSE A ERWÜNSCHT
U   M 120.7;        // FM REFERENZPUNKT Z-ACHSE AKTIV
S   M 119.2         // HM REFERENZIEREN VON ACHSE A DURCHFÜHREN
R   M 119.1         // HM REFERENZIEREN VON ACHSE A ERWÜNSCHT
NETWORK
TITLE = DURCHFÜHRUNG REFPKT A ANFAHREN
U   DB20.DBX 9.2;   // REFERENZIEREN VON KANALACHSE A
U   M 120.1;        // REF ACHSEN FREIGABE
UN  M 121.3;        // REFPKT X,Y,Z,ZUGLEICH ANFAHREN
S   M 119.2         // HM REFERENZIEREN VON ACHSE A DURCHFÜHREN
R   DB20.DBX 9.2;   // REFERENZIEREN VON KANALACHSE A
NETWORK
TITLE = REFERENZIEREN VON KANALACHSE A
U   M 119.2         // HM REFERENZIEREN VON ACHSE A ERWÜNSCHT
SPBN M100;
=   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
=   DB20.DBX 9.2;   // REFERENZIEREN VON KANALACHSE A
R   M 119.2         // HM REFERENZIEREN VON ACHSE A ERWÜNSCHT
M100: NOP 0;
NETWORK
TITLE = RUNDACHSE KLEMMUNG LÖSEN
UN  DB20.DBX 843.2; // A ACHSE IPO BEFEHL AKTIV
UN  DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
O   DB20.DBX 193.3; // M11 DYN.KLEMMUNG LÖSEN
U(
O   M 113.0;        // SONDERBETRIEB AKTIV
O   M 50.3;         // FREIGABEKREIS AUF AUSGABEMODUL DURCHGESCHALTET
)
R   A 6.1;          // RUNDACHSE KLEMMEN
S   A 6.2;          // RUNDACHSE FREIGEBEN
=   DB20.DBX 257.3; // M11 RÜCKMELDEN
U   DB20.DBX 193.3; // M11 DYN.KLEMMUNG LÖSEN
S   M 119.3         // HM M11 DYN.KLEMMUNG LÖSEN
NETWORK
TITLE = EINLESEFREIGABE WÄHREND DER KLEMMPHASE WEGNEHMEN
U   DB20.DBX 257.3; // M11 RÜCKMELDEN
UN  M 119.3         // HM M11 DYN.KLEMMUNG LÖSEN
U   DB20.DBX 340.3; // EINLESEFREIGABE
S   M 119.6         // EINLESEFREIGABE WEGNEHMEN (VON RUNDACHSE)
U   DB20.DBX 193.2; // M10 DYN.KLEMMUNG EIN
R   M 119.3         // HM M11 DYN.KLEMMUNG LÖSEN
UN  M 119.3         // HM M11 DYN.KLEMMUNG LÖSEN
U   DB20.DBX 843.2  // A Achse IPO Befehl aktiv
U   DB20.DBX 546.4  // Genauhalt fein für Achse 4
L   S5TIME#400MS;
SE  T 20;           // Genauhalt fein für Achse 4
U   T 20;           // Genauhalt fein für Achse 4
R   M 119.6         // EINLESEFREIGABE WEGNEHMEN (VON RUNDACHSE)
U   T 20;           // Genauhalt fein für Achse 4
FP  M 119.0         // Genauhalt fein für Achse 4
U   DB1.DBX 134.7   // Achse 10 ist referenziert
FP  M 119.7         // FM Achse 10 ist referenziert
NETWORK
TITLE = RUNDACHSE EINLESEFREIGABE
U   M 119.6         // EINLESEFREIGABE WEGNEHMEN (VON RUNDACHSE)
UN  DB20.DBX 862.0  // Transmit oder Tracyl aktiv
=   M 90.2;         // EINLESEFREIGABE WEGNEHMEN (VON RUNDACHSE)
NETWORK
TITLE = RUNDACHSE KLEMMEN
O   M 119.7         // FM Achse 10 ist referenziert
O   M 119.0         // FM Genauhalt fein für S2 Achse
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
O(
ON  M 50.3;         // FREIGABEKREIS AUF AUSGABEMODUL DURCHGESCHALTET
UN  M 113.0;        // SONDERBETRIEB AKTIV
)
S   A 6.1;          // RUNDACHSE KLEMMEN
R   A 6.2;          // RUNDACHSE FREIGEBEN
=   DB20.DBX 257.2; // M10 RÜCKMELDEN
R   M 119.3         // HM M11 DYN.KLEMMUNG LÖSEN
NETWORK
TITLE = ALARM 6080 Druckschalter Klemmung TANI fehlt
U   A 6.1;          // RUNDACHSE KLEMMEN
L   S5TIME#1S;
SE  T 7;            // STARTEN T7 ALS EINSCHALTVERZ.
U   T 7;            // STARTEN T7 ALS EINSCHALTVERZ.
UN  E 6.0           // DRUCKSCHALTER RUNDACHSE
S   DB2.DBX 10.0;   // 6080 Druckschalter Klemmung TANI fehlt
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 10.0;   // 6080 Druckschalter Klemmung TANI fehlt
NETWORK
TITLE = ALARM 6081 Klemmung TANI nicht offen
U   A 6.2;          // RUNDACHSE FREIGEBEN
L   S5TIME#1S;      //
SE  T 6;            // STARTEN T6 ALS EINSCHALTVERZ.
U   T 6;            // STARTEN T6 ALS EINSCHALTVERZ.
U   E 6.0           // DRUCKSCHALTER RUNDACHSE
S   DB2.DBX 10.1;   // 6081 Klemmung TANI nicht offen
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 10.1;   // 6081 Klemmung TANI nicht offen
END_FUNCTION
FUNCTION FC 30 : VOID
NAME: PNEUM_SCHRAUBSTOCK
BEGIN
NETWORK
TITLE = FM TASTE SCHRAUBSTOCK ZU
U   DB20.DBX 829.6  // TASTE SCHRAUBSTOCK ZU
UN  DB20.DBX 829.7  // TASTE SCHRAUBSTOCK AUF
O   M 138.1;        // FM FUSZTASTER SPANNMITTEL ZU
FP  M 138.7;        // FM TASTE SCHRAUBSTOCK ZU
NETWORK
TITLE = FM TASTE SCHRAUBSTOCK AUF
U   DB20.DBX 829.7  // TASTE SCHRAUBSTOCK AUF
UN  DB20.DBX 829.6  // TASTE SCHRAUBSTOCK ZU
O   M 138.2;        // FM FUSZTASTER SPANNMITTEL AUF
FP  M 139.1;        // FM TASTE SCHRAUBSTOCK AUF
NETWORK
TITLE = FREIGABE SCHRAUBSTOCK ZU/AUF
U   M 110.0;        // AUX-ON
U   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
UN  M 131.7;        // ACHSEN IN BEWEGUNG
UN  M 152.0;        // WERKZEUGWENDEN AKTIV
=   M 138.0;        // FREIGABE SCHRAUBSTOCK ZU/AUF
NETWORK
TITLE = HM SCHRAUBSTOCK ZU (M26)
U   DB20.DBX 324.0; // PROGRAMM LÄUFT
U   DB20.DBX 195.2; // M26 DYN.
S   M 139.5;        // HM SCHRAUBSTOCK ZU (M26)
NETWORK
TITLE = LEISTUNG SCHRAUBSTOCK ZU
U   M 138.0;        // FREIGABE SCHRAUBSTOCK ZU/AUF
U(;
U   M 138.7;        // FM TASTE SCHRAUBSTOCK ZU
UN  M 139.1;        // FM TASTE SCHRAUBSTOCK AUF
O   DB20.DBX 195.2; // M26 DYN.
O(;
O   M 169.1;        // ROBOTIK/FM SCHRAUBSTOCK ZU
O   DB20.DBX 302.2; // DNC/SCHRAUBSTOCK ZU
);
U(;
ON  DB20.DBX 324.0; // PROGRAMM LÄUFT
O   DB20.DBX 324.1; // KERN/STOP
);
O(;
U   M 139.5;        // HM SCHRAUBSTOCK ZU (M26)
UN  M 131.7;        // ACHSEN IN BEWEGUNG
);
);
S   A 5.6 ;         // PNEUM.SCHRAUBSTOCK SCHLIESSEN
R   A 5.7 ;         // PNEUM.SCHRAUBSTOCK ÖFFNEN
=   DB20.DBX 259.2; // M26 AUSLÖSEN
S   M 139.7;        // HM M26 STATISCH
R   M 139.6;        // HM M25 STATISCH
NETWORK
TITLE = RÜCKS. HM SCHRAUBSTOCK ZU (M26)
U   M 139.5;        // HM SCHRAUBSTOCK ZU (M26)
UN  M 131.7;        // ACHSEN IN BEWEGUNG
R   M 139.5;        // HM SCHRAUBSTOCK ZU (M26)
NETWORK
TITLE = HM SCHRAUBSTOCK AUF (M25)
U   DB20.DBX 324.0; // PROGRAMM LÄUFT
U   DB20.DBX 195.1; // M25 DYN.
S   M 139.4;        // HM SCHRAUBSTOCK AUF (M25)
NETWORK
TITLE = LEISTUNG SCHRAUBSTOCK AUF
U   M 138.0;        // FREIGABE SCHRAUBSTOCK ZU/AUF
U(;
U   M 139.1;        // FM TASTE SCHRAUBSTOCK AUF
UN  M 138.7;        // FM TASTE SCHRAUBSTOCK ZU
O   DB20.DBX 195.1; // M25 DYN.
O(;
O   M 169.3;        // ROBOTIK/FM SCHRAUBSTOCK AUF
O   DB20.DBX 302.1; // DNC/SCHRAUBSTOCK AUF
);
U(;
ON  DB20.DBX 324.0; // PROGRAMM LÄUFT
O   DB20.DBX 324.1; // KERN/STOP
);
O(;
U   M 139.4;        // HM SCHRAUBSTOCK AUF (M25)
UN  M 131.7;        // ACHSEN IN BEWEGUNG
);
);
S   A 5.7 ;         // PNEUM.SCHRAUBSTOCK ÖFFNEN
R   A 5.6 ;         // PNEUM.SCHRAUBSTOCK SCHLIESSEN
=   DB20.DBX 259.1; // M25 AUSLÖSEN
S   M 139.6;        // HM M25 STATISCH
R   M 139.7;        // HM M26 STATISCH
NETWORK
TITLE = RÜCK. HM SCHRAUBSTOCK AUF (M26)
U   M 139.4;        // HM SCHRAUBSTOCK AUF (M25)
UN  M 131.7;        // ACHSEN IN BEWEGUNG
R   M 139.4;        // HM SCHRAUBSTOCK AUF (M25)
NETWORK
TITLE = SCHRAUBSTOCK GESPANNT
U   A 5.6 ;         // PNEUM.SCHRAUBSTOCK SCHLIESSEN
U   E 5.7;          // DRUCKSCHALTER SCHRAUBSTOCK
=   M 138.5;        // SCHRAUBSTOCK GESPANNT
NETWORK
TITLE = SCHRAUBSTOCK ENTSPANNT
U   A 5.7 ;         // PNEUM.SCHRAUBSTOCK ÖFFNEN
UN  E 5.7;          // DRUCKSCHALTER SCHRAUBSTOCK
ON  M 138.5;        // SCHRAUBSTOCK GESPANNT
=   M 138.4;        // SCHRAUBSTOCK OFFEN
NETWORK
TITLE = SCHRAUBSTOCKBEW. AKTIV  AFG/EFG
U   A 5.6 ;         // PNEUM.SCHRAUBSTOCK SCHLIESSEN
UN  E 5.7;          // DRUCKSCHALTER SCHRAUBSTOCK
O;
U   A 5.7 ;         // PNEUM.SCHRAUBSTOCK ÖFFNEN
U   E 5.7;          // DRUCKSCHALTER SCHRAUBSTOCK
O   M 139.5;        // HM SCHRAUBSTOCK ZU (M26)
O   M 139.4;        // HM SCHRAUBSTOCK AUF (M25)
=   M 91.5;         // SCHRAUBSTOCKBEW. AKTIV  AFG/EFG
NETWORK
TITLE = 6077  SCHRAUBSTOCK NICHT BEREIT
U   A 5.6 ;         // PNEUM.SCHRAUBSTOCK SCHLIESSEN
L   S5TIME#1S;      // 10X0,1S
SE  T 32;           // STARTEN T32 ALS EINSCHALTVERZ.
UN  E 5.7;          // DRUCKSCHALTER SCHRAUBSTOCK
L   S5TIME#200MS;   // 2X0,1S
SE  T 33;           // STARTEN T33 ALS EINSCHALTVERZ.
U   A 5.7 ;         // PNEUM.SCHRAUBSTOCK ÖFFNEN
L   S5TIME#3S;      // 10X0,1S 8++++++
SE  T 34;           // STARTEN T34 ALS EINSCHALTVERZ.
U   E 5.7;          // DRUCKSCHALTER SCHRAUBSTOCK
L   S5TIME#200MS;   // 2X0,1S
SE  T 35;           // STARTEN T35 ALS EINSCHALTVERZ.
U   T 32;           // SCHRAUBST. ZU/ÜBERW. SCHARF
U   T 33;           // DRUCKSCHALTER SCHRAUBST. (0,2S)
O;
U   T 34;           // SCHRAUBST. AUF/ÜBERW. SCHARF
U   T 35;           // DRUCKSCHALTER SCHRAUBST. (0,2S)
O;
UN  M 138.5;        // SCHRAUBSTOCK GESPANNT
U(;
O   DB20.DBX 224.3; // M3 STATISCH
O   DB20.DBX 224.4; // M4 STATISCH
);
S   DB2.DBX 9.5;    // 6077  SCHRAUBSTOCK NICHT BEREIT
U   DB2.DBX 9.5;    // 6077  SCHRAUBSTOCK NICHT BEREIT
R   A 5.6 ;         // PNEUM.SCHRAUBSTOCK SCHLIESSEN
R   A 5.7 ;         // PNEUM.SCHRAUBSTOCK ÖFFNEN
UN  A 5.6 ;         // PNEUM.SCHRAUBSTOCK SCHLIESSEN
UN  A 5.7 ;         // PNEUM.SCHRAUBSTOCK ÖFFNEN
U(
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
)
R   DB2.DBX 9.5;    // 6077  SCHRAUBSTOCK NICHT BEREIT
NETWORK
TITLE = 6050  M25/M26 BEI LAUFENDEM HA
UN  M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
U   DB20.DBX 195.1; // M25 DYN.
S   DB2.DBX 6.2;    // 6050  M25 BEI LAUFENDEM HA
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 6.2;    // 6050  M25 BEI LAUFENDEM HA
NETWORK
TITLE = 7084  Schraubstock offen
O   M 138.4;        // SCHRAUBSTOCK OFFEN
=   M 23.7;         // HM 7084  Schraubstock offen !
NETWORK
TITLE = SPS/OB-->SCHRAUBSTOCK GESPANNT
U   M 138.5;        // SCHRAUBSTOCK GESPANNT
=   DB20.DBX 310.2; // SPS/OB-->SCHRAUBSTOCK GESPANNT
NETWORK
TITLE = SPS/OB-->SCHRAUBSTOCK ENTSPANNT
U   M 138.4;        // SCHRAUBSTOCK OFFEN
=   DB20.DBX 310.1; // SPS/OB-->SCHRAUBSTOCK ENTSPANNT
NETWORK
TITLE = SCHRAUBSTOCK/FC DNC-QUITTUNG
U   DB1.DBX 1366.0; // DNC-BETRIEBN AKTIV
U(;
U   DB20.DBX 302.2; // DNC/SCHRAUBSTOCK ZU
UN  M 139.7;        // HM M26 STATISCH
O;
U   DB20.DBX 302.1; // DNC/SCHRAUBSTOCK AUF
UN  M 139.6;        // HM M25 STATISCH
);
S   DB20.DBX 318.1; // SCHRAUBSTOCK/FC DNC-QUITTUNG
END_FUNCTION
FUNCTION FC 61 : VOID
NAME: Steuerung zum Positionieren des Werkzeugmagazines
BEGIN
NETWORK
TITLE = PLC Kennung für ACC
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
S   DB1.DBX 1366.6; // 1= neue PLC-SW mit Richtungslogik
NETWORK
TITLE = Drehrichtung löschen
O   M 124.5         // FM Werkzeugmagazin in Position stoppen
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   M 158.0         // Werkzeugmagazin linkslauf
R   M 158.1         // Werkzeugmagazin rechtslauf
NETWORK
TITLE = Einmalige Richtungsbestimmung
U   M 124.4         // FM Werkzeugmagazin auf Position fahren
U   M 124.1         // Werkzeugmagazin referenziert
SPBN   M5102;
NETWORK
TITLE = DIFFERENZ T-WORT/ISTZÄHLER
L   MB 160          // Sollzähler Werkzeugmagazin
L   Z 3;            // LADE ISTZÄHLER
-I;                 // SUBTRAKTION
T   MW 156;         // TRANSFERIERE MW156
NETWORK
TITLE = ZÄHLERDIFFERENZ < 0
L   MW 156;         // LADE MW 156
L   0;              // LADE KONSTANTE DEZ 0
>=I;                // VERGLEICH AUF GRÖSSER GLEICH
SPB M5101;
L   MW 156;         // LADE MW 156
L   20;             // LADE KONSTANTE DEZ 20
+I;                 // ADDITION
T   MW 156;         // TRANSFERIERE IN MW 156
M5101: NOP 0;
NETWORK
TITLE = ZÄHLERDIFFERENZ > 10
L   MW 156;         // LADE MW 156
L   10;             // LADE KONSTANTE DEZ 10
<=I;                // VERGLEICH AUF KLEINER GLEICH
S   M 158.1         // Werkzeugmagazin rechtslauf
R   M 158.0         // Werkzeugmagazin linkslauf
SPB M5102;
L   MW 156;         // LADE MW 156
L   20;             // LADE KONSTANTE DEZ 20
-I;                 // SUBTRAKTION
T   MW 156;         // TRANSFERIERE IN MW 156
S   M 158.0         // Werkzeugmagazin linkslauf
R   M 158.1         // Werkzeugmagazin rechtslauf
M5102: NOP 0;
END_FUNCTION
ORGANIZATION_BLOCK OB 1
NAME: ORGANISATIONSBAUSTEIN
BEGIN
CALL   FC 34;          // ZUWEISEN DER EINGÄNGE AC 95 - ACC
CALL   FC 49           // Werkzeugwender Eingangsfilter
U     M 11.7          // USBSPS auf Ueberwachungsbetrieb eingestellt
CC     FC 22           // CYCLISCHE ÜBERWACHUNG PIC
CALL   FC 11;          // Neues Sicherheitskonzept
U   DB10.DBX 76.5;  // SPS-MSD /Späneförderer
CC   FC 24;          // Späneförderer
CALL   FC 13 ;         // INITIALISIERUNGEN ÜBERWACHUNGEN UND ALARME
U   DB1.DBX 1426.1; // ROBOTIK SCHNITTSTELLE AKTIVIERT MIT DISKETTE
U   DB10.DBX 75.7;  // ROBOTIK SCHNITTSTELLE AKTIVIERT IN MSD
CC   FC 40;          // ROBOTIK-INTERFACE
CALL   FC  9 ;         // RESET
CALL   FC 10 ;         // BEI PROGRAMM ENDE RESET ODER NEUSTART
CALL   FC 21 ;         // FUSZTASTER FÜR PNEUMATISCHES SPANNMITTEL
CALL   FC 17 ;         // UE Modul Netzeinspeisung
UN  DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
CC   FC 53           // Normaler// Von T-Wort Werkzeugmagazinpositionierung
U   DB1.DBX 1366.4  // 1= Chaotischer-  0= Normaler WZW                      *
CC   FC 51           // Von T-Wort Werkzeugmagazinpositionierung
CALL   FC 61           // Steuerung zum Positionieren des Werkzeugmagazines
CALL   FC 50           // Werkzeugmagazin und Werkzeugköcheransteuerung
UN  M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
CC   FC 52 ;         // WERKZEUGWENDER
U   M 200.0         // WERKZEUGWENDER EINSTELLARBEITEN
U(
O   DB20.DBX 324.4  // REFERENZPUNKT AKTIV
O   M 50.1;         // SCHLÜSSELSCHALTER AUF SONDERBETRIEB
)
CC   FC 55;          // WERKZEUGWENDER EINSTELLARBEITEN
CALL   FC 59 ;         // Werkzeugspannsystem
CALL   FC  3 ;         // HA STEUERUNG und LEISTUNG SPINDEL0
CALL   FC  4 ;         // X,Y,Z-ACHSE, Bremssteuerung, Imp.und Reglerfreigabe
U   DB10.DBX 75.1   // PNEUM. SCHRAUBSTOCK AKTIVIEREN
CC     FC 30;          // PNEUM. SCHRAUBSTOCK
UN  DB10.DBX 75.1   // PNEUM. SCHRAUBSTOCK AKTIVIEREN
S   A 5.7 ;         // PNEUM.SCHRAUBSTOCK ÖFFNEN
U   DB10.DBX 75.3;  // SPS-MSD /AUSBLASEN
CC   FC 25;          // AUSBLASEINRICHTUNG
U   DB10.DBX 75.0;  // SPS-MSD /TÜRAUTOMATIK
CC   FC 27;          // TÜRAUTOMATIK
CALL   FC  7 ;         // AUX-ON
CALL   FC 12 ;         // SONDERBETRIEB
CALL   FC  6 ;         // REF ACHSEN
CALL   FC  5 ;         // JOG ACHSEN
CALL   FC 16 ;         // INC ACHSEN
O    DB10.DBX 100.1  // 100mm Walter Rundachse aktivieren
O    DB10.DBX 100.3  // 80mm Walter Rundachse aktivieren
UN   DB10.DBX 100.2  // 4.5. Schrittmotor Rundachse aktivieren
UN   DB10.DBX 100.4  // 4.5. Schrittmotor Rundachse aktivieren
CC   FC 37 ;         // RUNDACHSE
O    DB10.DBX 100.2  // 4.5. Schrittmotor Rundachse aktivieren
O    DB10.DBX 100.4  // 4.5. Schrittmotor Rundachse aktivieren
UN   DB10.DBX 100.1  // 100mm Walter Rundachse aktivieren
UN   DB10.DBX 100.3  // 80mm Walter Rundachse aktivieren
CC   FC 38 ;         // 4.5. Schrittmotor Rundachse
CALL   FC 19 ;         // SPINDEL HALT/START
CALL   FC 20 ;         // VORSCHUB HALT/START
CALL   FC  1 ;         // SCHMIERMITTEL und PNEUMATIKÖLSTANDÜBERWACHUNG
CALL   FC  2 ;         // STÜCKZÄHLER
CALL   FC 18 ;         // KÜHLMITTEL (M8/M9)
CALL   FC 28 ;         // WARNLEUCHTE
CALL   FC 15 ;         // MELDUNGEN 7000
CALL   FC 32 ;         // LED ANSTEUERUNG
;;    CALL   FC 60 ;         // FREI PROGRAMMIERBARE M-BEFEHLE
CALL   FC 65 ;         // HANDRAD
CALL   FC  8 ;         // NC-STARTVERR. UND BEDIENTASTEN
CALL   FC 14 ;         // PLC NC-ÜBERGABE UND AFG/EFG/SFG
CALL   FC 35;          // ZUWEISEN DER AUSGÄNGE ACC
;;DB10.DBX B100.0 ; ACC_MSD B100=1  (mill250_.MSD) Standartmaschine
;;DB10.DBX B100.1 ; ACC_MSD B100=2  (mill250A.MSD) Mit 100mm Walter Rundachse
;;DB10.DBX B100.2 ; ACC_MSD B100=4  (mill250B.MSD) Mit 4.5. Schrittmotor Rundachse mit 1:87 Untersetzung
;;DB10.DBX B100.3 ; ACC_MSD B100=8  (mill250C.MSD) Mit 80 mm Walter Rundachse
;;DB10.DBX B100.4 ; ACC_MSD B100=16  (mill250C.MSD) Mit 4.5. Schrittmotor Rundachse mit 1:67 Untersetzung
U   DB25.DBX 332.0; // NC-SATZ FERTIG
R   DB25.DBX 332.0; // NC-SATZ FERTIG
END_ORGANIZATION_BLOCK
