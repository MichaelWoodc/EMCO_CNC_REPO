[Info]
Version=1.19
CheckChange=d1453739
PLCFileName=Turn332.awl
//   ***********************************************************************
//   *                                                                     *
//   *                    SPS-PROGRAMM fuer PCTurn345-II                   *
//   *                                                                     *
//   ***********************************************************************
//   V0.06  29.08.2000  LE  Prototyp-Version
//   V0.07  09.10.2000  LE  Vorserienstand
//   V0.08  18.10.2000  LE  Bit fuer Laborsimulator Maschinenlizenz eingebaut
//   V0.09  10.01.2001  LE  810T mit AWZ, C-Achse und Transmit eingebaut
//                          LED's an der Tastatur aktiviert
//                          bei AUX-ON wird Shutdown verhindert
//                          0TB, 21TB mit AWZ, C-Achse und Transmit eingebaut
//   V0.10  22.01.2001  LE  Fehler WZW bei laufenden AWZ behoben
//                          Neustart nach M30 generell Zeitverzoegert
//                          BMeld. 'SPM auf Endlage' im DRY ausgeblendet
//                          NC-Stop vom Kern durchlassen (MDA ohne M30)
//                          MSD fuer analoge SPM-Ueberwachung aufgetrennt
//   V0.11  02.03.2001  LE  Reset und NC-Stop ueber Oberfl. anfordern
//                          (neue Nahtstellenbits eingefuehrt)
//                          810/Fanuc: C Positionssatz nur bei M52 absetzen
//                          'VSH/EFG WZW-Schwenk' auch wenn AWZ nicht in Position
//   V0.12  29.03.2001  LE  div. Behebungen mit Reset und NC-Start
//                          Kuehlmittel richtiggestellt, Kupa-Spuelung neu
//                          AWZ bei WZW-Anford. neu positionieren wenn notwendig
//                          Endlagenueberw. Zange verbessert (nicht geklammert)
//   V0.13  11.05.2001  LE  Simulation Pinole korrigiert
//                          Fehler NC-Stop behoben (NC-Stop direkt an AC)
//   V0.14  24.09.2001  LE  zus. Min/Max Speicher fuer SPM Bero-Diagnose
//                          PLC-Stueckzaehler bei 840D aktiviert (nur Zaehler)
//   V0.15  18.12.2001  LE  Fehler 810T-Ueberspeichern von AWZ behoben
//                          EMCOTronic freigegeben
//          20.12.2001      STV/LM Interface freigegeben
//                          AWZ positionieren nicht mit M136.1 sonder M137.7
//          05.03.2002      autom. Reitstock getestet und freigegeben
//                          Maschinenkonfiguration auch ueber M90..M95 progr.
//          07.03.2002      versch. BM's bei Fanuc nicht ausgeben (Bildwechsel)
//                            (7201, 7202, 7203, 7257, 7275)
//                          AWZ-Spindel ausschalten und positionieren besser
//                            getrennt (Probleme in 15.45 bei SPOS[2])
//   V0.16  03.10.2002  LE  INC-Var für Sie840D realisiert
//                          Warnlampe 3fach eingebunden
//                          AWZ-Positionierung verbessert
//                          Meldg. der 1x-Taste nur über PLC-Bit anzeigen
//                          Meldg. Tuer offen bei Fanuc verhindert
//   V0.17  28.02.2003  LE  'Pinole zur. bei Aux-On' verb. und abwählbar (MD)
//                          WZW im JOG auch Linkslauf (WZW + Aux-On Taste glzt.)
//                          Tuerautomatik 'Landert' eingebunden
//                          'Festo' Robotikinterface eingebunden (FC109)
//          10.03.2003  LE  Ueberwachung 3.CAN-Modul ueber PLC-MD zuschaltbar
//                          BM 'Pinole in Zwischenstellung' immer anzeigen
//                          FM 'Stoerung n=0' nur bei 'Tuer zu, Antriebe ein'
//   V0.18  22.07.2003  LE  PLC bei TNC426 sperren
//                          Problem AWZ bei G332 behoben (sporad. Alarm)
//                          Sollstueckzahlvergleich bei Fanuc berichtigt
//          03.10.2003  LE  Handradfunktionen eingebunden
//   V1.00  27.11.2003  LE  Erweiterungen fuer ACC eingebaut
//          10.12.2003  LE  Initialisierung Interpolationsplatine
//          12.01.2004  LE  Analogkanäle im FC 11 richtiggestellt
//          02.02.2004  LE  bei Futter gespannt wurde falsche Grenze verr. (FC87 nach M075:)
//          09.02.2004  LE  FC 11 Analogwerte ACC korrigiert
//                          FC 75: Wechsel der Mode-Bits verbesert (sporad. Ausfälle)
//          08.03.2004  RH  Verzögerung JOG (T1) von 50ms auf 100ms erhöht;
//                          mit win98 am MOC wird ca.55ms benötigt, um den INC-Befehl abzuschicken.
//          08.03.2004  RH  Verzögerung INC-Komando löschen (T6);sporadische Umschaltung von INC auf JOG
//          08.03.2004  RH  Verzögerung INC JOG Verriegelung (T7);sporadische Umschaltung von INC auf JOG
//          15.04.2004  RH  FC 100: Feedoverride beim referenzieren auf 100% gesetzt;
//                          IB-bit für referenzieren mit Feedoverride  USER_DATA_HEX[17.0]
//   V1.01  27.05.2004  RH  FC 109: Robotik-Interface ohne Tuerautomatik; FC 93: Robotik mit Pinole
//   V1.02  04.06.2004  RH  FC 60: Überwachung 2.SW-Endschalter mit autom. Reitstock (inkl. Endlage hinten)
//          26.01.2005  LE  PAL freigegeben
//   V1.03  02.06.2006  LE  Sauter WZW mit EK600 eingebunden (OB1, FC 75)
//   V1.04  04.10.2006  LE  Türautomatik Landert NTiB eingebunden (FC 61, FC 109)
//                          'VS-Halt manuell' mit Reset-Taste rücksetzen (FC60)
//                          wenn S1 im Achsbetrieb ist wird bei Tür auf die Bremse aktiviert (FC71)
//                          Steuerung S1, S2 bei NC-Stop / Tür auf verbessert ( FC 51, FC 72)
//                          MSD: unter [Axis] AxisType2=SRSpindle eingefügt
//   V1.05  18.12.2007  LE  FC 100: Kommandobits VS-Override nur setzen wenn nötig, nicht dauernd
//                          FC 100: VS-Override unter 4% wirkt bei REF als 0%
//                          FC 72: AC-Anforderung fuer Spindelbetrieb eingebunden
//                          FC 70: S1 RFG bei Pendeln aus verzoegern (sonst Al.8234)
//                          gesamtes File korrigiert, Import in Step7 wieder möglich
//   V1.06  25.06.2008  LE  LED Aux on leuchtet wenn Aux-On
//                          LED's Spindeloverride zugewiesen
//   V1.07  06.08.2008  LE  autom. Tür im Stangenbetrieb bei M30 nicht öffnen
//                          bei NC-Start nach Tür zu war für 'H4 M99' falscher Merker verknuepft
//          11.08.2008  LE  Funktion Tür auf im Stangenbetrieb korrigiert
//   V1.08  13.01.2010  LE  manuelle Spindelsteuerung fuer FAGOR (wie EMCOTronic)
//
//   V1.13  19.07.2012  SCF Freie Ausgänge wenn Robotik nicht installiert ist eingebaut (A 7.7 Spindel läuft für Fa.Engel)
//                          DB10.DBX 44.7 // USER_DATA_HEX[4] Bit7 SPM auf/zu mit Tasten bei geschlossener Tür erlauben eingebaut
//
//
//   V1.15  08.04.2013  SCF Bei eingeschaltetem AWZ Antrieb kein AUX OFF mehr möglich
//   V1.16  19.04.2013  SCF SPS/Oberflächen Signal --> SPM vorhanden eingebaut (DB20.DBX 382.1).
//                          Dadurch funktioniert der DNC-Befehl für Spannmittel auf/zu.
//   V1.18              SCF E 5.4 Kupa Spuelanforderung und A 6.2 Kupa Spindelspuelung werden nicht mehr benötigt.
//
//
//
//
//   Türautomatik Langer+Laumann A 8.0 (M 212.0)Freigabe Türmotorschütz
//                                   E 8.0 Türautomatik Sicherheitstürschaltleiste nicht ausgelöst
//                                   E 8.6 Überwachung Motorschütz
//                                   E 8.1 Von Türsteuergerät Tür offen
//                                   E 8.7 Fehler Türautomatik Langer+Laumann
//                                   A 6.4 Tür auf
//                                   A 6.5 Tür zu
//
//    Für Concept TURN 450:
//
//    Anschluss Handradplatine Y5A030000:
//
//                    VON                    NACH Y5A030000
//    Einzelader bl   Klemme X11:1 +24V      X1:1   +24V
//    Einzelader bl   Klemme X11:2 GND       X1:2   GND
//    Einzelader bl   ACC  X900:1 +5V        X1:3   +5V
//    Handradkabel -- rot                    X1:4   +5V
//    Handradkabel -- blau                   X1:5   GND
//    Handradkabel -- grau                   X1:6   B-Spur
//    Handradkabel -- rosa                   X1:7   A-Spur
//    Einzelader bl   ACC X422:10  E 9.6      X1:8   Takt
//    Einzelader bl   ACC X422:11  E 9.7      X1:9   Richtung
//
//    Zustimtaster:
//                    VON                    NACH
//    Handradkabel -- gelb                   Bedienmodul 1DA0.R3-X581 X1:4
//    Handradkabel -- grün                   Bedienmodul 1DA0.R3-X581 X1:5
//
//   ***********************************************************************
//   Simulationsbetrieb mit der Maschinenlizenz ET345-II (AC95):
//      - 'Turn332.awl' in 'Pcmasch0.awl' kopieren
//      - in 'Turn332.msd' die NC und PLC auf Simulation stellen:
//          [Control]
//          SimulateNC=1
//          SimulatePLC=1
//          ControlInISR=0  (keine PC-Com Karte)
//      - in 'Turn332.msd' das Bit fuer Simulation setzen
//          [PLC-MSD]
//          B49=0x21  (Bit 0 setzen)
//      - im entspr. INI-File 'NCStartOnlyFromPLC' auskommentieren
//   ***********************************************************************
//   Unterschiede zu Step7:
//      - 'FP' und 'FN' sind VKE-Begrenzend und weisen die Flanke dem beim
//        Befehl angegebenen Operanden zu
//      - es koennen keine DB's definiert werden (DB100 wird vom Besy angelegt)
//      - es gibt keine lokalen Variablen in Bausteinen
//      - PLC-Alarme werden von der Oberflaeche nur angezeigt solange die
//        entspr. Bits anstehen. Quittierung muss PLC verwalten!!
//      - Vorsicht bei 'Veroderung' von 'Und'-Bloecken: VKE stimmt sporad. nicht
//        (entweder Klammern oder mit Hilfsmerker arbeiten)
//      - Vorsicht bei Zaehlern mit schnellen Signalen (siehe autom. RS)!
//**************************************************************************
FUNCTION FC 27: VOID
NAME: TUERAUTOMATIK Langer+Laumann
BEGIN
//   Tuerautomatik Langer+Laumann     A 8.0 (M 212.0)Freigabe Türmotorschütze
//                                   E 8.0 Türautomatik Sicherheitstürschaltleiste nicht ausgelöst
//                                   E 8.6 Überwachung Motorschütz
//                                   E 8.1 Von Türsteuergerät Tür offen
//                                   E 8.7 Fehler Türautomatik Langer+Laumann
//                                   A 6.4 Tür auf
//                                   A 6.5 Tür zu
//  X6:5     E 6.2  E 8.0       // Türautomatik Sicherheitstürschaltleiste nicht ausgelöst
//  X6:6     E 6.3  E 8.1       // Von Türsteuergerät Tür offen
//  X6:7     E 6.4  E 8.7       // Störung Türsteuergerät Langer+Laumann
//  X6:11    E 6.5  ?????       // Schutzschalter Türsteuergerät
//  X5:7     E 7.4  E 8.6       // Türautomatik Motorschützüberwachung
//
//  X6:10    A 6.3  A 6.5      // LEISTUNG TÜR ZU
//  X6:9     A 3.5  A 6.4      // LEISTUNG TÜR AUF
//  X421:11  A 9.7  A 8.0      // Türmotorschütze ein
// DNC Betrieb:
// DB1.DBX 1382.0  Maschinentüre auf
// DB1.DBX 1382.1  Maschinentüre zu
// DB1.DBX 1382.2  Maschinentüre Stop
NETWORK
TITLE = SPS/OB-->TÜRAUTOMATIK AKTIV
U   DB10.DBX 41.4;      // USER_DATA_HEX[1] Türautomatik Langer+Laumann angewählt
=   DB1.DBX 1418.0;     // SPS/RM-->TÜRAUTOMATIK AKTIV
NETWORK
TITLE = M0,M1 AUSLÖSUNG
O   DB20.DBX 192.0; // M0 DYNAMISCH
=   DB20.DBX 256.0; // M0 RÜCKMELDEN
U   DB20.DBX 192.1; // M1 DYNAMISCH
U   DB20.DBX 324.5; // BEDINGTER HALT AKTIV
=   DB20.DBX 256.1; // M1 RÜCKMELDEN
U   DB20.DBX 192.1; // M1 DYNAMISCH
U   DB20.DBX 324.5; // BEDINGTER HALT AKTIV
O   DB20.DBX 192.0; // M0 DYNAMISCH
=   M 34.1;         // HM M0/M1/ DYNAMISCH
NETWORK
TITLE =Flanke M2/M30
// 1.07: im Stangenbetrieb verriegelt
U(    ;
O     DB20.DBX 192.2;     // M02 dyn.
O     DB20.DBX 195.6;     // M30 dyn.
)     ;
U(    ;
ON    M 5.3;              // H4 M99
O     M 155.7;            // 1x aktiv
)     ;
=     M 173.7;            // Fl. M2/M30
NETWORK 7
TITLE = M2, M30 RÜCKMELDEN
O   DB20.DBX 348.1; // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT
O   DB20.DBX 348.2; // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT UND GESTARTET
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUSGELÖST
=   DB20.DBX 259.6; // M30 RÜCKMELDEN
=   DB20.DBX 256.2; // M2 RÜCKMELDEN
NETWORK
TITLE = ALARM 6103 Türautomatikschütz nicht abgefallen
UN  E 8.0           // Türautomatik Sicherheitstürschaltleiste nicht ausgelöst
UN  E 8.6           // Türautomatik Motorschützüberwachung
L   S5T#300MS       //
SE  T 8
U   T 8
S   DB2.DBX 12.7;   // 6103 Türautomatikschütz nicht abgefallen
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 12.7;   // 6103 Türautomatikschütz nicht abgefallen
NETWORK  2
TITLE = FM ENDSCH. TÜR OFFEN
U   E 8.1;          // Von Türsteuergerät Tür offen
FP  M 144.3;        // FM ENDSCH. TÜR OFFEN
NETWORK
TITLE = FM Türautomatik Sicherheitstürschaltleiste ausgelöst
U   E 8.0           // Türautomatik Sicherheitstürschaltleiste nicht ausgelöst
FP  M 143.1;        // FM Türautomatik Sicherheitstürschaltleiste ausgelöst
NETWORK
TITLE = FM Störung Türsteuergerät Langer+Laumann
U   E 8.7           // Störung Türsteuergerät Langer+Laumann
FP  M 144.1;        // FM Störung Türsteuergerät Langer+Laumann
NETWORK
TITLE = Türmotorschütze einschalten
UN  DB2.DBX 0.0;    // 6000  NOT-AUS
=   A 8.0           // Türmotorschütze ein
NETWORK
TITLE = ACHSEN IN BEWEGUNG
L   DB1.DBB 1358;   // ACHSEN IN BEWEGUNG
L   B#16#0;         // LADE KONSTANTE HEX 0
<>I;                // VERGLEICH AUF UNGLEICH
=   M 131.7;        // ACHSEN IN BEWEGUNG
NETWORK
TITLE = ISTDREHZAHL KLEINER 20U/MIN
O   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
O   DB1.DBX 1356.3; // HAUPTANTRIEB STILLSTAND
L   S5T#100MS;      // 25X0,1S
SE  T 73
U   T 73
=   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
NETWORK
TITLE = FREIGABE ZUM TÜRENTRIEGELN
U(
ON  DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
ON  DB20.DBX 324.0; // PROGRAMM LÄUFT: RÜCKSETZEN MIT PROGRAMM ENDE BZW. RESET.
O   DB20.DBX 324.1; // STOP-ZUSTAND
O   DB20.DBX 325.1; // STOP-ZUSTAND DURCH SINGLE
)
U(
ON  DB20.DBX 326.2; // BETRIEBSART MDI
ON  DB20.DBX 324.0; // PROGRAMM LÄUFT: RÜCKSETZEN MIT PROGRAMM ENDE BZW. RESET.
O   DB20.DBX 324.1; // STOP-ZUSTAND
O   DB20.DBX 325.1; // STOP-ZUSTAND DURCH SINGLE
)
UN  M 131.7;        // ACHSEN IN BEWEGUNG
UN  A 0.3           // Hauptantrieb Reglerfreigabe
U   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
=   M 55.7;         // FREIGABE ZUM TÜRENTRIEGELN
NETWORK  3
TITLE = FREIGABE TÜR ZU/AUF
UN  DB2.DBX 0.0;    // 6000  NOT-AUS
U   M 55.7;         // FREIGABE ZUM TÜRENTRIEGELN
U   E 8.0           // Türautomatik Sicherheitstürschaltleiste nicht ausgelöst
=   M 143.0;        // FREIGABE TÜR ZU/AUF
NETWORK  6
TITLE = HM TIPPBETRIEB TÜR ZU bei AUX OFF
;       U   DB1.DBX 1374.0; // TASTE TÜR ZU  (MSTT)
;       UN  DB1.DBX 1374.1; // TASTE TÜR AUF (MSTT)
U   M 201.5;        // ext. Cycle-Start Taste (Türautomatik=Taste Tuer zu)
UN  M 136.0;        // AUX-ON
U   M 143.0;        // FREIGABE TÜR ZU/AUF
=   M 143.7;        // HM TIPPBETRIEB TÜR ZU
R   M 143.6;        // HM TIPPBETRIEB TÜR AUF
R   M 143.4;        // HM TÜR AUF STAT.
NETWORK  7
TITLE = HM TIPPBETRIEB TÜR AUF bei AUX OFF
;       U   DB1.DBX 1374.1; // TASTE TÜR AUF (MSTT)
;       UN  DB1.DBX 1374.0; // TASTE TÜR ZU  (MSTT)
U   E 2.4;          // Taste Tuer oeffnen
UN  M 136.0;        // AUX-ON
U   M 143.0;        // FREIGABE TÜR ZU/AUF
=   M 143.6;        // HM TIPPBETRIEB TÜR AUF
R   M 143.7;        // HM TIPPBETRIEB TÜR ZU
R   M 143.5;        // HM TÜR ZU STAT.
NETWORK  8
TITLE = HM TÜR ZU STAT.
U   E 9.4;          // Robotik Tuer zu
UN  E 9.5;          // Robotik Tuer auf
FP  M 170.4;        // ROBOTIK/FM TÜR ZU
;       U   DB1.DBX 1374.0; // TASTE TÜR ZU  (MSTT)
;       UN  DB1.DBX 1374.1; // TASTE TÜR AUF (MSTT)
U   M 201.5;        // ext. Cycle-Start Taste (Türautomatik=Taste Tuer zu)
U   M 136.0;        // AUX-ON
U   M 143.0;        // FREIGABE TÜR ZU/AUF
O(  ;
O   DB1.DBX 1382.1; // DNC/TÜR ZU
O   M 170.4;        // ROBOTIK/FM TÜR ZU
O   M 149.2;        // M 146.2 // Anforderung Tür zu von Dauerlauf
U   M 143.0;        // FREIGABE TÜR ZU/AUF
)   ;
O(
U   M 12.0          // Tür zu und Verriegelt
U   M 143.0;        // FREIGABE TÜR ZU/AUF
U   M 212.5         // A 6.5 LEISTUNG TÜR ZU
UN  M 143.4;        // HM TÜR AUF STAT.
UN  M 143.6;        // HM TIPPBETRIEB TÜR AUF
)
O(
O   DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
O   M 201.5;        // ext. Cycle-Start Taste (Türautomatik=Taste Tuer zu)
U   E 6.2;           // Tuer zu, Antriebe ein
)
S   M 143.5;        // HM TÜR ZU STAT.
R   M 143.4;        // HM TÜR AUF STAT.
R   M 143.6;        // HM TIPPBETRIEB TÜR AUF
NETWORK  9
TITLE = HM TÜR AUF STAT. M0/M1/M2/M30 DYNAMISCH
O   DB20.DBX 348.1; // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT (M2,M30)
O   M 34.1;         // HM M0/M1 DYNAMISCH
O   M 173.7;        // Fl. M2/M30
S   M 144.0;        // HM TÜR AUF STAT. (M0/M1/M2/M30)
NETWORK 10
TITLE = HM TÜR AUF STAT.
U   E 9.5;          // Robotik Tuer auf
UN  E 9.4;          // Robotik Tuer zu
FP  M 170.3;        // ROBOTIK/FM TÜR AUF
;       U   DB1.DBX 1374.1; // TASTE TÜR AUF (MSTT)
;       UN  DB1.DBX 1374.0; // TASTE TÜR ZU  (MSTT)
U   E 2.4;          // Taste Tuer oeffnen
U   M 136.0;        // AUX-ON
U   M 143.0;        // FREIGABE TÜR ZU/AUF
O(  ;
O   DB1.DBX 1382.0; // DNC/TÜR AUF
O   M 170.3;        // ROBOTIK/FM TÜR AUF
U   M 143.0;        // FREIGABE TÜR ZU/AUF
UN  E 8.1;          // Von Türsteuergerät Tür offen
)   ;
O(   ;
U   M 143.0;        // FREIGABE TÜR ZU/AUF
U   M 144.0;        // HM TÜR AUF STAT. (M0/M1/M2/M30)
)
O(   ;
U   DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
U   E 8.1;          // Von Türsteuergerät Tür offen
)
S   M 143.4;        // HM TÜR AUF STAT.
R   M 212.5         // A 6.5 LEISTUNG TÜR ZU
R   M 143.5;        // HM TÜR ZU STAT.
R   M 143.7;        // HM TIPPBETRIEB TÜR ZU
R   M 144.0;        // HM TÜR AUF STAT. (M0/M1/M2/M30)
NETWORK 12
TITLE = ABBRUCH TÜRBEWEGUNG
O   DB2.DBX 0.0;    // 6000  NOT-AUS
O   DB1.DBX 1440.0  // RESET AUSGELÖST
O   DB1.DBX 1370.3; // TASTE RESET
O   DB1.DBX 1382.2; // DNC/TÜR STOP
O   M 143.1;        // FM Türautomatik Sicherheitstürschaltleiste ausgelöst
ON  E 8.0           // Türautomatik Sicherheitstürschaltleiste nicht ausgelöst
O   M 144.1;        // FM Störung Türsteuergerät Langer+Laumann
U   M 81.3;         // M 91.3 // TÜRBEWEGUNG AKTIV  AFG/EFG
R   M 143.5;        // HM TÜR ZU STAT.
R   M 143.4;        // HM TÜR AUF STAT.
R   M 144.0;        // HM TÜR AUF STAT. (M0/M1/M2/M30)
NETWORK 14
TITLE = LEISTUNG TÜR ZU
O   M 143.7;        // HM TIPPBETRIEB TÜR ZU
O   M 143.5;        // HM TÜR ZU STAT.
=   M 212.5;        // A 6.5 LEISTUNG TÜR ZU
FP  M 144.6;        // FM TÜR ZU
U   M 144.6;        // FM TÜR ZU
S   M 144.7;        // HM TÜR ZU
NETWORK 15
TITLE = LEISTUNG TÜR AUF
O   M 143.4;        // HM TÜR AUF STAT.
O   M 143.6;        // HM TIPPBETRIEB TÜR AUF
=   M 212.4;        // A 6.4 LEISTUNG TÜR AUF
NETWORK 16
TITLE = TÜR OFFEN
U   M 212.4         // A 6.4 LEISTUNG TÜR AUF
U   E 8.1;          // Von Türsteuergerät Tür offen
=   M 143.2;        // TÜR OFFEN
NETWORK
TITLE = HM TÜR ZU LÖSCHEN
U   M 144.7;        // HM TÜR ZU
U   M 212.5         // A 6.5 LEISTUNG TÜR ZU
U   E 6.2;          // Tuer zu, Antriebe ein
R   M 144.7;        // HM TÜR ZU
NETWORK 17
TITLE = TÜRBEWEGUNG AKTIV  AFG/EFG
U   M 144.7;        // HM TÜR ZU
U   M 212.5         // A 6.5 LEISTUNG TÜR ZU
UN  E 6.2;          // Tuer zu, Antriebe ein
O   ;
U   M 212.4          //A 6.4 LEISTUNG TÜR AUF
UN  E 8.1;          // Von Türsteuergerät Tür offen
=   M 81.3;         // M 91.3 // TÜRBEWEGUNG AKTIV  AFG/EFG
;  NETWORK 18
;  TITLE = 6064  TÜR NICHT BEREIT
;
;8(((((       U   M 81.3;         // M 91.3 // TÜRBEWEGUNG AKTIV  AFG/EFG
;       L   S5T#10S;        //
;       SE  T ;           // STARTEN T 37 ALS EINSCHALTVERZ.
;       U   T ;           // TÜR AUF/ZEITÜBERWACHUNG
;       S   DB2.DBX 8.0;    // 6064  TÜR NICHT BEREIT
;       R   A 6.5 ;         // LEISTUNG TÜR ZU
;       R   A 6.4 ;         // LEISTUNG TÜR AUF
;       O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
;       O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
;       R   DB2.DBX 8.0;    // 6064  TÜR NICHT BEREIT
NETWORK 19
TITLE = SPS/OB-->TÜR OFFEN
U   M 143.2;        // TÜR OFFEN
=   DB1.DBX 1390.0; // SPS/RM-->TÜR OFFEN
NETWORK 20
TITLE = TÜR/FC DNC-QUITTUNG
U   DB1.DBX 1366.0; // DNC-BETRIEB AKTIV
U(  ;
U   DB1.DBX 1382.1; // DNC/TÜR ZU
UN  M 143.5;        // HM TÜR ZU STAT.
O   ;
U   DB1.DBX 1382.0; // DNC/TÜR AUF
UN  M 143.4;        // HM TÜR AUF STAT.
O   ;
U   DB1.DBX 1382.2; // DNC/TÜR STOP
U(  ;
O   M 143.7;        // HM TIPPBETRIEB TÜR ZU
O   M 143.6;        // HM TIPPBETRIEB TÜR AUF
O   M 143.5;        // HM TÜR ZU STAT.
O   M 143.4;        // HM TÜR AUF STAT.
)   ;
)   ;
S   DB1.DBX 1398.1; // TÜR/FC DNC-QUITTUNG
NETWORK 21
TITLE = Automatischer Cycle-Start nach Tür zu
U   M 212.5         // A 6.5 LEISTUNG TÜR ZU
U   E 6.2;          // Tuer zu, Antriebe ein
FP  M 149.0;        // M 146.0 // FM NC-Startanforderung nach Tür zu
U   M 149.0;        // M 146.0 // FM NC-Startanforderung nach Tür zu
U   DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
L   S5T#1S;         // 1S
SV  T 9             // IMPULS (VERLÄNGERT)
U   T 9             // IMPULS (VERLÄNGERT)
=   M 148.5;        // NC-Start Tuer zu
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
O   DB1.DBX 1440.0  // RESET AUSGELÖST
ON  DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
R   T 9
NETWORK 22
TITLE = Automatisches Tür schliessen im Dauerlauf
U   M 300.7
U   E 8.1;          // Von Türsteuergerät Tür offen
U   M 212.4         // A 6.4 LEISTUNG TÜR AUF
FP  M 149.1;        // M 146.1 // FM Anforderung Tür zu (Dauerlauf)
U   M 149.1;        // M 146.1 // FM Anforderung Tür zu (Dauerlauf)
U   M 300.7
U   DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
L   S5T#1S;         // 1S
SV  T 12            // IMPULS (VERLÄNGERT)
U   T 12            // IMPULS (VERLÄNGERT)
=   M 149.2;        // M 146.2 // Anforderung Tür zu von Dauerlauf
NETWORK
TITLE = Zeitverzögerung Tuer zu
U   E 6.2;              // Tuer zu, Antriebe ein
L   S5T#600MS;
SE  T 64;
END_FUNCTION
ORGANIZATION_BLOCK OB 1
TITLE=ZYK_OB
NAME: ZYK_OB
BEGIN
NETWORK
TITLE = DNC Ref.-Anforderung
U     DB1.DBX 1366.0;     // DNC aktiv
U     DB20.DBX 1.1;       // Ref.-Anforderung
UN    M 193.0             // DNC Ref.-Anforderung
SPBN   M1001;
R     DB20.DBX 1.1;       // Ref.-Anforderung
S     M 193.0             // DNC Ref.-Anforderung
M1001:  NOP 0;
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUSGELÖST
R   M 193.0             // DNC Ref.-Anforderung
NETWORK
TITLE = Abbruch bei nicht getestete Oberflaechen
O     DB1.DBX 1402.0;     // Steuerungskennung SINUMERIK 810/820
O     DB1.DBX 1402.1;     // Fanuc 0TB
O     DB1.DBX 1402.2;     // Emcotronic TM02
O     DB1.DBX 1403.0;     // Fanuc 21TB
=     M 114.0;            // 810T / 0TB / 21TB
NETWORK
TITLE = E-Zuweisung AC95/ACC
// V1.00: Netzwerk neu
// V1.03: Sauter EK600 eingebunden
U     DB10.DBX59.0;       // USER_DATA_HEX[19]: ACC
SPB   EACC;
// Zuweisungen AC95
L     EB 0;
T     MB 200;
L     EB 1;
T     MB 201;
L     EB 2;
T     MB 202;
SPA   EACE;
EACC: NOP   0;
// Zuweisungen ACC
L     EB 16;
T     MB 200;
;      U     E 2.7;
;      =     M 200.3;            // Bero Spindelbremse
;      U     E 2.5;              // Fussschalter Pinole vor
;      =     M 200.5;
;      U     E 2.6;              // Fussschalter Pinole zurueck
;      =     M 200.6;
L     EB 3;
T     MB 201;
// Initialisierung Interpolationsmodul
ON    A 1.6;
O     A 1.6;
=     A 1.6;              // Umschaltung IDP
U     DB1.DBX 1370.0;     // 1. PLC-Durchlauf
=     A 1.7;              // Reset IDP
UC    FC 11;
U     E 6.5;              // Duplo Al.-Bit 0
=     M 248.5;
U     E 6.6;              // Duplo Al.-Bit 1
=     M 248.6;
U     E 6.7;              // Duplo Al.-Bit 2
=     M 248.7;
UN    DB10.DBX 52.0;      // USER_DATA_HEX[12]: Sauter EK600
SPB   EACE;
UN    E 6.5;              // BB EK600
=     M 248.5;
=     M 248.6;
=     M 248.7;
EACE: NOP   0;
NETWORK
TITLE = Zuweisungen fuer PC-Steuerung
U     DB1.DBX 1370.0;     // 1. PLC-Durchlauf
=     M 110.2;            // Grst.
UN    M 110.2;
SPB   M021;
// remanente Bereiche aus SE/Daten laden
L     DB15.DBW 20;        // Maschinenkonfig. in SE-Daten
T     MW 5;               // akt. Maschinenkonfiguration
L     DB15.DBW 22;        // SPM-Zustand
T     DB100.DBW 167;
M021: NOP   0;
// Achskommandos von externen Quellen ruecksetzen (ausser INC!)
U     M 110.1;            // "1"
UN    DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
R     DB20.DBX 1.1;       // REF-Kommando
R     DB20.DBX 1.2;       // JOG-Kommando
// Zuweisung der Tasten auf Merkerschnittstelle
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M022;
U     DB20.DBX 826.0;     // SKIP
=     M 100.7;
U     DB20.DBX 826.1;     // DRY
=     M 100.6;
U     DB20.DBX 826.2;     // C-
=     M 100.4;
U     DB20.DBX 826.3;     // X+
=     M 100.3;
U     DB20.DBX 826.4;     //
=     M 100.2;
U     DB20.DBX 826.5;     //
=     M 100.5;
U     DB20.DBX 826.6;     //
=     M 101.5;
U     DB20.DBX 826.7;     //
=     M 102.5;
U     DB20.DBX 827.0;     //
=     M 105.6;
U     DB20.DBX 827.1;     //
=     M 105.5;
U     DB20.DBX 827.2;     //
=     M 105.4;
U     DB20.DBX 827.3;     // 1x
=     M 101.7;
U     DB20.DBX 827.4;     // OPT
=     M 101.6;
U     DB20.DBX 827.5;     // Z-
=     M 101.4;
U     DB20.DBX 827.6;     // Eilgang
=     M 101.3;
U     DB20.DBX 827.7;     // Z+
=     M 101.2;
U     DB20.DBX 828.0;     //
=     M 101.1;
U     DB20.DBX 828.1;     //
=     M 101.0;
U     DB20.DBX 828.2;     //
=     M 105.3;
U     DB20.DBX 828.3;     //
=     M 105.2;
U     DB20.DBX 828.4;     //
=     M 105.1;
U     DB20.DBX 828.5;     //
=     M 105.0;
O     DB20.DBX 828.6;     // Reset-Taste MSTT
O     DB1.DBX 1440.0;     // NC Reset Kommando
=     M 102.7;
U     DB20.DBX 828.7;     // SBL
=     M 102.6;
U     DB20.DBX 829.0;     //
=     M 102.4;
U     DB20.DBX 829.1;     // X-
=     M 102.3;
U     DB20.DBX 829.2;     // C+
=     M 102.2;
U     DB20.DBX 829.6;     //
=     M 106.6;
O     DB20.DBX 829.7;     //
O     E 2.5;              // Fussschalter Pinole vor
=     M 106.5;
O     DB20.DBX 830.0;     //
O     E 2.6;              // Fussschalter Pinole zurueck
=     M 106.4;
U     DB20.DBX 830.1;     //
=     M 102.1;
U     DB20.DBX 830.2;     // PRT
=     M 102.0;
U     DB20.DBX 830.3;     //
=     M 104.5;
U     DB20.DBX 830.4;     // REF
=     M 104.3;
U     DB20.DBX 830.5;     //
=     M 104.0;
U     DB20.DBX 830.6;     // %-
=     M 106.7;
U     DB20.DBX 830.7;     // 100%
=     M 106.3;
U     DB20.DBX 831.0;     // %+
=     M 107.5;
U     DB20.DBX 831.1;     // KM
=     M 106.2;
U     DB20.DBX 831.2;     // SPF
=     M 106.1;
U     DB20.DBX 831.3;     //
=     M 106.0;
U     DB20.DBX 831.4;     // NC-Stop
=     M 104.7;
U     DB20.DBX 831.5;     // NC-Start
=     M 104.6;
U     DB20.DBX 831.6;     // VS-Halt
=     M 104.4;
U     DB20.DBX 832.0;     // VS-Start
=     M 104.2;
U     DB20.DBX 832.1;     // HA-Stop
=     M 104.1;
U     DB20.DBX 832.3;     // HA Start
=     M 107.7;
U     DB20.DBX 832.4;     // AUX OFF
=     M 107.6;
U     DB20.DBX 832.6;     // AUX-ON
=     M 107.4;
M022: NOP   0;
NETWORK
TITLE =MSTT auf defekte Tasten pruefen
O(    ;
L     DB20.DBW 826;
L     0;
<>I   ;
)     ;
O(    ;
L     DB20.DBW 828;
L     0;
<>I   ;
)     ;
O(    ;
L     DB20.DBW 830;
L     0;
<>I   ;
)     ;
O     DB20.DBX 832.0;
O     DB20.DBX 832.1;
O     DB20.DBX 832.2;
O     DB20.DBX 832.3;
O     DB20.DBX 832.4;
O     DB20.DBX 832.5;
U     DB20.DBX 832.6;     // AUX-On Taste
UN    M 136.0;            // AUX-ON
=     DB2.DBX 2.0;        // FM6100 Tastatur defekt
NETWORK
TITLE =H-Aenderung um einen PLC-Zyklus verlaengern
O     DB20.DBX 182.1;     // H-Aenderung Spindel 0
O     M 111.1;            // HM H-Aend. verz.
=     M 111.0;            // H-Aenderung verl.
U     DB20.DBX 182.1;     // H-Aenderung Spindel 0
=     M 111.1;            // HM H-Aend. verz.
NETWORK
TITLE =Progr. laeuft wie Siemens
U     DB20.DBX 324.0;     // Progr. laeuft K0
UN    DB20.DBX 324.1;     // Progr. stop K0
UN    DB20.DBX 325.1;     // Stop durch Single K0
UN    M 111.5;            // M00/M01 aktiv
=     M 111.7;            // Programm laeuft
=     DB20.DBX 851.5;     // LED NC-Start Taste
NETWORK
TITLE =Progr. unterbrochen wie Siemens
O     DB20.DBX 324.1;     // Progr. stop K0
O     DB20.DBX 325.1;     // Stop durch Single K0
O     M 111.5;            // M00/M01 aktiv
U     DB20.DBX 324.0;     // Progr. laeuft K0
=     M 111.6;            // Programm unterbrochen
=     DB20.DBX 851.4;     // LED NC-Stop Taste
NETWORK
TITLE =M00/M01 aktiv nachbilden
U     DB20.DBX 192.1;     // M01 dyn.
U     DB20.DBX 324.5;     // Opt. Stop aktiv
O     DB20.DBX 192.0;     // M00 dyn.
S     M 111.5;            // M00/M01 aktiv
UN    DB20.DBX 324.1;     // Progr. stop K0
UN    DB20.DBX 324.0;     // Progr. laeuft K0
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
O     DB20.DBX 0.2;       // NC-Start Kommando
R     M 111.5;            // M00/M01 aktiv
NETWORK
TITLE =autom. Offsetabgleich in allen Achsen
U     M 155.2;            // BA REF
U     M 137.7;            // Antriebe ein
U     M 201.6;            // SS Hand/Auto
U     M 201.4;            // SS Dateneingabe
U     M 104.6;            // Taste NC-Start
U     M 104.2;            // Taste VS-Start
U     DB3.DBX 1.1;        // BM7209 Referenzpunkt nicht erreicht
UN    M 136.4;            // Summe Fahrbefehl
S     M 70.0;             // autom. Abgleich aktiv
U     DB1.DBX 1468.0;     // Abgleich beendet, Daten gesichert
S     M 70.7;             // autom. Abgleich beendet
O     M 102.7;            // Taste Reset
ON    M 136.0;            // AUX-ON
R     M 70.0;             // autom. Abgleich aktiv
R     M 70.7;             // autom. Abgleich beendet
U     M 70.0;             // autom. Abgleich aktiv
FP    M 70.1;             // Fl. Rundachse ein Offsetabgleich
U     M 70.0;             // autom. Abgleich aktiv
U     M 110.4;            // Spindel im Rundachsbetrieb
L     S5T#10S;            // Zeitwert ev. in MSD ablegen ?
SI    T 0;                // Dauer autom. Abgleich
U     T 0;
=     DB1.DBX 1466.0;     // Offsetabgleich durchfuehren
=     M 70.6;             // Abgleich durchfuehren AC
U     M 70.0;             // autom. Abgleich aktiv
UN    M 70.7;             // autom. Abgleich beendet
=     DB3.DBX 8.6;        // BM7270 Offsetabgleich aktiv
U     M 70.7;             // autom. Abgleich beendet
=     DB3.DBX 8.7;        // BM7271 Offsetabgleich beendet, Daten gesichert
NETWORK
TITLE =Spindelleistungen an die Oberflaeche
L     DB1.DBW 1472;       // ACIF Analogkanal 1
T     DB1.DBW 1508;       // Leistung abs. Achse 3
L     DB1.DBW 1474;       // ACIF Analogkanal 2
T     DB1.DBW 1506;       // Leistung abs. Achse 2
NETWORK
TITLE =Eingangsbehandlung fuer Laborsimulator
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
CC    FC 10;
NETWORK
TITLE =Zyklisches Anwenderprogramm
UC    FC 101;
NETWORK
TITLE =Zeitgesteuertes Anwenderprogramm
UN    DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
CC    FC 100;
NETWORK
TITLE =eingeschwenktes WZ an Anzeige melden
L     DB100.DBW 31;       // WZW Istpos. aus SW
UN    DB100.DBX 37.0;     // HM WZW referenzieren
SPB   M051;
L     0;
M051: T     DB20.DBW 356;       // akt. WZ fuer Anzeige
NETWORK
TITLE = Kommandoanforderung REF generieren
L     DB20.DBW 8;         // REF-Anforderung fuer alle Achsen
L     0;
<>I   ;
FP    DB20.DBX 1.1;       // Anforderung REF
UN    M 137.7;            // Antriebe ein
R     DB20.DBX 1.1;       // Anforderung REF
NETWORK
TITLE = Kommandoanforderung INC generieren
// INC-Kommandos werden gelöscht wenn keine JOG-Taste gesetzt ist
// und nicht kpl. neu generiert. Das deshalb, weil HH355 keine
// Betriebsarten hat bzw. bei INC-Var die Schrittweite nur von
// der Oberfläche kommen kann. Alle anderen Quellen als die JOG-Tasten
// auf der MSTT werden somit auch blockiert (z.B. Ziffernblock)
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   NINC;
L     DB20.DBD16;         // INC Schrittweite
L     +0.0E+0;
>R    ;
=     DB100.DBX 8.7;      // INC positiv
L     DB20.DBW 14;        // INC Achsnummer
L     0;                  // Achsnr. X
==I   ;
=     DB100.DBX 8.0;      // INC X
L     DB20.DBW 14;        // INC Achsnummer
L     2;                  // Achsnr. Z
==I   ;
=     DB100.DBX 8.2;      // INC Z
O(    ;
U     DB100.DBX 8.0;      // INC X
U     DB100.DBX 8.7;      // INC positiv
U     DB20.DBX 10.0;      // X+ konv
)     ;
O(    ;
U     DB100.DBX 8.0;      // INC X
UN    DB100.DBX 8.7;      // INC positiv
U     DB20.DBX 12.0;      // X- konv
)     ;
O(    ;
U     DB100.DBX 8.2;      // INC Z
U     DB100.DBX 8.7;      // INC positiv
U     DB20.DBX 10.2;      // Z+ konv
)     ;
O(    ;
U     DB100.DBX 8.2;      // INC Z
UN    DB100.DBX 8.7;      // INC positiv
U     DB20.DBX 12.2;      // Z- konv
)     ;
NOT   ;
L S5T#100MS;
SE    T 6;                 // Verzoegerung JOG
U     T 6;
U     DB20.DBX 1.3;       // INC-Kommando löschen
R     DB20.DBX 1.3;       // INC-Kommando löschen
NINC: NOP   0;
NETWORK
TITLE = Funktionen fuer Handrad
// V0.18: Netzwerk neu
O     DB20.DBX 326.5;     // BA INC1 von Kern
O     DB20.DBX 326.6;     // BA INC10 von Kern
O     DB20.DBX 326.7;     // BA INC100 von Kern
O     DB20.DBX 327.0;     // BA INC1000 von Kern
=     M 97.6;             // Handrad-BA
U(    ;
O(    ;
U     M 97.6;             // Handrad-BA
UN    M 97.7;             // HM Handrad aktiv
)     ;
O     M 97.0;             // Fl. Anwahl INC1
O     M 97.1;             // Fl. Anwahl INC10
O     M 97.2;             // Fl. Anwahl INC100
O     M 97.3;             // Fl. Anwahl INC1000
)     ;
U     DB10.DBX 50.1;      // USER_DATA_HEX[10]: Handrad vorhanden
=     DB1.DBX 1440.2;     // Handrad Anwahl
O     M 138.0;            // Fl. Reset
ON    M 97.6;             // Handrad-BA
U     M 97.7;             // HM Handrad aktiv
=     DB1.DBX 1440.3;     // Handrad Abwahl
U     DB1.DBX 1440.2;     // Handrad Anwahl
S     M 97.7;             // HM Handrad aktiv
U     DB1.DBX 1440.3;     // Handrad Abwahl
R     M 97.7;             // HM Handrad aktiv
// INC-Schrittweite bei BA-Wechsel mitführen
U     DB20.DBX 326.5;     // BA INC1 von Kern
FP    M 97.0;             // Fl. Anwahl INC1
UN    M 97.0;
SPB   INC1;
L     +1.0E-06;
T     DB20.DBD 16;        // INC Schrittweite
INC1: NOP   0;
U     DB20.DBX 326.6;     // BA INC10 von Kern
FP    M 97.1;             // Fl. Anwahl INC10
UN    M 97.1;
SPB   INC2;
L     +1.0E-05;
T     DB20.DBD 16;        // INC Schrittweite
INC2: NOP   0;
U     DB20.DBX 326.7;     // BA INC100 von Kern
FP    M 97.2;             // Fl. Anwahl INC100
UN    M 97.2;
SPB   INC3;
L     +1.0E-04;
T     DB20.DBD 16;        // INC Schrittweite
INC3: NOP   0;
U     DB20.DBX 327.0;     // BA INC1000 von Kern
FP    M 97.3;             // Fl. Anwahl INC1000
UN    M 97.3;
SPB   INC4;
L     +1.0E-03;
T     DB20.DBD 16;        // INC Schrittweite
INC4: NOP   0;
NETWORK
TITLE = Kommandoanforderung JOG generieren
// das JOG-Kommando wird verzoegert, weil die Tasten und das von
// der Oberflaeche generierte INC-Kommando nicht immer im selben
// PLC Durchlauf kommen
L     DB20.DBD 10;        // JOG+ und JOG- fuer alle Achsen
L     DB100.DBD 810;      // Schattenspeicher JOG-Kommandos
<>D   ;                   // bei jeder Aenderung...
S     DB100.DBX 9.0;      // HM JOG aktiv
U     DB100.DBX 9.0;      // HM JOG aktiv
L     S5T#100MS;
SE    T 1;                // Verzoegerung JOG
U     T 1;
=     DB20.DBX 1.2;       // JOG-Kommando
O     DB20.DBX 1.2;       // JOG-Kommando
O     DB20.DBX 1.3;       // INC-Kommando
O     DB100.DBX 9.1;      // INC JOG Verriegelung
R     DB100.DBX 9.0;      // HM JOG aktiv
// Schattenspeicher fuer Flankenerkennung
L     DB20.DBD 10;        // JOG+ und JOG- fuer alle Achsen
T     DB100.DBD 810;      // Schattenspeicher JOG-Kommandos
// Verriegelung INC gegen JOG: weil sonst bei Betaetigung
// von zwei Tasten und loslassen einer Taste ein Jog-Kommando
// generiert wuerde.
U     DB20.DBX 1.3;       // INC-Kommando
S     DB100.DBX 9.1;      // INC JOG Verriegelung
L     DB20.DBD 10;        // JOG+ und JOG- fuer alle Achsen
L     0;                  // keine Taste mehr betaetigt?
==D   ;
L     S5T#150MS;
SE    T 7;                 // Verzoegerung JOG
U     T 7;
U     DB100.DBX 9.1;      // INC JOG Verriegelung
R     DB100.DBX 9.1;      // INC JOG Verriegelung
NETWORK
;      L     DB20.DBD 192;
;      T     DB20.DBD 256;
;      L     DB20.DBD 196;
;      T     DB20.DBD 260;
;      L     DB20.DBD 200;
;      T     DB20.DBD 264;
;      L     DB20.DBD 204;
;      T     DB20.DBD 268;
;      L     DB20.DBD 208;
;      T     DB20.DBD 272;
;      L     DB20.DBD 212;
;      T     DB20.DBD 276;
;      L     DB20.DBD 216;
;      T     DB20.DBD 280;
;      L     DB20.DBD 220;
;      T     DB20.DBD 284;
NETWORK
TITLE =LED's an Tastatur bei Shutdown loeschen
U     DB1.DBX 1440.1;     // Shutdown angefordert
S     M 0.3;              // Shutdown aktiv
UN    M 0.3;              // Shutdown aktiv
SPB   _NSD;
L     0;
T     DB20.DBD 846;
T     DB20.DBD 850;
T     DB20.DBD 854;
T     DB20.DBD 858;
_NSD: NOP   0;
NETWORK
TITLE = A-Zuweisung AC95/ACC
// V1.00: Netzwerk neu
// V1.03: Sauter EK600 eingebunden
U     DB10.DBX59.0;       // USER_DATA_HEX[19]: ACC
SPB   AACC;
// Zuweisungen AC95
L     MB 209;
T     AB 0;
U     M 208.0;
=     A 0.0;
U     M 208.1;
=     A 0.1;
U     M 208.2;
=     A 0.2;
U     M 208.3;
=     A 0.3;
L     MD 210;
T     AD 6;
L     MB 216;
T     AB 5;
SPA   AACE;
AACC: NOP   0;
// Zuweisungen ACC
L     MB 208;
T     AB 0;
U     M 209.5;
=     A 3.5;
U     M 209.6;
=     A 3.4;
U     M 209.7;
=     A 3.3;
U     M 210.0;
=     A 6.0;
U     M 210.1;
=     A 6.1;
U     M 210.2;
=     A 6.2;
U     M 210.3;
=     A 6.3;
U     M 210.4;
=     A 9.0;
U     M 210.5;
=     A 9.1;
U     M 210.6;
=     A 9.2;
U     M 210.7;
=     A 9.3;
U     M 211.0;
=     A 9.4;
U     M 211.1;
=     A 9.5;
U     M 211.2;
=     A 9.6;
U     M 211.3;
=     A 9.7;
U     M 211.4;
=     A 5.0;
U     M 211.5;
=     A 5.1;
U     M 211.6;
=     A 5.2;
U     M 211.7;
=     A 5.3;
U     M 209.4;
=     A 5.4;
U     M 212.1;
=     A 5.5;
U     M 212.2;
=     A 5.6;
U     M 212.3;
=     A 5.7;
U     M 212.4;
=     A 6.4;
U     M 212.5;
=     A 6.5;
U     M 212.6;
=     A 6.6;
U     M 212.7;
=     A 6.7;
L     MB 213;
T     AB 7;
NETWORK
TITLE = Freie Ausgänge wenn Robotik nicht installiert ist
U     DB1.DBX 1426.1;     // Robotik-Interface installiert (SW)
SPB     M0001;
ON    DB1.DBX 1356.3;     // Spindel steht NC
ON    DB1.DBX 1356.2;     // AWZ steht NC
=     A 7.7               // Spindel läuft (für Fa.Engel 05.07.2012 )
M0001: NOP   0;
U     M 214.1;
=     A 8.1;
L     MB 216;
T     AB 10;
AACE: NOP   0;
END_ORGANIZATION_BLOCK
//**************************************************************************
// DB100 wird vom PLC-Besy mit fester Laenge (1000 Bytes) angelegt!!!
// Diese Definition ist nur wegen Import in S7 notwendig
// Es koennen dzt. keine anderen DB's definiert werden!!!
DATA_BLOCK DB 100       // zweiter Merkerbereich
STRUCT
daten : ARRAY  [1 .. 1000 ] OF BYTE ;
END_STRUCT ;
BEGIN
// keine Vorbelegungen - DB100 ist nicht remanent!!
END_DATA_BLOCK
//**************************************************************************
FUNCTION FC 101 : VOID
TITLE=EMCO_SPRUNG
NAME: E_SPRUNG
BEGIN
NETWORK
TITLE =SET
O     M 110.1;
ON    M 110.1;
S     M 110.1;            // "1"
R     M 110.0;            // "0"
NETWORK
TITLE = PLC-Stueckzaehler
UC    FC  2;            // PLC-Stueckzaehler
NETWORK
TITLE = Sicherheitstechnik/Sonderbetrieb
UC    FC 51;           // Sicherheitstechnik/Sonderbetriebsart
NETWORK
TITLE = Betriebsarten/Konfiguration
UC    FC 52;           // Betriebsarten/Konfiguration
NETWORK
TITLE = PLC-MD Aufbereiten
UC    FC 53;
NETWORK
TITLE = Kuehlmittel
UC    FC 50;              // Kuehlmittel
NETWORK
TITLE = Spaenefoerderer
UC    FC 54;           // Spaenefoerderer
NETWORK
TITLE = Referenzieren/Freifahren
UN    DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
CC    FC 59;              // autom. Referenzieren
NETWORK
TITLE = Achsen
UC    FC 60;           // Achsen
NETWORK
TITLE = Tuerautomatik
U     DB10.DBX 41.1;      // USER_DATA_HEX[1] Türautomatik Landert angewählt
UN    DB10.DBX 41.4;      // USER_DATA_HEX[1] Türautomatik Langer+Laumann angewählt
CC    FC 61;              // Bst. Türautomatik
NETWORK
TITLE = Tuerautomatik Langer+Laumann
UN    DB10.DBX 41.1;      // USER_DATA_HEX[1] Türautomatik Landert angewählt
U     DB10.DBX 41.4;      // USER_DATA_HEX[1] Türautomatik Langer+Laumann angewählt
CC    FC 27
NETWORK
TITLE = Spindeln
UC    FC 70;           // Spindelsteuerung
UC    FC 71;           // Hauptantrieb 1
UC    FC 72;           // AWZ
NETWORK
TITLE = Werkzeugwender SMA
UC    FC 75;              // Werkzeugwender SMA
NETWORK
TITLE = Hilfsantriebe
UC    FC 80;           // Hilfsantriebe
NETWORK
TITLE = Schmiermittel
UC    FC 81;           // Schmiermittel
NETWORK
TITLE = Spannmittel
UC    FC 87;           // Analoge Spannwegueberwachung
UC    FC 88;           // Spannmittel Steuerbaustein
UC    FC 89;           // Spannmitel Ventile
NETWORK
TITLE = Reitstock
UC    FC 92;              // autom. Reitstock
UC    FC 93;              // Pinolenreitstock
NETWORK
TITLE = STV/LM
UC    FC 114;           // EMCO Standard-Interface
NETWORK
TITLE = Robotik-Interface Festo
U     DB1.DBX 1426.1;     // Robotik-Interface installiert (SW)
U     DB10.DBX 48.2;      // USER_DATA_HEX[8]: Robotik-Interface
=     M 191.0;            // Robotik aktiv
UC    FC 109;           // Festo Robotik-Interface
NETWORK
TITLE = Auffangschale
UC    FC 115;             // Auffangschale
NETWORK
TITLE = Warnlampe
UC    FC 82;           // Warnlampe 3fach
NETWORK
TITLE = NC-Start Dauerlauf
UC    FC 83;           // NC-Start Dauerlauf
NETWORK
TITLE = NC-Start/NC-Stop
UC    FC 84;           // NC Start-Stop
NETWORK
TITLE = NC-Daten lesen/schreiben, PI-Dienste
UC    FC 55;           // NC-Daten lesen/schreiben, PI-Dienste
NETWORK
TITLE = FM,BM nach Einschalten initialisieren
// nicht bei PC-Maschine
NETWORK
TITLE = PLC=>NC Transfer
UC    FC 85;           // PLC=>NC
END_FUNCTION
//**************************************************************************
FUNCTION FC 50 : VOID
TITLE=KUEHLMITTEL
NAME: KM
BEGIN
NETWORK
TITLE = SPS/OB-->KÜHLMITTEL AKTIV
SET;
=     DB20.DBX 382.3;     // SPS/RM-->KÜHLMITTEL AKTIV
NETWORK
TITLE = Flanke Taste Kuehlmittel
U     M 106.2;            // Taste Kuehlmittel
FP    M 162.5;            // Flanke Taste KM
NETWORK
TITLE = Kuehlmittel 1 programmiert
O     DB20.DBX 192.7;     // M07 dyn.
O     DB20.DBX 193.0;     // M08 dyn.
O     DB20.DBX 201.6;     // M78 dyn.
O(
U     DB20.DBX 302.6;     // DNC KM ein
U     DB1.DBX 1366.0;     // DNC-BETRIEB AKTIV
)
O(    ;
U     DB20.DBX 201.5;     // M77 dyn.
UN    DB10.DBX 58.3;      // USER_DATA_HEX[18] Minimalschmierung
)     ;
S     M 162.1;            // KM1 programmiert
O     DB20.DBX 201.5;     // M77 dyn.
O     M 162.4;            // alle KM aus
R     M 162.1;            // KM1 programmiert
NETWORK
TITLE = Kuehlmittel 2 programmiert
O     DB20.DBX 200.7;     // M71 dyn.
S     M 162.2;            // KM2 programmiert
O     DB20.DBX 201.0;     // M72 dyn.
O     M 102.7;            // Reset Taste
O     DB20.DBX 192.2;     // M02 dyn.
O     DB20.DBX 195.6;     // M30 dyn.
ON    M 136.0;            // AUX-ON
R     M 162.2;            // KM2 programmiert
NETWORK
TITLE = Kuehlmittel 3 programmiert
U     DB20.DBX 201.5;     // M77 dyn.
S     M 162.3;            // KM3 programmiert
O     DB20.DBX 192.7;     // M07 dyn.
O     DB20.DBX 193.0;     // M08 dyn.
O     DB20.DBX 201.6;     // M78 dyn.
O     M 162.4;            // alle KM aus
ON    DB10.DBX 58.3;      // USER_DATA_HEX[18] Minimalschmierung
R     M 162.3;            // KM3 programmiert
NETWORK
TITLE = alle Kuehlmittel ausschalten
O     M 102.7;            // Reset Taste
O     DB20.DBX 192.2;     // M02 dyn.
O     DB20.DBX 193.1;     // M09 dyn.
O     DB20.DBX 195.6;     // M30 dyn.
O     M 164.0;            // FN KM ein mit Taste
O(
U     DB20.DBX 302.5;     // DNC KM aus
U     DB1.DBX 1366.0;     // DNC-BETRIEB AKTIV
)
;      U     DB20.DBX 225.0;    // M8 STAT.
U     M 164.1             // M8 Gespeichert
ON    M 136.0;            // AUX-ON
O     DB1.DBX 1440.0      // RESET AUSGELÖST
O     DB1.DBX 1370.3;     // RESETTASTE GEDRÜCKT
=     DB20.DBX 257.1;     // M9 RÜCKMELDEN
=     M 162.4;            // alle KM aus
R     M 164.1             // M8 Gespeichert
NETWORK
TITLE = KM unterbrochen mit Taste
O(    ;
U     M 162.5;            // Fl. Taste KM
U     M 162.7;            // KM unterbrochen mit Taste
)     ;
O(    ;
UN    M 162.1;            // KM1 programmiert
UN    M 162.2;            // KM2 programmiert
UN    M 162.3;            // KM3 programmiert
)     ;
R     M 162.7;            // KM unterbrochen mit Taste
SPB   M061;
U     M 162.5;            // Fl. Taste KM
U     M 162.0;            // KM Freigabe
UN    M 162.7;            // KM unterbrochen mit Taste
S     M 162.7;            // KM unterbrochen mit Taste
M061: NOP   0;
NETWORK
TITLE = KM ein mit Taste
O(    ;
U     M 106.2;            // Taste KM
UN    E 6.2;              // Tuer zu, Antriebe ein
)     ;
O(    ;
U     M 162.5;            // Fl. Taste KM
UN    M 162.6;            // KM ein mit Taste
)     ;
UN    M 162.1;            // KM1 programmiert
UN    M 162.2;            // KM2 programmiert
UN    M 162.3;            // KM3 programmiert
U     M 136.0;            // AUX-ON
S     M 162.6;            // KM ein mit Taste
SPB   M071;
U     M 162.5;            // Fl. Taste KM
U     M 162.6;            // KM ein mit Taste
O     M 162.1;            // KM1 programmiert
O     M 162.2;            // KM2 programmiert
O     M 162.3;            // KM3 programmiert
O     M 162.4;            // alle KM aus
ON    E 6.2;              // Tuer zu, Antriebe ein
R     M 162.6;            // KM ein mit Taste
M071: NOP   0;
O     M 162.6;           // KM ein mit Taste
FN    M 164.0;           // FN KM ein mit Taste
NETWORK
TITLE = Kuehlmittel Freigabe
U(    ;
O     E 6.2;              // Tuer zu, Antriebe ein
O     M 106.2;            // Taste Kuehlmittel
)     ;
UN    M 111.5;            // M00/M01 aktiv
UN    DB20.DBX 324.2;     // DRY aktiv
UN    M 162.7;            // KM unterbrochen mit Taste
U(    ;
ON    M 111.6;            // Programm unterbrochen
ON    M 155.1;            // BA AUTO
)     ;
U(    ;
ON    M 146.3;            // VSH/EFG WZW schwenkt
O     DB10.DBX 40.7;      // USER_DATA_HEX[0] KM bei WZW nicht wegschalten
)     ;
U     E 5.6;              // Therm. Stoerung sofort
=     M 162.0;            // KM Freigabe
NETWORK
TITLE = Leistung Kuehlmittelpumpe 1
O(    ;
U(    ;
O     M 162.1;            // KM1 programmiert
O     M 162.6;            // KM ein mit Taste
)     ;
U     M 162.0;            // KM Freigabe
UN    DB10.DBX 42.6;      // USER_DATA_HEX[2] 2.Kuehlmittelpumpe
)     ;
O(    ;
U     M 162.2;            // KM2 programmiert
U     M 162.0;            // KM Freigabe
U     DB10.DBX 42.6;      // USER_DATA_HEX[2] 2.Kuehlmittelpumpe
)     ;
=     A 4.2;              // Leistung KM1
NETWORK
TITLE = Leistung Kuehlmittelpumpe 2
O(    ;
U(    ;
O     M 162.1;            // KM1 programmiert
O     M 162.6;            // KM ein mit Taste
)     ;
U     M 162.0;            // KM Freigabe
U     DB10.DBX 42.6;      // USER_DATA_HEX[2] 2.Kuehlmittelpumpe
)     ;
O(    ;
U     M 162.2;            // KM2 programmiert
U     M 162.0;            // KM Freigabe
UN    DB10.DBX 42.6;      // USER_DATA_HEX[2] 2.Kuehlmittelpumpe
)     ;
=     M 210.3;            // Leistung KM2
NETWORK
TITLE = MV Minimalschmierung
UN    DB10.DBX 58.3;      // USER_DATA_HEX[18] Minimalschmierung
SPB   M111;
U     M 162.3;            // KM3 programmiert
U     M 162.0;            // KM Freigabe
=     M 210.1;            // MV Minimalschmierung
M111: NOP   0;
NETWORK
TITLE = M8 RÜCKMELDEN
O     A 4.2;             // Leistung KM1
O     M 210.3;           // Leistung KM2
=     DB20.DBX 257.0;    // M8 RÜCKMELDEN
S     M 164.1            // M8 Gespeichert
NETWORK
TITLE = Schwimmerschalter Minimalschmierung
U     E 5.3;              // Schwimmerschalter Minimalschmierung
U     M 110.7;            // Blinktakt
U     DB10.DBX 58.3;      // USER_DATA_HEX[18] Minimalschmierung
=     DB3.DBX 3.7;        // BM7231 Minimalschmiersystem leer
NETWORK
TITLE = LED KM-Taste
U     M 162.7;            // KM unterbrochen mit Taste
U     M 110.7;            // Blinktakt
O     A 4.2;              // Leistung KM1
O     M 210.3;            // Leistung KM2
O     M 210.1;            // MV Minimalschmierung
=     DB20.DBX 851.1;     // LED KM-Taste
O     A 4.2;              // Leistung KM1
O     M 210.3;            // Leistung KM2
O     M 210.1;            // MV Minimalschmierung
=     DB20.DBX 310.6;     // Rueckmeldung KM spritzt
O     M 162.7;            // KM unterbrochen mit Taste
O     DB20.DBX 310.6;     // Rueckmeldung KM spritzt
=     DB20.DBX 310.5;     // Rueckmeldung KM ein
;NETWORK
;TITLE = Kupa Spindelverlaengerung spuelen
;      U     E 5.4;              // Kupa Spuelanforderung
;      X     DB10.DBX 44.2;      // Kupa invert. Spueleingang
;      U     M 111.7;            // Progr. laeuft
;      U     M 136.6;            // Sp. laeuft
;      U     DB10.DBX 44.3;      // Kupa Spuelung aktiv
;      =     M 210.2;            // Kupa Spindelspuelung
NETWORK
TITLE = KÜHLMITTEL/NEG.DNC-QUITTUNG
O(
U     DB20.DBX 302.6;     // DNC/KÜHLMITTEL EIN
UN    M 162.0;            // KM Freigabe
)
;      O(
;      U     DB20.DBX 302.5;     // DNC/KÜHLMITTEL AUS
;      UN    M 162.0;            // KM Freigabe
;      )
=     DB20.DBX 318.3      // KÜHLMITTEL/NEG.DNC-QUITTUNG
END_FUNCTION
//**************************************************************************
FUNCTION FC 51 : VOID
TITLE=SICHERHEITSTECHNIK
NAME: SICHTECH
BEGIN
NETWORK
TITLE = Sonderbetriebsart
U(    ;
U     DB20.DBX 324.7;     // SBL aktiv
U     DB20.DBX 324.2;     // DRY aktiv
ON    M 155.1;            // BA AUTO
)     ;
U     M 201.6;            // Schluesselschalter H/A
=     M 137.1;            // Einrichtebetriebsart
=     M 210.7;            // Sonderbetriebsart
=     DB20.DBX 340.2      // Sonderbetrieb an AC
NETWORK
TITLE = Verr. Start Einrichtbetrieb
O     M 155.1;            // BA Auto
O     M 155.4;            // BA MDA
U     M 201.6;            // Schluesselschalter H/A
=     M 145.5;            // Verr. Start Einrichtbetrieb
NETWORK
TITLE = Verr. Start Tuer
UN    T 42;               // Antriebe ein verzoegert
=     M 145.2;            // Verr. Start Antriebe ein
NETWORK
TITLE = BM7218 Tuer offen
UN    E 6.2;              // Tuer zu, Antriebe ein
U     M 136.0;            // Aux On
U(    ;
UN    DB1.DBX 1402.1;     // Fanuc 0TB
UN    DB1.DBX 1403.0;     // Fanuc 21TB
O     M 137.2;            // Start Befehle
)     ;
S     DB3.DBX 2.2;        // BM7218 Tuer offen
O     E 6.2;              // Tuer zu, Antriebe ein
O     M 138.0;            // Fl. Reset Taste
R     DB3.DBX 2.2;        // BM7218 Tuer offen
// V1.04: Anw. entfernt weil bei Tür auf das Progr. abgebrochen wurde
//      UN    E 6.2;              // Tuer zu, Antriebe ein
//      U     M 136.0;            // Aux On
//      FP    DB20.DBX 3.3;       // Rundachspos. sofort abbrechen
//      FP    DB25.DBX 3.3;       // Rundachspos. sofort abbrechen
O     E 8.1;              // Von Türsteuergerät Tür offen
=     DB1.DBX 1390.0;     // Rueckmeldung Tuer offen
U     E 6.2;              // Tuer zu, Antriebe ein
=     DB1.DBX 1390.1;     // Rueckmeldung Tuer zu
NETWORK
TITLE =VSH/EFG Antriebe nicht bereit
U     M 111.7;            // Programm laeuft
UN    DB20.DBX 324.3;     // Satzsuchl. akt.
UN    T 42;               // Antriebe ein verzoegert
=     M 147.4;            // VSH/EFG Antriebe nicht bereit
NETWORK
TITLE = FM6123 Tuerueberwachung
UN    E 6.2;              // Tuer zu, Antriebe ein
U     E 7.0;              // Not Aus
U     M 136.0;            // Aux On
U(    ;
O     M 111.7;            // Programm laeuft
O     M 136.6;            // Spindel laeuft
)     ;
S     DB2.DBX 4.7;        // FM6123 Tuerueberwachung
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 4.7;        // FM6123
NETWORK
TITLE = Flanke Schluesselsch. Einrichtbetrieb
U     M 201.6;            // Schluesselschalter H/A
FP    M 140.4;            // Flanke SS Einrichten
NETWORK
TITLE = Flanke Umschalten Sonderbetriebsart
U     M 140.4;            // Flanke SS Einrichten
U(    ;
UN    M 110.5;            // AWZ im Rundachsbetrieb
UN    DB10.DBX 40.3;      // USER_DATA_HEX[0]: keine AWZ
O     M 111.7;            // Programm laeuft
O     M 136.6;            // Spindel laeuft
)     ;
U     M 136.0;            // Aux On
=     M 140.5;            // Flanke Umschalten Sonder-BA
NETWORK
TITLE = BM7226 "Falsche Betriebsart" setzen
U     M 137.2;            // Start Befehle
U     M 145.5;            // Verriegel. Start Einrichtebetrieb
O     M 164.2;            // HA Start Befehl
O     M 140.5;            // Fl. Umschalten SBA
S     DB3.DBX 3.2;        // BM 7226 "Falsche Betriebsart"
NETWORK
TITLE = Meldung "Falsche Betriebsart" ruecks.
ON    M 201.6;            // Schluesselschalter H/A
O     M 102.7;            // Taste Reset
R     DB3.DBX 3.2;        // BM7226 "Falsche Betriebsart"
NETWORK
TITLE = Start Befehle
U     M 148.2;            // NC Start gesamt
UN    M 155.5;            // BA Jog
O     M 164.5;            // HA-start manuell
O     M 164.2;            // HA-start Befehl
=     M 137.2;            // Start Befehle
NETWORK
TITLE = FM6109 Stoerung E/R-Modul
U     M 136.0;            // AUX-ON
UN    E 5.5;              // BB E/R-Modul
L     S5T#3S;
SE    T 41;               // Ueberwachungszeit E/R-Modul
U     T 41;
S     DB2.DBX 3.1;        // FM6109 Stoerung E/R-Modul
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 3.1;
NETWORK
TITLE = FM6110 Stoerung AS-Signale
U(    ;
UN    E 6.2;              // Tuer zu, Antriebe ein
UN    E 4.7;              // n=0 von Bedienmodul
UN    E 6.0;              // AS-Kette
O     ;
U     A 4.1;              // FG Antriebe
U     E 6.2;              // Tuer zu, Antriebe ein
U     E 6.0;              // AS-Kette
)     ;
L     S5T#2S;             // Ueberwachungszeit AS-Signale
SE    T 43;
U     T 43;
=     M 138.5;            // Ueberwachung AS-Signale
S     DB2.DBX 3.2;        // FM6110 Stoerung AS-Signale
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 3.2;
NETWORK
TITLE = FM6111 Stoerung AUX-ON Schuetz
U(    ;
UN    E 6.1;              // Ueberwachungskontakt K11
UN    A 4.0;              // AUX-ON, SW-FG Antriebe
UN    A 4.1;              // FG Antriebe
O     ;
U     E 6.1;              // Ueberwachungskontakt K11
U     A 4.0;              // AUX-ON, SW-FG Antriebe
)     ;
L     S5T#200MS;
SE    T 40;               // Ueberwachungszeit K11
U     T 40;
S     DB2.DBX 3.3;        // FM6111 Stoerung AUX-ON Schuetz
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 3.3;
NETWORK
TITLE = IFG HSA/Achsen
U     M 136.0;            // AUX-ON
U     E 6.2;              // Tuer zu, Antriebe ein
O     ;
UN    DB1.DBX 1356.3;     // Spindel steht NC
U     A 4.1;              // FG Antriebe
L     S5T#1S;
SA    T 26;               // Ausschaltverz. Antriebe
U     T 26;
UN    DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
=     A 4.1;              // FG Antriebe
NETWORK
TITLE = Antriebe ein
U     M 136.0;            // AUX ON
U     E 5.5;              // Betriebsbereit E/R-Modul
U     A 4.1;              // FG Antriebe
=     M 137.7;            // Antriebe ein
NETWORK
TITLE = Antriebe ein verzoegert
U     M 137.7;            // Antriebe ein
U     E 6.2;              // Tuer zu, Antriebe ein
L     S5T#300MS;
SE    T 42;               // Verzoegerungszeit Antriebe ein
NETWORK
TITLE =Spindel laeuft unverzoegert
UN    DB1.DBX 1356.3;     // Spindel steht NC
U     M 208.3;            // RFG Spindel 611U
=     M 136.6;            // Spindel laeuft
NETWORK
TITLE = Spindel laeuft verzoegert
UN    DB1.DBX 1356.3;     // Spindel steht NC
L     S5T#1S;
SA    T 102;              // Verzoegerung f. M136.7
U     T 102;
=     M 136.7;            // Spindel laeuft verz.
NETWORK
TITLE = n=0 an Sicherheitsplatine
// V0.07: M 67.1 eingebunden um NC-Alarme zu verhindern
// V0.15: 'M32 gespeichert' eingebunden damit Tuer nicht freigegeben wird
U(    ;
ON    M 67.1;             // VSH/EFG AWZ Positionieren
O     DB3.DBX 2.2;        // BM7218 Tuer offen
)     ;
UN    M 136.7;            // Spindel laeuft verz.
U     DB1.DBX 1356.2;     // AWZ steht NC
UN    M 111.7;            // Programm laeuft
UN    M 174.7;            // M32 gespeichert
=     M 210.6;            // n=0 an Sicherheitsplatine
NETWORK
TITLE =FM6126 Stoerung Drehzahlueberwachung
// V0.17: Ueberwachung nur bei 'Tuer zu, Antriebe ein'
U(    ;
O(    ;
UN    E 2.0;              // n<nmin von 611U
UN    E 4.7;              // n=0 von Bedienmodul
)     ;
O(    ;
U     DB1.DBX 1356.3;     // Spindel steht NC
U     M 210.6;            // n=0 an Sicherheitsplatine
U     E 4.7;              // n=0 von Bedienmodul
)     ;
)     ;
U     M 136.0;            // AUX-ON
U     M 201.3;            // HA Ready 611U
U     E 6.2;              // Tuer zu, Antriebe ein
UN    M 110.4;            // Spindel ist Rundachse
L     S5T#500MS;
SE    T 23;
U     T 23;
S     DB2.DBX 5.2;        // FM6126 Stoerung Drehzahlueberwachung
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 5.2;        // FM6126 Stoerung Drehzahlueberwachung
END_FUNCTION
//**************************************************************************
FUNCTION FC 52 : VOID
TITLE=BA_KONF
NAME: BA_KONF
BEGIN
NETWORK
TITLE = BA Anwahl von Robotik-Interface
// V0.17: Netzwerk neu
U     M 191.6;            // Rob. BA Ref. anwaehlen
U     M 191.0;            // Robotik aktiv
S     DB20.DBX 289.2;     // BA REF
U     M 191.7;            // Rob. BA Auto anwaehlen
U     M 191.0;            // Robotik aktiv
S     DB20.DBX 288.4;     // BA AUTO
NETWORK
TITLE = BA Anwahl abspeichern
// BA JOG beim Einschalten vorbesetzen (EDIT bei 840D oder Simulator)
U     M 110.2;            // Grst.
S     DB100.DBX500.0;     // Bit fuer BA JOG
L     DB20.DBW 288;
L     0;
==I   ;
SPB   M011;
L     DB20.DBW 288;       // Betriebsartenumschaltung
T     DB100.DBW 500;      // BA-Anwahl gespeichert
M011: NOP   0;
NETWORK
TITLE =BA Ref. nach einschalten automatisch vorgeben
O(    ;
U(    ;
O     DB3.DBX 1.3;        // BM7211 Refpunkt Wender fehlt
O     M 137.3;            // Referenzpunkt nicht erreicht
)     ;
UN    M 70.0;             // autom. Offsetabgleich aktiv
U     M 107.4;            // AUX-ON Taste
UN    M 90.0;             // autom. Ref. aktiv
)     ;
O(    ;
UN    DB3.DBX 1.3;        // BM7211 Refpunkt Wender fehlt
UN    M 137.3;            // Referenzpunkt nicht erreicht
)     ;
ON    M 136.0;            // AUX-ON
O     M 111.6;            // Programm unterbrochen
O     M 111.7;            // Programm laeuft
SPB   M021;
// Anwahl BA REF an Nahtstelle legen
L     W#16#0400;          // Bit BA REF setzen
T     DB20.DBW 288;       // Nahtstelle fuer BA-Anwahl
SPA   M022;
M021: NOP   0;
// Anwahl abgespeicherte BA an Nahtstelle anlegen
L     DB100.DBW 500;      // BA-Anwahl gespeichert
T     DB20.DBW 288;       // Nahtstelle fuer BA-Anwahl
M022: NOP   0;
NETWORK
TITLE =BA-Wechsel in NC anfordern falls notwendig
L     DB20.DBW 326;       // stat. Kanalzustand BA
L     DB20.DBW 288;       // Nahtstelle fuer BA-Anwahl
<>I   ;
=     DB20.DBX 0.0;       // BA-Wechselanforderung NC
NETWORK
TITLE =BA Auto aktiv
U     DB20.DBX 326.4;     // BA AUTO von Kern
=     M 155.1;
NETWORK
TITLE =BA MDA aktiv
U     DB20.DBX 326.2;     // BA MDA von Kern
=     M 155.4;
NETWORK
TITLE =BA REF aktiv
U     DB20.DBX 327.2;     // BA REF von Kern
=     M 155.2;
NETWORK
TITLE =BA JOG aktiv
O     DB20.DBX 326.0;      // BA JOG von Kern
O     DB20.DBX 326.5;      // BA INC1 von Kern
O     DB20.DBX 326.6;      // BA INC10 von Kern
O     DB20.DBX 326.7;      // BA INC100 von Kern
O     DB20.DBX 327.0;      // BA INC1000 von Kern
O     DB20.DBX 327.1;      // BA INC10000 von Kern
O     DB20.DBX 327.3;      // BA INC VAR von Kern
=     M 155.5;
NETWORK
TITLE =Betriebsartenwechselsperre
//      U(    ;
//      U(    ;
//      O     M 155.1;
//      O     M 155.2;
//      O     M 155.4;
//      )     ;
//      U     M 111.7;
//      O     ;
//      U(    ;
//      O     M 146.3;
//      ON    M 248.7;
//      )     ;
//      U(    ;
//      U     M 155.2;
//      U     M 191.4;
//      NOT   ;
//      )     ;
//      ON    M 248.7;
//      )     ;
//      U(    ;
//      U     M 107.4;
//      U     M 201.6;
//      NOT   ;
//      )     ;
//      =     DB11.DBX 0.4;
NETWORK
TITLE = M90 und H0 M99  SPM links Handspannfutter
U(    ;
L     DB20.DBW 186;
L     0;
==I   ;
)     ;
U     DB20.DBX 204.3;     // M99 dyn.
U     M 111.0;            // H-Aenderung verl.
O     DB20.DBX 203.2;     // M90 dyn.
R     M 5.0;
R     M 5.1;
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
NETWORK
TITLE = M91 und H1 M99  SPM links Futter (Zug)
U(    ;
L     DB20.DBW 186;
L     1;
==I   ;
)     ;
U     DB20.DBX 204.3;     // M99 dyn.
U     M 111.0;            // H-Aenderung verl.
O     DB20.DBX 203.3;     // M91 dyn.
S     M 5.0;
R     M 5.1;
R     M 5.2;              // SPM Endlagenueberw. aus
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
NETWORK
TITLE = M92 und H2 M99  SPM links Spannzange (Druck)
U(    ;
L     DB20.DBW 186;
L     2;
==I   ;
)     ;
U     DB20.DBX 204.3;     // M99 dyn.
U     M 111.0;            // H-Aenderung verl.
O     DB20.DBX 203.4;     // M92 dyn.
R     M 5.0;
S     M 5.1;
R     M 5.2;              // SPM Endlagenueberw. aus
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
NETWORK
TITLE = M93 und H3 M99  SPM1 links Endlagenueberbrueckung deaktivieren
U(    ;
L     DB20.DBW 186;
L     3;
==I   ;
)     ;
U     DB20.DBX 204.3;     // M99 dyn.
U     M 111.0;            // H-Aenderung verl.
O     DB20.DBX 203.5;     // M93 dyn.
S     M 5.2;              // SPM Endlagenueberw. aus
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
NETWORK
TITLE = M94 und H4 M99 Anwahl STV/LM
U(    ;
L     DB20.DBW 186;
L     4;
==I   ;
)     ;
U     DB20.DBX 204.3;     // M99 dyn.
U     M 111.0;            // H-Aenderung verl.
O     DB20.DBX 203.6;     // M94 dyn.
S     M 5.3;
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
NETWORK
TITLE = M95 und H5 M99 Abwahl STV/LM
U(    ;
L     DB20.DBW 186;
L     5;
==I   ;
)     ;
U     DB20.DBX 204.3;     // M99 dyn.
U     M 111.0;            // H-Aenderung verl.
O     DB20.DBX 203.7;     // M95 dyn.
R     M 5.3;
S     DB20.DBX 348.3;     // Anforderung SE-Daten sichern
NETWORK
TITLE = Konfiguration sofort in Settingdaten speichern
L     MW 5;               // akt. Maschinenkonfiguration
T     DB15.DBW 20;        // Maschinenkonfig. in SE-Daten
NETWORK
TITLE =BM7043 Sollstueckzahl erreicht
U     DB20.DBX 204.0;     // M96
S     DB3.DBX 5.3;        // BM7043 Sollstueckzahl erreicht
END_FUNCTION
//**************************************************************************
FUNCTION FC 53 : VOID
TITLE=PLC_MD
NAME: PLC_MD
BEGIN
END_FUNCTION
//**************************************************************************
FUNCTION FC 55 : VOID
TITLE=NC_DAT_RW
NAME: NCDAT_RW
BEGIN
NETWORK
TITLE =NC-Daten schreiben anfordern
U     M 137.4;            // Fl. 2Hz
S     M 189.0;            // NC-Daten schreiben anfordern
U     M 189.1;            // NC-Daten schreiben done
R     M 189.0;
NETWORK
TITLE = NC-Daten schreiben Request
U     M 189.0;            // NC-Daten schreiben anfordern
UN    M 189.3;            // NC-Daten schreiben error
=     M 189.4;            // NC-Daten schreiben request
NETWORK
TITLE = NC-Daten schreiben
// hier fehlt noch der R-Transfer in die NC
END_FUNCTION
//**************************************************************************
FUNCTION FC 54 : VOID
TITLE=SPAENEFOERDERER
NAME: SPF
BEGIN
NETWORK
TITLE = negative FL. Taste Spaenefoerderer
U     M 106.1;            // Taste Spaenefoerderer
FN    DB100.DBX 183.1;    // neg Flanke
NETWORK
TITLE = HM Pausenzeitaenderung RS
ON    DB20.DBX 199.3;     // M59 dyn
ON    M 111.0;            // H-Aenderung verl.
R     DB100.DBX 182.1;    // HM Pausenzeitaenderung
NETWORK
TITLE = bei keiner Pausenzeitaenderung springe nach "no_c"
ON    DB20.DBX 199.3;     // M59 dyn.
ON    M 111.0;            // H-Aenderung verl.
SPB   no_c;
NETWORK
TITLE =lade neue Pausenzeit(untere 16bits) in DB82
L     DB20.DBW 186;       // H-Wort Ganzzahl
T     DB10.DBW 6;         // USER_DATA_INT[3] SPF Pausenzeit
NETWORK
TITLE =HM Pausenzeitaenderung
U     M 110.1;            // "1"
S     DB100.DBX 182.1;    // HM Pausenzeitaenderung
NETWORK
TITLE =Vergleich Einschaltzeit mit Zaehlerstand
no_c: U(    ;
L     DB100.DBW 82;       // aktueller Zaehlerstand Einschaltzeit
L     DB10.DBW 4;         // USER_DATA_INT[2] SPF Laufzeit
==I   ;
)     ;
R     DB100.DBX 182.0;    // Spaenefoerderer ein
=     DB100.DBX 182.2;    // HM Einschaltzaehler
NETWORK
TITLE = Einschaltzeitzaehler
U     DB100.DBX 182.0;    // SPF ein
UN    M 216.7;              // SPF zurueck
U     M 110.7;            // Blinktakt
ZV    Z 23;               // Einschaltzeit
U(    ;
O     DB100.DBX 182.2;    // HM Einschaltzaehler
ON    M 136.0;            // AUX-ON
O     DB100.DBX 182.1;    // HM Pausenzeitaenderung
O(    ;
U     DB100.DBX 183.1;    // neg Fl. Taste
UN    T 45;               // verzoegerte Taste
)     ;
)     ;
R     Z 23;
L     Z 23;
T     DB100.DBW 82;       // aktueller Einschaltzaehlerstand
NETWORK
TITLE =Vergleich Pausenzeit mit Zaehlerwert
O(    ;
L     DB100.DBW 84;       // aktueller Pausenzeitzaehlerwert
L     DB10.DBW 6;         // USER_DATA_INT[3] SPF Pausenzeit
==I   ;
)     ;
O(    ;
U     DB100.DBX 183.1;    // neg. Fl Taste
UN    T 45;               // verz. Taste
)     ;
S     DB100.DBX 182.0;    // SPF ein
=     DB100.DBX 182.3;    // HM Pausenzeitzaehler
NETWORK
TITLE = Pausenzeitzaehler
U     M 111.7;            // Programm laeuft
UN    DB100.DBX 182.0;    // SPF ein
UN    M 216.7;              // SPF zurueck
U     M 110.7;            // Blinktakt
ZV    Z 22;               // Pausenzeit
O     DB100.DBX 182.3;    // HM Pausenzeitzaehler
O     DB100.DBX 182.1;    // HM Pausenzeitaenderung
ON    M 136.0;            // AUX-ON
R     Z 22;
L     Z 22;
T     DB100.DBW 84;       // aktueller Pausenzeitzaehlerwert
NETWORK
TITLE = SPF ruecksetzen
O     DB100.DBX 182.1;    // HM SPF Pausenzeit
ON    M 136.0;            // AUX-ON
R     DB100.DBX 182.0;    // SPF ein
NETWORK
TITLE = SPF vorwaerts
U     DB100.DBX 182.0;    // SPF ein
UN    M 106.1;            // Taste SPF
U     E 5.7;              // Therm. Stoerungen
UN    M 216.7;              // SPF rueckwaerts
U     M 136.0;            // AUX-ON
=     M 216.6;              // SPF vorwaerts
NETWORK
TITLE = Verz.Zeit SPF rueckwaerts
U     M 106.1;            // Taste SPF
L     S5T#1S;             // 1s
SE    T 45;               // Zeit SPF rueckwaerts
NETWORK
TITLE = SPF rueckwaerts
U     T 45;               // Zeit SPF rueckwaerts
U     E 5.7;              // Therm. Stoerungen
U     M 136.0;            // AUX-ON
UN    M 216.6;              // SPF vorwaerts
=     M 216.7;              // SPF rueckwaerts
NETWORK
TITLE = LED SPF-Taste
U     M 216.7;              // SPF rueckwaerts
U     M 110.7;            // Blinktakt
O     M 216.6;              // SPF vorwaerts
=     DB20.DBX 851.2;     // LED SPF-Taste
END_FUNCTION
//**************************************************************************
FUNCTION FC 59 : VOID
TITLE=REFERENZIEREN
NAME: REF
BEGIN
NETWORK
TITLE = Taste Ref., DNC Ref., Robotik Ref.
O(    ;
U     M 104.6;            // NC-Start Taste
U     M 155.2;            // BA Ref
U     DB1.DBX 1402.2;     // Steuerungskennung EMCOTRONIC TM02
)     ;
O     M 190.2;            // Rob. Ref. Start
O     M 193.0             // DNC Ref.-Anforderung
O     M 104.3;            // REF-Taste
FP    M 91.0;             // Fl. Taste REF
U     M 100.3;            // Taste X+
FP    M 92.0;             // Fl. Taste X+
U     M 101.2;            // Taste Z+
FP    M 92.1;             // Fl. Taste Z+
NETWORK
TITLE = autom. Ref. starten
U     M 155.2;            // BA REF
U     M 137.7;            // Antriebe ein
UN    M 70.0;             // autom. Offsetabgleich aktiv
U     M 91.0;             // Fl. Taste REF
S     M 90.0;             // autom. Ref. aktiv
NETWORK
TITLE = autom. Ref. ruecksetzen
O     M 102.7;            // Reset Taste
ON    M 137.7;            // Antriebe ein
O     M 91.7;             // Fl. autom Ref. fertig
ON    M 155.2;            // BA REF
SPBN  _REF;
R     M 90.0;             // autom. Ref. aktiv
R     M 193.0             // DNC Ref.-Anforderung
L     0;
T     MB 90;              // Schrittketet Ref.
_REF: NOP   0;
NETWORK
TITLE = X-Achse autom. referenzieren
U     M 90.0;             // autom. Ref. aktiv
U     M 91.0;             // Fl. Taste REF
S     M 90.1;             // Ref. X aktiv
=     M 93.0;             // Fl. Ref. X autom.
NETWORK
TITLE = Referenzpunkt X erreicht
U     DB1.DBX 134.0;      // Achse 0 ist ref.
FP    M 91.1;             // Fl. Ref-X erreicht
NETWORK
TITLE = Z-Achse autom. referenzieren
U     M 90.0;             // autom. Ref. aktiv
U     M 90.1;             // Ref. X aktiv
U     M 91.1;             // Fl. Ref-X erreicht
S     M 90.2;             // Ref. Z aktiv
R     M 90.1;             // Ref. X aktiv
=     M 93.1;             // Fl. Ref. Z autom.
NETWORK
TITLE = Referenzpunkt Z erreicht
U     DB1.DBX 134.1;      // Achse 1 ist ref.
FP    M 91.2;             // Fl. Ref-Z erreicht
NETWORK
TITLE = WZW autom. referenzieren
U     M 90.0;             // autom. Ref. aktiv
U     M 90.2;             // Ref. Z aktiv
U     M 91.2;             // Fl. Ref-Z erreicht
S     M 90.3;             // Ref. WZW aktiv
R     M 90.2;             // Ref. Z aktiv
=     M 93.2;             // Fl. Ref. WZW autom.
NETWORK
TITLE = Referenzpunkt WZW erreicht - autom. referenzieren fertig
U     M 90.3;             // Ref. WZW aktiv
U     DB100.DBX 37.3;     // HM WZW ref. aktiv
U     E 6.3;              // WZW verriegelt
R     M 90.3;             // Ref. WZW aktiv
FP    M 91.7;             // Fl. autom. Ref. fertig
NETWORK
TITLE = Ref. Kommando X-Achse
U     M 92.0;             // Fl. Taste X+
UN    M 90.0;             // autom. Ref. aktiv
U     M 155.2;            // BA REF
O     M 93.0;             // Fl. Ref. X autom.
=     DB20.DBX 8.0;       // Kanalachse X referenzieren
NETWORK
TITLE = Ref. Kommando Z-Achse
U     M 92.1;             // Fl. Taste Z+
UN    M 90.0;             // autom. Ref. aktiv
U     M 155.2;            // BA REF
O     M 93.1;             // Fl. Ref. Z autom.
=     DB20.DBX 8.2;       // Kanalachse Z referenzieren
NETWORK
TITLE = BM7210 autom. Ref. aktiv
U     M 90.0;             // autom. Ref. aktiv
=     DB3.DBX 1.2;        // BM7210 autom. Ref. aktiv
END_FUNCTION
//**************************************************************************
FUNCTION FC 60 : VOID
TITLE=ACHSEN
NAME: ACHSEN
BEGIN
NETWORK
TITLE =Gewindeschneiden aktiv
U     DB20.DBX 544.4;     // Interpolationsart Gewinde aktiv
=     M 115.0;            // G33/G331/G332 aktiv
NETWORK
TITLE = Vorschubhalt manuell setzen
U     M 104.4;            // Taste Vorschub halt
U(    ;
O     M 111.7;            // Programm laeuft
O     M 111.6;            // Programm unterbrochen
)     ;
UN    M 115.0;            // G33/G331/G332 aktiv
U(    ;
O     M 136.4;            // Summe Fahrbefehl
O     M 111.6;            // Programm unterbrochen
)     ;
S     DB3.DBX 0.4;        // BM7204 "Vorschubhalt manuell"
NETWORK
TITLE = Vorschubhalt mauell ruecksetzen
U     M 104.2;            // Taste Vorschub start
UN    M 147.3;            // VSH/EFG Hauptantrieb manuell
UN    M 172.2;            // AWZ Unterbrechung
ON    M 155.1;
O     M 102.7;            // Reset-Taste
R     DB3.DBX 0.4;        // BM7204 "Vorschubhalt manuell"
U     DB3.DBX 0.4;        // BM7204 "Vorschubhalt manuell"
U     M 110.7;            // Blinktakt
=     DB20.DBX 852.0;     // LED Vorschub-Start Taste
NETWORK
TITLE =Bremse X-Achse
// steht deshalb vor Zuweisung der NC-RFG, weil die NC bei einem
// schwerwiegendem Achsfehler dieses Signal wegnimmt!!
U     M 137.7;            // Antriebe ein
U     DB1.DBX 1460.0;     // RFG NC-Achse 0
U     M 201.0;            // BB X/Z-Modul
UN    DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
=     M 216.2;              // Bremse X-Achse
NETWORK
TITLE = RFG X-Achse
U(    ;
UN    DB20.DBX 546.0;     // Genauhalt fein Achse 0
U     DB1.DBX 1460.0;     // RFG NC-Achse 0
O     M 137.7;            // Antriebe ein
)     ;
U     M 136.0;            // Aux on
U     E 6.2;              // Tuer zu, Antriebe ein
=     DB1.DBX 1460.0;     // RFG NC-Achse 0
=     M 208.0;            // RFG Achse X 611U
NETWORK
TITLE = X+ konv.
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M051;
ON    M 137.3;            // Referenzpunkt nicht erreicht
O     M 201.6;            // SS Hand/Auto
U     M 100.3;            // Taste X+
U     M 208.0;            // RFG Achse 1
U     M 137.7;            // Antriebe ein
U     M 155.5;            // NC-BA JOG/INC
=     DB20.DBX 10.0;      // X+ konv.
M051: NOP   0;
NETWORK
TITLE = X- konv.
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M061;
ON    M 137.3;            // Referenzpunkt nicht erreicht
O     M 201.6;            // SS Hand/Auto
U     M 102.3;            // Taste X-
U     M 208.0;            // RFG Achse 1
U     M 137.7;            // Antriebe ein
U     M 155.5;            // NC-BA JOG/INC
=     DB20.DBX 12.0;      // X- konv
M061: NOP   0;
NETWORK
TITLE = FM6112 Stoerung X-Achse
U     T 42;               // Antriebe ein verzoegert
UN    M 201.0;            // Bb X-Achse
S     DB2.DBX 3.4;        // FM6112
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 3.4;
NETWORK
TITLE =RFG Z-ACHSE
U(    ;
UN    DB20.DBX 546.1;     // Genauhaltfein Achse 1
U     DB1.DBX 1460.1;     // RFG NC-Achse 1
O     M 137.7;            // Antriebe ein
)     ;
U     M 136.0;            // Aux on
U     E 6.2;              // Tuer zu, Antriebe ein
=     M 208.1;            // RFG Achse Z 611U
=     DB1.DBX 1460.1;     // RFG NC-Achse 1
NETWORK
TITLE = Z+ konv.
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M091;
ON    M 137.3;            // Referenzpunkt nicht erreicht
O     M 201.6;            // SS Hand/Auto
U     M 101.2;            // Taste Z+
U     M 208.1;            // RFG Achse 2
U     M 137.7;            // Antriebe ein
U     M 155.5;            // NC-BA JOG/INC
=     DB20.DBX 10.2;      // Z+ konv.
M091: NOP   0;
NETWORK
TITLE = Z- konv.
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M101;
ON    M 137.3;            // Referenzpunkt nicht erreicht
O     M 201.6;            // SS Hand/Auto
U     M 101.4;            // Taste Z-
U     M 208.1;            // RFG Achse 2
U     M 137.7;            // Antriebe ein
U     M 155.5;            // NC-BA JOG/INC
=     DB20.DBX 12.2;      // Z- konv.
M101: NOP   0;
NETWORK
NETWORK
TITLE = FM6113"Stoerung Z-Achse"
U     T 42;               // Antriebe ein verzoegert
UN    M 201.1;            // Bb Z-Achse
S     DB2.DBX 3.5;        // FM6113
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 3.5;
NETWORK
TITLE = Eilgang Ueberlagerung
U     M 101.3;            // Taste Eilgang
U     T 42;               // Antriebe ein verzoegert
UN    M 201.6;            // Schluesselsch. AUTO/HAND
=     DB20.DBX 824.0;     // Eilgangueberlagerung Kanal 0
NETWORK
TITLE = Summe Fahrbefehl
ON    DB20.DBX 842.0;     // X-Achse IPO Befehl
ON    DB20.DBX 842.2;     // Z-Achse IPO Befehl
=     M 136.4;            // Summe Fahrbefehl
NETWORK
TITLE = BM7209 "Referenzpunkt nicht erreicht"
ON    DB1.DBX 134.0;      // X: Ref. err.
ON    DB1.DBX 134.1;      // Z: Ref. err.
U     M 136.0;            // AUX-ON
=     M 137.3;            // Referenzpunkt nicht erreicht
U     M 137.3;            // Referenzpunkt nicht erreicht
UN    M 70.0;             // autom. Offsetabgleich aktiv
UN    DB3.DBX 1.2;        // BM77210 autom. Ref. aktiv
UN    DB3.DBX 2.2;        // BM7218 Tuer offen
=     DB3.DBX 1.1;        // BM7209 Referenzpunkt nicht erreicht
NETWORK
TITLE = LED REF-Taste ansteuern
U     DB3.DBX 1.1;        // BM77209 Referenzpunkt nicht erreicht
U     M 110.7;            // Blinktakt
O     DB3.DBX 1.2;        // BM7210 autom. Ref. aktiv
=     DB20.DBX 850.4;     // LED REF-Taste
NETWORK
TITLE = 2.SW-Endschalter Z+
U(    ;
L     DB10.DBD 108;       // USER_DATA_FLOAT[12]
L     +1E+03;             // Umrechnen in [m]
/R    ;
L     DB1.DBD 0;          // X-Position
>R    ;
)     ;
U     DB10.DBX 41.2;      // USER_DATA_HEX[1] autom. Reitstock
=     DB20.DBX 343.6;     // 2.SW-Endschalter Z+
NETWORK
TITLE = 2.SW-Endschalter X-
U(    ;
L     DB10.DBD 112;       // USER_DATA_FLOAT[13]
L     +1E+03;             // Umrechnen in [m]
/R    ;
L     DB1.DBD 4;          // Z-Position
<R    ;
)     ;
U     DB10.DBX 41.2;      // USER_DATA_HEX[1] autom. Reitstock
O(    ;
U     DB10.DBX 41.2;      // USER_DATA_HEX[1] autom. Reitstock
UN    E 4.3;              // Reitstock Endlage hinten
)     ;
=     DB20.DBX 343.1;     // 2.SW-Endschalter X-
END_FUNCTION
//**************************************************************************
FUNCTION FC 61 : VOID
TITLE=TUERAUTOMATIK
NAME: TUERAUTO
BEGIN
// V0.17: Landert Tuerautomatik neu
// V1.04: Landert NTiB eingebunden
NETWORK
TITLE = SPS/OB-->TÜRAUTOMATIK AKTIV
U     DB10.DBX 41.1;      // USER_DATA_HEX[1] Türautomatik Landert angewählt
=     DB1.DBX 1418.0;     // SPS/RM-->TÜRAUTOMATIK AKTIV
NETWORK
TITLE = Tuer referenzieren NTiB-2
U     M 201.5;            // ext. Cycle-Start Taste
U     E 8.0;              // Landert Tuer nicht referenziert
UN    E 8.7;              // Tuerautomatix ext. Fehler
U     DB10.DBX 41.3;      // USER_DATA_HEX[1] Tuerautomatik Landert NTiB angewählt
S     M 194.7;            // Tür zu ohne Hindernis
U     M 201.5;            // ext. Cycle-Start Taste
U     E 8.0;              // Landert Tuer nicht referenziert
U     E 8.7;              // Tuerautomatix ext. Fehler
U     E 6.2;              // Tuer zu, Antriebe ein
U     M 194.7;            // Tür zu ohne Hindernis
O     ;
U     M 194.0;            // autom. Eichlauf Tuer zu
U     E 8.7;              // Tuerautomatix ext. Fehler
U     E 6.2;              // Tuer zu, Antriebe ein
S     M 194.1;            // Tuer auf Anschlag geschlossen
O     M 194.1;            // Tuer auf Anschlag geschlossen
O     M 102.7;            // Reset-Taste
O     E 2.4;              // Taste Tuer oeffnen
R     M 194.7;            // Tür zu ohne Hindernis
U     M 194.1;            // Tuer auf Anschlag geschlossen
R     M 194.0;            // Eichlauf Tuer zu
R     M 194.1;            // Tuer auf Anschlag geschlossen
S     M 194.2;            // Eichlauf Tuer zu ausblenden
U     M 194.2;            // Eichlauf Tuer zu ausblenden
L     S5T#300MS;
SE    T 16;
U     T 16;
S     M 194.3;            // Eichlauf Tuer quittieren
U     M 194.3;            // Eichlauf Tuer quittieren
UN    E 8.0;              // Landert Tuer nicht referenziert
O     M 102.7;            // Reset-Taste
O     E 2.4;              // Taste Tuer oeffnen
R     M 194.3;            // Eichlauf Tuer quittieren
R     M 194.2;            // Eichlauf Tuer zu ausblenden
NETWORK
TITLE =Zeitverzögerung Tuer zu
U     E 6.2;              // Tuer zu, Antriebe ein
L     S5T#600MS;
SE    T 64;
NETWORK
TITLE =Flanke Tuer zu
U     T 64;
FP    M 173.4;            // verz. Tuer zu
NETWORK
TITLE =NC-Start nach Tuer zu
// 1.07: für H4 M99 war falscher Merker verknuepft
U(    ;
UN    M 5.3;              // H4 M99
U     M 173.4;            // verz. Tuer zu
O     ;
U     M 5.3;              // H4 M99
U     M 180.6;            // ???
)     ;
U     E 6.2;              // Tuer zu, Antriebe ein
U     M 155.1;            // BA AUTO
UN    M 137.1;            // Einrichte-BA
UN    M 111.6;            // Programm unterbrochen
UN    M 191.0;            // Robotik aktiv
UN    DB1.DBX 1366.0;     // DNC aktiv
=     M 148.5;            // NC-Start Tuer zu
NETWORK
TITLE =Flanke M0/M1
O     DB20.DBX 192.0;     // M00 dyn.
O     DB20.DBX 192.1;     // M01 dyn.
=     M 173.2;            // Fl. M0/M1
NETWORK
TITLE =Flanke M2/M30
// 1.07: im Stangenbetrieb verriegelt
U(    ;
O     DB20.DBX 192.2;     // M02 dyn.
O     DB20.DBX 195.6;     // M30 dyn.
)     ;
U(    ;
ON    M 5.3;              // H4 M99
O     M 155.7;            // 1x aktiv
)     ;
=     M 173.7;            // Fl. M2/M30
NETWORK
TITLE =Tuer auf gespeichert
U     E 2.4;              // Taste Tuer oeffnen
FP    M 173.0;            // Fl. Taste Tuer auf
O(    ;
U(    ;
O     M 173.2;            // Fl. M0/M1
O     M 173.7;            // Fl. M2/M30
O     M 173.5;            // Tuer auf gesp.
)     ;
U     E 6.2;              // Tuer zu, Antriebe ein
)     ;
O(    ;
U     M 173.0;            // Fl. Taste Tuer auf
UN    E 8.0;              // Landert Tuer nicht geeicht
U     M 136.0;            // AUX-ON
)     ;
O(    ;
U     M 190.7;            // Tuer auf Robotik
U     M 191.0;            // Robotik aktiv
UN    M 111.7;            // Programm laeuft
UN    M 136.6;            // Spindel laeuft
U     M 136.0;            // AUX-ON
)     ;
O(    ;
U     DB1.DBX 1382.0;     // DNC Tuer auf
U     DB1.DBX 1366.0;     // DNC-BETRIEB AKTIV
U     M 210.6;            // n=0 an Sicherheitsplatine
)     ;
S     M 173.5;            // Tuer auf gesp.
ON    M 136.0;            // AUX-ON
O     E 8.1;              // Von Türsteuergerät Tür offen
O     E 8.0;              // Landert Tuer nicht geeicht
O     T 66;               // max. Zeit Tuer auf gesp.
O     M 173.3;            // Tuer Bewegung abgebrochen
R     M 173.5;            // Tuer auf gesp.
U     M 173.5;            // Tuer auf gesp.
L     S5T#3S;
SE    T 66;               // max. Zeit Tuer auf gesp.
NETWORK
TITLE =Tuer zu gespeichert
U     M 201.5;            // ext. Cycle-Start Taste
FP    M 173.1;            // Fl. Taste Tuer zu
O(    ;
O     M 173.1;            // Fl. Taste Tuer zu
O(
U     DB1.DBX 1382.1;     // DNC Tuer zu
U     DB1.DBX 1366.0;     // DNC-BETRIEB AKTIV
)
UN    E 8.0;              // Landert Tuer nicht referenziert
U     M 136.0;            // AUX-ON
)     ;
O(    ;
U     M 190.6;            // Tuer zu Robotik
U     M 191.0;            // Robotik aktiv
U     M 136.0;            // AUX-ON
)     ;
O(    ;
U     DB100.DBX 752.7;    // IB-Bit Tuerautomatik
U     M 155.1;            // BA AUTO
U     M 136.0;            // AUX-ON
UN    M 5.3;              // H4 M99
U     E 8.1;              // Von Türsteuergerät Tür offen
)     ;
S     M 173.6;            // Tuer zu gesp.
O     T 64;               // verz. Tuer zu
O     T 65;               // max. Zeit Tuer zu gesp.
O     M 173.3;            // Tuer Bewegung abgebrochen
R     M 173.6;            // Tuer zu gesp.
U     M 173.6;            // Tuer zu gesp.
L     S5T#3S;
SE    T 65;               // max. Zeit Tuer zu gesp.
NETWORK
TITLE =Ventil Tuer auf
U     DB1.DBX 1356.3;
UN    E 4.7;              // N=0 vom Bedienmodul
U(    ;
O     M 173.5;            // Tuer auf gesp.
O     M 180.0;
O     M 192.1;            // Eichlauf auf
O     M 194.3;            // Eichlauf Tuer quittieren
O     E 2.4;              // Taste Tuer oeffnen
)     ;
UN    M 111.7;            // Programm laeuft
=     M 212.4;            // Landert Tuer auf
NETWORK
TITLE =Ventil Tuer zu
O(    ;
U     M 201.5;            // ext. Cycle-Start Taste
U     E 8.0;              // Landert Tuer nicht referenziert
UN    M 194.2;            // Eichlauf Tuer zu ausblenden
)     ;
O(    ;
U     M 192.3;            // Eichlauf zu
UN    M 194.2;            // Eichlauf Tuer zu ausblenden
)     ;
O(    ;
U     M 194.0;            // autom. Eichlauf Tuer zu
UN    M 194.2;            // Eichlauf Tuer zu ausblenden
)     ;
O     M 194.3;            // Eichlauf Tuer quittieren
O     M 173.6;            // Tuer zu gesp.
=     M 212.5;            // Landert Tuer zu
NETWORK
TITLE =Tuerbew. abgebrochen
U     M 212.4;            // Landert Tuer auf
U     M 212.5;            // Landert Tuer zu
=     M 173.3;            // Tuer Bewegung abgebrochen
NETWORK
TITLE = IB-Bit Tuerautom. ruecksetzen
U     M 110.2;            // Grst. OB1
R     DB100.DBX 752.7;    // IB-Bit Tuerautomatik
END_FUNCTION
//**************************************************************************
FUNCTION FC 70 : VOID
TITLE=SPINDEL
NAME: SPINDEL
BEGIN
NETWORK
TITLE =Spindeloverride aus Tasten
U     M 106.3;            // Taste 100%
FP    DB100.DBX  127.1;
// Spindeloverride 100% vorgeben
UN    DB100.DBX 127.1;    // Fl. Taste 100%
UN    M 110.2;            // Grst. OB1
SPB   ovkl;
L     100;                // 100%
T     DB100.DBW  128;
SPA   oven;
ovkl: U     M 106.7;            // Taste -%
FP    DB100.DBX 127.3;
// Spindeloverride -5% (bis. min. 50%)
UN    DB100.DBX 127.3;    // Fl. Taste -%
SPB   ovgr;
L     DB100.DBW 128;
L     50;                 // 50%
==I   ;
SPB   oven;
L     DB100.DBW 128;
L     5;                  // 5%
-I    ;
T     DB100.DBW 128;
SPA   oven;
ovgr: U     M 107.5;            // Taste % groesser
FP    DB100.DBX 127.5;
// Spindeloverride +5% (bis max. 120%)
UN    DB100.DBX 127.5;    // Fl. Taste +%
SPB   oven;
L     DB100.DBW 128;
L     120;                // 120%
==I   ;
SPB   oven;
L     DB100.DBW 128;
L     5;                  // 5%
+I    ;
T     DB100.DBW 128;
oven: NOP   0;
NETWORK
TITLE = LED's Spindeloverride
// V1.06: NW neu
L     DB20.DBW 360;       // Sp.-Override ist
L     1000;               // 100%
<I   ;
=     DB20.DBX 850.6;     // LED %-
L     DB20.DBW 360;       // Sp.-Override ist
L     1000;               // 100%
==I   ;
=     DB20.DBX 850.7;     // LED 100%
L     DB20.DBW 360;       // Sp.-Override ist
L     1000;               // 100%
>I   ;
=     DB20.DBX 851.0;     // LED %+
NETWORK
TITLE = Startverriegelungen
L     MB 145;             // Startverriegelungen
L     0;
==I   ;
=     M 137.0;            // keine Startverriegelungen
NETWORK
TITLE =M3
O(    ;
U     DB20.DBX 192.3;     // M3 dyn.
U     M 137.0;            // keine Startverriegelungen
UN    M 201.6;            // Schluesselschalter H/A
)     ;
O(    ;
U     M 167.3;            // Spindel drehen aktiv
U(    ;
O     M 110.7;            // Blinktakt
O     DB10.DBX 42.4;      // USER_DATA_HEX[2]: STV/LM Spindel drehen
)     ;
)     ;
O     M 68.0;             // TM02 Spindel RL
=     M 166.3;            // M3
=     DB20.DBX 256.3;     // M3 RÜCKMELDEN
S     M 166.0;            // Spindel ein
S     DB20.DBX 340.0;     // Spindel ein CW (Spindel 0)
R     DB20.DBX 340.1;     // Spindel ein CCW (Spindel 0)
NETWORK
TITLE =M4
O(    ;
U     DB20.DBX 192.4;     // M4 dyn.
U     M 137.0;            // keine Startverriegelungen
UN    M 201.6;            // Schluesselschalter H/A
)     ;
O(    ;
U     M 167.3;            // Spindel drehen aktiv
UN    M 110.7;            // Blinktakt
UN    DB10.DBX 42.4;      // USER_DATA_HEX[2]: STV/LM Spindel drehen
)     ;
O     M 68.1;             // TM02 Spindel LL
=     M 166.4;            // M4
=     DB20.DBX 256.4;     // M4 RÜCKMELDEN
S     M 166.0;            // Spindel ein
R     DB20.DBX 340.0;     // Spindel ein CW (Spindel 0)
S     DB20.DBX 340.1;     // Spindel ein CCW (Spindel 0)
NETWORK
TITLE =M5
U     DB20.DBX 199.2;     // M58 dyn.
U     M 167.3;            // Spindel drehen aktiv
O     DB20.DBX 192.5;     // M5 dyn.
O     M 68.2;             // TM02 Spindel aus
=     M 166.5;            // M5
NETWORK
TITLE =M5
O     M 166.5;            // HM M5
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
O     DB20.DBX 192.2;     // M02 dyn.
O     DB20.DBX 195.6;     // M30 dyn.
O     M 165.2;            // C-Achse ein
R     M 166.0;            // Spindel ein
R     DB20.DBX 340.0;     // Spindel ein CW (Spindel 0)
R     DB20.DBX 340.1;     // Spindel ein CCW (Spindel 0)
=     DB20.DBX 256.5;     // M5 RÜCKMELDEN
NETWORK
TITLE = Rundachsensteuerung fuer 810T/Fanuc und Offsetabgleich
O(    ;
U     DB20.DBX 198.4;     // M52 dyn.
U     M 114.0;            // 810T / 0TB / 21TB
)     ;
O     M 70.1;             // Fl. Rundachse ein Offsetabgleich
UN    M 110.4;            // Spindel im Rundachsbetrieb
S     M 165.2;            // C-Achse ein
U     DB20.DBX 194.3;     // M19 dyn.
U     M 114.0;            // 810T / 0TB / 21TB
S     M 167.0;            // HM M19 aktiv 810/Fanuc
U(    ;
O(    ;
U     DB20.DBX 198.5;     // M53 dyn.
U     M 114.0;            // 810T / 0TB / 21TB
)     ;
O     DB20.DBX 192.2;     // M2 dyn.
O     DB20.DBX 192.3;     // M3 dyn.
O     DB20.DBX 192.4;     // M4 dyn.
O     DB20.DBX 195.6;     // M30 dyn.
O     M 138.0;            // Fl. Reset-Taste
ON    M 136.0;            // AUX ON
)     ;
U     M 110.4;            // Spindel im Rundachsbetrieb
S     M 165.3;            // C-Achse aus
R     M 167.0;            // HM M19 aktiv 810/Fanuc
L     MB 165;             // Zustandsmerker Positionierung
L     0;
<>I   ;
=     M 167.1;            // VSH/EFG Spindelpositionierung
// Spindel in Positionierbetrieb schalten
U     M 165.2;            // C-Achse ein
UN    M 136.6;            // Spindel laeuft
S     M 165.4;            // C-Achse Positionierung anfordern
R     M 165.2;
SPBN  ORI1;
S     DB25.DBX 3.5;       // Spindelumschaltung
S     DB25.DBX 340.3;     // Einlesefreigabe K1
S     DB25.DBX 340.4;     // Achsfreigabe K1
L     0;
T     DB25.DBB 822;       // Spindelnummer
L     1;
T     DB25.DBB 823;       // Rundachsbetrieb
O     M 70.1;             // Fl. Rundachse ein Offsetabgleich
O     M 167.0;            // HM M19 aktiv 810/Fanuc
R     M 165.4;            // C-Achse Positionierung anfordern
ORI1: NOP   0;
// Positionssatz C-Achse abschicken (C-Achse referenzieren)
U     M 165.4;            // C-Achse Positionierung anfordern
U     M 110.4;            // Spindel im Rundachsbetrieb
U     M 208.3;            // RFG Spindel 611U
S     M 165.1;            // Positionierung laeuft
R     M 165.4;
SPBN  ORI2;
=     DB25.DBX 1.4;       // NC-Satz abschicken
=     DB25.DBX 548.5;     // Genauhaltmodus setzen
=     DB25.DBX 548.7;     // G0
S     DB25.DBX 21.4;      // Positionieranforderung Kanalachse C
L     0.0E+00;            // Zielposition 0 Grad
T     DB25. DBD 70;       // Positionswert Kanalachse C
ORI2: NOP   0;
U     M 165.1;            // Positionierung laeuft
U     DB25.DBX 332.0;     // NC-Satz fertig
R     M 165.1;            // Positionierung laeuft
R     DB25.DBX 332.0;     // NC-Satz fertig
// Spindel in Spindelbetrieb zurueckschalten
U     M 165.3;            // C-Achse aus
L     S5T#300MS;
SE    T 10;               // Verzoegerung C-Achse aus
U     T 10;
U     M 165.3;            // C-Achse aus
R     M 165.3;
SPBN  ORI3;
S     DB25.DBX 3.5;       // Spindelumschaltung
L     0;
T     DB25.DBB 822;       // Spindelnummer
L     0;
T     DB25.DBB 823;       // Spindelbetrieb
ORI3: NOP   0;
NORI: NOP   0;
NETWORK
TITLE =Rundachsbetrieb der Spindel
L     DB1.DBB 1447;       // Achstyp fuer Achse 3
L     3;                  // Kennung Rundachse
==I   ;
=     M 110.4;            // Spindel im Rundachsbetrieb
=     M 209.7;            // Parametersatzumschaltung HA
NETWORK
TITLE = BM7222 "Spindelhalt manuell"
U(    ;
ON    M 111.7;            // Programm laeuft
ON    M 136.4;            // Summe Fahrbefehl
O     DB3.DBX 0.4;        // BM7204 "VSH manuell"
)     ;
U(    ;
O(    ;
UN    M 110.4;            // Spindel im Rundachsbetrieb
U     M 208.3;            // RFG Spindel 611U
U     M 166.0;            // Spindel ein
)     ;
O(    ;
UN    M 110.5;            // AWZ im Rundachsbetrieb
U     M 208.2;            // RFG AWZ 611U
U     M 66.0;             // AWZ ein
)     ;
)     ;
U     M 104.1;            // Taste Spindel stop
UN    M 68.0;             // TM02 Spindel RL
UN    M 68.1;             // TM02 Spindel LL
S     DB3.DBX 2.6;        // BM7222 "Spindelhalt manuell"
U     M 107.7;            // Taste Spindel start
U     DB3.DBX 2.6;        // BM700046
U     M 137.0;            // keine Start Verriegelungen
UN    M 67.1;             // VSH/EFG AWZ Spindelpositionierung laeuft
O     M 102.7;            // Reset-Taste
R     DB3.DBX 2.6;        // BM7222
U     DB3.DBX 2.6;        // BM7222 "Spindelhalt manuell"
U     M 110.7;            // Blinktakt
=     DB20.DBX 852.3;     // LED Spindel-Start Taste
NETWORK
TITLE = Man. Spindel Start/Stop fuer EMCOTRONIC
UN    DB1.DBX 1402.2;     // Steuerungskennung TM02
UN    DB1.DBX 1403.3;     // Steuerungskennung FAGOR F8055
SPB   NOET;
U     M 107.7;            // Taste Spindel start
UN    M 104.1;            // Taste Spindel stop
U     M 137.7;            // Antriebe ein
U     M 155.5;            // BA JOG
L     S5T#1S;
SE    T 53;               // Zeit RL/LL
=     M 68.3;             // HM TM02 Spindel ein
U     M 68.3;             // HM TM02 Spindel ein
UN    T 53;               // Zeit RL/LL
S     M 68.0;             // TM02 Spindel RL
R     M 68.1;             // TM02 Spindel LL
U     M 68.3;             // HM TM02 Spindel ein
U     T 53;               // Zeit RL/LL
R     M 68.0;             // TM02 Spindel RL
S     M 68.1;             // TM02 Spindel LL
U(    ;
O     M 104.1;            // Taste Spindel stop
O     M 102.7;            // Reset Taste
ON    M 137.7;            // Antriebe ein
ON    M 155.5;            // BA JOG
)     ;
U(    ;
O     M 68.0;             // TM02 Spindel RL
O     M 68.1;             // TM02 Spindel LL
)     ;
R     M 68.0;             // TM02 Spindel RL
R     M 68.1;             // TM02 Spindel LL
=     M 68.2;             // TM02 Spindel aus
NOET: NOP   0;
NETWORK
TITLE = VSH/EFG Spindel
U     DB3.DBX 2.6;        // BM7222
U(    ;
O     M 111.7;            // Prg. laeuft K1
O     M 111.6;            // Programm unterbrochen
)     ;
O     M 167.1;            // VSH/EFG Spindelpositionierung
=     M 147.3;            // EFG/VSH Spindel
NETWORK
TITLE = RFG Spindel
O(    ;
U     M 166.0;            // Spindel ein
UN    DB20.DBX 324.2;     // DRY
)     ;
O     M 110.4;            // Spindel im Rundachsbetrieb
O     M 165.2;            // C-Achse ein
UN    DB3.DBX 2.6;        // BM7222 Spindelhalt man.
UN    DB3.DBX 9.0;        // BM7272 SPM offen
UN    DB3.DBX 9.1;        // BM7273 SPM auf Endlage
UN    M 111.5;            // M00/M01 aktiv
UN    T 108;              // Bero Spindelbremse verz.
U     M 137.7;            // Antriebe ein
=     M 208.3;            // RFG Spindel 611U
O(    ;
U     M 137.7;            // Antriebe ein
U     M 201.3;            // BB Spindel 611U
U     M 208.3;            // RFG Spindel 611U
//      UN    T 108;            // Bero Spindelbremse verz.
//      UN    M 25.0;           // HM BM7275 Spindelbremse aktiv
)     ;
O(    ;
U     DB1.DBX 1460.3;     // NC-RFG Achse 3
U(;                       // 1.05: RFG bei Pendeln aus verzoegern
ON    DB1.DBX 1356.3;     // Spindel steht NC
O     T 106;              // Spindel pendln aus verz.
)     ;
)     ;
=     DB1.DBX 1460.3;     // NC-RFG Achse 3
NETWORK
TITLE = FM6116 Stoerung Hauptantrieb
U     T 42;               // Antriebe ein verzoegert
UN    M 201.3;            // Bb Spindel 611U
S     DB2.DBX 4.0;        // FM6116
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 4.0;
NETWORK
TITLE = EFG/VSH Spindel im Sollbereich ausblenden
U     E 2.1;              // N=Nsoll von 611U
L     S5T#100MS;
SE    T 52;
U     T 52;
FP    M 166.2;            // HM Flanke
U     M 166.2;
R     M 166.7;
NETWORK
TITLE =HM M3M4M5
O     M 166.3;            // M3
O     M 166.4;            // M4
O     M 166.5;            // M5
=     M 166.1;            // HM M3/M4/M5
NETWORK
TITLE =setzen
O     M 166.1;            // HM M3/M4/M5
O     DB3.DBX 2.6;        // BM7222 "Spindelhalt manuell"
O     M 111.5;            // M00/M01 aktiv
S     M 166.7;            // EFG/VSH Spindel im Sollbereich ausbl.
NETWORK
TITLE =HM Spindelbetrieb
U     M 166.1;            // HM
S     M 166.6;            // HM Spindelbetrieb
NETWORK
TITLE = HM Spindelbetrieb
O     M 102.7;            // Reset-Taste
O     M 110.4;            // Spindel im Rundachsbetrieb
R     M 166.6;
END_FUNCTION
//**************************************************************************
FUNCTION FC 71 : VOID
TITLE=HA_1
NAME: HA_1
BEGIN
NETWORK
TITLE = Spindelbremse ein
O(    ;
U     DB20.DBX 193.2;     // M10
UN    DB20.DBX 324.2;     // DRY
)     ;
O(    ;
U     M 155.5;            // BA JOG
U     M 107.4;            // Taste AUX ON
U     M 104.1;            // Taste Spindel Stop
)     ;
O(    ;
U     M 110.4;            // Spindel im Rundachsbetrieb
U     M 155.1;            // BA AUTO
UN    E 6.2;              // Tuer zu, Antriebe ein
)     ;
S     M 167.7;            // Spindelbremse ein
NETWORK
TITLE = Fahrbefehl fuer Spindel 0 bilden
O     M 166.3;            // M3
O     M 166.4;            // M4
ON    DB20.DBX 843.4;     // IPO-Befehl K0 C-Achse
ON    DB25.DBX 843.4;     // IPO-Befehl K1 C-Achse
=     M 167.6;            // Fahrbef. S0
NETWORK
TITLE = Spindelbremse aus
U     M 167.6;            // Fahrbef. S0
U     A 4.3;              // MV Spindelbremse
O     DB20.DBX 193.3;     // M11 dyn.
O     DB20.DBX 862.0;     // Transmit/Tracyl aktiv
ON    M 136.0;            // AUX-ON
O     M 102.7;            // Reset-Taste
R     M 167.7;            // Spindelbremse ein
NETWORK
TITLE = Ventil Spindelbremse
U     M 167.7;            // Spindelbremse ein
U     DB1.DBX 1356.3;     // Achse Steht
L     S5T#500MS;
SE    T 107;
U     T 107;
=     A 4.3;              // MV Spindelbremse
NETWORK
TITLE = Bero Spindelbremse verzoegern
U     E 2.7;              // Bero Spindelbremse
L     S5T#300MS;
SA    T 108;              // Bero Spindelbremse verz.
NETWORK
TITLE = VSH/EFG Spindelbremse
O(    ;
U     M 167.7;            // Spindelbremse ein
UN    E 2.7;              // Bero Spindel indexiert
)     ;
O(    ;
UN    M 167.7;            // Spindelbremse ein
U     T 108;              // Bero Spindelbremse verz.
)     ;
=     DB100.DBX 1.5;      // VSH/EFG Spindelbremse
NETWORK
TITLE = BM7275 Spindelbremse aktiv
O     M 167.7;            // Spindelbremse ein
O     T 108;              // Bero Spindelbremse verzoegert
=     M 25.0;             // HM BM7275 Spindelbremse aktiv
U     M 25.0;             // HM BM7275 Spindelbremse aktiv
U(    ;
UN    DB1.DBX 1402.1;     // Fanuc 0TB
UN    DB1.DBX 1403.0;     // Fanuc 21TB
O     DB2.DBX 6.7;        // FM6215 Zeitueberwachung Hilfsfunktion
)     ;
=     DB3.DBX 9.3;        // BM7275 Spindelbremse aktiv
NETWORK
TITLE = FM6205 Bero Spindelbremse fehlt
U(    ;
U     A 4.3;              // MV Spindelbremse
UN    E 2.7;              // Bero Spindelbremse
O     ;
UN    A 4.3;              // MV Spindelbremse
U     E 2.7;              // Bero Spindelbremse
)     ;
L     S5T#2S;
SE    T 58;
U     T 58;
S     DB2.DBX 1.6;        // FM6205
U     M 102.7;            // Reset Taste
R     DB2.DBX 1.6;        // FM6205
NETWORK
TITLE = M57 Spindel drehen ein
U     DB20.DBX 199.1;     // M57 dyn.
S     M 167.3;            // Spindel drehen aktiv
SPBN  M571;
S     DB20.DBX 2.3;       // NC-Kommando Drehzahl setzen
L     0;                  // Nr. der Hauptspindel
T     DB20.DBW 594;       // Spindelnummer f. Drehzahlkommando
L     DB10.DBW 10;        // USER_DATA_INT[5]: Pendeldrehzahl
ITD   ;
DTR   ;
L     +6.0E+01;
/R    ;
T     DB20.DBD 160;       // Drehzahl in [U/s]
M571: NOP   0;
NETWORK
TITLE = M58 Spindel drehen aus
O     DB20.DBX 199.2;     // M58 dyn.
O     M 138.0;            // Fl. Reset Taste
ON    M 137.7;            // Antriebe ein
R     M 167.3;            // Spindel drehen aktiv
S     M 167.4;            // Spindel drehen aus
ON    M 136.6;            // Spindel laeuft
ON    M 137.7;            // Antriebe ein
R     M 167.4;            // Spindel drehen aus
NETWORK
TITLE = Spindel drehen verzoegert
O     M 167.3;            // Spindel drehen aktiv
O     M 167.4;            // Spindel drehen aus
L     S5T#300MS;
SA    T 106;              // Spindel drehen verzoegert
END_FUNCTION
//**************************************************************************
FUNCTION FC 72 : VOID
TITLE=AWZ_1
NAME: AWZ_1
BEGIN
NETWORK
TITLE = Abbruch - keine AWZ
U     DB10.DBX 40.3;      // USER_DATA_HEX[0]: keine AWZ
S     M 66.7;             // AWZ Kuppelpos. erreicht
R     M 147.7;            // EFG/VSH AWZ
S     DB1.DBX 1460.2;     // NC-RFG Achse 2
R     DB2.DBX 3.0;        // FM6108 Stoerung AWZ
BEB   ;
NETWORK
TITLE =Rundachsbetrieb AWZ
// V0.18: Netzwerk hierher verschoben (lt. Yvonne)
L     DB1.DBB 1446;       // Achstyp fuer Achse 2
L     3;                  // Kennung Rundachse
==I   ;
=     M 110.5;            // AWZ im Rundachsbetrieb
=     M 209.6;            // Parametersatzumschaltung AWZ
NETWORK
TITLE =M2=3
O(    ;
U     DB20.DBX 202.3;     // M83 dyn.
U     M 111.0;            // H-Aenderung verl.
U     DB1.DBX 1402.0;     // Steuerungskennung SINUMERIK 810/820
)     ;
O     DB20.DBX 642.3;     // M3 Spindel 1
U     M 137.0;            // keine Startverriegelungen
S     M 66.0;             // AWZ Spindel ein
S     M 66.3;             // HM M2=3
U     M 66.3;
UN    M 110.5;            // AWZ Rundachse
S     DB20.DBX 341.1;     // Spindel 1 ein CW
R     DB20.DBX 341.2;     // Spindel 1 ein CCW
=     M 67.3;             // HM AWZ Start RL als Spindel
R     M 66.3;
NETWORK
TITLE =M2=4
O(    ;
U     DB20.DBX 202.4;     // M84 dyn.
U     M 111.0;            // H-Aenderung verl.
U     DB1.DBX 1402.0;     // Steuerungskennung SINUMERIK 810/820
)     ;
O     DB20.DBX 642.4;     // M4 Spindel 1
U     M 137.0;            // keine Startverriegelungen
S     M 66.0;             // AWZ Spindel ein
S     M 66.4;             // HM M2=4
U     M 66.4;
UN    M 110.5;            // AWZ Rundachse
R     DB20.DBX 341.1;     // Spindel 1 ein CW
S     DB20.DBX 341.2;     // Spindel 1 ein CCW
=     M 67.4;             // HM AWZ Start LL als Spindel
R     M 66.4;
NETWORK
TITLE =M2=5
O(    ;
U     DB20.DBX 202.5;     // M85 dyn.
U     DB1.DBX 1402.0;     // Steuerungskennung SINUMERIK 810/820
)     ;
O(    ;
U     DB100.DBX 35.3;     // HM WZW Start
UN    M 66.7;             // AWZ Kuppelpos. erreicht
)     ;
O     DB20.DBX 642.5;     // M5 Spindel 1
O     DB20.DBX 192.2;     // M02 dyn.
O     DB20.DBX 195.6;     // M30 dyn.
=     M 66.5;             // HM M2=5 (mit Positionierung!)
// M2=5 loeschen wenn im Achsbetrieb (AC wuerde auf Spindel umschalten!)
U     M 110.5;            // AWZ Rundachsbetrieb
R     DB20.DBX 642.5;     // M5 Spindel 1
NETWORK
TITLE =AWZ Spindel ein bei Anforderung vom AC
// 1.05: NW neu wegen sporad. Umschaltprobleme
U(    ;
L     DB20.DBB 822;       // Spindelnummer
L     1;                  // S2
==I   ;
)     ;
U(    ;
L     DB20.DBB 823;       // Spindelbetriebsart
L     0;                  // Spindelbetrieb
==I   ;
)     ;
U     DB20.DBX 3.5;       // Umschaltanforderung Spindel
S     M 66.0;             // AWZ Spindel ein
NETWORK
TITLE =AWZ Spindel ausschalten
O     M 66.5;             // HM M2=5 (mit Positionierung!)
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
O     DB20.DBX 328.1;     // Rundachsumschaltung von AC angefordert S1
R     M 66.0;             // AWZ Spindel ein
R     DB20.DBX 341.1;     // Spindel 1 ein CW
R     DB20.DBX 341.2;     // Spindel 1 ein CCW
NETWORK
TITLE = AWZ Kuppelposition ueberwachen
L     DB10.DBD 60;        // USER_DATA_FLOAT[0]: Kuppelposition AWZ
L     -0.5E+00;           // Toleranz 0.5 Grad
-R    ;
L     -1.0E+03;
/R    ;
T     DB100.DBD 200;      // Kuppelpos. min. Toleranz
L     DB10.DBD 60;        // USER_DATA_FLOAT[0]: Kuppelposition AWZ
L     -0.5E+00;           // Toleranz 0.5 Grad
+R    ;
L     -1.0E+03;
/R    ;
T     DB100.DBD 204;      // Kuppelpos. max. Toleranz
L     DB1.DBD 8;          // Istpos. AWZ-Spindel
L     DB100.DBD 200;      // Kuppelpos. min. Toleranz
<R    ;
=     M 66.1;             // HM Kuppelpos. < Min.
L     DB1.DBD 8;          // Istpos. AWZ-Spindel
L     DB100.DBD 204;      // Kuppelpos. max. Toleranz
>R    ;
=     M 66.2;             // HM Kuppelpos. > Max.
UN    M 66.1;             // HM Kuppelpos. < Min.
UN    M 66.2;             // HM Kuppelpos. > Max.
U     M 110.5;            // AWZ Rundachsbetrieb
UN    M 66.6;             // AWZ Achse Joggen
UN    M 65.1;             // Positionierung laeuft
=     M 66.7;             // AWZ Kuppelpos. erreicht
// AWZ-Position bei WZW Start speichern (zur Diagnose)
UN    DB100.DBX 35.3;     // HM WZW Start
SPB   M061;
L     DB1.DBD 8;          // Istpos. AWZ-Spindel
T     DB100.DBD 8;        // AWZ-Pos. bei WZW
M061: NOP   0;
NETWORK
TITLE = AWZ PLC-Funktionen Rundachse, Positionierung
U     M 66.0;             // AWZ Spindel ein
UN    DB3.DBX 2.6;        // BM7222 Spindelhalt manuell
UN    M 111.5;            // M00/M01 aktiv
U     M 110.5;            // AWZ im Rundachsbetrieb
U     M 208.2;            // RFG AWZ 611U
S     M 65.3;             // AWZ-Achse aus
ON    M 66.0;             // AWZ Spindel ein
O     DB3.DBX 2.6;        // BM7222 Spindelhalt manuell
UN    M 110.5;            // AWZ im Achsbetrieb
U     M 208.2;            // RFG AWZ 611U
O     M 66.5;             // HM M2=5 (mit Positionierung!)
S     M 65.2;             // AWZ-Achse ein
U     M 137.7;            // Antriebe ein
FP    M 67.0;             // HM AWZ positionieren
O(    ;   // wenn Kanal auf Reset wartet, muss Pos.-Kommando wiederholt werden!
U     M 65.1;             // Positionierung laeuft
U     DB25.DBX 332.2;     // Kanal wartet auf Reset-Quittung
)     ;
O(    ;
U     M 66.5;             // HM M2=5 (mit Positionierung!)
UN    M 65.1;             // Positionierung laeuft
)     ;
O(    ;                   // V1.04 Verknüpfung geändert
U     M 67.0;             // HM AWZ positionieren
UN    M 111.5;            // M00/M01 aktiv
UN    M 111.6;            // Progr. unterbrochen
)     ;
S     M 65.0;             // AWZ Positionieren
L     MB 65;              // Zustandsmerker Positionierung
L     0;
<>I   ;
=     M 67.1;             // VSH/EFG Spindelpositionierung
// Spindel in Positionierbetrieb schalten
U     M 65.2;             // AWZ-Achse ein
U     E 2.2;              // N<Nmin AWZ 611U
U     DB1.DBX 1460.2;     // NC-RFG Achse 2
UN    M 65.4;
S     M 65.4;             // AWZ-Achse ein laeuft
SPBN  ORI1;
S     DB25.DBX 3.5;       // Spindelumschaltung
L     1;
T     DB25.DBB 822;       // Spindelnummer
L     1;
T     DB25.DBB 823;       // Rundachsbetrieb
S     DB25.DBX 340.3;     // Einlesefreigabe K1
S     DB25.DBX 340.4;     // Achsfreigabe K1
ORI1: NOP   0;
U     M 65.4;             // AWZ-Achse ein laeuft
U     M 110.5;            // AWZ im Achsbetrieb
U     M 65.2;             // AWZ-Achse ein
R     M 65.4;
R     M 65.2;
// Nach Reset muss die AWZ erneut in den Rundachsbetrieb geschaltet werden
U    DB1.DBX 1440.0    // Reset ausgeloest
R    M 65.4        // AWZ-Achse ein laeuft
// Positionssatz AWZ-Achse abschicken
U     M 65.0;             // Positionierung aktiv
U     M 110.5;            // AWZ im Rundachsbetrieb
U     DB1.DBX 1460.2;     // NC-RFG Achse 2
UN    DB25.DBX 332.2;     // Kanal wartet auf Reset-Quittung
UN    M 65.1;             // Positionierung laeuft
S     M 65.1;             // Positionierung laeuft
R     M 65.0;
SPBN  ORI2;
=     DB25.DBX 1.4;       // NC-Satz abschicken
=     DB25.DBX 548.5;     // Genauhaltmodus setzen
=     DB25.DBX 549.1;     // G1 100%
=     DB25.DBX 549.4;     // F-Wert [Grad/s] in DBD556
S     DB25.DBX 21.3;      // Positionieranforderung Kanalachse B
L     +1.8E+03;           // Vorschub fuer Positionierung
T     DB25.DBD 556;       // F-Wert
L     DB10.DBD 60;        // USER_DATA_FLOAT[0]: Kuppelposition AWZ
T     DB25. DBD 66;       // Positionswert Kanalachse B
ORI2: NOP   0;
U     M 65.1;             // Positionierung laeuft
U     DB25.DBX 332.0;     // NC-Satz fertig
O     DB25.DBX 332.2;     // Kanal wartet auf Reset-Quittung
O     DB25.DBX 3.3;       // C-Achspositionierung abbrechen
O     M 138.0;            // Fl. Reset-Taste
ON    M 136.0;            // AUX-ON
R     DB25.DBX 332.0;     // NC-Satz fertig
R     M 65.1;             // Positionierung laeuft
R     M 65.0;             // Positionierung aktiv
// AWZ in Spindelbetrieb zurueckschalten
U     M 65.3;             // AWZ-Achse aus
R     M 65.3;
SPBN  ORI3;
S     DB25.DBX 3.5;       // Spindelumschaltung
L     1;
T     DB25.DBB 822;       // Spindelnummer
L     0;
T     DB25.DBB 823;       // Spindelbetrieb
ORI3: NOP   0;
NETWORK
TITLE =AWZ Drehzahl aus H-Wort zwischenspeichern
U(    ;
O     DB20.DBX 202.3;     // M83 dyn.
O     DB20.DBX 202.4;     // M84 dyn.
)     ;
U     M 111.0;            // H-Aenderung verl.
U     DB1.DBX 1402.0;     // Steuerungskennung SINUMERIK 810/820
S     M 67.2;             // S-Kommando senden
SPBN  ORI4;
L     DB20.DBW 186;       // H-Wort
DTR   ;                   // in Float wandeln
L     +60E+00;
/R    ;                   // in U/s umrechnen
T     DB20.DBD 564;       // S-Wert [U/s] Spindel 1
ORI4: NOP   0;
NETWORK
TITLE =Sonderbehandlung Kommando S-Ueberspeichern
U(    ;
L     DB20.DBW 594;       // Spindelnr. fuer Ueberspeichern
L     1;                  // Nr. der AWZ-Spindel
==I   ;
)     ;
U     M 110.5;            // AWZ Rundachse
U     DB20.DBX 2.3;       // Kommando S-Ueberspeichern
S     M 67.2;             // S-Kommando senden
R     DB20.DBX 2.3;       // Kommando S-Ueberspeichern
SPBN  nueb;
L     DB20.DBD 160;       // S-Wert Ueberspeichern [U/s]
T     DB20.DBD 564;       // S-Wert [U/s] Spindel 1
nueb: NOP   0;
// AWZ Drehzahl im AC erst im Spindelbetrieb aktivieren
U(    ;
O     M 67.3;             // HM AWZ Start RL als Spindel
O     M 67.4;             // HM AWZ Start LL als Spindel
)     ;
U     M 67.2;             // S-Kommando senden
S     DB20.DBX 1.4;       // NC-Satz abschicken
S     DB20.DBX 549.5;     // S-Wert fuer Spindel 1
O     DB20.DBX 549.5;     // S-Wert fuer Spindel 1
ON    M 66.0;             // AWZ Spindel ein
R     M 67.2;             // S-Kommando senden
NETWORK
TITLE = JOG-Tasten AWZ
U     M 100.2;            // Taste C+
U     M 104.1;            // Taste Spindel Halt
U     M 155.5;            // BA JOG
U     M 110.5;            // AWZ Rundachse
=     DB25.DBX 11.3;      // JOG Achse B+
U     M 102.4;            // Taste C-
U     M 104.1;            // Taste Spindel Halt
U     M 155.5;            // BA JOG
U     M 110.5;            // AWZ Rundachse
=     DB25.DBX 13.3;      // JOG Achse B-
O     DB25.DBX 11.3;      // JOG Achse B+
O     DB25.DBX 13.3;      // JOG Achse B-
=     M 66.6;             // AWZ Achse Joggen
FP    DB25.DBX 1.2;       // JOG Anforderung start
SPB   NJOG;
O     DB25.DBX 11.3;      // JOG Achse B+
O     DB25.DBX 13.3;      // JOG Achse B-
FN    DB25.DBX 1.2;       // JOG Anforderung ende
NJOG: NOP   0;
NETWORK
TITLE = VSH/EFG AWZ
U     DB3.DBX 2.6;        // BM7222 Spindelhalt manuell
U(    ;
O     M 111.7;            // Prg. laeuft K1
O     M 111.6;            // Programm unterbrochen
)     ;
O     M 67.1;             // VSH/EFG AWZ Positionieren
=     M 147.7;            // EFG/VSH AWZ
NETWORK
TITLE = RFG AWZ
U     M 137.7;            // Antriebe ein
=     M 208.2;            // RFG AWZ 611U
O(    ;
U     M 137.7;            // Antriebe ein
U     M 201.2;            // BB AWZ 611U
)     ;
O(    ;
U     DB1.DBX 1460.2;     // NC-RFG Achse 2
UN    DB1.DBX 1356.2;     // AWZ steht NC
)     ;
=     DB1.DBX 1460.2;     // NC-RFG Achse 2
NETWORK
TITLE = FM6108 Stoerung AWZ
U     T 42;               // Antriebe ein verzoegert
UN    M 201.2;            // BB AWZ 611U
S     DB2.DBX 3.0;        // FM6108 Stoerung AWZ
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 3.0;
END_FUNCTION
//**************************************************************************
FUNCTION FC 75 : VOID
TITLE=WZW_SMA
NAME: WZW_SMA
BEGIN
NETWORK
TITLE = SPS/OB-->WERKZEUGW. AKTIV
SET;
=   DB20.DBX 382.0; // SPS/RM-->WERKZEUGW. AKTIV
NETWORK
TITLE =Anforderung Wender Referenzieren
O(    ;
U     M 248.5;            // Alarmbit 0
U     M 248.6;            // Alarmbit 1
U     M 248.7;            // Alarmbit 2
)     ;
O(    ;
UN    M 25.1;             // HM BM7257 Werkzeugwender schwenkt
UN    E 6.3;              // WZW verriegelt
)     ;
O(    ;
U     M 90.3;             // WZW Ref. aktiv
UN    DB100.DBX 37.0;     // HM WZW referenzieren
)     ;
O     M 110.2;            // Grst. OB1
S     DB100.DBX 37.0;     // HM WZW referenzieren
FP    DB100.DBX 37.4;     // Fl. WZW ref. setzen
SPBN  M021;
L     0;
T     DB100.DBW 31;       // WZW Sollpos. aus SW
M021: NOP   0;
U     DB100.DBX 37.3;     // WZW ref. aktiv
U     E 6.3;              // WZW verriegelt
R     DB100.DBX 37.0;     // HM WZW referenziert
SPBN  M022;
L     1;                  // Pos. 1 nach Ref.
T     DB100.DBW 31;       // WZW Sollpos. aus SW
M022: NOP   0;
ON    DB100.DBX 37.0;     // HM WZW referenzieren
O     DB100.DBX 37.4;     // Fl. WZW ref. setzen
O     M 102.7;            // Reset Taste
R     DB100.DBX 37.2;     // WZW ref. Start
R     DB100.DBX 37.3;     // WZW ref. aktiv
NETWORK
TITLE =BM7211 Refpunkt Wender fehlt
U     DB100.DBX 37.0;     // HM WZW referenzieren
U     M 136.0;            // AUX-ON
U     E 6.2;              // Tuer zu, Antriebe ein
UN    M 137.3;            // Referenzpunkt nicht erreicht
UN    DB3.DBX 2.2;        // BM7218 Tuer offen
=     DB3.DBX 1.3;        // BM7211 Refpunkt Wender fehlt
NETWORK
TITLE =Refpunkt Wender fehlt
U     DB100.DBX 37.0;
U(    ;
O     M 155.1;            // BA AUTO
O     M 155.4;            // BA MDA
)     ;
U(    ;
O     M 154.3;            // Fl. NC-Start Taste
O     M 111.7;            // Programm laeuft
)     ;
S     DB2.DBX 1.4;        // FM6203 Refpunkt Wender fehlt
U(    ;
O     M 102.7;            // Reset-Taste
O     M 155.5;            // BA JOG
)     ;
R     DB2.DBX 1.4;        // FM6203 Refpunkt Wender fehlt
NETWORK
TITLE = WZW referenzieren starten
O     M 105.2;            // WZW Taste
O     M 90.3;             // WZW Ref. aktiv
U     M 155.2;            // BA REF
U     DB100.DBX 37.0;     // HM WZW referenzieren
S     DB100.DBX 37.2;     // WZW ref. Start
FP    DB100.DBX 37.6;     // Fl. ref. Start
U     DB100.DBX 37.2;
UN    E 6.3;              // WZW verriegelt
S     DB100.DBX 37.3;     // WZW ref. aktiv
NETWORK
TITLE = WZW Startkriterien Normalbetrieb
U     M 105.2;            // WZW Taste
FP    DB100.DBX 37.5;     // Fl. WZW Taste
U(    ;
O     DB20.DBX 182.0;     // T-Aenderung
O(    ;
U     M 155.5;            // BA JOG
U     DB100.DBX 37.5;     // Fl. WZW Taste
UN    M 146.3;            // VSH/EFG WZW
)     ;
)     ;
UN    M 102.7;            // Reset-Taste
U     M 137.7;            // Antriebe ein
UN    DB100.DBX 37.0;     // WZW referenzieren
S     DB100.DBX 35.0;     // WZW Start angefordert
=     DB100.DBX 35.1;     // HM WZW Start angefordert
NETWORK
TITLE = Berechnung der neuen Zielposition
UN    DB100.DBX 35.0;     // WZW Start angefordert
SPB   M052;
U     DB20.DBX 182.0;     // T-Aend.
SPB   M051;
// Zielpos. bei manuellem Schwenk:
U     M 105.2;            // WZW Taste
U     M 107.4;            // Taste AUX-ON
SPB   M053;
L     DB100.DBW 31;       // WZW Istpos. aus SW
L     1;
+I    ;
T     DB100.DBW 31;       // WZW Istpos. aus SW
L     DB100.DBW 31;       // WZW Istpos. aus SW
L     12;
<=I   ;
SPB   M052;
L     1;
T     DB100.DBW 31;
SPA   M052;
M053: NOP   0;
L     DB100.DBW 31;       // WZW Istpos. aus SW
L     1;
-I    ;
T     DB100.DBW 31;       // WZW Istpos. aus SW
L     DB100.DBW 31;       // WZW Istpos. aus SW
L     0;
<>I   ;
SPB   M052;
L     12;
T     DB100.DBW 31;
SPA   M052;
M051: NOP   0;
// T-Wort gueltig?
L     DB20.DBW 184;       // T-Wort
L     1;
<I    ;
R     DB100.DBX 35.0;
SPB   M052;
L     DB20.DBW 184;       // T-Wort
L     12;
>I    ;
R     DB100.DBX 35.0;
SPB   M052;
// muss geschwenkt werden?
L     DB20.DBW 184;       // T-Wort
L     DB100.DBW 31;       // WZW Istpos. SW
==I   ;
R     DB100.DBX 35.0;
SPB   M052;
// gueltiges T-Wort uebernehmen
L     DB20.DBW 184;       // T-Wort
T     DB100.DBW 31;       // WZW Istpos. SW
M052: NOP   0;
NETWORK
TITLE = BM falsche WZ-Position
//      UN    DB100.DBX 35.0;     // WZW Start angefordert
//      U     DB100.DBX 35.1;     // HM WZW Start angefordert
//      S                         // BM falsche WZ-Position
NETWORK
TITLE = Abwahl Rilo mit M50
O     DB100.DBX 35.1;     // HM WZW Start angefordert
O     M 102.7;            // Reset-Taste
ON    M 137.7;            // Antriebe ein
R     DB100.DBX 35.2;     // Schwenk mit M50
U     DB20.DBX 198.2;     // M50 dyn.
U     DB20.DBX 182.0;     // T-Aend.
S     DB100.DBX 35.2;     // Schwenk mit M50
NETWORK
TITLE =Betriebsart Bit MODE01
// V1.00: Mode-Wechsel geändert (Wender erkannte Mode 'Reset')
// V1.03: für EK600 geändert
O(    ;
UN    DB100.DBX 35.2;     // Schwenk mit M50
U     M 136.0;            // AUX-ON
UN    M 102.7;            // Reset-Taste
)     ;
O(    ;
U     DB100.DBX 35.0;     // WZW Start angefordert
U     DB100.DBX 35.1;     // HM WZW Start angefordert
)     ;
O     DB100.DBX 87.7;     // EK600 IB-Bit
=     M 211.7;            // WZW Bit Mode 1
NETWORK
TITLE =Betriebsart Bit MODE02
// V1.00: Mode-Wechsel geändert (Wender erkannte Mode 'Reset')
// V1.03: für EK600 geändert
U(    ;
O(    ;
U     DB100.DBX 35.2;     // Schwenk mit M50
U     M 136.0;            // AUX-ON
UN    M 102.7;            // Reset-Taste
)     ;
O(    ;
U     DB100.DBX 35.0;     // WZW Start angefordert
U     DB100.DBX 35.1;     // HM WZW Start angefordert
)     ;
)     ;
UN    DB100.DBX 87.7;     // EK600 IB-Bit
=     M 210.0;            // WZW Bit Mode 2
NETWORK
TITLE =Betriebsart Bit MODE03
// V1.03: Mode 3 für EK600 neu
U      M 136.0;            // AUX-ON
UN     M 102.7;            // Reset-Taste
U      DB100.DBX 87.7;     // EK600 IB-Bit
=      M 214.1;            // WZW Bit Mode 3
NETWORK
TITLE = EK600 IB-Bit
// V1.03: NW neu
O     M 110.2;            // Grst.
ON    DB10.DBX 52.0;      // Sauter EK600
R     DB100.DBX 87.7;     // EK600 IB-Bit
U     DB100.DBX 87.7;     // EK600 IB-Bit
BEB;
NETWORK
TITLE = Ausgaenge fuer Sollposition
U     DB100.DBX 31.0;
=     M 211.0;
U     DB100.DBX 31.1;
=     M 211.1;
U     DB100.DBX 31.2;
=     M 211.2;
U     DB100.DBX 31.3;
=     M 211.3;
NETWORK
TITLE =Paritybit berechnen und vorgeben
O(    ;
U     M 211.0;
UN    M 211.1;
UN    M 211.2;
UN    M 211.3;
)     ;
O(    ;
UN    M 211.0;
U     M 211.1;
UN    M 211.2;
UN    M 211.3;
)     ;
O(    ;
UN    M 211.0;
UN    M 211.1;
U     M 211.2;
UN    M 211.3;
)     ;
O(    ;
U     M 211.0;
U     M 211.1;
U     M 211.2;
UN    M 211.3;
)     ;
O(    ;
UN    M 211.0;
UN    M 211.1;
UN    M 211.2;
U     M 211.3;
)     ;
O(    ;
U     M 211.0;
U     M 211.1;
UN    M 211.2;
U     M 211.3;
)     ;
=     M 211.4;            // WZW Parity
NETWORK
TITLE =Start vorgeben (PSTART)
O     DB100.DBX 35.0;     // WZW Start angefordert
O     DB100.DBX 37.6;     // Fl. ref. Start
S     DB100.DBX 35.3;     // HM WZW Start
R     DB100.DBX 35.0;     // WZW Start angefordert
U     DB100.DBX 35.3;     // HM WZW Start
U     M 66.7;             // AWZ Kuppelpos. erreicht
S     DB100.DBX 37.1;     // WZW Start ausgeben
U     T 118;              // Startimpuls verz.
U     T 119;              // min. Impulslaenge
UN    E 6.3;              // WZW verriegelt
O     M 102.7;            // Reset Taste
ON    M 137.7;            // Antriebe ein
R     DB100.DBX 35.3;     // HM WZW Start
R     DB100.DBX 37.1;     // WZW Start ausgeben
U     DB100.DBX 37.1;     // WZW Start ausgeben
L     S5T#60MS;           // Startimpuls verzoegert
SE    T 118;
U     T 118;
L     S5T#100MS;          // Min. Impulslaenge
SE    T 119;
=     M 211.6;            // WZW Start Signal
NETWORK
TITLE = BM7257 Werkzeugwender schwenkt
UN    DB100.DBX 37.0;     // HM WZW referenzieren
U     DB100.DBX 35.3;     // HM WZW Start
S     M 25.1;             // HM BM7257 Werkzeugwender schwenkt
U     E 6.3;              // WZW verriegelt
FP    DB100.DBX 37.7;     // Fl. WZW verriegelt
O     DB100.DBX 37.7;     // Fl. WZW verriegelt
O     DB100.DBX 1.0;      // VSH/EFG WZW Alarm
O     M 102.7;            // Reset-Taste
ON    M 136.0;            // AUX-ON
R     M 25.1;             // HM BM7257 Werkzeugwender schwenkt
U     M 25.1;             // HM BM7257 Werkzeugwender schwenkt
U(    ;
UN    DB1.DBX 1402.1;     // Fanuc 0TB
UN    DB1.DBX 1403.0;     // Fanuc 21TB
O     DB2.DBX 6.7;        // FM6215 Zeitueberwachung Hilfsfunktion
)     ;
=     DB3.DBX 7.1;        // BM7257 Werkzeugwender schwenkt
NETWORK
TITLE =FM6216 WZW1 Spannungsversorgung fehlt
U     M 248.5;
UN    M 248.6;
UN    M 248.7;
S     DB2.DBX 7.0;        // FM6216 WZW1 Spannungsversorgung fehlt
U     M 102.7;            // Reset-Taste
R     DB2.DBX 7.0;        // FM6216 WZW1 Spannungsversorgung fehlt
NETWORK
TITLE =FM6217 WZW1 ueberspannung
UN    M 248.5;
U     M 248.6;
UN    M 248.7;
S     DB2.DBX 7.1;        // FM6217 WZW1 ueberspannung
U     M 102.7;            // Reset Taste
R     DB2.DBX 7.1;        // FM6217 WZW1 ueberspannung
NETWORK
TITLE =FM6218 WZW1 ueberstrom Servomodul
U     M 248.5;
U     M 248.6;
UN    M 248.7;
S     DB2.DBX 7.2;        // FM6218 WZW1 ueberstrom Servomodul
U     M 102.7;            // Reset-Taste
R     DB2.DBX 7.2;        // FM6218 WZW1 ueberstrom Servomodul
NETWORK
TITLE =FM6219 WZW1 Fehler bei Entriegelung
UN    M 248.5;
UN    M 248.6;
U     M 248.7;
S     DB2.DBX 7.3;        // FM6219 WZW1 Fehler bei Entriegelung
U     M 102.7;            // Reset-Taste
R     DB2.DBX 7.3;        // FM6219 WZW1 Fehler bei Entriegelung
NETWORK
TITLE =FM6220 WZW1 Fehler bei Verriegelung
U     M 248.5;
UN    M 248.6;
U     M 248.7;
S     DB2.DBX 7.4;        // FM6220 WZW1 Fehler bei Verriegelung
U     M 102.7;            // Reset-Taste
R     DB2.DBX 7.4;        // FM6220 WZW1 Fehler bei Verriegelung
NETWORK
TITLE = FM6221 WZW1 Fehler beim Referieren
UN    M 248.5;
U     M 248.6;
U     M 248.7;
S     DB2.DBX 7.5;        // FM6221 WZW1 Fehler beim Referieren
U     M 102.7;            // Taste Reset
R     DB2.DBX 7.5;        // FM6221 WZW1 Fehler beim Referieren
NETWORK
TITLE = FM6222 WZW1 Kein Referenzpunkt erreicht/Zeitueberwachung
U     M 248.5;
U     M 248.6;
U     M 248.7;
S     DB2.DBX 7.6;        // FM6222 Kein Refpunkt err./Zeitueberw.
U     M 102.7;            // Reset-Taste
R     DB2.DBX 7.6;        // FM6222 Kein Refpunkt err./Zeitueberw.
NETWORK
TITLE =VSH/EFS bei Fehler Wender
O     DB2.DBX 7.0;        // FM6216
O     DB2.DBX 7.1;        // FM6217
O     DB2.DBX 7.2;        // FM6218
O     DB2.DBX 7.3;        // FM6219
O     DB2.DBX 7.4;        // FM6220
O     DB2.DBX 7.5;        // FM6221
O     DB2.DBX 7.6;        // FM6222
=     DB100.DBX 1.0;      // VSH/EFG WZW Alarm
=     M 142.0;            // NC-Stop WZW-Alarm
NETWORK
TITLE =EFS bei Schwenk Wender
O     M 25.1;             // HM BM7257 Werkzeugwender schwenkt
ON    E 6.3;              // WZW verriegelt
UN    M 155.2;            // BA REF
=     M 146.3;            // VSH/EFG WZW
NETWORK
TITLE =Schneller Schwenk
U     M 111.7;            // Programm laeuft
UN    DB20.DBX 324.2;     // DRY aktiv
UN    DB20.DBX 324.7;     // SBL aktiv
=     M 211.5;            // WZW schnell schwenken
NETWORK
TITLE =LED WZW-Taste
U     DB100.DBX 37.0;     // HM WZW referenzieren
U     M 136.0;            // AUX-ON
U     M 110.7;            // Blinktakt
O     M 25.1;             // HM BM7257 Werkzeugwender schwenkt
=     DB20.DBX 848.3;     // LED WZW-Taste
END_FUNCTION
//**************************************************************************
FUNCTION FC 80 : VOID
TITLE=HILFSANTRIEBE
NAME: AUX
BEGIN
NETWORK
TITLE = Aux Off Meldungen
U(
L     DB2.DBD 2;          // Aux Off Fehlermeldungen
L     L#0;
==D   ;
)
U(
L     DB2.DBD 12;          // Aux Off Fehlermeldungen
L     L#0;
==D   ;
)
=     M 177.2;            // HM Aux Off Alarme
NETWORK
TITLE = Verzoegerung Aux On
U     M 107.4;            // Taste Aux On
UN    M 107.6;            // Taste Aux Off
L     S5T#700MS;
SE    T 44;
NETWORK
TITLE = FM6008 Fehlender CAN-Teilnehmer
// V0.17: 3.Modul ueberwachen (abh. von PLC-Bit)
// V1.00: bei ACC nicht überprüfen
O(    ;
U     DB1.DBX 1414.2;     // 3. CAN Eingangsport
U     DB1.DBX 1415.2;     // 3. CAN Ausgangsport
)     ;
ON    DB10.DBX 50.0;      // USER_DATA_HEX[10]: 3.CAN-Modul vorh.
=     M 177.0;            // 3. CAN-Modul ok
U(    ;
ON    M 177.0;            // 3. CAN-Modul ok
ON    DB1.DBX 1414.0;     // 1. CAN Eingangsport
ON    DB1.DBX 1414.1;     // 2. CAN Eingangsport
ON    DB1.DBX 1415.0;     // 1. CAN Ausgangsport
ON    DB1.DBX 1415.1;     // 2. CAN Ausgangsport
)     ;
UN    DB10.DBX59.0;       // USER_DATA_HEX[19]: ACC
=     DB2.DBX 1.0;        // FM6008 Fehlender CAN-Teilnehmer
NETWORK
TITLE = Auswertung fataler SPS Fehler
L     DB2.DBB 0;          // fatale SPS Fehler
L     0;
==I   ;
=     M 177.3;            // HM SPS fatale Fehler
NETWORK
TITLE = Aux On
// V0.17: Robotik neu
O(
U     M 190.0;            // Rob. Aux-On
U     M 191.0;            // Robotik aktiv
)
O(
U     DB1.DBX 1382.4;     // DNC Aux-On
U     DB1.DBX 1366.0;     // DNC-BETRIEB AKTIV
)
O     T 44;
S     M 136.0;            // AUX-ON
NETWORK
TITLE = Verzoegerung autom. Aux Off
UN    M 107.4;            // Taste Aux on
U     M 155.1;            // BA Auto
U     M 137.7;            // Antriebe ein
UN    M 111.7;            // Prg. laeuft
UN    M 136.7;            // Spindel laeuft verz.
UN    M 136.4;            // Summe Fahrbefehl
UN    M 111.5;            // M00/M01 aktiv
UN    DB10.DBX 40.4;      // USER_DATA_HEX[0] kein autom. Aux Off
L     S5T#600S;
SE    T 24;
NETWORK
TITLE = Aux Off
O(    ;
U     T 24;               // Verz. autom. AUX-OFF
U     M 136.0;            // AUX-On
)     ;
O(    ;
U     M 107.6;            // AUX-OFF Taste
UN    M 111.7;            // Progr. laeuft K1
UN    M 136.4;            // Summe Fahrbefehl
UN    M 136.7;            // Spindel laeuft verz.
U     DB1.DBX 1356.2;     // AWZ steht NC
)     ;
O     DB2.DBX 1.0;        // FM6008 Fehlender CAN-Teilnehmer
O(
U     DB1.DBX 1382.3;     // DNC Aux-Off
U     DB1.DBX 1366.0;     // DNC-BETRIEB AKTIV
)
O     DB1.DBX 1370.1;     // Fataler NC-Fehler steht an
ON    E 7.0;              // Not-Aus Taste
ON    M 177.2;            // HM AUX OFF Alarme
ON    M 177.3;            // HM fatale SPS Fehler
R     M 136.0;            // AUX-On
NETWORK
TITLE = Zeitverz. Aux off
U     M 136.0;            // Aux on
L     S5T#1S;
SA    T 36;               // Ausschaltverz. Aux on
NETWORK
TITLE =Zeitverz. Aux off
U     M 136.0;            // Aux on
L     S5T#2S;
SA    T 35;               // Ausschaltverz. Aux on
NETWORK
TITLE = Flanke Aux On
U     M 136.0;            // Aux On
UN    A 4.0;              // Leistung Hilfsbetriebe ein / Aux on
=     M 136.1;            // Flanke Aux On
NETWORK
TITLE = Leistung Hydraulik
U     M 136.0;            // Aux On
=     A 4.0;              // Leistung Hilfsbetriebe ein / Aux on
NETWORK
TITLE = Shutdown der Oberflaeche verhindern
U     M 136.0;            // Aux On
UN    DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
R     DB1.DBX 1440.1;     // Shutdown verhindern
NETWORK
TITLE = Schwimmerschalter Hydraulik
UN    E 4.0;              // Schwimmerschalter Hydraulik
S     DB2.DBX 2.2;        // FM6102 Schwimmerschalter Hydraulik
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 2.2;        // FM6102 Schwimmerschalter Hydraulik
NETWORK
TITLE = BM7208 "Aux Off"
// V1.06: LED leuchtet bei Aux-On
UN    M 136.0;            // Aux On
=     DB3.DBX 1.0;        // BM7208 "Aux Off"
U     M 136.0;            // Aux On
=     DB1.DBX 1390.2;     // glob. Rueckmeldung Aux-On
O     M 110.7;            // Blinktakt
O     M 136.0;            // Aux On
=     DB20.DBX 852.6;     // LED Aux-On Taste
NETWORK
TITLE = FM6101 "Thermische Stoerungen"
U(    ;
ON    M 136.4;            // Summe Fahrbefehle
ON    M 146.3;            // EFG/VSH WZW schwenkt
)     ;
U(    ;
UN    M 111.7;            // Programm laeuft
UN    E 5.7;              // Therm. Stoerung Stop nach Programmende
ON    E 5.6;              // Therm. Stoerung sofort
)     ;
S     DB2.DBX 2.1;        // FM6101 Therm. Stoerungen
U     M 138.0;            // Fl. Reset-Taste
R     DB2.DBX 2.1;
END_FUNCTION
//**************************************************************************
FUNCTION FC 81 : VOID
TITLE=SCHMIERUNG
NAME: SM
BEGIN
NETWORK
TITLE = Schmierzeit ueberwachen
U(    ;
L     DB100.DBW 64;       // Zaehlerwert Schmiermittel
L     DB10.DBW 2;         // USER_DATA_INT[1] Pausenzeit SM
>=I   ;
)     ;
UN    M 163.0;            // SM ein
=     M 163.1;            // Schmierzeit erreicht
NETWORK
TITLE = Schmierweg Linearachse erfassen
U     DB1.DBX 1422.0;     // Wegimpuls Achse 0
ZV    Z 0;                // Meterzaehler Achse 0
U     DB1.DBX 1422.1;     // Wegimpuls Achse 1
ZV    Z 1;                // Meterzaehler Achse 1
O(    ;
L     Z 0;                // Meterzaehler Achse 0
L     DB10.DBW 38;        // USER_DATA_INT[19]: Schmierweg [m]
>=I   ;
)     ;
O(    ;
L     Z 1;                // Meterzaehler Achse 1
L     DB10.DBW 38;        // USER_DATA_INT[19]: Schmierweg [m]
>=I   ;
)     ;
=     M 163.5;            // Schmierweg erreicht
R     Z 0;
R     Z 1;
R     Z 2;
NETWORK
TITLE = Schmiermittel ein
U     M 107.4;            // Taste AUX-ON
FP    M 131.3;
O(    ;
U     M 131.3;            // Fl. AUX-ON Taste
U     M 136.0;            // AUX-ON
UN    E 4.1;              // DS SM
)     ;
O(    ;
U     M 163.1;            // Schmierzeit erreicht
UN    DB10.DBX 40.2;      // USER_DATA_HEX[0] wegabh. Schmierung
UN    DB2.DBX 6.3;        // FM6211 Schmierueberwachung
)     ;
O(    ;
U     M 163.5;            // Schmierweg erreicht
U     DB10.DBX 40.2;      // USER_DATA_HEX[0] wegabh. Schmierung
UN    DB2.DBX 6.3;        // FM6211
)     ;
O     M 136.1;            // Fl. AUX-ON
S     M 163.0;            // SM ein
R     DB2.DBX 6.3;        // FM6211
R     M 146.0;            // VSH/EFG SM
FP    M 163.3;            // Fl. SM ein
NETWORK
TITLE =Zaehler Schmiermittel
U(    ;
U     M 136.4;            // Summe Fahrbefehle
UN    DB10.DBX 40.2;      // USER_DATA_HEX[0] wegabh. Schmierung
O     M 163.0;            // SM ein
)     ;
U     M 110.7;            // 1 HZ
FP    M 163.4;            // Fl. SM Zaehler
UN    M 163.4;            // Fl. SM Zaehler
SPB   M041;
L     DB100.DBW 64;       // Zaehler SM
L     1;
+I    ;
T     DB100.DBW 64;
M041: NOP   0;
UN    M 163.3;            // Fl. SM ein
SPB   M042;
L     0;
T     DB100.DBW 64;       // Zaehler SM
T     DB100.DBW 66;       // Zaehler SM-Fehler
M042: NOP   0;
NETWORK
TITLE = Zeitueberwachung SM-Pumpe
U(    ;
L     DB100.DBW 64;       // Zaehler SM
L     DB10.DBW 0;         // USER_DATA_INT[0] Zeitueberwachung SM
>=I   ;
)     ;
U     M 163.0;            // SM ein
=     M 163.2;            // HM Zeitueberwachung
NETWORK
TITLE = FM6211 "Schmierueberwachung"
U(    ;
U     E 4.1;              // DS SM
UN    A 4.7;              // Lstg.Schmiermittelpumpe
ON    E 4.2;              // Schwimmerschalter
O     M 163.2;            // HM Zeitueberwachung
)     ;
U     M 163.0;            // SM ein
S     DB2.DBX 6.3;        // FM 6211
NETWORK
TITLE =Schmiermittel ein ruecksetzen
O     DB2.DBX 6.3;        // FM6211
O     E 4.1;              // DS SM
ON    M 136.0;            // Aux On
R     M 163.0;            // SM ein
NETWORK
TITLE = Leistung Schmiermittelpumpe
U     M 163.0;            // SM ein
U     E 5.7;              // Therm. Stoerung Programmende
=     A 4.7;              // Lstg. Schmiermittelpumpe
=     DB20.DBX 311.4;     // Rueckmeldung SM
NETWORK
TITLE =Zaehler EFG/VSH FM6211
U     M 111.7;            // Programm laeuft
U     DB2.DBX 6.3;        // FM6211
U     M 110.7;            // 1Hz
FP    M 163.6;            // Fl. Fehlerzeit
UN    M 163.6;            // Fl. Fehlerzeit
SPB   M091;
L     DB100.DBW 66;       // Zaehler SM-Fehler
L     1;
+I    ;
T     DB100.DBW 66;
M091: NOP   0;
NETWORK
TITLE =Zeitueberwachung SM-Fehler
L     DB10.DBW 2;         // USER_DATA_INT[1] Pausenzeit SM
L     2;
/I    ;
L     DB100.DBW 66;       // Zaehler SM-Fehler
<=I   ;
S     M 146.0;            // EFG/VSH FM700103
END_FUNCTION
//**************************************************************************
FUNCTION FC 82 : VOID
TITLE=WARNLAMPE
NAME: WARNL
BEGIN
NETWORK
TITLE = Abbruch
UN    DB10.DBX 43.7;      // USER_DATA_HEX[3]: Warnlampe 3fach
BEB   ;
NETWORK
TITLE = PLC-Alarme
U     M 110.1;            // "1"
R     DB100.DBX 290.0;    // HM PLC-Alarm
L     DB2.DBD 0;
L     0;
<>D;
S     DB100.DBX 290.0;    // HM PLC-Alarm
L     DB2.DBD 4;
L     0;
<>D;
S     DB100.DBX 290.0;    // HM PLC-Alarm
L     DB2.DBD 8;
L     0;
<>D;
S     DB100.DBX 290.0;    // HM PLC-Alarm
NETWORK
TITLE = Warnlampe rot progr.
U     M 110.1;            // "1"
R     DB100.DBX 290.1;    // Warnl. rot progr.
NETWORK
TITLE = Warnlampe ROT ... Fehler
U(    ;
ON    E 7.0;              // Not-Aus
O     DB100.DBX 290.0;    // HM PLC-Alarm
O     DB100.DBX 290.1;    // Warnl. rot progr.
O     DB1.DBX 1366.3;     // NC-Alarm steht an
)     ;
U     M 110.7;            // Blinktakt
=     M 212.1;            // Warnlampe rot
NETWORK
TITLE = Warnlampe GRUEN ... Automatik
U     M 155.1;            // BA AUTO
U(    ;
O     M 110.7;            // Blinktakt
O     M 111.7;            // Progr. laeuft
)     ;
U     M 136.0;            // AUX-ON
=     M 212.2;            // Warnlampe gruen
NETWORK
TITLE = Warnlampe WEISS ... Beladung
U(    ;
O     M 5.3;              // STV/LM aktiv
O     M 5.4;              // Handling aktiv
)     ;
UN    M 155.7;            // 1x aktiv
=     M 212.3;            // Warnlampe weiss
END_FUNCTION
//**************************************************************************
FUNCTION FC 83 : VOID
TITLE=AUTO_NC_START
NAME: AUT_NCST
BEGIN
NETWORK
TITLE = M32 gespeichert setzen
U     DB20.DBX 196.0;     // M32 dyn.
UN    M 201.6;            // Schluesselschalter H/A
S     M 174.7;            // M32 gespeichert
NETWORK
TITLE = M32 gespeichert ruecksetzen
O     M 154.1;            // Impuls Programm laeuft
O(    ;
U     M 148.4;            // NC-Start LM/STV/Dauerlauf
UN    M 148.3;            // keine Start Verriegelung
)     ;
O     DB20.DBX 828.6;     // Reset-Taste MSTT
ON    M 136.0;            // AUX-ON
O     M 137.1;            // Einrichte BA
ON    M 155.1;            // BA AUTO
O     M 155.7;            // 1x aktiv
O     DB3.DBX 5.3;        // BM7043 "Sollstueckzahl erreicht"
R     M 174.7;            // M32 gespeichert
NETWORK
TITLE = Programm-laeuft abfallverzoegern
U     M 111.7;            // Programm laeuft
L     S5T#500MS;
SA    T 30;               // Progr.-laeuft abfallverz.
NETWORK
TITLE = NC-Start LM/STV/Dauerlauf
U     M 174.7;            // M32 gespeichert
UN    T 30;               // Progr.-laeuft abfallverz.
=     M 148.4;            // NC-Start LM/STV/Dauerlauf
NETWORK
TITLE = Programm-Ende an NC zuruekmelden
O     DB20.DBX 192.2;     // M02 dyn.
O     DB20.DBX 195.6;     // M30 dyn.
=     DB20.DBX 348.1;     // M30 an NC; Progr. laeuft wird zurueckgesetzt
NETWORK
TITLE = Quittierung BM7043 Sollstueckz. erreicht
U     M 138.0;            // Fl. Reset-Taste
R     DB3.DBX 5.3;        // BM7043 Sollstueckz. erreicht
END_FUNCTION
//**************************************************************************
FUNCTION FC 84 : VOID
TITLE=NC_START_STOP
NAME: NC_START
BEGIN
NETWORK
TITLE = NC-Stop
O(    ;
U(    ;
O     M 142.0;            // NC-Stop WZW-Alarm
O     M 142.1;            // NC-Stop Zeitueberw. EFG
ON    M 136.0;            // Aux-On
O     M 104.7;            // Taste NC-Stop
O     M 201.6;            // SS Hand/Auto
O     DB20.DBX 192.0;     // M00 dyn.
O     DB3.DBX 1.1;        // BM7209 Referenzpunkt nicht erreicht
O(    ;
U     DB20.DBX 192.1;     // M01 dyn.
U     DB20.DBX 324.5;     // Opt. Stop aktiv
)     ;
)     ;
U     M 111.7;            // Prog. laeuft K1
)     ;
O     DB20.DBX 0.1;       // NC-Stop von PC-Tastatur
=     DB20.DBX 0.1;       // NC-Stop Kommando K0
NETWORK
TITLE = HM3 NC-Start
L     MB 145;             // Startverriegelungen
L     0;
==I   ;
=     M 148.3;            // HM keine Startverr.
NETWORK
TITLE = HM2 NC-Start
O     M 155.5;            // BA Jog
O     M 148.3;            // HM keine Startverr.
=     M 148.1;            // HM  Freigabe NC-Start
NETWORK
TITLE = Flanke Taste NC-Start
U(    ;
U     M 201.5;            // Taste NC-Start extern
UN    DB10.DBX 41.1;      // USER_DATA_HEX[1] Türautomatik Landert angewählt
O     M 104.6;            // Taste NC-Start MSTT
)     ;
FP    M 154.3;            // Flanke Taste NC-Start
NETWORK
TITLE = NC-Start gesamt
O     M 154.3;            // Fl. NC-Start Taste
O     M 148.4;            // NC-Start LM/STV/Dauerlauf
O     M 148.5;            // NC-Start Tuerautomatik
O     M 190.3;            // Rob. NC-Start
O(    ;
U     DB1.DBX 1366.0;     // DNC aktiv
U     DB20.DBX 0.2;       // NC-Start Anforderung
)     ;
=     M 148.2;            // NC-Start gesamt
NETWORK
TITLE = Flanke Prg. laeuft
U     M 111.7;            // Prg. laeuft
FP    M 154.1;            // Flanke Prg. laeuft
NETWORK
TITLE = NC-Start
U     M 148.1;            // HM Freigabe NC-Start
U     M 136.0;            // Aux On
UN    DB2.DBX 6.3;        // FM6211 Schmierueberwachung
UN    DB3.DBX 0.4;        // BM7204 Vorschubhalt manuell
UN    DB3.DBX 1.1;        // BM7209 Referenzpunkt nicht erreicht
UN    DB3.DBX 1.2;        // BM7210 Referenzieren aktiv
UN    DB3.DBX 1.6;        // BM7214 WZW abgebrochen
UN    DB3.DBX 5.3;        // BM7043 Sollstueckzahl erreicht
UN    DB3.DBX 2.6;        // BM7222 Spindelhalt manuell
=     M 148.7             // Freigabe
U     M 148.7             // Freigabe
U     M 148.2;            // NC-Start gesamt
U(    ;
ON    T 30;               // Progr.-laeuft abfallverz.
O     M 111.6;            // Programm unterbrochen
O     DB20.DBX 325.1;     // Stop durch SBL
)     ;
=     DB20.DBX 3.6;       // NC-Start Kommando an Oberflaeche
U     DB20.DBX 0.2;       // FC NC-START
U     M 148.7             // Freigabe
=     DB20.DBX 0.2;       // FC NC-START
NETWORK
TITLE = NC-START/NEG.DNC-QUITTUNG
U   DB1.DBX 1366.0;      // DNC-BETRIEB AKTIV
U   DB20.DBX 0.2;        // FC NC-START
UN  DB20.DBX 3.6;        // NC-Start Kommando an Oberflaeche
=   DB20.DBX 319.0;      // NC-START/NEG.DNC-QUITTUNG
END_FUNCTION
//**************************************************************************
FUNCTION FC 85 : VOID
TITLE=PLC_NC_TRANSFER
NAME: PLC_NC_T
BEGIN
NETWORK
TITLE = Hilfsmerker Einlesesperre
L     MW 146;             // Merkerwort Einlesesperren
L     0;
==I   ;
=     M 177.5;            // HM Einlesefreigabe
NETWORK
TITLE = Einlesesperre FM
L     DB2.DBB1;
L     0;
==I   ;
=     DB100.DBX900.0;     // HM EFG Fehlermeldungen
NETWORK
TITLE = Einlesesperre BM
L     DB3.DBB0;
L     0;
==I   ;
=     DB100.DBX900.1;     // HM EFG Betriebsmeldungen
NETWORK
TITLE = Einlesesperre DB100
L     DB100.DBB1;         // Datenbyte VSH/EFG
L     0;
==I   ;
=     DB100.DBX900.2;     // HM EFG DB100.DBB1
NETWORK
TITLE = Einlesesperre
U(    ;
O(    ;
U     DB100.DBX900.0;     // HM EFG Fehlermeldungen
U     DB100.DBX900.1;     // HM EFG Betriebsmeldungen
U     DB100.DBX900.2;     // HM EFG DB100.DBB1
U     M 177.5;            // HM Einlesesperre
)     ;
O(    ;
U     M 107.4;            // Taste Aux On
U     M 155.5;            // BA JOG
)     ;
O     M 155.2;            // BA REF (sonst referenzieren nicht moeglich)
)     ;
UN    M 70.0;             // autom. Offsetabgleich aktiv
=     DB20.DBX 340.3;     // EFG Kanal 0
NETWORK
TITLE = Zeitueberw. Einlesefreigabe
UN    DB20.DBX 340.3;     // EFG Kanal 0
UN    T 114;
U     M 111.7;            // Programm laeuft
UN    DB3.DBX 0.4;        // BM7204 Vorschubhalt man.
UN    DB3.DBX 2.6;        // BM7222 Spindelhalt man.
U     M 110.7;            // 1Hz
ZV    Z 24;
U     DB20.DBX 340.3;     // EFG Kanal 0
R     Z 24;
L     Z 24;
T     DB100.DBW 310;
NETWORK
TITLE = FM6215 "Zeitueberw. Einlesesperre"
L     DB100.DBW 310;      // Zaehlerstand
L     DB10.DBW 8;         // USER_DATA_INT[4] Zeitueberw. EFG
==I   ;
S     DB2.DBX 6.7;
=     M 142.1;            // NC-Stop Zeitueberw. EFG
R     Z 24;               // FM6215 "Zeitueberw. Einlesesperre"
O     M 138.0;            // Fl. Reset-Taste
O     DB1.DBX 1370.2;     // Alarm-Quittiertaste gedrueckt
R     DB2.DBX 6.7;
NETWORK
TITLE = Unbedingtes VSH Tippen BA Auto
U     M 155.1;            // BA Auto
UN    M 137.7;            // Antriebe ein
UN    M 104.6;            // NC-Start Taste
UN    M 201.5;            // NC-Start Taste extern
=     M 142.4;            // Unbedingtes VSH Tippen BA Auto
NETWORK
TITLE = Flanke Taste 1x
U     M 101.7;            // Taste 1x
FP    M 153.3;            // Flanke Taste 1x
NETWORK
TITLE = 1x aktiv setzen
O(    ;
U     M 153.3;            // Flanke Taste 1x
UN    M 155.7;            // 1x aktiv
)     ;
O(    ;
UN    M 5.3;              // STV/LM aktiv
UN    M 5.4;              // Handling aktiv
)     ;
O     DB3.DBX 5.3;        // BM7043 Sollstueckzahl erreicht
O     M 110.2;            // Grundstellung OB1
O     DB1.DBX 1383.0;     // DNC 1x ein
S     M 155.7;            // 1x aktiv
S     M 176.7;            // HM 1x
NETWORK
TITLE = 1x aktiv rueksetzen
U     M 153.3;            // Flanke Taste 1x
UN    M 176.7;            // HM 1x
O     DB1.DBX 1382.7;     // DNC 1x aus
R     M 155.7;            // 1x aktiv
NETWORK
TITLE = Programmiertes 1x ruecksetzen
U     M 110.1;            // "1"
R     M 176.7;            // HM 1x
NETWORK
TITLE = BM7264 Einzelstueck
U     M 153.3;            // Flanke Taste 1x
U     M 155.7;            // 1x aktiv
U(    ;
O     M 5.3;              // STV/LM aktiv
O     M 5.4;              // Handling aktiv
)     ;
S     DB3.DBX 8.0;        // BM7264 Einzelstueck
R     DB3.DBX 8.1;        // BM7265 Dauerbetrieb
NETWORK
TITLE = BM7265 Dauerbetrieb
U(    ;
O     M 153.3;            // Flanke Taste 1x
O(    ;
U     M 154.3;            // Flanke Taste NC-Start
UN    DB20.DBX 324.7;     // SBL aktiv
U     M 155.1;            // BA Auto
)     ;
)     ;
UN    M 155.7;            // 1x aktiv
S     DB3.DBX 8.1;        // BM7265 Dauerbetrieb
R     DB3.DBX 8.0;        // BM7264 Einzelstueck
NETWORK
TITLE =3 sek
U(    ;
O     DB3.DBX 8.0;        // BM7264 Einzelstueck
O     DB3.DBX 8.1;        // BM7265 Dauerbetrieb
)     ;
UN    M 153.3;
L     S5T#3S;
SE    T 51;
NETWORK
TITLE = BM7264,BM7265 ruecksetzen
O     T 51;
ON    DB10.DBX 48.0;      // USER_DATA_HEX[8]: 1x Meldg. anzeigen
R     DB3.DBX 8.0;        // BM7264 Einzelstueck
R     DB3.DBX 8.1;        // BM7265 Dauerbetrieb
NETWORK
TITLE = LED 1x-Taste
U     M 155.7;            // 1x aktiv
=     DB20.DBX 847.3;     // LED 1x
=     DB1.DBX 1390.3;     // Rueckmeldung 1x aktiv
NETWORK
TITLE = DRY
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M171;
U     M 100.6;            // DRY Taste
UN    M  10.0;            // HM Fl. DRY
UN    M 111.5;            // M00/M01 aktiv
UN    M 111.6;            // Programm unterbrochen
UN    M 111.7;            // Programm laeuft
=     DB20.DBX 1.0;       // DRY Toggle
U     M 100.6;            // DRY Taste
=     M  10.0;            // HM Fl. DRY
M171: NOP   0;
U     DB20.DBX 324.2;     // DRY aktiv
=     DB20.DBX 846.1;     // LED DRY-Taste
NETWORK
TITLE = SKIP
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M181;
U     M 100.7;            // SKIP Taste
UN    M 10.1;             // HM Fl. SKIP
=     DB20.DBX 2.6;       // SKIP Toggle
U     M 100.7;            // SKIP Taste
=     M 10.1;             // HM Fl. SKIP
M181: NOP   0;
U     DB20.DBX 324.6;     // SKIP aktiv
=     DB20.DBX 846.0;     // LED SKIP-Taste
NETWORK
TITLE = M01
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M191;
U     M 101.6;            // OPT Taste
UN    M 10.2;             // HM Fl. OPT
=     DB20.DBX 3.1;       // OPT Toggle
U     M 101.6;            // OPT Taste
=     M 10.2;             // HM Fl. OPT
M191: NOP   0;
U     DB20.DBX 324.5;     // OPT aktiv
=     DB20.DBX 847.4;     // LED OPT-Taste
NETWORK
TITLE = SBL
U     DB10.DBX 49.0;      // USER_DATA_HEX[9]: Laborsimulator
SPB   M201;
U     M 102.6;            // SBL Taste
UN    M 10.3;             // HM Fl. SBL
=     DB20.DBX 0.5;       // SBL Toggle
U     M 102.6;            // SBL Taste
=     M 10.3;             // HM Fl. SBL
M201: NOP   0;
U     DB20.DBX 324.7;     // SBL aktiv
=     DB20.DBX 848.7;     // LED SBL-Taste
NETWORK
TITLE = PRT
// nicht bei PC-Maschine
NETWORK
TITLE =WKS/MKS
// nicht bei PC-Maschine
NETWORK
TITLE = Achsfreigabe Kanal 0
U(    ;
UN    DB20.DBX 340.3;     // EFG Kanal 0
UN    M 146.3;            // VSH/EFG WZW
O     DB100.DBX 22.0;     // VSH unbed. Tippen BA AUTO
O     M 142.4;            // VSH unbedingt tippen
ON    M 137.7;            // Antriebe ein
ON    E 6.2;              // Freig. Ausgabemodul
)     ;
NOT   ;
=     DB20.DBX 340.4;     // Achsfreigabe Kanal 0
NETWORK
TITLE = Spindelfreigabe HA Kanal 0, 1
U     M 208.3;            // RFG Spindel 611U
UN    M 110.4;            // Spindel im Rundachsbetrieb
UN    M 165.0;            // M19 aktiv
UN    M 25.0;             // HM BM7275 Spindelbremse aktiv
UN    DB20.DBX 328.0;     // Rundachsumschaltung von AC angefordert S0
=     DB20.DBX 340.5;     // Spindelfreigabe K0 Spindel 0
U     M 110.0;            // "0"
=     DB25.DBX 340.5;     // Spindelfreigabe K1 Spindel 0
U     M 110.4;            // Spindel im Rundachsbetrieb
R     DB20.DBX 328.0;     // Rundachsumschaltung angefordert S0
NETWORK
TITLE = Spindelfreigabe AWZ Kanal 0, 1
U     M 208.2;            // RFG AWZ 611U
UN    M 110.5;            // AWZ im Rundachsbetrieb
UN    M 111.5;            // M00/M01 aktiv
UN    DB20.DBX 328.1;     // Rundachsumschaltung von AC angefordert S1
UN    DB3.DBX 2.6;        // BM7222 Spindelhalt man.
UN    M 67.1;             // VSH/EFG AWZ Positionieren
UN    M 146.3;            // VSH/EFG WZW schwenkt
U     M 66.0;             // AWZ ein
=     DB20.DBX 341.3;     // Spindelfreigabe K0 Spindel 1
U     M 110.0;            // "0"
=     DB25.DBX 341.3;     // Spindelfreigabe K1 Spindel 1
U     M 110.5;            // AWZ im Rundachsbetrieb
R     DB20.DBX 328.1;     // Rundachsumschaltung angefordert S1
NETWORK
TITLE = Reset
// Reset-Taste nur als Flanke anlegen (sonst Kommandopuffer-Ueberlauf!!)
U     DB20.DBX 828.6;     // Reset-Taste MSTT
FP    M 138.0;            // Fl. Reset-Taste
O     M 136.1;            // Flanke Aux On
O     M 138.0;            // Fl. Reset-Taste
O(    ;
UN    E 6.2;              // Antriebe ein
U     M 138.7;            // HM Fl. E6.2
U     M 137.1;            // Einrichtebetriebsart
U     M 136.6;            // Spindel laeuft
UN    M 110.4;            // Spindel ist Rundachse
)     ;
=     DB20.DBX 4.0;       // Reset ueber Oberflaeche anfordern K0
=     DB25.DBX 4.0;       // Reset ueber Oberflaeche anfordern K1
U     E 6.2;              // Antriebe ein
=     M 138.7;            // HM Fl. E6.2
NETWORK
TITLE = Not Aus
UN    E 7.0;              // Not Aus Taste
S     DB1.DBX 1390.5;     // Not Aus an NC
S     DB2.DBX 0.0;        // FM6000 Not-Aus
U     M 102.7;            // Reset Taste
R     DB1.DBX 1390.5;
R     DB2.DBX 0.0;        // FM6000 Not-Aus
NETWORK
TITLE = Takt 2Hz bilden
UN    M 20.5;             // HM 2Hz
L     S5T#500MS;
SE    T 20;               // Zeit Takt High
U     T 20;
S     M 20.5;
U     M 20.5;
L     S5T#500MS;          // Zeit Takt Low
SE    T 21;
U     T 21;
R     M 20.5;
NETWORK
TITLE = Flanke 2Hz
U     M 110.7;            // Takt 1Hz
UN    M 20.5;             // HM 2Hz
O     ;
UN    M 110.7;
U     M 20.5;
=     M 137.4;            // Flanke 2Hz
NETWORK
TITLE = HM 2Hz
U     M 20.5;             // Takt 1Hz
=     M 110.7;            // HM 2Hz
NETWORK
TITLE =Programm laeuft ausschaltverzoegert
U     M 111.7;            // Programm laeuft
L     S5T#500MS;
SA    T 88;
END_FUNCTION
//**************************************************************************
FUNCTION FC 87 : VOID
TITLE=SPM_ANALOG
NAME: SPM_ANA
BEGIN
// Min- und Max-Werte werden in den SE-Daten gesichert. Die Anforderung zum
// Sichern kommt bei jeder SPM-Zustandsaenderung aus dem FC88
NETWORK
TITLE = Abbruch
UN    DB10.DBX 49.5;      // USER_DATA_HEX[9] Analogueberw. SPM1
BEB   ;
NETWORK
TITLE = Analogwert vom AC-IF einlesen und an NC melden
L     DB1.DBW 1476;       // ACIF Analogkanal 3
T     DB100.DBW 406;      // Analogwert SPM 1
L     DB100.DBW 406;      // Analogwert SPM 1
DTR   ;
T     DB1.DBD 1544;       // $A_INA[3]
NETWORK
TITLE = Minimalwert SPM1
U     M 155.5;            // BA JOG
U     M 136.0;            // AUX-ON
U(    ;
L     DB100.DBW 406;      // Analogwert SPM1
L     DB15.DBW 24;        // Min-Wert SPM1
<I    ;
)     ;
SPBN  M031;
L     DB100.DBW 406;      // Analogwert SPM1
T     DB15.DBW 24;        // Min-Wert SPM1
M031: NOP   0;
U(    ;
L     DB100.DBW 406;      // Analogwert SPM1
L     DB100.DBW 408;      // Min-Wert Diagnose
<I    ;
)     ;
SPBN  M032;
L     DB100.DBW 406;      // Analogwert SPM1
T     DB100.DBW 408;      // Min-Wert Diagnose
M032: NOP   0;
NETWORK
TITLE = Maximalwert SPM1
U     M 155.5;            // BA JOG
U     M 136.0;            // AUX-ON
U(    ;
L     DB100.DBW 406;      // Analogwert SPM1
L     DB15.DBW 26;        // Max-Wert SPM1
>I    ;
)     ;
SPBN  M041;
L     DB100.DBW 406;      // Analogwert SPM1
T     DB15.DBW 26;        // Max-Wert SPM1
M041: NOP   0;
U(    ;
L     DB100.DBW 406;      // Analogwert SPM1
L     DB100.DBW 410;      // Max-Wert Diagnose
>I    ;
)     ;
SPBN  M042;
L     DB100.DBW 406;      // Analogwert SPM1
T     DB100.DBW 410;      // Max-Wert Diagnose
M042: NOP   0;
NETWORK
TITLE = Min./Max.-Ruecksetzen
U     M 155.5;            // BA JOG
U     M 107.4;            // AUX-ON Taste
U(    ;
O     M 106.6;            // SPM Taste
O     M 201.7;            // ext. SPM-Taste
)     ;
SPBN  M051;
L     DB100.DBW 406;      // Analogwert SPM1
T     DB15.DBW 24;        // Min-Wert SPM1
T     DB15.DBW 26;        // Max-Wert SPM1
T     DB100.DBW 408;      // Min-Wert Diagnose
T     DB100.DBW 410;      // Max-Wert Diagnose
M051: NOP   0;
NETWORK
TITLE = Werte vorbesetzen wenn USER_DATA_INT[12]=0 (alte Version)
L     DB10.DBW 24;        // USER_DATA_INT[12] Grenze SPM zu
L     0;
>I    ;
SPB   M052;
L     DB10.DBW 22;        // USER_DATA_INT[11] Grenze SPM offen
T     DB10.DBW 24;        // USER_DATA_INT[12] Grenze SPM zu
M052: NOP   0;
NETWORK
TITLE = Endlage vorne SPM1
U     DB10.DBX 49.2;      // USER_DATA_HEX[9] SPM1 Max-Wert vorne
SPB   M061;
UN    M 5.0;              // SPM Futter
SPB   M063;
L     DB15.DBW 24;        // Min-Wert SPM1
L     DB10.DBW 22;        // USER_DATA_INT[11] Grenze SPM offen
+I    ;
T     DB100.DBW 426;
M063: NOP   0;
UN    M 5.1;              // SPM Zange
SPB   M064;
L     DB15.DBW 24;        // Min-Wert SPM1
L     DB10.DBW 24;        // USER_DATA_INT[12] Grenze SPM zu
+I    ;
T     DB100.DBW 426;
M064: NOP   0;
L     DB100.DBW 406;      // Analogwert SPM1
L     DB100.DBW 426;
<I    ;
=     DB100.DBX 418.2;    // Endlage vorne SPM1
SPA   M062;
M061: NOP   0;
UN    M 5.0;              // SPM Futter
SPB   M065;
L     DB15.DBW 26;        // Max-Wert SPM1
L     DB10.DBW 22;        // USER_DATA_INT[11] Grenze SPM offen
-I    ;
T     DB100.DBW 426;
M065: NOP   0;
UN    M 5.1;              // SPM Zange
SPB   M066;
L     DB15.DBW 26;        // Max-Wert SPM1
L     DB10.DBW 24;        // USER_DATA_INT[12] Grenze SPM zu
-I    ;
T     DB100.DBW 426;
M066: NOP   0;
L     DB100.DBW 406;      // Analogwert SPM1
L     DB100.DBW 426;
>I    ;
=     DB100.DBX 418.2;    // Endlage vorne SPM1
M062: NOP   0;
NETWORK
TITLE = Endlage hinten SPM1
U     DB10.DBX 49.2;      // USER_DATA_HEX[9] SPM1 Max-Wert vorne
SPB   M071;
UN    M 5.1;              // SPM Zange
SPB   M073;
L     DB15.DBW 26;        // Max-Wert SPM1
L     DB10.DBW 22;        // USER_DATA_INT[11] Grenze SPM offen
-I    ;
T     DB100.DBW 426;
M073: NOP   0;
UN    M 5.0;              // SPM Futter
SPB   M074;
L     DB15.DBW 26;        // Max-Wert SPM1
L     DB10.DBW 24;        // USER_DATA_INT[12] Grenze SPM zu
-I    ;
T     DB100.DBW 426;
M074: NOP   0;
L     DB100.DBW 406;      // Analogwert SPM1
L     DB100.DBW 426;
>I    ;
=     DB100.DBX 418.3;    // Endlage hinten SPM1
SPA   M072;
M071: NOP   0;
UN    M 5.1;              // SPM Zange
SPB   M075;
L     DB15.DBW 24;        // Min-Wert SPM1
L     DB10.DBW 22;        // USER_DATA_INT[11] Grenze SPM offen
+I    ;
T     DB100.DBW 426;
M075: NOP   0;
UN    M 5.0;              // SPM Futter
SPB   M076;
L     DB15.DBW 24;        // Min-Wert SPM1
L     DB10.DBW 24;        // USER_DATA_INT[12] Grenze SPM zu
+I    ;
T     DB100.DBW 426;
M076: NOP   0;
L     DB100.DBW 406;      // Analogwert SPM1
L     DB100.DBW 426;
<I    ;
=     DB100.DBX 418.3;    // Endlage hinten SPM1
M072: NOP   0;
END_FUNCTION
//**************************************************************************
FUNCTION FC 88 : VOID
TITLE=SPM_STEU
NAME: SPM_STEU
BEGIN
NETWORK
TITLE = SPS/OB-->PNEUM. SPM AKTIV
SET
=   DB20.DBX 382.1; // SPS/RM-->PNEUM. SPM AKTIV
NETWORK
TITLE = Impuls Steigende FL. SPM Taste
U(    ;
U     M 201.7;            // ext. SPM Taste
U     M 136.0;            // AUX-ON
O     M 106.6;            // SPM-Taste
)     ;
U(
UN    E 6.2;              // Tuer zu, Antriebe ein
O     DB10.DBX 44.7;      // USER_DATA_HEX[4] SPM auf/zu mit Tasten bei geschlossener Tür
)
UN    M 111.7;            // Progr. laeuft
FP    DB100.DBX 167.3;    // Fl. SPM-Taste
NETWORK
TITLE = HM SPM ruecksetzen
// V0.17: Robotik neu
O(    ;
U     DB100.DBX 167.3;    // Fl. SPM-Taste
U     DB100.DBX 168.1;    // HM SPM Aenderung
)     ;
O(    ;
U     E 4.5;              // Fusstaster SPM auf
UN    M 111.7;            // Progr. laeuft
)     ;
O(    ;
U     M 190.4;            // SPM auf Robotik
U     M 191.0;            // Robotik aktiv
UN    M 111.7;            // Progr. laeuft
)     ;
O(    ;
U     DB20.DBX 302.1;     // DNC SPM oeffnen
U     DB1.DBX 1366.0;     // DNC-BETRIEB AKTIV
)     ;
=     DB100.DBX 169.0;    // HM SPM auf man.
U     DB20.DBX 195.1;     // M25 dyn.
UN    DB20.DBX 324.2;     // DRY
=     DB100.DBX 169.1;    // HM M25
U(    ;
O     DB100.DBX 169.0;    // HM SPM auf man.
O     DB100.DBX 169.1;    // HM M25
O     DB100.DBX 174.1;    // SPM auf LM
)     ;
U(    ;
O     M 136.0;            // AUX-ON
O     ;                   // zum Druckabbau in der Hydraulik bei AUX-OFF
U     M 201.7;            // ext. SPM Taste
U     M 106.6;            // SPM-Taste
)     ;
U(    ;
ON    M 136.6;            // Spindel laeuft
O     DB100.DBX 166.0;    // Spindel drehen bei offenem SPM
)     ;
R     DB100.DBX 167.1;    // SPM Zustand
NETWORK
TITLE = HM SPM setzen
// V0.17: Robotik neu
O(    ;
U     DB100.DBX 167.3;    // Fl. SPM-Taste
UN    DB100.DBX 168.1;    // HM SPM Aenderung
)     ;
O(    ;
U     E 4.6;              // Fusstaster SPM zu
UN    M 111.7;            // Programm laeuft
)     ;
O(    ;
U     M 190.5;            // SPM zu Robotik
U     M 191.0;            // Robotik aktiv
UN    M 111.7;            // Progr. laeuft
)     ;
O(    ;
U     DB20.DBX 302.2;     // DNC SPM schliessen
U     DB1.DBX 1366.0;     // DNC-BETRIEB AKTIV
)     ;
=     DB100.DBX 169.2;    // HM SPM zu man.
U     DB20.DBX 195.2;     // M26 dyn.
UN    DB20.DBX 324.2;     // DRY
=     DB100.DBX 169.3;    // HM M26
U(    ;
O     DB100.DBX 169.2;    // HM SPM zu man.
O     DB100.DBX 169.3;    // HM M26
O     DB100.DBX 174.2;    // SPM zu LM
)     ;
U(    ;
O     M 136.0;            // AUX-ON
O     ;           // zum Druckabbau in der Hydraulik bei AUX-OFF
U     M 201.7;            // ext. SPM Taste
U     M 106.6;            // SPM-Taste
)     ;
S     DB100.DBX 167.1;    // SPM Zustand
NETWORK
TITLE = pos. Fl. SPM betaetigt
U     DB100.DBX 167.1;    // SPM Zustand
UN    DB100.DBX 168.1;    // HM SPM Aenderung
=     DB100.DBX 168.2;    // Pos. Fl. SPM Zustand
NETWORK
TITLE = neg. Fl. SPM betaetigt
UN    DB100.DBX 167.1;    // SPM Zustand
U     DB100.DBX 168.1;    // HM SPM Aenderung
=     DB100.DBX 168.3;    // neg. Fl. SPM Zustand
NETWORK
TITLE = HM SPM Betaetigung
U     DB100.DBX 167.1;    // SPM Zustand
=     DB100.DBX 168.1;    // HM SPM Aenderung
NETWORK
TITLE = Zustand in Settingdaten sichern
L     DB100.DBW 167;
T     DB15.DBW 22;
O     M 168.2;
O     M 168.3;
S     DB20.DBX 348.3;     // Anforderung Settingdaten sichern
NETWORK
TITLE = FM6200 SPM1 offen fehlt
U     DB20.DBX 195.1;     // M25 dyn.
UN    DB20.DBX 324.2;     // DRY
ON    DB100.DBX 167.1;    // SPM Zustand
S     DB100.DBX 1.1;
UN    M 5.0;
UN    M 5.1;
O     DB100.DBX 167.5;    // HM SPM geloest
O     DB100.DBX 167.3;    // Fl. SPM-Taste
O     M 102.7;            // Reset-Taste
ON    M 136.0;            // AUX-ON
R     DB100.DBX 1.1;
U     DB100.DBX 1.1;
L     S5T#5S;
SE    T 34;
U     T 34;
S     DB2.DBX 1.1;        // FM6200 SPM offen fehlt
U     M 102.7;            // Reset-Taste
R     DB2.DBX 1.1;        // FM6200 SPM offen fehlt
NETWORK
TITLE = FM6201 SPM1 zu fehlt
U     DB20.DBX 195.2;     // M26 dyn.
UN    DB20.DBX 324.2;     // DRY
O     DB100.DBX 167.1;    // SPM Zustand
S     DB100.DBX 1.2;
UN    M 5.0;
UN    M 5.1;
O     DB100.DBX 167.4;    // HM SPM gespannt
O     DB100.DBX 167.3;    // Fl. SPM-Taste
O     M 102.7;            // Reset-Taste
ON    M 136.0;            // AUX-ON
R     DB100.DBX 1.2;
U     DB100.DBX 1.2;
L     S5T#5S;
SE    T 32;
U     T 32;
S     DB2.DBX 1.2;        // FM6201 SPM zu fehlt
U     M 102.7;            // Reset-Taste
R     DB2.DBX 1.2;        // FM6201 SPM zu fehlt
NETWORK
TITLE = BM7273/7273 setzen
U     M 148.2;            // NC-Start gesamt
UN    DB20.DBX 324.7;     // SBL
O     M 107.7;            // Spindel-Start Taste
S     DB3.DBX 9.0;        // BM7272 SPM offen
S     DB3.DBX 9.1;        // BM7273 SPM auf Endlage
NETWORK
TITLE = BM7272 ruecksetzen
O     DB100.DBX 167.4;    // HM SPM gespannt
O     M 102.7;            // Reset-Taste
O     DB100.DBX 166.0;
O     M 111.7;            // Programm laeuft
O     DB20.DBX 324.2;     // DRY
R     DB3.DBX 9.0;        // BM7272 SPM offen
NETWORK
TITLE = HM SPM auf Endlage
O(    ;
U     DB100.DBX 418.3;    // Endlage hinten SPM1
U     M 5.0;
U     DB100.DBX 167.1;    // SPM Zustand
)     ;
O(    ;
U     DB100.DBX 418.2;    // Endlage vorne SPM1
U     M 5.1;
U     DB100.DBX 167.1;    // SPM Zustand
)     ;
=     DB100.DBX 167.6;    // HM SPM auf Endlage
NETWORK
TITLE = SPM auf Endlage
U     DB100.DBX 167.6;    // HM SPM auf Endlage
U(    ;
ON    M 201.4;            // SS Dateneingabe
O(    ;
U     M 155.1;            // BA AUTO
UN    M 155.7;            // HM 1x
)     ;
)     ;
UN    DB10.DBX 49.4;      // USER_DATA_HEX[9] Ueberbrueckung analoge SPM-Ueberw.
UN    M 5.2;
=     DB100.DBX 167.7;    // SPM auf Endlage
NETWORK
TITLE = BM7273 ruecksetzen
ON    DB100.DBX 167.7;    // SPM auf Endlage
O     M 102.7;            // Reset-Taste
O     DB20.DBX 324.2;     // DRY
R     DB3.DBX 9.1;        // BM7273 SPM auf Endlage
NETWORK
TITLE = NC-Start Sperre BM7272/7273
U(    ;
O     DB3.DBX 9.0;        // BM7272 SPM offen
O     DB3.DBX 9.1;        // BM7273 SPM Endlage
O(    ;
U(    ;
ON    DB100.DBX 167.4;    // HM SPM gespannt
O     DB100.DBX 167.7;    // SPM auf Endlage
)     ;
UN    DB20.DBX 324.7;     // SBL
UN    DB20.DBX 324.2;     // DRY
)     ;
)     ;
UN    DB10.DBX 46.3;      // USER_DATA_HEX[6] keine NC-Start Sperre bei SPM offen
=     M 145.1;            // Verr. Start SPM
NETWORK
TITLE = FM6119 SPM Druckueberwachung
U     M 136.6;            // Spindel laeuft
UN    DB100.DBX 167.4;    // HM SPM gespannt
U     M 136.0;            // AUX-ON
UN    DB100.DBX 166.0;    // Spindel ein bei offenem SPM
UN    T 106;              // Spindel drehen verz.
U     E 6.2;              // Tuer zu, Antriebe ein
UN    DB20.DBX 324.2;     // DRY
S     DB2.DBX 4.3;        // FM76119 SPM Druckueberw.
U     M 102.7;            // Reset-Taste
R     DB2.DBX 4.3;        // FM76119 SPM Druckueberw.
NETWORK
TITLE = FM6120 SPM Endlagenueberwachung
U     M 136.6;            // Spindel laeuft
U     DB100.DBX 167.7;    // SPM auf Endlage
U     M 136.0;            // AUX-ON
UN    DB20.DBX 324.2;     // DRY
S     DB2.DBX 4.4;        // FM6120 SPM Endlagenueberw.
U     M 102.7;            // Reset-Taste
R     DB2.DBX 4.4;        // FM6119 SPM Endlagenueberw.
NETWORK
TITLE = SPANNMITTEL/NEG.DNC-QUITTUNG
O(
U     DB20.DBX 302.2;     // DNC/SPANNMITTEL ZU
UN    DB100.DBX 167.1;    // SPM Zustand
)
O(
U     DB20.DBX 302.1;     // DNC/SPANNMITTEL AUF
U     DB100.DBX 167.1;    // SPM Zustand
)
=     DB20.DBX 318.1;     // SPANNMITTEL/NEG.DNC-QUITTUNG
END_FUNCTION
//**************************************************************************
FUNCTION FC 89 : VOID
TITLE=SPM_VENT
NAME: SPM_VENT
BEGIN
NETWORK
TITLE = Ventil 1 Spannmittel links
O     DB100.DBX 168.2;    // pos. Fl. SPM Zustand
O     DB100.DBX 168.3;    // neg. Fl. SPM Zustand
U     DB100.DBX 167.0;    // Kippmerker Futter/Zange
L     S5T#200MS;
SV    T 33;
U     T 33;
U(    ;
O     M 5.0;
O     M 5.1;
)     ;
=     M 210.4;            // MV Spannzylinder vor
NETWORK
TITLE = Ventil 2 Spannmittel links
O     DB100.DBX 168.2;    // pos. Fl. SPM Zustand
O     DB100.DBX 168.3;    // neg. Fl. SPM Zustand
UN    DB100.DBX 167.0;    // Kippmerker Futter/Zange
L     S5T#200MS;
SV    T 37;
U     T 37;
U(    ;
O     M 5.0;
O     M 5.1;
)     ;
=     M 210.5;            // MV Spannzylinder zurueck
NETWORK
TITLE = Einschaltverz. Druckschalter
U     DB100.DBX 167.1;    // SPM Zustand
U     E 4.4;              // Druckschalter SPM
L     S5T#100MS;
SA    T 39;
U     T 39;
L     S5T#400MS;
SE    T 38;               // DS SPM verzoegert
UN    DB100.DBX 167.1;    // SPM Zustand
R     T 38;               // DS SPM verzoegert
NETWORK
TITLE = SPM gespannt
O(    ;
U     DB100.DBX 167.1;    // SPM Zustand
U     T 38;               // DS SPM verzoegert
)     ;
O(    ;
UN    M 5.0;
UN    M 5.1;
)     ;
=     DB100.DBX 167.4;    // HM SPM gespannt
=     DB20.DBX 310.2;     // Rueckmeldg. SPM geschl.
NETWORK
TITLE = SPM geloest
UN    DB100.DBX 167.1;    // SPM Zustand
U     E 4.4;              // Druckschhalter SPM
U(    ;
O(    ;
U     DB100.DBX 418.2;    // Endlage vorne SPM1
U     M 5.0;
)     ;
O(    ;
U     DB100.DBX 418.3;    // Endlage hinten SPM1
U     M 5.1;
)     ;
)     ;
=     DB100.DBX 167.5;    // HM SPM geloest
=     DB20.DBX 310.1;     // Rueckmeldg. SPM offen
NETWORK
TITLE = Kippmerker Futter/Zange
O(    ;
U     DB100.DBX 167.1;    // SPM Zustand
U     M 5.0;
)     ;
O(    ;
UN    DB100.DBX 167.1;    // SPM Zustand
U     M 5.1;
)     ;
=     DB100.DBX 167.0;    // Kippmerker Futter/Zange
NETWORK
TITLE = LED SPM-Taste
U     DB100.DBX 167.4;    // HM SPM gespannt
U     M 136.0;            // AUX-ON
U(    ;
U     DB100.DBX 167.6;    // HM SPM auf Endlage
U     M 110.7;            // Blinktakt
ON    DB100.DBX 167.6;    // HM SPM auf Endlage
)     ;
=     DB20.DBX 849.6;     // LED SPM-Taste
END_FUNCTION
//**************************************************************************
FUNCTION FC 92 : VOID
TITLE=REITSTOCK
NAME: RS
BEGIN
NETWORK
TITLE =Abbruch
UN    DB10.DBX 41.2;      // USER_DATA_HEX[1] autom. Reitstock
BEB   ;
L     DB100.DBW 277;      // Reitstock akt. Position
DTR   ;
T     DB1.DBD 1540;       // $A_INA[2]: Pos. autom. RS
// gesp. Werte bei Hochlauf init.
UN    M 110.2;            // Grst. OB1
SPB   RS_1;
L     DB15.DBW 32;        // SE-Daten Zielpos. M21
T     DB100.DBW 171;      // Zielpos. vorne
L     DB15.DBW 30;        // SE-Daten Zielpos. M20
T     DB100.DBW 275;      // Zielpos. M20
RS_1: NOP 0;
NETWORK
TITLE = Verzoegerung Druckschalter Reitstock
U     E 5.2;              // DS Reitstock vorne
L     S5T#300MS;
SE    T 87;               // Verz. DS Reitstock vorne
NETWORK
TITLE = BM7201 VSH/ELS Reitstock vor
U     DB20.DBX 194.5;     // M21 dyn.
UN    DB20.DBX 324.2;     // DRY
S     M 25.3;             // HM BM7201 Reitstock faehrt vor
O     T 87;               // Verz. DS Reitstock vorne
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
O     DB2.DBX 8.6;        // FM6230 Reitstock Positionsueberwachung
R     M 25.3;             // HM BM7201 Reitstock faehrt vor
U     M 25.3;             // HM BM7201 Reitstock faehrt vor
U(    ;
UN    DB1.DBX 1402.1;     // Fanuc 0TB
UN    DB1.DBX 1403.0;     // Fanuc 21TB
O     DB2.DBX 6.7;        // FM6215 Zeitueberwachung Hilfsfunktion
)     ;
=     DB3.DBX 0.1;        // BM7201 Reitstock faehrt vor
NETWORK
TITLE = Endlage vorne merken
U     E 5.2;              // Drucksch. Teil gespannt
L     S5T#200MS;
SE    T 85;               // DS Reitstock vorne verz.
U     M 155.5;            // BA JOG
U     A 4.5;              // MV RS Schleichgang
U     M 106.5;            // Taste RS vor
U     T 85;               // DS Reitstock vorne verz.
=     DB100.DBX 170.2;    // HM Reitstock Endlage vorne
UN    DB100.DBX 170.2;    // HM Reitstock Endlage vorne
SPB   M001;
L     DB100.DBW 277;      // Reitstock akt. Position
T     DB100.DBW 171;      // Zielpos. vorne
T     DB15.DBW 32;        // SE-Daten Zielpos. M21
U     M 110.1;            // "1"
S     DB20.DBX 348.3;     // Anforderung SE-daten sichern
M001: NOP   0;
ON    DB20.DBX 194.5;     // M21 dyn.
ON    M 111.0;            // H-Aenderung verl.
SPB   M003;
L     DB20.DBD 186;       // H-Wort (Ganzzahl)
T     DB100.DBW 171;      // Zielpos. vorne
T     DB15.DBW 32;        // SE-Daten Zielpos. M21
U     M 110.1;            // "1"
S     DB20.DBX 348.3;     // Anforderung SE-daten sichern
M003: NOP   0;
NETWORK
TITLE = BM7202 VSH/ELS Reitstock zurueck
U     DB20.DBX 194.4;     // M20 dyn.
UN    DB20.DBX 324.2;     // DRY
S     M 25.4;             // HM BM7202 Reitstock faehrt zurueck
U     M 170.0;            // HM Zielpos. M20 erreicht
UN    DB20.DBX 194.4;     // M20 dyn.
O     E 4.3;              // RS hinten
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
R     M 25.4;             // HM BM7202 Reitstock faehrt zurueck
U     M 25.4;             // HM BM7202 Reitstock faehrt zurueck
U(    ;
UN    DB1.DBX 1402.1;     // Fanuc 0TB
UN    DB1.DBX 1403.0;     // Fanuc 21TB
O     DB2.DBX 6.7;        // FM6215 Zeitueberwachung Hilfsfunktion
)     ;
=     DB3.DBX 0.2;        // BM7202 Reitstock faehrt zurueck
NETWORK
TITLE = Zielposition hinten (Hxxx M20)
UN    DB20.DBX 194.4;     // M20 dyn.
SPB   M002;
L     DB20.DBW 186;       // H-Wort (Ganzzahl)
T     DB100.DBW 275;      // Zielpos. M20
U     M 110.1;            // "1"
S     DB20.DBX 348.3;     // Anforderung SE-daten sichern
U     M 111.0;            // H-Aenderung verl.
SPB   M002;
L     0;
T     DB100.DBW 275;      // Zielpos. M20
U     M 110.1;            // "1"
S     DB20.DBX 348.3;     // Anforderung SE-daten sichern
M002: NOP   0;
L     DB100.DBW 275;      // Zielpos. M20
L     25;
>I    ;
SPB   M030;
L     0;
T     DB100.DBW 275;      // Zielpos. M20
U     M 110.1;            // "1"
S     DB20.DBX 348.3;     // Anforderung SE-daten sichern
M030: L     DB100.DBW 277;      // Reitstock akt. Position
L     DB100.DBW 275;      // Zielpos. M20
<I    ;
=     M 170.0;            // HM Zielpos. M20 erreicht
L     DB100.DBW 275;      // Zielpos. M20
T     DB15.DBW 30;        // SE-Daten Zielpos. M20
NETWORK
TITLE = Reitstock zurueck
U(    ;
U     M 155.5;            // BA JOG
U     M 106.4;            // Taste RS zurueck
O     M 25.4;             // HM BM7202 Reitstock faehrt zurueck
)     ;
U     M 136.0;            // AUX-ON
O     ;
U(    ;
UN    A 4.5;              // MV RS Schleichgang vor
UN    M 216.0;              // MV RS vor
U     E 4.3;              // RS hinten
UN    DB10.DBX 47.6;      // USER_DATA_HEX[7] Selbsthaltung RS hinten aus
O     DB100.DBX 170.5;    // HM Reitstock ref.
)     ;
U     E 7.0;              // Not-Aus
=     DB100.DBX 170.3;    // HM Reitstock zurueck
NETWORK
TITLE = HM Reitstock referieren
U     M 104.3;            // REF Taste
UN    DB1.DBX 134.0;      // X-Achse referenziert
UN    DB1.DBX 134.1;      // Z-Achse referenziert
U     E 6.2;              // Tuer zu, Antriebe ein
S     DB100.DBX 170.5;    // HM Reitstock ref.
O     M 102.7;            // Reset Taste
O     E 4.3;              // RS hinten
R     DB100.DBX 170.5;    // HM Reitstock ref.
NETWORK
TITLE = Umschaltung zurueck auf Schleichgang
L     DB100.DBW 275;
L     DB10.DBW 12;        // USER_DATA_INT[6] Schleichweg RS zurueck
+I    ;
T     DB100.DBW 271;
L     DB100.DBW 277;      // Reitstock akt. Position
L     DB100.DBW 271;
<=I   ;
=     DB100.DBX 170.4;    // HM Schleichweg hinten
NETWORK
TITLE = Reitstock zurueck Eilgang
U     DB100.DBX 170.3;    // HM Reitstock zurueck
UN    DB100.DBX 170.4;    // HM Schleichweg hinten
U     E 6.2;              // Tuer zu, Antriebe ein
U(    ;
ON    M 155.5;            // BA JOG
O     M 101.3;            // Eilgang Taste
)     ;
UN    M 100.1;
=     DB100.DBX 170.0;    // HM Reitstock Eilgang
NETWORK
TITLE =Umschaltung zurueck Eilgang
U     DB100.DBX 170.0;    // HM Reitstock Eilgang
=     M 216.1;              // MV RS zurueck
NETWORK
TITLE =Reitstock zurueck Schleichgang
U     DB100.DBX 170.3;    // HM Reitstock zurueck
U(    ;
O     DB100.DBX 170.4;    // HM Schleichweg hinten
O     M 155.5;            // BA JOG
)     ;
=     A 4.6;              // MV Schleichgang zurueck
NETWORK
TITLE =FM6230 Positionsueberwachung Reitstock
U     M 111.7;            // Programm laeuft
U(    ;
L     DB100.DBW 277;      // Reitstock akt. Position
L     DB100.DBW 171;      // Zielpos. vorne
>I    ;
)     ;
S     DB2.DBX 8.6;        // FM6230 Reitstock Positionsueberwachung
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
R     DB2.DBX 8.6;        // FM6230 Reitstock Positionsueberwachung
NETWORK
TITLE =FM6231 Reitstock hinten fehlt
U     M 216.1;              // MV RS zurueck
UN    E 4.3;              // RS hinten
L     S5T#25S;
SE    T 86;
U     T 86;
S     DB2.DBX 8.7;        // FM6231 Reitstock hinten fehlt
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
R     DB2.DBX 8.7;        // FM6231 Reitstock hinten fehlt
NETWORK
TITLE =HM Druckueberwachung Reitstock
U     T 87;               // Verz. DS Reitstock vorne
U     A 4.5;              // MV RS Schleichgang vor
S     DB100.DBX 170.7;
O     A 4.6;              // MV RS Schleichgang zurueck
O     M 216.1;              // MV RS zurueck
R     DB100.DBX 170.7;
NETWORK
TITLE =FM6118 Druckueberwachung Reitstock
UN    T 87;               // Verz. DS Reitstock vorne
U     M 136.6;            // Spindel laeuft
U     M 136.0;            // AUX-ON
U     DB100.DBX 170.7;
S     DB2.DBX 4.2;        // FM6118 Druckueberwachung Reitstock
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
R     DB2.DBX 4.2;        // FM6118 Druckueberwachung Reitstock
NETWORK
TITLE =B3 B4 Ueberwachung Reitstock
U     E 5.0;              // RS Bero Pos. 1
FP    DB100.DBX 173.1;
U     E 5.1;              // RS Bero Pos. 2
FP    DB100.DBX 173.2;
O     DB100.DBX 173.1;
O     DB100.DBX 173.2;
=     DB100.DBX 173.0;    // Fl. Pos. Beros
NETWORK
TITLE =FM6229 B3 B4 ueberwachung Reitstock
U(    ;
O     A 4.5;              // MV RS Schleichgang vor
O     A 4.6;              // MV RS Schleichgang zurueck
O     M 216.0;              // MV RS vor
O     M 216.1;              // MV RS zurueck
)     ;
UN    DB100.DBX 173.0;    // Fl. Pos. Beros
UN    E 4.3;              // RS hinten
UN    E 5.2;              // DS Teil gespannt
L     S5T#3S;
SE    T 11;
U     T     11;
S     DB2.DBX 8.5;        // FM6229 Reitstock B3 B4 fehlt
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
R     DB2.DBX 8.5;        // FM6229 Reitstock B3 B4 fehlt
NETWORK
TITLE =Spindelhalt Reitstock
O     DB2.DBX 8.5;        // FM6229 Reitstock B3 B4 fehlt
O     DB2.DBX 8.6;        // FM6230 Reitstock Positionsueberwachung
O     DB2.DBX 8.7;        // FM6231 Reitstock hinten fehlt
=     DB100.DBX 23.3;     // Spindelhalt Reitstock
NETWORK
TITLE =VSG / EFG Reitstock
O     M 25.3;             // HM BM7201 Reitstock faehrt vor
O     M 25.4;             // HM BM7202 Reitstock faehrt zurueck
O     DB2.DBX 8.5;        // FM6229 Reitstock B3 B4 fehlt
O     DB2.DBX 8.6;        // FM6230 Reitstock Positionsueberwachung
O     DB2.DBX 8.7;        // FM6231 Reitstock hinten fehlt
=     M 146.6;            // VSH/EFG Reitstock
END_FUNCTION
//**************************************************************************
FUNCTION FC 93 : VOID
TITLE=PINOLE
NAME: PINOLE
BEGIN
NETWORK
TITLE = Abbruch
UN    DB10.DBX 58.2;      // USER_DATA_HEX[18]: Pinolenreitstock
BEB   ;
NETWORK
TITLE = SPS/OB--> AUTOMATISCHE PINOLE AKTIV
SET
=     DB20.DBX 382.2;     // SPS/RM-->AUTOMATISCHE PINOLE AKTIV
NETWORK
TITLE = Verzoegerung Druckschalter Reitstock
U     E 5.2;              // DS Pinole Teil gespannt
L     S5T#300MS;
SE    T 90;               // Teil gespannt verz.
NETWORK
TITLE = BM7201 ELS Pinole faehrt vor
U     DB20.DBX 194.5;     // M21 dyn.
UN    DB20.DBX 324.2;     // DRY aktiv
O(    ;
U     M 193.2;            // Robotik Pinole vor
U     M 191.0;            // Robotik aktiv
UN    M 111.7;            // Progr. laeuft
)     ;
O(    ;
U     DB20.DBX 302.4      // DNC Pinole ausfahren
U     DB1.DBX 1366.0;     // DNC-BETRIEB AKTIV
)     ;
S     M 25.3;             // HM BM7201 Reitstock faehrt vor
O     T 90;               // Teil gespannt verz.
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
O     DB2.DBX 8.6;        // FM6230 Reitstock Positionsueberwachung
R     M 25.3;             // HM BM7201 Reitstock faehrt vor
U     M 25.3;             // HM BM7201 Reitstock faehrt vor
U(    ;
UN    DB1.DBX 1402.1;     // Fanuc 0TB
UN    DB1.DBX 1403.0;     // Fanuc 21TB
O     DB2.DBX 6.7;        // FM6215 Zeitueberwachung Hilfsfunktion
)     ;
=     DB3.DBX 0.1;        // BM7201 Reitstock faehrt vor
NETWORK
TITLE = BM7202 ELS Pinole faehrt zurueck
U     DB20.DBX 194.4;     // M20 dyn.
UN    DB20.DBX 324.2;     // DRY aktiv
O(    ;
U     M 193.3;            // Robotik Pinole zurueck
U     M 191.0;            // Robotik aktiv
UN    M 111.7;            // Progr. laeuft
)     ;
O(    ;
U     DB20.DBX 302.3      // DNC Pinole einziehen
U     DB1.DBX 1366.0;     // DNC-BETRIEB AKTIV
UN    M 111.7;            // Progr. laeuft
)     ;
S     M 25.4;             // HM BM7202 Reitstock faehrt zurueck
O     E 5.0;              // Pinole hinten
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
R     M 25.4;             // HM BM7202 Reitstock faehrt zurueck
U     M 25.4;             // HM BM7202 Reitstock faehrt zurueck
U(    ;
UN    DB1.DBX 1402.1;     // Fanuc 0TB
UN    DB1.DBX 1403.0;     // Fanuc 21TB
O     DB2.DBX 6.7;        // FM6215 Zeitueberwachung Hilfsfunktion
)     ;
=     DB3.DBX 0.2;        // BM7202 Reitstock faehrt zurueck
NETWORK
TITLE =Pinole Schleichgang/Eilgang vor
// V1.00;  Robotik
U(    ;
O(    ;
UN    M 111.7;            // Progr. laeuft
U     M 106.5;            // Taste Pinole vor
)     ;
O(    ;
UN    A 4.6;              // MV Pinole zurueck
U     T 90;               // Teil gespannt verz.
O     M 25.3;             // HM BM7201 Reitstock faehrt vor
)     ;
)     ;
U     M 136.0;            // AUX-ON
UN    DB2.DBX 8.6;        // FM6230 Reitstock Positionsueberwachung
=     M 169.0;            // HM Pinole vor
U     M 169.0;            // HM Pinole vor
U(    ;
ON    E 6.2;              // Tuer zu, Antriebe ein
O     M 201.6;            // SS Hand/Auto
)     ;
=     A 4.5;              // MV Pinole Schleichgang vor
U     M 169.0;            // HM Pinole vor
U     E 6.2;              // Tuer zu, Antriebe ein
UN    M 201.6;            // SS Hand/Auto
=     M 216.0;              // MV Pinole vor Eilgang
NETWORK
TITLE = Pinole zurueck
// V1.00; Robotik
U(    ;
UN    M 111.7;            // Programm laeuft
U     M 106.4;            // Taste Pinole zurueck
O     M 25.4;             // HM BM7202 Reitstock faehrt zurueck
)     ;
U     M 136.0;            // AUX-ON
O     ;
U(    ;
UN    A 4.5;              // MV Pinole Schleichgang vor
UN    M 216.0;            // MV Pinole vor Eilgang
U     E 5.0;              // Pinole hinten
UN    DB10.DBX 47.6;      // USER_DATA_HEX[7] Selbsthaltung Pinole hinten aus
O     M 169.5;            // Pinole zurueck bei AUX-ON
)     ;
U     E 7.0;              // NOT-AUS
=     A 4.6;              // MV Pinole zurueck
NETWORK
TITLE = Pinole zurueck bei AUX-ON
U     M 136.1;            // Fl. AUX-ON
UN    E 5.0;              // Pinole hinten
UN    DB10.DBX 47.7;      // USER_DATA_HEX[7]: kein Pinole zurueck bei Aux-On
S     M 169.5;            // Pinole zurueck bei AUX-ON
O     E 5.0;              // Pinole hinten
ON    M 136.0;            // AUX-ON
R     M 169.5;            // Pinole zurueck bei AUX-ON
NETWORK
TITLE = Bero Pinole vorne ueberfahren
U     E 5.1;              // Pinole vorne
FN    M 169.1;            // Fl. Pinole vorne ueberfahren
NETWORK
TITLE = FM6230 VSH/ELS Positionsueberwachung Pinole
U(    ;
O     M 111.7;            // Programm laeuft
O     M 136.6;            // Spindel laeuft
)     ;
U(    ;
U     T 90;               // Teil gespannt verz.
UN    E 5.1;              // Pinole vorne
O     ;
U     M 216.0;              // MV Pinole vor Eilgang
U     M 169.1;            // Fl. Pinole vorne ueberfahren
)     ;
S     DB2.DBX 8.6;        // FM6230 Reitstock Positionsueberwachung
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
R     DB2.DBX 8.6;        // FM6230 Reitstock Positionsueberwachung
NETWORK
TITLE = FM5231 VSH/ELS Pinole hinten fehlt
U     A 4.6;              // MV Pinole zurueck
UN    E 5.0;              // Pinole hinten
L     S5T#5S;
SE    T 91;               // Ueberwachungszeit Pinole hinten
U     T 91;               // Ueberwachungszeit Pinole hinten
S     DB2.DBX 8.7;        // FM6231 Reitstock hinten fehlt
O     M 102.7;            // Reset-Taste
ON    M 136.0;            // AUX-ON
R     DB2.DBX 8.7;        // FM6231 Reitstock hinten fehlt
NETWORK
TITLE = HM Druckberwachung Pinole
U     T 90;               // Teil gespannt verz.
U(    ;
O     A 4.5;              // MV Pinole Schleichgang vor
O     M 216.0;              // MV Pinole vor Eilgang
)     ;
S     M 169.7;            // HM Druckueberw. aktiv
U     A 4.6;              // MV Pinole zurueck
R     M 169.7;            // HM Druckueberw. aktiv
NETWORK
TITLE = FM6118 Druckberwachung Pinole
UN    T 90;               // Teil gespannt verz.
U     M 136.6;            // Spindel laeuft
U     M 136.0;            // AUX-ON
U     M 169.7;            // HM Druckueberw. aktiv
S     DB2.DBX 4.2;        // FM6118 Druckueberwachung Pinole
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
R     DB2.DBX 4.2;        // FM6118 Druckueberwachung Pinole
NETWORK
TITLE = FM6107 VSH/ELS Pinole vorne fehlt
U(    ;
O     A 4.5;              // MV Pinole Schleichgang vor
O     M 216.0;              // MV Pinole vor Eilgang
)     ;
UN    T 90;               // Teil gespannt verz.
L     S5T#14S;
SE    T 92;               // Ueberwachungszeit Pinole vorne
U     T 92;               // Ueberwachungszeit Pinole vorne
S     DB2.DBX 2.7;        // FM6107 Pinole vorne fehlt
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
R     DB2.DBX 2.7;        // FM6107 Pinole vorne fehlt
NETWORK
TITLE = BM7203 ELS Pinole in Zwischenstellung
UN    E 5.0;              // Pinole hinten
UN    E 5.1;              // Pinole vorne
U     M 136.0;            // AUX-ON
UN    M 25.3;             // HM BM7201 Reitstock faehrt vor
UN    M 25.4;             // HM BM7202 Reitstock faehrt zurueck
=     M 25.2;             // HM BM7203 Reitstock in Zwischenstellung
=     M 145.6;            // Verr. Start Pinole
=     DB3.DBX 0.3;        // BM7203 Reitstock in Zwischenstellung
NETWORK
TITLE = Spindelhalt bei Alarm
O     DB2.DBX 2.7;        // FM6107 Pinole vorne fehlt
O     DB2.DBX 8.6;        // FM6230 Reitstock Positionsueberwachung
O     DB2.DBX 8.7;        // FM6231 Reitstock hinten fehlt
O     M 25.2;             // HM BM7203 Reitstock in Zwischenstellung
=     DB100.DBX 23.3;     // Spindelhalt Pinole
NETWORK
TITLE = VSH/EFG Pinole
O     DB2.DBX 2.7;        // FM6107 Pinole vorne fehlt
O     DB2.DBX 8.6;        // FM6230 Reitstock Positionsueberwachung
O     DB2.DBX 8.7;        // FM6231 Reitstock hinten fehlt
O     M 25.3;             // HM BM7201 Reitstock faehrt vor
O     M 25.4;             // HM BM7202 Reitstock faehrt zurueck
O     M 25.2;             // HM BM7203 Reitstock in Zwischenstellung
=     M 146.6;            // VSH/EFG Pinole
NETWORK
TITLE = Peripherierueckmeldg. Pinole
U     E 5.0;              // Pinole hinten
=     DB20.DBX 310.3;     // Rueckmeld. Pinole hinten
U     E 5.1;              // Pinole vorne
U     T 90;               // Teil gespannt verz.
=     DB20.DBX 310.4;     // Rueckmeld. Pinole gespannt
NETWORK
TITLE = Negative DNC Quittung für Pinole
O(
U     DB20.DBX 302.4;     // DNC/PINOLE VOR
UN    DB3.DBX 0.1;        // BM7201 Reitstock faehrt vor
)
O(
U     DB20.DBX 302.3;     // DNC/PINOLE ZURÜCK
UN    DB3.DBX 0.2;        // BM7202 Reitstock faehrt zurueck
UN    M 141.4;            // HM PINOLE ZURÜCK STAT.
)
U     DB1.DBX 1366.0;     // DNC-BETRIEB AKTIV
=     DB20.DBX 318.2      // Negative DNC Quittung für Pinole
END_FUNCTION
//**************************************************************************
FUNCTION FC 100 : VOID
TITLE=INT_10MS
NAME: INT_10MS
BEGIN
NETWORK
TITLE =Vorschuboverride
// VS-Override nur von MSTT zulassen (MF2 uebergibt neg. Wert)
L     DB20.DBW 152;       // VS-Override Kanal 0
L     0;
<I    ;                   // neg. Wert?
R     DB20.DBX 1.6;       // Kommando Feed-Override verhindern
L     DB25.DBW 152;       // VS-Override Kanal 1
L     0;
<I    ;                   // neg. Wert?
R     DB25.DBX 1.6;       // Kommando Feed-Override verhindern
NETWORK
TITLE = Override fuer Referenzieren
// 1.05: akt. Feed-Override der Oberflaeche abspeichern:
UN    DB20.DBX 1.6;       // Kommando Feed-Override
SPB   nsve;
L     DB20.DBW 152;       // VS-Override Kanal 0
T     DB100.DBW 152;      // HM VS-Override Kanal 0
nsve: NOP   0;
O     DB10.DBX 57.0;      // USER_DATA_HEX[17] IB-bit fuer Ref. mit Feedoverride
ON    M 155.2;            // BA Ref
SPB   REIB;
U     DB1.DBX 134.0;      // X: Ref. err.
U     DB1.DBX 134.1;      // Z: Ref. err.
FP    M 199.4;            // Flanke X/Z referenziert
U     DB1.DBX 134.0;      // X: Ref. err.
U     DB1.DBX 134.1;      // Z: Ref. err.
ON    M 136.0;            // AUX-ON
SPB   MMM;
L     DB20.DBW 152;       // VS-Override Kanal 0
L     20;                 // 2%
>I    ;
SPB   v100;
L     0;                  // 0%
T     DB20.DBW 152;       // VS-Override Kanal 0
T     DB25.DBW 152;       // VS-Override Kanal 1
SPA   vkdo;
v100: L     1000;
T     DB20.DBW 152;       // VS-Override Kanal 0
T     DB25.DBW 152;       // VS-Override Kanal 1
vkdo: U(    ;                   // V1.05: Aenderungsbit nur wenn notwendig
L     DB20.DBW 358;       // VS-Override K0 Ist
L     DB20.DBW 152;       // VS-Override K0 Kommando
<>I   ;
)     ;
=     DB20.DBX 1.6;       // Kommando Feed-Override
=     DB25.DBX 1.6;       // Kommando Feed-Override
MMM:  NOP 0;
UN    M 199.4;            // Flanke X/Z referenziert
U     M 136.0;            // AUX-ON
SPB   BBB;
L     DB100.DBW 152;      // HM VS-Override Kanal 0
T     DB20.DBW 152;       // VS-Override Kanal 0
T     DB25.DBW 152;       // VS-Override Kanal 1
U(    ;                   // V1.05: Aenderungsbit nur wenn notwendig
L     DB20.DBW 358;       // VS-Override K0 Ist
L     DB20.DBW 152;       // VS-Override K0 Kommando
<>I   ;
)     ;
=     DB20.DBX 1.6;       // Kommando Feed-Override
=     DB25.DBX 1.6;       // Kommando Feed-Override
BBB:  NOP 0;
REIB: NOP 0;
NETWORK
TITLE =Spindeloverride
// PC-Steuerung: Override an NC uebergeben
L     DB100.DBW 128;
L     10;
*I    ;                   // Wert in Promille umrechnen
T     DB20.DBW 154;       // Spindeloverride an NC Kanal 0
T     DB25.DBW 154;       // Spindeloverride an NC Kanal 1
O     DB100.DBX 127.1;    // Fl. Taste 100%
O     DB100.DBX 127.3;    // Fl. Taste -%
O     DB100.DBX 127.5;    // Fl. Taste +%
O     M 110.2;            // Grst. OB1
=     DB20.DBX 1.7;       // Kommando Spindeloverride setzen
=     DB25.DBX 1.7;       // Kommando Spindeloverride setzen
NETWORK
TITLE =SPM-Analogwert
// wird im FC 87 eingelesen
NETWORK
TITLE =Timer freie M-FKT.
// nicht bei PC-Maschine
NETWORK
TITLE = Zaehler Reitstockposition
UN    DB10.DBX 41.2;      // USER_DATA_HEX[1] autom. Reitstock
BEB   ;
// es wurde statt Z10 ein Merkerwort verwendet, weil die Loesung
// mit Z10 bei hohen Geschwindigkeiten nicht richtig zaehlte!!
O(    ;
UN    M 120.0;            // HM Fl. B3
UN    M 120.1;            // HM Fl. B4
U     E 5.0;              // Bero Pos. 1
UN    E 5.1;              // Bero Pos. 2
)     ;
O(    ;
U     M 120.0;            // HM Fl. B3
UN    M 120.1;            // HM Fl. B4
U     E 5.0;              // Bero Pos. 1
U     E 5.1;              // Bero Pos. 2
)     ;
O(    ;
U     M 120.0;            // HM Fl. B3
U     M 120.1;            // HM Fl. B4
UN    E 5.0;              // Bero Pos. 1
U     E 5.1;              // Bero Pos. 2
)     ;
O(    ;
UN    M 120.0;            // HM Fl. B3
U     M 120.1;            // HM Fl. B4
UN    E 5.0;              // Bero Pos. 1
UN    E 5.1;              // Bero Pos. 2
)     ;
SPBN  M501;
L     MW 122;             // Reitstock Positionszaehler
L     1;
+I    ;
T     MW 122;             // Reitstock Positionszaehler
M501: NOP 0;
O(    ;
UN    M 120.0;            // HM Fl. B3
UN    M 120.1;            // HM Fl. B4
UN    E 5.0;              // Bero Pos. 1
U     E 5.1;              // Bero Pos. 2
)     ;
O(    ;
UN    M 120.0;            // HM Fl. B3
U     M 120.1;            // HM Fl. B4
U     E 5.0;              // Bero Pos. 1
U     E 5.1;              // Bero Pos. 2
)     ;
O(    ;
U     M 120.0;            // HM Fl. B3
U     M 120.1;            // HM Fl. B4
U     E 5.0;              // Bero Pos. 1
UN    E 5.1;              // Bero Pos. 2
)     ;
O(    ;
U     M 120.0;            // HM Fl. B3
UN    M 120.1;            // HM Fl. B4
UN    E 5.0;              // Bero Pos. 1
UN    E 5.1;              // Bero Pos. 2
)     ;
SPBN  M502;
L     MW 122;             // Reitstock Positionszaehler
L     1;
-I    ;
T     MW 122;             // Reitstock Positionszaehler
M502: NOP 0;
U     E 4.3;              // Reitstock hinten
FP    M 120.2;            // Fl. Reitstock hinten
U     M 120.2;            // Fl. Reitstock hinten
SPBN  M503;
L     11;
T     MW 122;             // Reitstock Positionszaehler
M503: NOP 0;
U     E 5.0;              // Bero Pos. 1
=     M 120.0;            // HM Fl. B3
U     E 5.1;              // Bero Pos. 2
=     M 120.1;            // HM Fl. B4
L     MW 122;             // Reitstock Positionszaehler
L     5;
*I    ;
L     50;
-I    ;
T     DB100.DBW 277;      // Reitstock akt. Position
NETWORK
TITLE =Umschaltung Schleichgang
L     DB100.DBW 171;      // Zielpos. vorne
L     DB10.DBW 12;        // USER_DATA_INT[6]: Schleichweg Reitstock zurueck
-I    ;
T     DB100.DBW 273;
L     DB100.DBW 277;      // Reitstock akt. Position
L     DB100.DBW 273;
>=I   ;
=     DB100.DBX 170.1;    // HM Schleichweg vor
NETWORK
TITLE =Eilgang
U     M 25.3;             // HM BM7201 Reitstock faehrt vor
UN    DB100.DBX 170.1;    // HM Schleichweg vor
U     M 136.0;            // AUX-ON
=     M 216.0;              // MV Eilgang vor
NETWORK
TITLE =Schleichgang
U(    ;
O(    ;
U     M 155.5;            // BA JOG
U     M 106.5;            // Taste Reitstock vor
)     ;
O(    ;
U     M 25.3;             // HM BM7201 Reitstock faehrt vor
UN    M 216.0;              // MV Eilgang vor
)     ;
O(    ;
UN    M 216.1;              // MV Eilgang zurueck
UN    A 4.6;              // MV Schleichgang zurueck
U     T 87;               // Verz. DS Reitstock vorne
)     ;
)     ;
U     M 136.0;            // AUX-ON
=     A 4.5;              // MV Schleichgang vor
END_FUNCTION
//**************************************************************************
FUNCTION FC 114 : VOID
TITLE=LM_STANDARD
NAME: LM_STD
BEGIN
NETWORK
TITLE =Abbruch
UN    DB10.DBX 44.0;      // USER_DATA_HEX[4] STV/LM programmierbar
R     DB2.DBX 5.0;        // FM6124 Stoerung Lademagazin
BEB   ;
NETWORK
TITLE =Vorschubbefehl
U(    ;
O     DB20.DBX 200.3;     // M67 dyn.
O(    ;
U     A 4.4;              // Vorschubbefehl
UN    DB20.DBX 200.4;     // M68 dyn.
U(    ;
ON    E 7.6;              // Start nach Stangenwechsel
ON    DB10.DBX 46.7;      // USER_DATA_HEX[6] Vorschubbef. res. mit E
)     ;
)     ;
)     ;
U     M 136.0;            // AUX-ON
U     M 155.1;            // BA AUTO
UN    DB20.DBX 324.2;     // DRY
UN    DB2.DBX 5.0;        // FM6124 Stoerung Lademagazin
UN    M 102.7;            // Reset Taste
U     E 6.2;              // Tuer zu, Antriebe ein
=     A 4.4;              // Vorschubbefehl
NETWORK
TITLE =Lader hat vorgeschoben
U     E 7.2;              // Lader hat vorgeschoben
=     DB1.DBX 1534.7;     // $A_IN[8]: Lader hat vorgeschoben
NETWORK
TITLE =Stange wechseln
U     DB20.DBX 200.5;     // M69 dyn.
UN    DB20.DBX 324.2;     // DRY
S     M 209.4;            // Stange wechseln
U(    ;
O     E 7.6;              // Start nach Stangenwechsel
O     M 102.7;            // Reset Taste
ON    M 136.0;            // AUX-ON
O     DB2.DBX 5.0;        // FM6124 Stoerung Lademagazin
)     ;
R     M 209.4;            // Stange wechseln
U     M 209.4;            // Stange wechseln
=     DB1.DBX 1534.6;     // $A_IN[7]: Stangenwechsel aktiv
NETWORK
TITLE =Stangenende
UN    E 7.5;              // Stangenende
=     DB1.DBX 1534.5;     // $A_IN[6]: Stangenende
NETWORK
TITLE = FM6124 Stoerung Lademagazin
UN    E 7.7;              // Stoerung Lademagazin
S     DB2.DBX 5.0;        // FM6124 Stoerung Lademagazin
U     M 102.7;            // Reset Taste
R     DB2.DBX 5.0;        // FM6124 Stoerung Lademagazin
U     DB2.DBX 5.0;        // FM6124 Stoerung Lademagazin
UN    DB20.DBX 46.6;      // USER_DATA_HEX[6] LM-Stoerung NC-Stop
=     DB100.DBX 0.6;      // Stoerung LM Vorschubsperre
U     DB2.DBX 5.0;        // FM6124 Stoerung Lademagazin
=     DB100.DBX 23.6;     // Stoerung LM Spindelsperre
U     DB2.DBX 5.0;        // FM6124 Stoerung Lademagazin
U     DB20.DBX 46.6;      // USER_DATA_HEX[6] LM-Stoerung NC-Stop
=     DB100.DBX 3.7;      // LM NC-Startsperre
NETWORK
TITLE =Spannmittel offen
U     DB100.DBX  167.5;   // SPM offen
=     M 209.5;            // Spannmittel offen
END_FUNCTION
//**************************************************************************
FUNCTION FC 109 : VOID
TITLE=FESTO_ROBOTIK
NAME: ROBOTIK
BEGIN
// V1.04: Landert NTiB eingebunden
NETWORK
TITLE = Abbruch
U     M 191.0;            // Robotik aktiv
SPB   M011;
L     0;
T     MB190;              // Steuersignale Robotik
T     MW192;              // Tuersignale Robotik
BEB   ;
M011: NOP   0;
NETWORK
TITLE = Eingaenge uebernehmen
U     E 8.2;              // Robotik Aux-On
UN    M 136.0;            // AUX-ON
=     M 190.0;            // Rob. Aux-On
U     E 8.3;              // Robotik Ref/Auto
FP    M 190.1;            // Fl. Ref/Auto
U     E 8.4;              // Robotik Ref. Start
FP    M 193.7;            // HM Fl. Ref. Start
U     M 193.7;            // HM Fl. Ref. Start
U     M 155.2;            // BA REF
S     M 192.0;            // Ref.-Ablauf Robotik
U     E 8.5;              // Robotik Pr. Start
U     M 155.1;            // BA AUTO
=     M 190.3;            // NC-Start Robotik
U     E 9.0;              // Robotik SPM auf
UN    E 9.1;              // Robotik SPM zu
=     M 190.4;            // SPM auf Robotik
U     E 9.1;              // Robotik SPM zu
UN    E 9.0;              // Robotik SPM auf
=     M 190.5;            // SPM zu Robotik
U     E 9.2;              // Robotik Pinole vor
UN    E 9.3;              // Robotik Pinole zurueck
=     M 193.2;            // Pinole vor Robotik
U     E 9.3;              // Robotik Pinole zurueck
UN    E 9.2;              // Robotik Pinole vor
=     M 193.3;            // Pinole zurueck Robotik
U     E 9.4;              // Robotik Tuer zu
UN    E 9.5;              // Robotik Tuer auf
O     M 192.7;            // Ref. Tuer zu
=     M 190.6;            // Tuer zu Robotik
U     E 9.5;              // Robotik Tuer auf
UN    E 9.4;              // Robotik Tuer zu
O     M 192.6;            // Ref. Tuer auf
=     M 190.7             // Tuer auf Robotik
NETWORK
TITLE = Ausgaenge zuweisen
U     E 8.1;              // Von Türsteuergerät Tür offen
O(    ;
UN    DB10.DBX 41.1;      // USER_DATA_HEX[1] Türautomatik Landert angewählt
UN    DB10.DBX 41.4;      // USER_DATA_HEX[1] Türautomatik Langer+Laumann angewählt
UN    E 6.2;              // Tuer zu, Antriebe ein
)     ;
=     M 213.5;            // Robotik Tuer offen
U     T 64;               // verz. Tuer zu
O(    ;
UN    DB10.DBX 41.1;      // USER_DATA_HEX[1] Türautomatik Landert angewählt
UN    DB10.DBX 41.4;      // USER_DATA_HEX[1] Türautomatik Langer+Laumann angewählt
U     E 6.2;              // Tuer zu, Antriebe ein
)     ;
=     M 213.4;            // Robotik Tuer zu
U     M 155.2;            // BA REF
=     M 212.6;            // Robotik Ref-Mode
U     DB100.DBX 167.5;    // HM SPM geloest
=     M 213.0;            // Robotik SPM offen
U     DB100.DBX 167.4;    // HM SPM gespannt
UN    DB100.DBX 167.6;    // HM SPM auf Endlage
=     M 213.1;            // Robotik SPM geschlossen
U     DB20.DBX 310.4;     // Rueckmeldung Pinole gespannt
=     M 213.2;            // Robotik Pinole Teil gespannt
U     DB20.DBX 310.3;     // Rueckmeldung Pinole hinten
=     M 213.3;            // Robotik Pinole hinten
NETWORK
TITLE = Umschaltung BA REF/AUTO
U     M 190.1;            // Fl. Ref/Auto
UN    M 155.2;            // BA REF
=     M 191.6;            // Rob. BA Ref anwaehlen
U     M 190.1;            // Fl. Ref/Auto
U     M 155.2;            // BA REF
=     M 191.7;            // Rob. BA Auto anwaehlen
NETWORK
TITLE = Robotik Maschine bereit
U     M 136.0;            // AUX-ON
U     M 213.5;            // Robotik Tuer offen
U     M 213.0;            // Robotik SPM geloest
U     M 212.7;            // Robotik Maschine referenziert
U     M 155.1;            // BA AUTO
U     M 155.7;            // 1x aktiv
=     M 213.6;            // Robotik Maschine bereit
NETWORK
TITLE = Robotik Status
U     DB20.DBX 224.1;     // M1 stat.
U     DB20.DBX 324.5;     // bed. Halt aktiv
O     DB20.DBX 224.0;     // M0 stat.
O     DB20.DBX 224.2;     // M2 stat.
O     DB20.DBX 227.6;     // M30 stat.
=     M 213.7;            // Robotik Programmstatus
NETWORK
TITLE = Robotik Maschine referenziert
L     DB1.DBD 70;         // Ref-Position X
L     -0.5E-03;           // 0.5mm
+R    ;
L     DB1.DBD 0;          // Istpos. X
<R    ;
=     M 191.1;            // X kollisionsfrei
L     DB1.DBD 74;         // Ref-Position Z
L     -0.5E-03;           // 0.5mm
+R    ;
L     DB1.DBD 4;          // Istpos. Z
<R    ;
=     M 191.2;            // Z kollisionsfrei
U     M 191.1;            // X kollisionsfrei
U     M 191.2;            // Z kollisionsfrei
U     DB1.DBX 134.0;      // X: Ref. err.
U     DB1.DBX 134.1;      // Z: Ref. err.
UN    DB100.DBX 37.0;     // HM WZW referenzieren
UN    DB100.DBX 37.3;     // WZW ref. aktiv
UN    DB3.DBX 1.2;        // BM7210 autom. Ref. aktiv
U(
ON    DB10.DBX 41.1;      // USER_DATA_HEX[1] Türautomatik Landert angewählt
ON    E 8.0;              // Landert Tuer nicht geeicht
)
UN    M 192.0;            // Ref.-Ablauf Robotik
=     M 212.7;            // Robotik Maschine referenziert
NETWORK
TITLE = Robotik Ref-Ablauf mit Landert Tuerantrieb
ON    DB10.DBX 41.1;      // USER_DATA_HEX[1] Türautomatik Landert angewählt
O     DB10.DBX 41.4;      // USER_DATA_HEX[1] Tuerautomatik LangerLaumann angewählt
SPB   NOTA;
UN    M 102.7;            // Reset Taste
U     M 136.0;            // AUX-ON
SPB   M081;
L     0;
T     MW192;
M081: NOP   0;
// Referenzierablauf fuer Landert NTiB
U     DB10.DBX 41.3;      // USER_DATA_HEX[1] Tuerautomatik Landert NTiB angewählt
SPBN   _MxP;
U     M 192.0;            // Ref.-Ablauf Robotik
U     E 8.0;              // Landert nicht geeicht
U     M 193.7;            // Fl. Ref. Start
S     M 192.3;            // Eichlauf zu
U     M 192.0;            // Ref.-Ablauf Robotik
UN    E 8.0;              // Landert nicht geeicht
U     M 193.7;            // Fl. Ref. Start
S     M 192.7;            // Ref. Tuer zu
U     M 192.3;            // Eichlauf zu
UN    E 8.0;              // Landert nicht geeicht
S     M 192.5;            // Eichlauf beendet
O     M 192.5;            // Eichlauf beendet
O     M 192.7;            // Ref. Tuer zu
U     M 192.0;            // Ref.-Ablauf Robotik
UN    E 8.0;              // Landert nicht geeicht
U     T 64;               // verz. Tuer zu
=     M 190.2;            // Rob. Ref. Start Achsen
U     M 190.2;            // Rob. Ref. Start Achsen
U     DB3.DBX 1.2;        // BM7210 autom. Ref. aktiv
R     M 192.0;
R     M 192.5;
R     M 192.7;
SPA   M083;
// Referenzierablauf fuer Landert MxP
_MxP: U     M 192.0;            // Ref.-Ablauf Robotik
U     E 8.0;              // Landert nicht geeicht
U     M 193.7;            // Fl. Ref. Start
S     M 192.1;            // Eichlauf auf
U     M 192.0;            // Ref.-Ablauf Robotik
UN    E 8.0;              // Landert nicht geeicht
U     M 193.7;            // Fl. Ref. Start
S     M 192.6;            // Ref. Tuer auf
U     E 8.7;              // Landert Summenstoerung 1
FP    M 191.5;            // pos. Fl. Summenstoerung 1
U     M 192.1;            // Eichlauf auf
U     M 191.5;            // pos. Fl. Summenstoerung 1
S     M 192.2;            // Eichlauf auf aktiv
U     M 192.1;
U     M 192.2;            // Eichlauf auf aktiv
UN    E 8.7;              // Landert Summenstoerung 1
S     M 192.3;            // Eichlauf zu
R     M 192.1;            // Eichlauf auf
R     M 192.2;            // Eichlauf auf aktiv
U     M 192.3;            // Eichlauf zu
U     M 191.5;            // pos. Fl. Summenstoerung 1
S     M 192.4;            // Eichlauf zu aktiv
U     M 192.3;
U     M 192.4;            // Eichlauf zu aktiv
UN    E 8.7;              // Landert Summenstoerung 1
UN    E 8.0;              // Landert nicht geeicht
S     M 192.5;            // Eichlauf beendet
R     M 192.3;            // Eichlauf zu
R     M 192.4;            // Eichlauf zu aktiv
U     M 192.6;            // Ref. Tuer auf
U     E 8.1;              // Von Türsteuergerät Tür offen
S     M 192.7;            // Ref. Tuer zu
R     M 192.6;            // Tuer auf
O     M 192.5;            // Eichlauf beendet
O     M 192.7;            // Ref. Tuer zu
U     M 192.0;            // Ref.-Ablauf Robotik
UN    E 8.0;              // Landert nicht geeicht
U     T 64;               // verz. Tuer zu
=     M 190.2;            // Rob. Ref. Start Achsen
U     M 190.2;            // Rob. Ref. Start Achsen
U     DB3.DBX 1.2;        // BM7210 autom. Ref. aktiv
R     M 192.0;
R     M 192.5;
R     M 192.7;
SPA   M083;
NOTA: UN    M 102.7;            // Reset Taste
U     M 136.0;            // AUX-ON
SPB   M082;
L     0;
T     MW192;
M082: UN    DB10.DBX 41.1;      // Option Tuerautomatik
U     M 192.0;            // Ref.-Ablauf Robotik
U     M 193.7;            // Fl. Ref. Start
U     E 6.2;              // Tuer zu, Antriebe ein
=     M 190.2;            // Rob. Ref. Start Achsen
U     M 192.0
U     DB3.DBX 1.2;        // BM7210 autom. Ref. aktiv
R     M 192.0;
M083: NOP   0;
END_FUNCTION
//**************************************************************************
FUNCTION FC 115 : VOID
TITLE=AUFFANGSCHALE
NAME: AFS
BEGIN
NETWORK
TITLE = Abbruch AFS
UN    DB10.DBX 41.0;      // USER_DATA_HEX[1] Auffangschale
R     db2.dbx 1.3;        // FM6202
R     DB2.DBX 8.2;        // FM6226
R     DB2.DBX 8.3;        // FM6227
BEB   ;
NETWORK
TITLE = Überwachung Lufteingangsventil Alarm 6104 Pneumatik Fehler
O(
U   E 16.3              // TÜR ZU von Türmodul
UN  E 16.4              // Druckschalter nach Lufteingangsventil
)
O(
UN  E 16.3              // TÜR ZU von Türmodul
U   E 16.4              // Druckschalter nach Lufteingangsventil
)
U   DB10.DBX 41.6;      // USER_DATA_HEX[1] Auffangschale mit Druckschalter
L   S5T#1S;
SE  T 13
U   T 13
S   DB2.DBX 13.0;   // 6104 Pneumatik Fehler
O   DB1.DBX 1370.2; // QUITTIERT. GEDRÜCKT (MSTT/PC)
O   DB1.DBX 1370.3; // RESET-TASTE GEDRUECKT
R   DB2.DBX 13.0;   // 6104 Pneumatik Fehler
NETWORK
TITLE = M1=24 AFS vor
U     DB20.DBX 645.0;     // M1=24 dyn.
=     DB20.DBX 709.0;     // Rueckmeldung M1=24 dyn.
S     DB100.DBX 176.1;    // HM M1=24
ON    M 136.0;            // AUX-ON
O     DB20.DBX 324.2;     // DRY
O     DB20.DBX 194.7;     // M23 dyn.
O     DB100.DBX 176.2;
//      O     DB21.DBX 1.7;       // PRT
R     DB100.DBX 176.1;    // HM M1=24
NETWORK
TITLE =Auffangschale einschwenken M24
U     DB20.DBX 195.0;     // M24 dyn.
U     E 6.2;              // Tuer zu, Antriebe ein
S     DB100.DBX 176.2;
U(    ;
ON    M 136.0;            // AUX-ON
O     DB20.DBX 324.2;     // DRY
O     DB20.DBX 194.7;     // M23 dyn.
//      O     DB21.DBX 1.7;     // PRT
)     ;
R     DB100.DBX 176.2;
NETWORK
TITLE = AFS vor
U(    ;
O     DB100.DBX 176.1;
O     DB100.DBX 176.2;
O     DB100.DBX 176.3;
O     DB100.DBX 176.4;
O     DB100.DBX 179.1;
O     DB100.DBX 179.2;
)     ;
U     M 136.0;            // AUX-ON
U     E 6.2;              // Tuer zu, Antriebe ein
=     M 216.4;              // MV AFS M24
NETWORK
TITLE =AFS zurueck
UN    M 216.4;              // MV AFS M24
U     E 7.1;              // AFS ausgeschwenkt
U     M 136.0;            // AUX-ON
U     E 6.2;              // Tuer zu, Antriebe ein
=     M 216.3;              // MV AFS M23
NETWORK
TITLE =AFS einschwenken
U(    ;
O     DB100.DBX 176.2;
O     DB100.DBX 176.3;
O     DB100.DBX 176.4;
O     DB100.DBX 179.2;
)     ;
U     E 7.4;              // AFS vorne
U     M 136.0;            // AUX-ON
U     E 6.2;              // Tuer zu, Antriebe ein
=     M 216.5;              // MV AFS einschwenken
NETWORK
TITLE =FM6202 Auffangschalenueberwachung
U     M 148.2;            // NC-Start gesamt
UN    M 155.5;            // BA-JOG
UN    DB100.DBX 176.3;
UN    DB100.DBX 176.4;
UN    DB20.DBX 324.7;     // SBL
S     DB100.DBX 176.0;
ON    E 7.3;              // AFS Position M23
O     M 102.7;            // Reset Taste
R     DB100.DBX 176.0;
U     DB100.DBX 176.0;
U     M 136.4;            // Summe Fahrbefehl
S     DB2.DBX 1.3;
NETWORK
TITLE = FM6226 Fehler AFS vorne
U     M 216.4;              // MV AFS M24
UN    E 7.4;              // AFS vorne
L     S5T#10S;
SE    T 80;
U     T 80;
S     DB2.DBX 8.2;
NETWORK
TITLE = FM6226 Fehler AFS vorne
U     M 216.3;              // MV AFS M23
U     E 7.4;              // AFS vorne
L     S5T#2S;
SE    T 81;
U     T 81;
S     DB2.DBX 8.2;
NETWORK
TITLE = FM6227 Fehler AFS eingeschwenkt
U     M 216.5;              // MV AFS einschwenken
UN    E 7.3;              // AFS Position M23
L     S5T#3S;
SE    T 82;
U     T 82;
S     DB2.DBX 8.3;
NETWORK
TITLE = FM6227 Fehler AFS eingeschwenkt
UN    M 216.5;              // MV AFS einschwenken
UN    E 7.1;              // AFS ausgeschwenkt
U     E 6.2;              // Tuer zu, Antriebe ein
UN    M 201.6;            // SS Hand/Auto
L     S5T#3S;
SE    T 83;
U     T 83;
S     DB2.DBX 8.3;
NETWORK
TITLE = FM510110/FM510111 ruecksetzen
U     M 102.7;            // Reset Taste
R     DB2.DBX 1.3;        // FM6202
R     DB2.DBX 8.2;        // FM6226
R     DB2.DBX 8.3;        // FM6227
END_FUNCTION
//**************************************************************************
FUNCTION FC  2 : VOID
TITLE=STUECKZAEHLER
NAME: STUECKZ
BEGIN
NETWORK
TITLE = STUECKZAEHLER (aus Simulations-PLC uebernommen)
UN    DB20.DBX 192.2;
UN    DB20.DBX 195.6;
O     DB20.DBX 324.2;
SPB   M214;
L     DB15.DBD 4;
L     DW#16#000F_423F;
<>D   ;
SPB   M211;
L     DW#16#0000_0000;
T     DB15.DBD 4;
M211: NOP 0 ;
L     DB15.DBD 4;
L     DW#16#0000_0001;
+D    ;
T     DB15.DBD 4;
L     DB15.DBD 8;
L     DW#16#000F_423F;
<>D   ;
SPB   M213;
L     DW#16#0000_0000;
T     DB15.DBD 8;
M213: NOP 0 ;
L     DB15.DBD 8;
L     DW#16#0000_0001;
+D    ;
T     DB15.DBD 8;
M214: L     DB15.DBD 0;
L     DW#16#0000_0000;
<>D   ;
SPB   M202;
SPA   M201;
M202: NOP 0 ;
U     DB1.DBX 1402.7;     // Steuerungskennung SINUMERIK 840D
SPB   M201;
L     DB15.DBD 4;
L     DB15.DBD 0;
<D    ;
SPB   M201;
U     M 110.1;            // "1"
S     DB3.DBX 5.3;        // BM7043 Sollstueckzahl erreicht
L     DW#16#0;
T     DB15.DBD 4;
M201: NOP   0;
END_FUNCTION
//**************************************************************************
FUNCTION FC 10 : VOID
TITLE=E_SIMULATOR
NAME: E_SIM
BEGIN
NETWORK
TITLE = Eingangsbehandlung fuer Laborsimulator
// Behandlung allg. Eingaenge
SET;
=     E 2.1;              // Nist=Nsoll HA
=     E 2.3;              // Nist=Nsoll AWZ
=     E 4.0;              // Schwimmerschalter Hydraulik
=     E 4.2;              // Schwimmerschalter SM
=     E 5.6;              // therm. Stoerung Programmende
=     E 5.7;              // therm. Stoerung sofort
=     E 7.0;              // Not-Aus
=     DB1.DBX 1414.0;     // 1. CAN Eingangsport
=     DB1.DBX 1414.1;     // 2. CAN Eingangsport
=     DB1.DBX 1414.2;     // 3. CAN Eingangsport
=     DB1.DBX 1415.0;     // 1. CAN Ausgangsport
=     DB1.DBX 1415.1;     // 2. CAN Ausgangsport
=     DB1.DBX 1415.1;     // 3. CAN Ausgangsport
// Simulation Tuer auf/zu ueber PC-Tastatur
U     DB1.DBX 1374.2;     // globale Taste Tuer auf/zu
FP    M 0.0;              // Fl. Tuertaste
U     M 0.0;              // Fl. Tuertaste
UN    M 0.1;              // HM Tuerzustand
O     M 110.2;            // Grst.
S     M 0.1;
SPB   M011;
U     M 0.0;              // Fl. Tuertaste
U     M 0.1;              // HM Tuerzustand
R     M 0.1;
M011: U     M 0.1;              // HM Tuerzustand
=     E 6.2;              // Tuer zu / Antriebe ein
// Behandlung Antriebssignale
U     M 136.0;            // AUX-ON
=     E 5.5;              // E/R-Modul ready
=     M 201.0;            // Ready X
=     M 201.1;            // Ready Z
=     M 201.2;            // Ready AWZ
=     M 201.3;            // Ready HA
UN    M 137.7;            // Antriebe ein
=     E 6.0;              // AS-Kette
=     E 6.1;              // Ueberwachungskontakt K11
// Behandlung Schmiermittel
U     A 4.7;              // Leistg. SM
=     E 4.1;              // DS SM
// Behandlung n=0
U     DB1.DBX 1356.2;     // AWZ steht NC
=     E 2.2;              // n<nmin AWZ
U     DB1.DBX 1356.3;     // Spindel steht NC
=     E 2.0;              // n<nmin HA
UN    M 210.6;            // n=0 von NC
=     E 4.7;              // n=0 von Bedienmodul
// Behandlung Auffangschale
UN    M 216.5;              // AFS einschwenken
=     E 7.1;              // AFS ausgeschwenkt
U     M 216.5;
=     E 7.3;              // AFS Pos. M23
U     M 216.4;              // AFS M24
=     E 7.4;              // AFS vorne
// Behandlung Pinole
U     A 4.6;              // MV Pinole zurueck
S     M 0.4;              // HM Pinole hinten
R     M 0.5;              // HM Pinole vorne
O     A 4.5;              // Pinole vor Schleichgang
O     M 216.0;              // Pinole vor Eilgang
S     M 0.5;              // HM Pinole vorne
R     M 0.4;              // HM Pinole hinten
U     M 0.4;              // HM Pinole hinten
L     S5T#600MS;          // simulierte Verfahrdauer
SE    T 5;
U     T 5;
=     E 5.0;              // Pinole hinten
U     M 0.5;              // HM Pinole vorne
L     S5T#1S;             // simulierte Verfahrdauer
SE    T 4;
U     T 4;
=     E 5.1;              // Pinole vorne
=     E 5.2;              // DS Reitstock Teil gespannt
// Simulation WZW
U     M 211.6;            // WZW Start
L     S5T#600MS;          // simulierte Schwenkzeit
SV    T 2;
UN    T 2;
=     E 6.3;              // WZW verriegelt
=     E 6.4;              // WZW geschwenkt
;// Simulation Spindelbremse
;      U     A 4.3;              // MV Spindelbremse
;      =     M 200.3;            // Bero Spindelbremse
NETWORK
TITLE = Tastenbehandlung fuer PC-Tastatur
U     DB1.DBX 1374.4;     // globale Taste Hilfsantr. ein
=     M 107.4;            // Aux-On Taste
S     M 136.0;            // AUX-ON
U     M 136.1;            // Fl. Aux-On
S     M 0.2;              // autom. Ref. starten
UN    M 137.3;            // Referenzpunkt nicht erreicht
UN    DB3.DBX 1.3;        // BM7211 Refpunkt Wender fehlt
//      O     M 102.7;            // Reset-Taste
ON    M 136.0;            // AUX-ON
R     M 0.2;              // autom. Ref. starten
U     M 0.2;              // autom. Ref. starten
U     M 155.2;            // BA REF.
L     S5T#300MS;          // Verzoegerung autom. Ref.
SS    T 3;
U     T 3;
=     DB20.DBX 8.0;       // Kanalachse X referenzieren
=     DB20.DBX 8.2;       // Kanalachse Z referenzieren
U     DB1.DBX 1374.3;     // globale Taste Hilfsantr. aus
=     M 107.6;            // Aux-Off Taste
U     DB1.DBX 1370.3;     // Reset-Taste gedrueckt
=     M 102.7;            // Reset-Taste
U     DB20.DBX 294.6;     // 1x-Taste (KM/Ausblasen)
=     M 101.7;            // 1x Taste
U     M 0.2;              // autom. Ref. starten
U     M 155.2;            // BA REF.
O     DB20.DBX 294.5;     // PLC-Taste WZW schwenken
=     M 105.2;            // WZW Taste
U     DB20.DBX 295.1;     // VS-Halt
=     M 104.4;
U     DB20.DBX 295.2;     // VS-Start
=     M 104.2;
U     DB20.DBX 295.3;     // Spindel-Halt
=     M 104.1;
U     DB20.DBX 295.4;     // Spindel-Start
=     M 107.7;
END_FUNCTION
//**************************************************************************
FUNCTION FC 11 : VOID
TITLE=ACC_ANA
NAME: ACC_ANA
BEGIN
NETWORK
TITLE = Analogwerte bei ACC einlesen
U     DB1.DBX 1370.0;     // 1. PLC-Durchlauf
S     M 199.1;            // AD Schritt 1
SPBN  _AS1;
L     4;                  // Kanal 2 anwaehlen
T     AB 12;              // Ansteuerung AD-Wandler
_AS1: UN    M 199.1;
SPB   _AS2;
U     M 199.1;            // AD Schritt 1
R     M 199.1;            // AD Schritt 1
S     M 199.2;            // AD Schritt 2
SPA   _AE1;
_AS2: UN    M 199.2;            // AD Schritt 2
SPB   _AW1;
U     M 199.2;            // AD Schritt 2
S     A 12.5;             // AD Hold
R     M 199.2;            // AD Schritt 2
S     M 199.3;            // AD Schritt 3
SPA   _AE1;
_AW1: UN    M 199.3;            // AD Schritt 3
SPB   _AE1;
U     A 12.2;             // AD Kanal Bit 0
UN    A 12.3;             // AD Kanal Bit 1
SPBN  _AW2;
L     EW 14;              // AD-Wert
L     1023;
UW;
T     DB1.DBW 1472;       // ACIF AIN 1
L     8;                  // Kanal 3 anwaehlen
T     AB 12;
SPA   _AW4;
_AW2: UN    A 12.2;             // AD Kanal Bit 0
U     A 12.3;             // AD Kanal Bit 1
SPBN  _AW3;
L     EW 14;              // AD-Wert
L     1023;
UW;
T     DB1.DBW 1474;       // ACIF AIN 2
L     12;                 // Kanal 4 anwaehlen
T     AB 12;
SPA   _AW4;
_AW3: U     A 12.2;             // AD Kanal Bit 0
U     A 12.3;             // AD Kanal Bit 1
SPBN  _AW4;
L     EW 14;              // AD-Wert
L     1023;
UW;
T     DB1.DBW 1476;       // ACIF AIN 3
L     4;
T     AB 12;
_AW4: U     M 199.3;            // AD Schritt 3
R     A 12.5;             // AD Hold
R     M 199.3;            // AD Schritt 3
S     M 199.1;            // AD Schritt 1
_AE1: NOP   0;
END_FUNCTION
