[Info]
Version=3.23
CheckChange=803c53c1
PLCFileName=Mill55.awl
//   ***********************************************************************
//   *                                                                     *
//   *                   SPS-PROGRAMM FÜR DIE PCM55                        *
//   *               AB VERS. 3.00    AC95 und ACC                         *
//   *                                                                     *
//   *      Designed by Friedrich Schörghofer          19.04.2002          *
//   *                                                                     *
//   ***********************************************************************
//   INHALTSVERZEICHNIS:
//
//      OB  1   ORGANISATIONSBAUSTEIN
//      FC  0   WERKZEUGWENDER SIMULIEREN
//      FC  1   INITIALISIERUNGEN
//      FC  2   STÜCKZÄHLER
//      FC  3   HAUPTANTRIEB FELDERER FU
//      FC 33   HAUPTANTRIEB LENZE FU
//      FC  4   BETRIEBSARTEN
//      FC  5   ACHSEN BEREITSCHAFT
//      FC  6   ACHSEN JOG,INC
//      FC  7   ACHSEN REFERENZIEREN
//      FC  8   AUX_ON AC95
//      FC 88   AUX_ON AC95/AC88 UMBAU AUF ACC
//      FC  9   AUX_ON ACC
//      FC 10   BEI PROGRAMM ENDE RESET ODER NEUSTART
//      FC 11   NC-START UND NC-STOP VON M0 ODER M1
//      FC 12   AFG/EFG
//      FC 13   NC-STARTVERR. UND BEDIENTASTEN
//      FC 14   ALARMSTATUS
//      FC 16   SPS->OBERFLÄCHEN SIGNALE
//      FC 17   TÜRAUTOMATIK
//      FC 18   EL. SCHRAUBSTOCK
//      FC 21   TEILAPPARAT
//      FC 22   AUSBLASEN
//      FC 23   SICHERHEITSSCHALTUNG ACC
//      FC 26   KÜHLMITTEL (M8/M9)
//      FC 30   PNEUM.SPANNMITTEL
//      FC 32   AUTOMATISCH IN BA REF UMSCHALTEN
//      FC 34   ZUWEISEN DER EINGÄNGE AC95 - ACC
//      FC 35   ZUWEISEN DER AUSGÄNGE AC95 - ACC
//      FC 40   ROBOTIK SCHNITTSTELLE
//      FC 41   RENISHAW MESSTASTER
//      FC 49   WERKZEUGPOSITIONEN INS MILL55_ACC.MSD ÜBERNEHMEN
//      FC 50   WERKZEUGWENDER
//      FC 51   WERKZEUGSPANNSYSTEM
//      FC 52   WERKZEUGPOSITION AUS DEN SETTINGDATEN SETZEN
//      FC 53   NEUES T-WORT ÜBERPRÜFEN UND NEUE WZW-SCHEIBENPOSITION FESTLEGEN
//      FC 54   WERKZEUG 1 ZURÜCKGEBEN
//      FC 55   WERKZEUG 0 ODER KEIN WERKZEUG ZURÜCKGEBEN
//      FC 56   WERKZEUG 2 BIS 8 ZURÜCKGEBEN
//      FC 57   WERKZEUG 1 AUFNEHMEN
//      FC 58   WERKZEUG 0 AUFNEHMEN
//      FC 59   WERKZEUG 2 BIS 8 AUFNEHMEN
//      FC 60   Z-ACHSE AUF ZIELPOSITION FAHREN
//      FC 61   A-ACHSE AUF ZIELPOSITION FAHREN
//      FC 62   LED ANSTEUERUNG
//      FC 63   WARNLEUCHTE
//      FC 64   M0 AUSLÖSEN BEI TXX UND CAMCONCEPT OBERFLÄCHE
//      FC 65   HANDRAD
//      EINSTELLBIT´S FÜR DIE INBETRIEBNAHME UND SERVICE:
//      M 200.0  = 0    ZUM EINSTELLEN DES WZW REFERENZPUNKTES
//      M 200.0  = 1    WZW-ACHSE FÄHRT NACH DEM REFERENZIEREN AUF 0 GRAD
//                      DIE WERKZEUGPOSITIONEN EINSTELLEN UND IN DAS MSD-FILE ÜBERNEHMEN
//                      DIE POSITION FÜR Z-ACHSE T1-T8 AUSDRÜCK UND EINZIEPOSITION ÜBERNEHMEN
//                      WERKZEUG EINZIEHEN UND AUSDRÜCKEN MIT "CTR ^" ERMÖGLICHEN
//      M 300.0         TÜR AUTOMATISCH AUF/ZU ÜBER PLC
//
//
//  EASY CYCLE-  WINNC- PC-Tastenbelegung MILL/TURN55:
//
//  ALT U        CTR ^   DB20.DBX 294.2   FUTTER AUF/ZU
//  ALT K        CTR 1   DB20.DBX 294.5   WZW EINE POSITION WEITERSCHWENKEN TURN55
//  ALT K        CTR 1   DB20.DBX 294.5   WERKZEUG AUSDRÜCKEN/EINZIEHEN MILL55
//  ALT O        CTR 2   DB20.DBX 294.6   AUSBLASEN EIN/AUS  (M71-EIN,M72-AUS)
//  ALT O        CTR 2   DB20.DBX 294.6   MINIMALMENGENKÜHLUNG EIN/AUS A 3.5 (M7-EIN,M9-AUS)
//                                        (NUR BEI NICHT AKTIVIERTER AUSBLASEINRICHTUNG)
//  ALT I        CTR 3   DB20.DBX 294.1   TEILAPPARAT EINE TEILUNG WEITERSCHWENKEN (M27)
//  ALT X        CTR 4   DB20.DBX 295.1   VORSCHUB HALT
//  ALT C        CTR 5   DB20.DBX 295.2   VORSCHUB START
//  ALT V        CTR 6   DB20.DBX 295.3   SPINDEL HALT
//  ALT B        CTR 7   DB20.DBX 295.4   SPINDEL START
//  ALT N        CTR 8   DB1.DBX 1374.4   HILFSANTRIEBE EIN
//  ALT H        CTR 9   DB20.DBX 294.4   SCHRAUBSTOCK/PINOLE VOR    (M26)
//  ALT J        CTR 0   DB20.DBX 294.3   SCHRAUBSTOCK/PINOLE ZURÜCK (M25)
//  ALT M        CTR ß   DB1.DBX 1374.3   HILFSANTRIEBE AUS
//  ALT P        CTR ´   DB1.DBX 1374.2   MASCHINENTÜRE AUF/ZU
//
//******************   Ein und Ausgänge für PCM 55 ACC   *************************
//
//  Gilt für beide Schrittmotormodule:
//
//          A 3.7    Freigabe Differenzialleitungstreiber Takte für SM
//
//
//  1.1 Steckerbelegung für 1. Schrittmotormodul Y5A011000
//
//
//  (-A111)
//
//  X3:1    E 0.0    Servo Ready X ( pys.Achse 0 )
//  X3:2             DIR X
//  X3:3             DIR X/
//  X3:4             CK  X
//  X3:5             CK  X/
//  X3:6    E 0.2    Servo Ready Z ( pys.Achse 1 )
//  X3:7             DIR Z
//  X3:8             DIR Z/
//  X3:9             CK  Z
//  X3:10            CK  Z/
//  X3:11   E 0.1    Servo Ready Y ( pys.Achse 2 )
//  X3:12            DIR Y
//  X3:13            DIR Y/
//  X3:14            CK  Y
//  X3:15            CK  Y/
//  X3:16   A 3.0    = M 17.0         // FREIGABE ACHSEN A
//
//  1.2 Steckerbelegung für 2. Schrittmotormodul Y5A011000
//  für Werkzeugwender MILL 55
//
//  (-A112)
//
//  X3:1 (   E 0.3    Servo SERVO READY HA    pys.Achse 3 )
//  X3:2 (            Diese Leitung wird auf der Schrittmotorkarte aufgetrennt damit  )
//  X3:3 (            es zu keinem Kurzschluss mit dem HA Sollwertmodul kommt   )
//  X3:4
//  X3:5
//  X3:6    E 0.5    Servo Ready Werkzeugwender ANTRIEB  ( pys.Achse 4 )
//  X3:7             DIR Z
//  X3:8             DIR Z/
//  X3:9             CK  Z
//  X3:10            CK  Z/
//  X3:11   E 0.4    Servo Ready Rundachs ANTRIEB        ( pys.Achse 5 )
//  X3:12            DIR Y
//  X3:13            DIR Y/
//  X3:14            CK  Y
//  X3:15            CK  Y/
//  X3:16   A 3.1    NUR ACC          // FREIGABE ACHSEN B
//
//
//  2. Steckerbelegung SOLLWERT-MODUL Y5A013000
//
//  (-A114)
//
//  X3:1             Drehfeldfrequenz
//  X3:2             GND
//  X3:3             // SOLLWERT 0-10V
//  X3:4             // RICHTUNG
//  X3:5
//  X3:6             +24V
//  X3:7    E 0.3    = M 15.7        // SERVO READY HA ( pys.Achse 3 )
//  X3:8
//  X3:9
//  X3:10
//  X3:11   A 0.3    = M 17.1        // REGLERFREIGABE HAUPTANTRIEB
//  X3:12   A 11.5
//  X3:13   A 11.6
//  X3:14
//  X3:15            GND
//
//
//  3. Ein und Ausgangsbelegung auf REF-Modul Y5A017000
//
//  (-A131)
//
//  X5:1    E16.0    REF1              // REF Schalter X-Achse
//  X5:2    E16.1    REF2              // REF Schalter Y-Achse
//  X5:3    E16.2    REF3              // REF Schalter Z-Achse
//  X5:4    E16.3    REF4              // REF BERO A-RUNDACHSE
//  X5:5    E16.4    REF5              // REF BERO WZW
//  X5:6    E17.0    SYNC1             // SYNC BERO X-Achse
//  X5:7    E17.1    SYNC2             // SYNC BERO Y-Achse
//  X5:8    E17.2    SYNC3             // SYNC BERO Z-Achse
//  X5:9    E17.5    SYNC4             // SYNC BERO A-RUNDACHSE
//  X5:10   E17.4    SYNC5             // SYNC BERO WZW-Achse
//  X5:11   E 2.3    = M 15.6;         // nur TURN!! RÄDERDECKELENDSCHALTER (IN SERIE MIT NOT-AUS!)
//  X6:1    E 2.0    = M 15.0          // 1.TürendschalterMASCHINENTÜR ZU (HAUPTMOTORSCHÜTZ EIN)
//                                        Überwachung ob HA-Schütze abgefallen sind (muß 0-Signal
//                                        bei offener Tür oder Räderdeckel oder gedrücktem NOT-AUS sein)
//  X6:2    E 2.1    = M 15.1          // 1.Türendschalter MASCHINENTÜR OFFEN
//                                       (0 Signal bei geschlossener Tür!)
//  X6:3    E 2.2    = M 15.2          // NOT AUS
//  X6:4    E 2.4    = M 16.0          // n=0 RELAIS VON LENZE-FU
//  X6:5    E 2.5    EIN 24V (nur ACC) // 2.Türendschalter MASCHINENTÜR ZU 2.Türendschalter
//  X6:6    E 2.6    EIN 24V
//  X6:7    E 2.7    EIN 5V
//  X6:8    E 3.0    EIN 5V
//  X6:9    A 3.5    AUS 24V (nur ACC) // AKTIVIEREN RENISHAW MESSTASTER MI 12 INTERFACE
//  X6:10   A 3.4    AUS 24V (nur ACC) // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
//  X6:11   A 3.3    AUS 24V           // KÜHLMITTEL (M8=EIN/M9=AUS)
//
//
//  4. Pinbelegung auf EINGANGS-Modul Y5A018000
//
//  (-A132)
//
//  X5:1    E 4.0      // WERKZEUG EINGEZOGEN
//  X5:2    E 4.1      // WERKZEUG AUSGEDRÜCKT
//  X5:3    E 4.2      // 12mm-BERO WERKZEUG BESTÜCKT
//  X5:4    E 4.3
//  X5:5    E 4.4      // TÜR OFFEN TÜRAUTOMATIK
//  X5:6    E 4.5      // DRUCKSCHALTER SCHRAUBSTOCK
//  X5:7    E 4.6
//  X5:8    E 4.7      SCHÄFER Teilapparat teilen fertig
//  X5:9    E 5.0      Robotik Tür schließen
//  X5:10   E 5.1      Robotik Tür öffnen
//  X5:11   E 5.2      Robotik Schraubstock öffnen
//  X6:1    E 5.3      Robotik Schraubstock schließen
//  X6:2    E 5.4      Handrad Takteingang (in MSD-File: PLCHandWheelInput0=5.4 )
//  X6:3    E 5.5      Handrad Richtungseingang
//  X6:4    E 5.6      Robotik Programm starten
//  X6:5    E 5.7      Robotik Vorschub halt
//  X6:6    E 6.0      Werkzeugeinzug oben
//  X6:7    E 6.1      Werkzeugeinzug unten
//  X6:8    E 6.2      STATUS RENISHAW MESSTASTER MI 12 INTERFACE
//  X6:9    E 6.3      \STATUS RENISHAW MESSTASTER MI 12 INTERFACE
//  X6:10   E 6.4      KEIN FEHLER RENISHAW MESSTASTER MI 12 INTERFACE
//  X6:11   E 6.5      LOW BATT RENISHAW MESSTASTER MI 12 INTERFACE
//
//
//
//  5. Pinbelegung auf AUSGANGS-Modul Y5A019000
//
//  (-A134)
//
//  X5:1    A 4.0    = M 18.0   // MINIMALSCHMIERUNG  (M7=EIN/M9=AUS)
//  X5:2    A 4.1    = M 18.1
//  X5:3    A 4.2    = M 18.2   // AUSBLASVENTIL
//  X5:4    A 4.3    = M 18.3   // TÜR AUF
//  X5:5    A 4.4    = M 18.4   // TÜR ZU
//  X5:6    A 4.5    = M 18.5   // SCHRAUBSTOCK VOR
//  X5:7    A 4.6    = M 18.6   // SCHRAUBSTOCK ZURÜCK
//  X5:8    A 4.7    = M 18.7   // TEILAPPARAT TEILEN
//  X5:9    A 5.0    = M 19.0   // ROBOTIK PROGRAMM STOP (M30,M0,M1,M2)
//  X5:10   A 5.1    = M 19.1   // ROBOTIK ACHSEN STEHEN AUF REF.PKT.
//  X6:1    A 5.2    = M 19.2
//  X6:2    A 5.3    = M 19.3   // ROBOTIK TÜR OFFEN
//  X6:3    A 5.4    = M 19.4   // ROBOTIK TÜR GESCHLOSSEN
//  X6:4    A 5.5    = M 19.5   // ROBOTIK SCHRAUBSTOCK HINTEN
//  X6:5    A 5.6    = M 19.6   // ROBOTIK SCHRAUBSTOCK GESPANNT
//  X6:6    A 5.7    = M 19.7   // ROBOTIK ALARMAUSGANG
//  X6:7    A 6.0    NUR ACC    // WERKZEUGSPANNMOTOR
//  X6:8    A 6.1    NUR ACC    // WERKZEUGKEGEL ABBLASEN
//  X6:9    A 6.2
//  X6:10   A 6.3
//
//
//******************   Ein und Ausgänge für PCM 55 AC95   *************************
//                 mit LENZE-FU und 3-Phasen Schrittmotore
//
//  1.Eingangsbelegung auf der AC-Steckerplatine Y4A091000 Hutschiene fremd FU:
//    (Axiscontroller Y4A 080 000 für Hutschiene)
//
//  X113.1          +24V
//  X113.2          GND
//  X113.3  E 0.0   Referenzschalter Achse 0 (X)
//
//  X115.1          +24V
//  X115.2          GND
//  X115.3  E 0.1   Referenzschalter Achse 1 (Y)
//
//  X117.1          +24V
//  X117.2          GND
//  X117.3  E 0.2   Referenzschalter Achse 2 (Z)
//
//          E 0.3   Referenzpunkt Achse 3 (HA)
//
//  X114.3          +24V (X) Bero
//  X114.3          GND (X) Bero
//  X114.3  E 0.4   Sync Achse 0 (X) Bero
//
//  X116.3          +24V (Y) Bero
//  X116.3          GND (Y) Bero
//  X116.3  E 0.5   Sync Achse 1 (Y) Bero
//
//  X118.3          +24V (Z) Bero
//  X118.3          GND (Z) Bero
//  X118.3  E 0.6   Sync Achse 2 (Z) Bero
//
//  X109.1          +5V
//  X109.2          GND
//  X109.3          frei
//  X109.4  E 0.7
//  X109.5
//
//          A 3.7    Freigabe Differenzialleitungstreiber Takte für SM
//  Flachbandkabelstecker zur Schrittmotorplatine 16pol. :
//  X104.1  E 1.0 // E 0.0 = M 15.3; // SERVO READY ACHSE 0  (X)
//  X104.6  E 1.1 // E 0.1 = M 15.4; // SERVO READY ACHSE 0  (Y)
//  X104.14 E 1.2 // E 0.2 = M 15.5; // SERVO READY ACHSE 0  (Z)
//  X104.16         Servo Enable (über X110.4 von X121.3 = A 0.1)
//
//
//  X111.1  E 1.5 // E 2.0 = M 15.0  // MASCHINENTÜR ZU (HAUPTMOTORSCHÜTZ EIN)
//                                   Überwachung ob HA-Schütze abgefallen sind (muß 0-Signal
//                                   bei offener Tür oder Räderdeckel oder gedrücktem NOT-AUS sein)
//  X111.2          GND
//  X111.3  E 1.6 // E 2.1 = M 15.1  // MASCHINENTÜR OFFEN
//                                   (0 Signal bei geschlossener Tür!)
//  X111.4  E 1.7 // E 2.3 = M 15.6; // RÄDERDECKELENDSCHALTER (IN SERIE MIT NOT-AUS!)
//
//  X110.1  E 1.4 // E 2.2 = M 15.2  // NOT AUS
//  X110.2          GND
//  X110.3  E 2.0   Reserveeingang
//  X110.4          Servoenable ( Signal kommt von X121.3 = A0.1). Dieser Eingang
//                  wird über den 16pol. Flachbandkabelstecker X104 Pin16 an die
//                  SM-Karte geleitet und gibt dort die Endstufen frei (funktionirt
//                  nur mit neuer SM-Karte Y4A031000).
//
//  X101.1  +5V
//  X101.2  GND
//  X101.3  E 2.1
//
//  X102.1  +5V
//  X102.2  GND
//  X102.3  E 2.2
//
//  X103.1  +24V
//  X103.2  GND
//  X103.3  E 2.3 // E 2.4 = M 16.0  // n=0 RELAIS VON LENZE-FU
//
//  X105.1          +24V
//  X105.2          GND
//  X105.3  E 2.4   Renishaw Messtaster (Sondermaschine für USA,Eingang nur für NC)
//
//          E 2.5   Sync X (für Monitoranzeige zum Einstellen der Refpkt.Schalter)
//          E 2.6   Sync Y (für Monitoranzeige zum Einstellen der Refpkt.Schalter)
//          E 2.7   Sync Z (für Monitoranzeige zum Einstellen der Refpkt.Schalter)
//
//  2. Ausgangsbelegung auf der AC-Steckerplatine Y4A081000:
//
//  X1070.1         Drehfeldfrequenz analog
//  X1070.2         GND
//  X1070.3         Sollwert analog
//  X1070.6         +24V
//  X1070.7 E 1.3 // E 0.3 = M 15.7  // SERVO READY HA
//
//  X107.2  A 0.0 // A 0.3 = M 17.1  // REGLERFREIGABE HAUPTANTRIEB
//  X107.6  RE/LI  (0/1)Drehrichtung Hauptantrieb
//  X107.7           GND
//
//  X121.1  +24V
//  X121.2  GND
//  X121.3  A 0.1 // A 3.0 = M 17.0  // FREIGABE ACHSEN
//                                  (AC95: wird mit X110.4 verbunden - Freigabe
//                                        der Schrittmotorendstufen)
//
//  X120.1          +24V
//  X120.2          GND
//  X120.3  A 0.2   Reserveausgang
//
//  X123.1          +24V
//  X123.2          GND
//  X123.3  A 0.3   Reserveausgang
//
//  X122.1          +24V
//  X122.2          GND
//  X122.3  A 0.4 // A 3.3 = M 17.2  // KÜHLMITTEL (M8=EIN/M9=AUS)
//
//  3. Ein/Ausgangsbelegung auf der CANBUS I/O Platine Y4A029000:
//
//  Eingangsbyte 4,5
//  X201.1  E 4.0   8****** NUR AC95 EL.SCHRAUBSTOCK “Kein Teil gespannt“
//  X201.2  E 4.1   8****** NUR AC95 EL.SCHRAUBSTOCK offen
//  X201.3  E 4.2   8****** NUR ACC  12mm-BERO WERKZEUG BESTÜCKT
//  X201.4  E 4.3
//  X201.5  E 4.4   // TÜR OFFEN TÜRAUTOMATIK
//  X201.6  E 4.5   8****** ACC/AC95 EL.SCHRAUBSTOCK TEIL GESPANNT (Signal von Spannmittelplatine)
//                  8****** ACC/AC95 Pneum. SCHRAUBSTOCK DRUCKSCHALTER
//  X201.7  E 4.6
//  X201.8  E 4.7   SCHÄFER Teilapparat teilen fertig
//  X201.9          +24V Versorgung
//  X201.10         GND Versorgung
//
//
//  X301.1  E 5.0   Robotik Tür schließen
//  X301.2  E 5.1   Robotik Tür öffnen
//  X301.3  E 5.2   Robotik Schraubstock öffnen
//  X301.4  E 5.3   Robotik Schraubstock schließen
//  X301.5  E 5.4   Robotik
//  X301.6  E 5.5   Robotik
//  X301.7  E 5.6   Robotik Programm starten
//  X301.8  E 5.7   Robotik Vorschub halt
//  X301.9          +24V Versorgung
//  X301.10         GND Versorgung
//
//
//  Ausgangsbyte 4,5
//  X501.1  A 4.0 // A 4.0  = M 18.0   MINIMALSCHMIERUNG
//  X501.2  A 4.1 // A 4.1  = M 18.1
//  X501.3  A 4.2 // A 4.2  = M 18.2   AUSBLASVENTIL
//  X501.4  A 4.3 // A 4.3  = M 18.3   TÜR AUF
//  X501.5  A 4.4 // A 4.4  = M 18.4   TÜR ZU
//  X501.6  A 4.5 // A 4.5  = M 18.5   SCHRAUBSTOCK SPANNEN
//  X501.7  A 4.6 // A 4.6  = M 18.6   SCHRAUBSTOCK ENTSPANNEN
//  X501.8  A 4.7 // A 4.7  = M 18.7   TEILAPPARAT TEILEN
//  X501.9          +24V Versorgung
//  X501.10         GND Versorgung
//  X502.1  A 5.0 // A 5.0  = M 19.0   ROBOTIK PROGRAMM STOP (M30,M0,M1,M2)
//  X502.2  A 5.1 // A 5.1  = M 19.1   ROBOTIK ACHSEN STEHEN AUF REF.PKT.
//  X502.3  A 5.2 // A 5.2  = M 19.2
//  X502.4  A 5.3 // A 5.3  = M 19.3   ROBOTIK TÜR OFFEN
//  X502.5  A 5.4 // A 5.4  = M 19.4   ROBOTIK TÜR GESCHLOSSEN
//  X502.6  A 5.5 // A 5.5  = M 19.5   ROBOTIK SCHRAUBSTOCK HINTEN
//  X502.7  A 5.6 // A 5.6  = M 19.6   ROBOTIK SCHRAUBSTOCK GESPANNT
//  X502.8  A 5.7 // A 5.7  = M 19.7   ROBOTIK ALARMAUSGANG
//  X502.9          +24V Versorgung
//  X502.10         GND Versorgung
//
//
//
//   SET; = 1
//   CLR; = 0
//
//   TIMER:
//   SI = IMPULS
//        DIE MAXIMALE IMPULSLÄNGE ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DIE MINIMALE IMPULSLÄNGE ENTSPRICHT DER LÄNGE DES EINGANGSSIGNALES
//
//   SV = IMPULS (VERLÄNGERT)
//        DIE IMPULSLÄNGE ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DIE DAUER DES EINGANGSSIGNALES HAT KEINEN EINFLUSS
//
//   SE = EINSCHALTVERZÖGERUNG
//        DIE VERZÖGERUNGSDAUER ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DAS EINGANGSSIGNAL MUSS MINDESTENS SOLANGE ANLIEGEN
//
//   SS = EINSCHALTVERZÖGERUNG (SPEICHERND)
//        DIE VERZÖGERUNGSDAUER ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DIE DAUER DES EINGANGSSIGNALES HAT KEINEN EINFLUSS
//
//   SA = AUSSCHALTIMPULS
//        DIE IMPULSLÄNGE ENTSPRICHT DER PROGRAMMIERTEN ZEIT
//        DER TIMER WECHSELT VON "0"AUF "1" UND DIE ZEIT WIRD GESTARTET
//            WENN DAS EINGANGSSIGNAL VON "1" AUF "0" GEHT.
//
// ZEITBASIS     BELEGUNG DES ZEITWORTES
//               16......8  7.......0
//    0,01 S     0000 0000  0000 0000
//    0,1  S     0001 0000  0000 0000
//    1    S     0010 0000  0000 0000
//   10    S     0011 0000  0000 0000
//                     H     Z     E   = ZEITWERT IM BCD-FORMAT
//                 00                  = BINÄRCODE FÜR ZEITBASIS
FUNCTION FC 64 : VOID
NAME: M0 AUSLÖSEN BEI TXX UND CAMCONCEPT OBERFLÄCHE
BEGIN
U   DB20.DBX 182.0; // NEUES T-WORT
SPBN M001;
L   DB20.DBW 184;   // T-WORT
L   DB20.DBW 356;   // AKTIVES WERKZEUG IN DB20
<>I
SPBN M001;
S   M 12.0
M001:NOP 0;
U   M 12.0
FP  M 12.1
U   M 12.1
SPBN M002;
=   DB20.DBX 192.0; // M0 DYNAMISCH
S   DB3.DBX 0.2;    // Meldung 7002  Werkzeug wechseln
R   M 12.0
M002:NOP 0;
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUSGELÖST
O   DB20.DBX 3.6;   // NC-START ANFORDERUNG
O   DB20.DBX 0.2;   // FC NC-START
R   DB3.DBX 0.2;    // 7002  Werkzeug wechseln
END_FUNCTION
FUNCTION FC 65 : VOID
NAME: HANDRAD
BEGIN
NETWORK
TITLE = Funktionen für Handrad
O     DB20.DBX 326.5;     // BA INC1 von Kern
O     DB20.DBX 326.6;     // BA INC10 von Kern
O     DB20.DBX 326.7;     // BA INC100 von Kern
O     DB20.DBX 327.0;     // BA INC1000 von Kern
=     M 98.6;             // Handrad-BA
U(    ;
O(    ;
U     M 98.6;             // Handrad-BA
UN    M 98.7;             // HM Handrad aktiv
)     ;
O     M 98.0;             // Fl. Anwahl INC1
O     M 98.1;             // Fl. Anwahl INC10
O     M 98.2;             // Fl. Anwahl INC100
O     M 98.3;             // Fl. Anwahl INC1000
)     ;
=     DB1.DBX 1440.2;     // Handrad Anwahl
O     DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O     DB1.DBX 1440.0; // RESET AUSGELÖST
ON    M 98.6;             // Handrad-BA
U     M 98.7;             // HM Handrad aktiv
=     DB1.DBX 1440.3;     // Handrad Abwahl
U     DB1.DBX 1440.2;     // Handrad Anwahl
S     M 98.7;             // HM Handrad aktiv
U     DB1.DBX 1440.3;     // Handrad Abwahl
R     M 98.7;             // HM Handrad aktiv
// INC-Schrittweite bei BA-Wechsel mitführen
U     DB20.DBX 326.5;     // BA INC1 von Kern
FP    M 98.0;             // Fl. Anwahl INC1
UN    M 98.0;
SPB   INC1;
L     +1.0E-06;
T     DB20.DBD 16;        // INC Schrittweite
INC1: NOP   0;
U     DB20.DBX 326.6;     // BA INC10 von Kern
FP    M 98.1;             // Fl. Anwahl INC10
UN    M 98.1;
SPB   INC2;
L     +1.0E-05;
T     DB20.DBD 16;        // INC Schrittweite
INC2: NOP   0;
U     DB20.DBX 326.7;     // BA INC100 von Kern
FP    M 98.2;             // Fl. Anwahl INC100
UN    M 98.2;
SPB   INC3;
L     +1.0E-04;
T     DB20.DBD 16;        // INC Schrittweite
INC3: NOP   0;
U     DB20.DBX 327.0;     // BA INC1000 von Kern
FP    M 98.3;             // Fl. Anwahl INC1000
UN    M 98.3;
SPB   INC4;
L     +1.0E-03;
T     DB20.DBD 16;        // INC Schrittweite
INC4: NOP   0;
END_FUNCTION
FUNCTION FC 88 : VOID
NAME: AUX_ON AC95/AC88 UMBAU AUF ACC
BEGIN
NETWORK  1
TITLE = FREIGABE DIFFERENZIALLEITUNGSTREIBER AUF ACC-MOTHERBOARD
U   M 110.3;        // AUX-ON MANUAL
S   A 3.7;          // FREIGABE DIFFERENZIALLEITUNGSTREIBER TAKTE FÜR SM
UN  M 110.3;        // AUX-ON MANUAL
L   S5TIME#1S;      // 10X0.1S
SE  T 10;
U   T 10;
R   A 3.7;          // FREIGABE DIFFERENZIALLEITUNGSTREIBER TAKTE FÜR SM
NETWORK  1
TITLE = ALARM NOT_AUS AC95 UMBAU AUF ACC
UN  M 15.2;         // NOT AUS
S   DB2.DBX 0.0;    // ALARM NOT-AUS
=   DB1.DBX 1390.5; // SPS>OBERFL. NOT AUS
O   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 0.0;    // ALARM NOT-AUS
NETWORK  2
TITLE = MELDUNG MASCHINENTÜR OFFEN AC95 UMBAU AUF ACC
U   M 15.1;          // MASCHINENTÜR OFFEN
UN  DB3.DBX 6.2;    // KEIN TEIL GESPANNT (M7050)
UN  DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
UN  DB3.DBX 2.7;    // MELDUNG 7023 WARTEZEIT HAUPTANTRIEB
UN  DB3.DBX 0.2;    // 7002  Werkzeug wechseln
=   DB3.DBX 5.0;    // MASCHINENTÜR OFFEN (7040)
NETWORK  3
TITLE = 6024 ALARM TÜR OFFEN AC95 UMBAU AUF ACC
O   M 131.7;        // ACHSEN IN BEWEGUNG
O   M 92.0;         // RUNDACHSE IN BEWEGUNG
U   DB20.DBX 326.4; // BETRIEBSART AUTO
=   M 102.0;        // HM
U   M 131.7;        // ACHSEN IN BEWEGUNG
U   DB20.DBX 327.2; // BETRIEBSART REFERENZ
=   M 102.1;        // HM
U   DB20.DBX 324.0; // PROGRAMM LÄUFT
UN  DB20.DBX 324.1; // STOP-ZUSTAND
U   DB20.DBX 326.4; // BETRIEBSART AUTO
=   M 102.2;        // HM
UN  M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
=   M 102.3;        // HM
O   M 102.0;        // HM
O   M 102.1;        // HM
O   M 102.2;        // HM
O   M 102.3;        // HM
=   M 110.2;        // HM TÜRALARM
U   M 110.2;        // HM TÜRALARM
U   M 15.1;         // MASCHINENTÜR OFFEN
S   DB2.DBX 3.0;    // 6024 ALARM TÜR OFFEN
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 3.0;    // 6024 ALARM TÜR OFFEN
NETWORK  4
TITLE = SUM ALARME 1 AC95 UMBAU AUF ACC
L   DB2.DBW 0;      // WORT ALARME 1
L   W#16#0;
<>I;
=   M 105.0;        // SUM ALARME1
NETWORK  5
TITLE = SUM ALARME 2 AC95 UMBAU AUF ACC
L   DB2.DBW 2;      // WORT ALARME 2
L   W#16#0;
<>I;
=   M 105.1;        // SUM ALARME 2
NETWORK  6
TITLE = SUM THERM.ALARME AC95 UMBAU AUF ACC
L   DB2.DBW 4;      // BYTE/THERM. ALARME
L   W#16#0;
<>I;
=   M 105.2;        // SUM THERM. ALARME
NETWORK  7
TITLE = ANWAHL AUX_ON AC95 UMBAU AUF ACC
U   DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
S   M 110.1;        // AUX_ON EIN
U   M 110.1;        // AUX_ON EIN
=   DB1.DBX 1390.2; // SPS>OBERFLÄCHE AUX-ON
S   M 110.0;        // AUX_ON AUTO
S   M 110.3;        // AUX-ON MANUAL
NETWORK  8
TITLE = ABWAHL AUX_ON AC95 UMBAU AUF ACC
O   M 105.0;        // SUM ALARME 1
O   M 105.1;        // SUM ALARME 2
O   M 105.2;        // SUM THERM. ALARME
O   DB1.DBX 1366.3; // ALARM AKTIV
R   M 110.3;        // AUX-ON MANUAL
UN  M 110.3;        // AUX-ON MANUAL
O   M 15.1;         // MASCHINENTÜR OFFEN
O   M 18.3;         // AUSGANGSMERKER TÜR AUF
O   DB1.DBX 1366.3; // ALARM AKTIV
R   M 110.0;        // AUX-ON AUTO
NETWORK  9
TITLE = HW-ÜBERPRÜFUNG TÜR ÖFFNEN AC95 UMBAU AUF ACC (ALARM 6009)
U   M 15.1;          // MASCHINENTÜR OFFEN
L   S5TIME#1S;      // 10X0.1S
SE  T 6;            // T6 EINSCHALTVERZÖGERT
U   T 6;            // T6
U   M 15.0;         // MASCHINENTÜR ZU (HAUPTMOTORSCHÜTZ EIN)
S   DB2.DBX 1.1;    // HW-FEHLER SICHERHEITSSCHALTUNG
NETWORK 10
TITLE = HW-ÜBERPRÜFUNG TÜR SCHLIESZEN AC95 UMBAU AUF ACC
UN  DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
UN  M 15.1;          // MASCHINENTÜR OFFEN
U   M 15.2;         // NOT AUS
U   M 15.6;         // RÄDERDECKEL GESCHLOSSEN
L   S5TIME#1S;      // 10X0.1S
SE  T 11;           // T11 EINSCHALTV.
U   T 11;           // T11
UN  M 15.0;         // MASCHINENTÜR ZU (HAUPTMOTORSCHÜTZ EIN)
S   DB2.DBX 1.1;    // HW-FEHLER SICHERHEITSSCHALTUNG
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 1.1;    // HW-FEHLER SICHERHEITSSCHALTUNG
END_FUNCTION
FUNCTION FC 41 : VOID
NAME:RENISHAW MESSTASTER
BEGIN
//  X6:9    A 3.5      // AKTIVIEREN RENISHAW MESSTASTER MI 12 INTERFACE
//  X6:8    E 6.2      // STATUS RENISHAW MESSTASTER MI 12 INTERFACE
//  X6:9    E 6.3      // \STATUS RENISHAW MESSTASTER MI 12 INTERFACE
//  X6:10   E 6.4      // KEIN FEHLER RENISHAW MESSTASTER MI 12 INTERFACE  (24VDC WENN KEIN FEHLER)
//  X6:11   E 6.5      // LOW BATT RENISHAW MESSTASTER MI 12 INTERFACE (24VDC WENN BATTERIE SCHWACH)
NETWORK
TITLE = MESSTASTER VORHANDEN
O   E 6.2           // STATUS RENISHAW MESSTASTER MI 12 INTERFACE
O   E 6.3           // \STATUS RENISHAW MESSTASTER MI 12 INTERFACE
=   M 11.0          // RENISHAW MESSTASTER MI 12 INTERFACE VORHANDEN
NETWORK
TITLE = AKTIVIEREN RENISHAW MESSTASTER MI 12 INTERFACE
U   DB20.DBX 328.4  // ANFORDERUNG MESSEN
UN  DB2.DBX 2.5;    // (A6021) OPTISCHE VERBINDUNG UNTERBROCHEN
U   E 6.2           // STATUS RENISHAW MESSTASTER MI 12 INTERFACE
UN  E 6.3           // \STATUS RENISHAW MESSTASTER MI 12 INTERFACE
O   M 11.4          // HM RENISHAW MESSTASTER MI 12 INTERFACE AUSSCHALTEN
O   M 11.5          // ABBRUCHMERKER RESETTASTE GEDRÜCKT
L   S5TIME#200MS;   // 200MS
SV  T 16;           // VERLÄNGERTER IMPULS
U   T 16;           // VERLÄNGERTER IMPULS
=   A 3.5           // AKTIVIEREN RENISHAW MESSTASTER MI 12 INTERFACE
UN  T 16;           // VERLÄNGERTER IMPULS
;       R   T 17;
R   M 11.4          // HM RENISHAW MESSTASTER MI 12 INTERFACE AUSSCHALTEN
U   A 3.5           // AKTIVIEREN RENISHAW MESSTASTER MI 12 INTERFACE
U   DB20.DBX 328.4  // ANFORDERUNG MESSEN
S   M 11.1          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT
U   M 11.1          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT
UN  E 6.2           // STATUS RENISHAW MESSTASTER MI 12 INTERFACE
U   E 6.3           // \STATUS RENISHAW MESSTASTER MI 12 INTERFACE
U   E 6.4           // KEIN FEHLER RENISHAW MESSTASTER MI 12 INTERFACE
R   M 11.1          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT
S   M 11.2          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT UND BEREIT
NETWORK
TITLE = ACC FREIGABE MESSEN
U   M 11.2          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT UND BEREIT
S   DB20.DBX 343.7  // ACC FREIGABE MESSEN
NETWORK
TITLE = MESSEN BEENDEN
U   M 11.2          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT UND BEREIT
U   E 6.4           // KEIN FEHLER RENISHAW MESSTASTER MI 12 INTERFACE
U   E 6.2           // STATUS RENISHAW MESSTASTER MI 12 INTERFACE
UN  E 6.3           // \STATUS RENISHAW MESSTASTER MI 12 INTERFACE
R   M 11.2          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT UND BEREIT
R   T 17;
S   M 11.3          // RENISHAW MESSEN OHNE FEHLER BEENDET
UN  DB20.DBX 328.4  // ANFORDERUNG MESSEN
U   M 11.3          // RENISHAW MESSEN OHNE FEHLER BEENDET
ON  M 11.0          // RENISHAW MESSTASTER MI 12 INTERFACE VORHANDEN
R   DB20.DBX 343.7  // ACC FREIGABE MESSEN
R   M 11.3          // RENISHAW MESSEN OHNE FEHLER BEENDET
S   M 11.4          // HM RENISHAW MESSTASTER MI 12 INTERFACE AUSSCHALTEN
UN  E 6.2           // STATUS RENISHAW MESSTASTER MI 12 INTERFACE
U   E 6.3           // \STATUS RENISHAW MESSTASTER MI 12 INTERFACE
U   E 6.4           // KEIN FEHLER RENISHAW MESSTASTER MI 12 INTERFACE
UN  M 11.2          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT UND BEREIT
UN  DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
FP  M 11.5          // ABBRUCHMERKER RESETTASTE GEDRÜCKT
NETWORK
TITLE = MESSEN ABBRECHEN
O   M 11.1          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT
O   M 11.2          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT UND BEREIT
O   M 11.3          // RENISHAW MESSEN OHNE FEHLER BEENDET
U   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   M 11.1          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT
R   M 11.2          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT UND BEREIT
R   M 11.3          // RENISHAW MESSEN OHNE FEHLER BEENDET
R   DB20.DBX 343.7  // ACC FREIGABE MESSEN
S   M 11.4          // HM RENISHAW MESSTASTER MI 12 INTERFACE AUSSCHALTEN
R   T 17;
NETWORK
TITLE = (A6021) OPTISCHE VERBINDUNG UNTERBROCHEN
;       U   M 11.2          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT UND BEREIT
U   M 11.1          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT
L   S5TIME#100S;     // 200M SEKUNDEN
SS  T 17;
U   T 17;
UN  E 6.4           // KEIN  FEHLER RENISHAW MESSTASTER MI 12 INTERFACE
S   DB2.DBX 2.5;    // (A6021) OPTISCHE VERBINDUNG UNTERBROCHEN
R   M 11.2          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT UND BEREIT
R   T 17;
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 2.5;    // (A6021) OPTISCHE VERBINDUNG UNTERBROCHEN
NETWORK
TITLE = 7044  MESSTASTER BATTERIE SCHWACH
U   M 11.2          // RENISHAW MESSTASTER MI 12 INTERFACE AKTIVIERT UND BEREIT
U   E 6.5           // LOW BATT RENISHAW MESSTASTER MI 12 INTERFACE
S   DB3.DBX 5.4;    // 7044  MESSTASTER BATTERIE SCHWACH
U   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB3.DBX 5.4;    // 7044  MESSTASTER BATTERIE SCHWACH
END_FUNCTION
FUNCTION FC 49 : VOID
NAME:WERKZEUGPOSITIONEN INS MILL55_ACC.MSD EINTRAGEN
BEGIN
//  CTR 1   DB20.DBX 294.5  POSITION FÜR WERKZEUG 1 ÜBERNEHMEN
//  CTR 2   DB20.DBX 294.6  POSITION FÜR WERKZEUG 2 ÜBERNEHMEN
//  CTR 3   DB20.DBX 294.1  POSITION FÜR WERKZEUG 3 ÜBERNEHMEN
//  CTR 4   DB20.DBX 295.1  POSITION FÜR WERKZEUG 4 ÜBERNEHMEN
//  CTR 5   DB20.DBX 295.2  POSITION FÜR WERKZEUG 5 ÜBERNEHMEN
//  CTR 6   DB20.DBX 295.3  POSITION FÜR WERKZEUG 6 ÜBERNEHMEN
//  CTR 7   DB20.DBX 295.4  POSITION FÜR WERKZEUG 7 ÜBERNEHMEN
//  CTR 8   DB1.DBX 1374.4  POSITION FÜR WERKZEUG 8 ÜBERNEHMEN
//  CTR ß   DB1.DBX 1374.3  POSITION FÜR Z-ACHSE T1-T8 AUSDRÜCK UND EINZIEPOSITION ÜBERNEHMEN
NETWORK
TITLE = POSITION FÜR WERKZEUG 1 ÜBERNEHMEN
U   DB20.DBX 294.5  // TASTE "CTR 1" POSITION FÜR WERKZEUG 1 ÜBERNEHMEN
SPBN M001;
L   1               // EINGESCHWENKTES WERKZEUG
T   DB20.DBW 356    // AKTUELLE WERKZEUGNUMMER ÜBERGEBEN
L   DB1.DBD 16      // VON DER ABSOLUTEN ACHSE 4 DIE ISTPOSITION LADEN
L   +1.0E+03;       // MULTIPLIZIEREN MIT 1000
*R;
T   DB10.DBD 32     // ISTPOSITION IN DIE MSD-DATEN SCHREIBEN
S   DB20.DBX 4.2    // COMMAND-BIT 2
M001:  NOP 0;
NETWORK
TITLE = POSITION FÜR WERKZEUG 2 ÜBERNEHMEN
U   DB20.DBX 294.6  // TASTE "CTR 2" POSITION FÜR WERKZEUG 2 ÜBERNEHMEN
SPBN M002;
L   2               // EINGESCHWENKTES WERKZEUG
T   DB20.DBW 356    // AKTUELLE WERKZEUGNUMMER ÜBERGEBEN
L   DB1.DBD 16      // VON DER ABSOLUTEN ACHSE 4 DIE ISTPOSITION LADEN
L   +1.0E+03;       // MULTIPLIZIEREN MIT 1000
*R;
T   DB10.DBD 36     // ISTPOSITION IN DIE MSD-DATEN SCHREIBEN
S   DB20.DBX 4.2    // COMMAND-BIT 2
M002:  NOP 0;
NETWORK
TITLE = POSITION FÜR WERKZEUG 3 ÜBERNEHMEN
U   DB20.DBX 294.1  // TASTE "CTR 3" POSITION FÜR WERKZEUG 3 ÜBERNEHMEN
SPBN M003;
L   3               // EINGESCHWENKTES WERKZEUG
T   DB20.DBW 356    // AKTUELLE WERKZEUGNUMMER ÜBERGEBEN
L   DB1.DBD 16      // VON DER ABSOLUTEN ACHSE 4 DIE ISTPOSITION LADEN
L   +1.0E+03;       // MULTIPLIZIEREN MIT 1000
*R;
T   DB10.DBD 40     // ISTPOSITION IN DIE MSD-DATEN SCHREIBEN
S   DB20.DBX 4.2    // COMMAND-BIT 2
M003:  NOP 0;
NETWORK
TITLE = POSITION FÜR WERKZEUG 4 ÜBERNEHMEN
U   DB20.DBX 295.1  // TASTE "CTR 4" POSITION FÜR WERKZEUG 4 ÜBERNEHMEN
SPBN M004;
L   4               // EINGESCHWENKTES WERKZEUG
T   DB20.DBW 356    // AKTUELLE WERKZEUGNUMMER ÜBERGEBEN
L   DB1.DBD 16      // VON DER ABSOLUTEN ACHSE 4 DIE ISTPOSITION LADEN
L   +1.0E+03;       // MULTIPLIZIEREN MIT 1000
*R;
T   DB10.DBD 44     // ISTPOSITION IN DIE MSD-DATEN SCHREIBEN
S   DB20.DBX 4.2    // COMMAND-BIT 2
M004:  NOP 0;
NETWORK
TITLE = POSITION FÜR WERKZEUG 5 ÜBERNEHMEN
U   DB20.DBX 295.2  // TASTE "CTR 5" POSITION FÜR WERKZEUG 5 ÜBERNEHMEN
SPBN M005;
L   5               // EINGESCHWENKTES WERKZEUG
T   DB20.DBW 356    // AKTUELLE WERKZEUGNUMMER ÜBERGEBEN
L   DB1.DBD 16      // VON DER ABSOLUTEN ACHSE 4 DIE ISTPOSITION LADEN
L   +1.0E+03;       // MULTIPLIZIEREN MIT 1000
*R;
T   DB10.DBD 48     // ISTPOSITION IN DIE MSD-DATEN SCHREIBEN
S   DB20.DBX 4.2    // COMMAND-BIT 2
M005:  NOP 0;
NETWORK
TITLE = POSITION FÜR WERKZEUG 6 ÜBERNEHMEN
U   DB20.DBX 295.3  // TASTE "CTR 6" POSITION FÜR WERKZEUG 6 ÜBERNEHMEN
SPBN M006;
L   6               // EINGESCHWENKTES WERKZEUG
T   DB20.DBW 356    // AKTUELLE WERKZEUGNUMMER ÜBERGEBEN
L   DB1.DBD 16      // VON DER ABSOLUTEN ACHSE 4 DIE ISTPOSITION LADEN
L   +1.0E+03;       // MULTIPLIZIEREN MIT 1000
*R;
T   DB10.DBD 52     // ISTPOSITION IN DIE MSD-DATEN SCHREIBEN
S   DB20.DBX 4.2    // COMMAND-BIT 2
M006:  NOP 0;
NETWORK
TITLE = POSITION FÜR WERKZEUG 7 ÜBERNEHMEN
U   DB20.DBX 295.4  // TASTE "CTR 7" POSITION FÜR WERKZEUG 7 ÜBERNEHMEN
SPBN M007;
L   7               // EINGESCHWENKTES WERKZEUG
T   DB20.DBW 356    // AKTUELLE WERKZEUGNUMMER ÜBERGEBEN
L   DB1.DBD 16      // VON DER ABSOLUTEN ACHSE 4 DIE ISTPOSITION LADEN
L   +1.0E+03;       // MULTIPLIZIEREN MIT 1000
*R;
T   DB10.DBD 56     // ISTPOSITION IN DIE MSD-DATEN SCHREIBEN
S   DB20.DBX 4.2    // COMMAND-BIT 2
M007:  NOP 0;
NETWORK
TITLE = POSITION FÜR WERKZEUG 8 ÜBERNEHMEN
U   DB1.DBX 1374.4  // TASTE "CTR 8" POSITION FÜR WERKZEUG 8 ÜBERNEHMEN
SPBN M008;
L   8               // EINGESCHWENKTES WERKZEUG
T   DB20.DBW 356    // AKTUELLE WERKZEUGNUMMER ÜBERGEBEN
L   DB1.DBD 16      // VON DER ABSOLUTEN ACHSE 4 DIE ISTPOSITION LADEN
L   +1.0E+03;       // MULTIPLIZIEREN MIT 1000
*R;
T   DB10.DBD 60     // ISTPOSITION IN DIE MSD-DATEN SCHREIBEN
S   DB20.DBX 4.2    // COMMAND-BIT 2
M008:  NOP 0;
NETWORK
TITLE = POSITION FÜR Z-ACHSE T1-T8 AUSDRÜCK UND EINZIEPOSITION ÜBERNEHMEN
U   DB1.DBX 1374.3  // Z-ACHSE T1-T8 AUSDRÜCK UND EINZIEPOSITION
SPBN M009;
L   14              // EINGESCHWENKTES WERKZEUG
T   DB20.DBW 356    // AKTUELLE WERKZEUGNUMMER ÜBERGEBEN
L   DB1.DBD 8       // VON DER ABSOLUTEN ACHSE 2 DIE ISTPOSITION LADEN
T   DB10.DBD 84     // ISTPOSITION IN DIE MSD-DATEN SCHREIBEN
S   DB20.DBX 4.2    // COMMAND-BIT 2
M009:  NOP 0;
END_FUNCTION
FUNCTION FC 50 : VOID
NAME:WERKZEUGWENDER
BEGIN
; MD 28    Z-ACHSE ZIELPOSITION
; MD 32    A-ACHSPOSITION FÜR EINGESPANNTES WERKZEUG
; MD 36    NEUE A-ACHSPOSITION
; MW 40    NEUES WERKZEUG VON GÜLTIGEM T-WORT
; MW 42    EINGESPANNTES WERKZEUG AUS DEN SETTINGDATEN
; MW 44    ERGEBNISS VON WERKZEUGHALTERÜBERPRÜFUNG(WERKZEUGHALTER FREI)
; MW 46    WERKZEUGBESTÜCKUNG (ERGEBNISS BEIM HERAUSFAHREN DER WZW-SCHEIBE VOM 12mm BERO)
; MD 64    Z-ACHSE T1-T8 AUSDRÜCK UND EINZIEPOSITION
; MD 74    Z-ACHSE WZW-SCHWENKPOSITION KEIN WERKZEUG IN DER SPINDEL
; MD 78    Z-ACHSE 1MM UNTER SW-ENDSCHALTER
;       L   DB10.DBD 80;    // Z-ACHSE T1-T8 AUSDRÜCK UND EINZIEPOSITION
;       L   DB10.DBD 88;    // Z-ACHSE WZW-SCHWENKPOSITION KEIN WERKZEUG IN DER SPINDEL
;       L   DB10.DBD 92;    // Z-ACHSE 1MM UNTER SW-ENDSCHALTER
NETWORK
TITLE =  BERECHNEN DER Z-ACHSE T1-T8 AUSDRÜCK UND EINZIEPOSITION
L   DB10.DBD 84;    // Z-ACHSE WZW-GREIFERPOSITION FÜR T1
L   +5.0E-03;
-R
T   MD 64;           // Z-ACHSE T1-T8 AUSDRÜCK UND EINZIEPOSITION
NETWORK
TITLE =  BERECHNEN DER Z-ACHSE WZW-SCHWENKPOSITION KEIN WERKZEUG IN DER SPINDEL
L   DB10.DBD 84;    // Z-ACHSE WZW-GREIFERPOSITION FÜR T1
L   +7.5E-02;       // 75 MM
+R
T   MD 74;          // Z-ACHSE WZW-SCHWENKPOSITION KEIN WERKZEUG IN DER SPINDEL
NETWORK
TITLE =  BERECHNEN DER Z-ACHSE 1MM UNTER SW-ENDSCHALTER
L   DB10.DBD 84;    // Z-ACHSE WZW-GREIFERPOSITION FÜR T1
L   +1.4E-01;       // 140 MM
+R
T   MD 78;          // Z-ACHSE 1MM UNTER SW-ENDSCHALTER
; M 27.0   WERKZEUG EINGEZOGEN
; M 27.1   WERKZEUG AUSGEDRÜCKT
; M 53.0   M70 WERKZEUG AUSDRÜCKEN
; M 53.1   M72 WERKZEUG EINZIEHEN
; DB15.DBW 25  (IN BITS VON 0 BIS 8) EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
NETWORK
TITLE = ABBRUCHMERKER AUS DEN SETTINGDATEN SETZEN
U   DB1.DBX 1370.0;  // 1.SPS SCHLEIFENDURCHLAUF
U   DB15.DBX 20.0   // WZW-ABGEBROCHEN IN DEN SETTINGDATEN SPEICHERN
S   M 52.3          // WZW-ABGEBROCHEN
NETWORK
TITLE = WZW AFG/EFG und NC_START VERR.
L   DB1.DBD 16      // ABSOLUTE ACHSE 4 ISTPOSITION
L   +0.00E-00;
==R
=   M 26.0          // ABSOLUTE ACHSE 4 (WZW) 0 GRAD ERREICHT
UN   M 26.0         // ABSOLUTE ACHSE 4 (WZW) 0 GRAD ERREICHT
U   DB1.DBX 134.4;  // REFERENZPUNKT WZW-ACHSE AKTIV
UN  DB10.DBX 100.0  // WZW MILL55 EINSTELLEN AKTIVIERT
=    M 90.2;        // ABSOLUTE ACHSE 4 (WZW) 0 GRAD ERREICHT AFG/EFG
=    M 96.0;        // ABSOLUTE ACHSE 4 (WZW) 0 GRAD ERREICHT NC_START VERR.
NETWORK
TITLE = WZW FREIGABE
U   M 26.0          // ABSOLUTE ACHSE 4 (WZW) 0 GRAD ERREICHT
U   M 120.2         // WZW-ACHSE 0 GRAD ERREICHT
O   M 25.0          // WERKZEUG 0 AUFNEHMEN FERTIG
O   DB10.DBX 100.0  // WZW MILL55 EINSTELLEN AKTIVIERT
UN  M 94.0;         // AUX-ON  SFG
U   M 52.2          // REFERENZPUNKT X,Y,Z UND WZW C-ACHSE AKTIV
UN  M 52.0;         // WERKZEUGWENDEN AKTIV
UN  M 52.3          // WZW-ABGEBROCHEN
UN  DB3.DBX 6.7;    // MELDUNG 7055 WEKZEUGSPANNSYSTEM ÖFFNEN
UN  DB3.DBX 7.0;    // MELDUNG 7056 ILLEGALE WERKZEUGNUMMER IN DEN SETTINGDATEN
UN  DB3.DBX 7.1;    // MELDUNG 7057 WEKZEUGHALTER BELEGT
UN  DB3.DBX 7.2;    // MELDUNG 7058 ACHSEN FREIFAHREN
U   DB25.DBX 340.4; // ACHSFREIGABE    K1
U   DB25.DBX 340.3; // EINLESEFREIGABE K1
=   M 27.5;         // WZW FREIGABE
FP  M 52.1          // FM WZW FREIGABE
UN  M 27.5;         // WZW FREIGABE
=   M 96.1          // NC_START VERR.
U   M 52.1          // FM WZW FREIGABE
R   M 94.7;         // SPINDELFREIGABE T-WORT IST GÜLTIG
R   M 90.5;         // ACHSFREIGABE T-WORT IST GÜLTIG
NETWORK
TITLE = WERKZEUGPOSITION AUS DEN SETTINGDATEN SETZEN
U   M 27.5;        // WZW FREIGABE
U   DB20.DBX 182.0  // NEUES T-WORT (WERKZEUG)
CC    FC 52;          // WERKZEUGPOSITION AUS DEN SETTINGDATEN SETZEN
NETWORK
TITLE = MELDUNG "7000  FALSCHES T-WORT" LÖSCHEN
U   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB3.DBX 0.0;    // 7000  FALSCHES T-WORT
NETWORK
TITLE = NEUES T-WORT ÜBERPRÜFEN
U   M 27.5;        // WZW FREIGABE
U   DB20.DBX 182.0  // NEUES T-WORT (WERKZEUG)
UN  M 20.2          // ILLEGALE WERKZEUGNUMMER IN DEN SETTINGDATEN
CC  FC 53;            // NEUES T-WORT ÜBERPRÜFEN UND NEUE WZW-SCHEIBENPOSITION FESTLEGEN
NETWORK
TITLE = T-WORT IST GÜLTIG UND HAUPTSPINDEL STOPPEN
U   M 27.5;        // WZW FREIGABE
U   DB20.DBX 182.0  // NEUES T-WORT (WERKZEUG)
U   M 22.6          // T-WORT UNGLEICH ISTWERKZEUG
UN  DB3.DBX 6.7;    // MELDUNG 7055 WEKZEUGSPANNSYSTEM ÖFFNEN
S   M 94.7;         // SPINDELFREIGABE T-WORT IST GÜLTIG
S   M 90.5;         // AFG / EFG T-WORT IST GÜLTIG
S   M 20.6;        // T-WORT IST GÜLTIG
R   M 22.7         // NEUES T-WORT GESPEICHERT FÜR MELDUNG 7001
NETWORK
TITLE =  "7055 WEKZEUGSPANNSYSTEM ÖFFNEN" UND "7057 WEKZEUGHALTER BELEGT" LÖSCHEN
U   M 27.1           // WERKZEUG AUSGEDRÜCKT
R   DB3.DBX 7.1;     // MELDUNG 7057 WEKZEUGHALTER BELEGT
NETWORK
TITLE = WERKZEUGWENDEN STARTEN WENN DIE HS-DREHZAHL KLEINER 20U/MIN IST
U   M 20.6;        // T-WORT IST GÜLTIG
U   M 114.3;        // HA ISTDREHZAHL KLEINER 20U/MIN
S   M 20.7;        // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
R   M 20.6;        // T-WORT IST GÜLTIG
NETWORK
TITLE = WERKZEUGWENDEN ABBRECHEN WENN VORGANG NOCH NICHT GESTARTET
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUGELÖST
U   M 20.6;        // T-WORT IST GÜLTIG
UN  M 20.7;        // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
R   M 94.7;         // SPINDELFREIGABE T-WORT IST GÜLTIG
R   M 90.5;         // AFG / EFG T-WORT IST GÜLTIG
R   M 20.6;        // T-WORT IST GÜLTIG
NETWORK
TITLE = WERKZEUGKEGEL ABBLASEN ABBRECHEN
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUGELÖST
O   DB1.DBX 1366.3; // ALARM AKTIV
O   DB3.DBX 7.2;    // MELDUNG 7058 ACHSEN FREIFAHREN
R   A 6.1           // WERKZEUGKEGEL ABBLASEN
NETWORK
TITLE = WERKZEUGWENDEN GESTARTET IN DEN SETTINGDATEN SICHERN
U   M 20.7;         // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
FP  M 52.4
U   M 52.4
S   DB15.DBX 20.0   // WZW-ABGEBROCHEN IN DEN SETTINGDATEN SPEICHERN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
NETWORK
TITLE = X/Y - ACHSE BEI AKTIVIERTER RUNDACHSE FREIFAHREN
U   M 52.7          // RUNDACHSE AKTIVIERT
SPBN M111;
NETWORK
TITLE = X/Y - ACHSE IST-POSITION SPEICHERN
U   M 20.7;         // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
UN  M 23.7          // Y-ACHSE AUF ZIELPOSITION FAHREN AKTIV
UN  M 23.5          // HM Y-ACHSE AUF ZIELPOSITION FAHREN AKTIV
SPBN M002;
L   DB1.DBD 4       // Ist-Position für Achse 1 (Y)
T   MD 56           // Ist-Position für Achse 1 (Y)
L   DB1.DBD 0       // Ist-Position für Achse 1 (X)
T   MD 60           // Ist-Position für Achse 1 (X)
M002:  NOP 0;
NETWORK
TITLE = X/Y - ACHSE AUF SOFTWAREENDSCHALTER FAHREN
U   M 20.7;         // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
UN  M 23.7          // Y-ACHSE AUF ZIELPOSITION FAHREN AKTIV
UN  M 23.5          // HM Y-ACHSE AUF ZIELPOSITION FAHREN AKTIV
SPBN M003;
S   M 23.7          // Y-ACHSE AUF ZIELPOSITION FAHREN AKTIV
;       S   DB25.DBX 340.3; // EINLESEFREIGABE K1
;       S   DB25.DBX 340.4; // ACHSFREIGABE    K1
=   DB25.DBX 1.4;   // NC-SATZ ABSCHICKEN
=   DB25.DBX 548.7  // G0 wird ausgelöst
=   DB25.DBX 548.5; // GENAUHALTMODUS SETZEN
=   DB25.DBX 20.0;  // POSITIONSANFORDERUNG FÜR X-ACHSE
=   DB25.DBX 20.1;  // POSITIONSANFORDERUNG FÜR Y-ACHSE
L   DB20.DBD 482    // MAX. SW-ENDSCHALTER FÜR Y-ACHSE FLOATWERT IN M
T   DB25.DBD 26;    // POSITIONSWERT FÜR Y-ACHSE
L   DB20.DBD 414    // MIN. SW-ENDSCHALTER FÜR X-ACHSE FLOATWERT IN M
T   DB25.DBD 22;    // POSITIONSWERT FÜR X-ACHSE
M003:  NOP 0;
U   DB25.DBX 332.0; // NC-SATZ FERTIG
U   M 20.7;         // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
S   M 23.5          // HM Y-ACHSE AUF ZIELPOSITION FAHREN AKTIV
R   M 23.7          // Y-ACHSE AUF ZIELPOSITION FAHREN AKTIV
NETWORK
TITLE = X/Y - ACHSE AUF ISTPOSITION FAHREN
U   M 26.0          // ABSOLUTE ACHSE 4 (WZW) 0 GRAD ERREICHT
U   M 21.5          // SCHRITT 5
U   M 20.7;         // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
UN  M 21.6          // Y-ACHSE AUF ZIELPOSITION FAHREN AKTIV
SPBN M004;
S   M 21.6          // Y-ACHSE AUF ZIELPOSITION FAHREN AKTIV
;       S   DB25.DBX 340.3; // EINLESEFREIGABE K1
;       S   DB25.DBX 340.4; // ACHSFREIGABE    K1
=   DB25.DBX 1.4;   // NC-SATZ ABSCHICKEN
=   DB25.DBX 548.7  // G0 wird ausgelöst
=   DB25.DBX 548.5; // GENAUHALTMODUS SETZEN
=   DB25.DBX 20.0;  // POSITIONSANFORDERUNG FÜR X-ACHSE
=   DB25.DBX 20.1;  // POSITIONSANFORDERUNG FÜR Y-ACHSE
L   MD 56           // Ist-Position für Achse 1 (Y)
T   DB25.DBD 26;    // POSITIONSWERT FÜR Y-ACHSE
L   MD 60           // Ist-Position für Achse 1 (X)
T   DB25.DBD 22;    // POSITIONSWERT FÜR X-ACHSE
M004:  NOP 0;
U   DB25.DBX 332.0; // NC-SATZ FERTIG
U   M 20.7;         // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
U   M 21.6          // Y-ACHSE AUF ZIELPOSITION FAHREN AKTIV
S   M 26.2;         // HM X/Y-ACHSE ZIELPOSITION ERREICHT
U   M 26.2;         // HM X/Y-ACHSE ZIELPOSITION ERREICHT
L   S5TIME#500MS;
SE  T 15;           // T15 EINSCHALTVERZÖGERT
U   T 15;           // T15 EINSCHALTVERZÖGERT
S   M 20.4          // WERKZEUG AUFNEHMEN FERTIG
R   M 26.2;         // HM X/Y-ACHSE ZIELPOSITION ERREICHT
M111:  NOP 0;
NETWORK
TITLE = WERKZEUG ZURÜCKGEBEN
U   M 20.7;        // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
UN  M 20.3          // WERKZEUG ZURÜCKGEBEN FERTIG
SPBN M005;
U   M 42.1          // WERKZEUG 1 EINGESPANNT
CC   FC 54           // WERKZEUG 1 ZURÜCKGEBEN
SPB  M005;
O   M 20.1          // WERKZEUG AUSGEDRÜCKT UND KEINE WERKZEUGNUMMER GESPEICHERT
O   M 42.0          // WERKZEUG 0 EINGESPANNT
CC   FC 55           // WERKZEUG 0 ODER KEIN WERKZEUG ZURÜCKGEBEN
SPB  M005;
CALL   FC 56           // WERKZEUG 2 BIS 8 ZURÜCKGEBEN
M005:  NOP 0;
NETWORK
TITLE = NEUES WERKZEUG AUFNEHMEN
U   M 20.7;        // WERKZEUGWENDER STARTEN VON GÜLTIGEN T-WORT
U   M 20.3          // WERKZEUG ZURÜCKGEBEN FERTIG
UN  M 20.4          // WERKZEUG AUFNEHMEN FERTIG
SPBN M006;
U   M 40.1          // WERKZEUG 1 ANGEWÄHLT
CC   FC 57           // WERKZEUG 1 AUFNEHMEN
SPB  M006;
U   M 40.0          // WERKZEUG 0 ANGEWÄHLT
CC   FC 58           // WERKZEUG 0 AUFNEHMEN
SPB  M006;
CALL   FC 59           // WERKZEUG 2 BIS 8 AUFNEHMEN
M006:  NOP 0;
NETWORK
TITLE = WERKZEUGWENDEN BEENDEN
U   M 20.4          // WERKZEUG AUFNEHMEN FERTIG
O   M 26.1          // NEUES T-WORT GLEICH ISTWERKZEUG
SPBN M007;
L   DB20.DBW 184;   // LADE T-WORT
T   DB20.DBW 356;   // AKTIVES WERKZEUG IN DB20
L   0
T   MD 20
R   DB15.DBX 20.0   // WZW-ABGEBROCHEN IN DEN SETTINGDATEN SPEICHERN
L   MW 40           // MW 40
T   DB15.DBW 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
R   M 90.5;         // AFG / EFG T-WORT IST GÜLTIG
R   M 94.7;         // SPINDELFREIGABE T-WORT IST GÜLTIG
M007:  NOP 0;
NETWORK
TITLE = WERKZEUGWENDEN AKTIV
L   MB 20;         // SCHRITTKETTE
L   0;
<>I;
=   M 52.0;         // WERKZEUGWENDEN AKTIV
NETWORK
TITLE = WERKZEUGWENDEN ABBRECHEN
U   M 52.0;         // WERKZEUGWENDEN AKTIV
U(
ON  A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
O   DB2.DBX 0.0;    // ALARM NOT-AUS
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUSGELÖST
)
O(
U   E 16.4          // REF BERO WZW
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
)
O(
U   E 16.4          // REF BERO WZW
U   M 120.5         // FM AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
)
SPBN M001;
L   0
T   MD 20           // WZW-SCHRITTMERKER
S   M 52.3          // WZW-ABGEBROCHEN
R   M 94.7;         // SPINDELFREIGABE T-WORT IST GÜLTIG
R   M 90.5;         // AFG / EFG T-WORT IST GÜLTIG
R   M 26.2;         // HM X/Y-ACHSE ZIELPOSITION ERREICHT
M001:  NOP 0;
NETWORK
TITLE = MELDUNG 7058 ACHSEN FREIFAHREN
U   E 16.4          // REF BERO WZW
U   M 120.5         // FM AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
O(
ON  M 26.0          // ABSOLUTE ACHSE 4 (WZW) 0 GRAD ERREICHT
O   E 16.4          // REF BERO WZW
U   M 52.3          // WZW-ABGEBROCHEN
)
O   M 25.0          // WERKZEUG 0 AUFNEHMEN FERTIG
FP  M 25.1
U   M 25.1
UN  DB3.DBX 6.7;    // MELDUNG 7055 WEKZEUGSPANNSYSTEM ÖFFNEN
SPBN M008;
S   DB3.DBX 7.2;    // MELDUNG 7058 ACHSEN FREIFAHREN
S   DB15.DBX 20.0   // WZW-ABGEBROCHEN IN DEN SETTINGDATEN SPEICHERN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
=   DB1.DBX 1440.0; // NC-RESET AUSLÖSEN
M008:  NOP 0;
NETWORK
TITLE = MELDUNG 7058 ACHSEN FREIFAHREN LÖSCHEN
U   E 16.2          // REF Schalter Z-Achse (0-SIGNAL WENN ACHSE OBEN!!!!)
FN  M 25.3
U   DB3.DBX 7.2;    // MELDUNG 7058 ACHSEN FREIFAHREN
U   M 25.3
SPBN M2008;
=   DB1.DBX 1440.0; // RESET AUSGELÖST
M2008:  NOP 0;
UN  E 16.2          // REF Schalter Z-Achse (0-SIGNAL WENN ACHSE OBEN!!!!)
U   M 120.5         // FM AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
;       ON  E 16.4          // REF BERO WZW
O   DB3.DBX 7.1;    // MELDUNG 7057 WEKZEUGHALTER BELEGT
FP  M 25.2
U   M 52.3          // WZW-ABGEBROCHEN
UN  DB3.DBX 7.2;    // MELDUNG 7058 ACHSEN FREIFAHREN
O   M 25.2
O   M 25.3
ON  E 16.4          // REF BERO WZW
SPBN M009;
R   M 52.3          // WZW-ABGEBROCHEN
R   DB3.DBX 7.2;    // MELDUNG 7058 ACHSEN FREIFAHREN
R   DB15.DBX 20.0   // WZW-ABGEBROCHEN IN DEN SETTINGDATEN SPEICHERN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
M009:  NOP 0;
END_FUNCTION
FUNCTION FC 54 : VOID
NAME:WERKZEUG 1 ZURÜCKGEBEN
BEGIN
NETWORK
TITLE = ÜERPRÜFEN OB WERKZEUGHALTER 1 FREI IST
L   DB1.DBD 16
L   +0.70E-02;
<R;
U   E 4.2;          // 12mm-BERO WERKZEUG BESTÜCKT
;       S   M 46.1          // WERKZEUG 1 VORHANDEN
S   DB3.DBX 7.1;     // MELDUNG 7057 WEKZEUGHALTER BELEGT
=   DB1.DBX 1440.0; // NC-RESET AUSLÖSEN
BEB
NETWORK
TITLE = SCHRITT 0 Z-ACHSE WZW-GREIFERPOSITION FÜR T1
UN  M 23.0          // SCHRITT 0
SPBN M001;
L   DB10.DBD 84;    // Z-ACHSE WZW-GREIFERPOSITION FÜR T1
T   MD 28           // Z-ACHSE ZIELPOSITION
CC   FC 60           // Z-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
S   M 23.0          // SCHRITT 0
R   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
M001:  NOP 0;
NETWORK
TITLE = SCHRITT 1 WZW-SCHEIBE AUF RÜCKGABEPOSITION FAHREN
U   M 23.0          // SCHRITT 0
UN  M 23.1          // SCHRITT 1
UN  DB3.DBX 7.1;    // MELDUNG 7057 WEKZEUGHALTER BELEGT
SPBN M002;
S   M 21.7          // HM WEKZEUGWENDERSCHEIBE MIT 20% EILGANG FAHREN
S   M 20.5          // ÜBERPRÜFEN OB WERKZEUGPLATZ FREI IST
L   MD 32           // A-ACHSPOSITION FÜR EINGESPANNTES WERKZEUG
T   MD 48           // A-ACHSE ZIELPOSITION
CC   FC 61           // A-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
R   M 20.5          // ÜBERPRÜFEN OB WERKZEUGPLATZ FREI IST
R   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
R   M 21.7          // HM WEKZEUGWENDERSCHEIBE MIT 20% EILGANG FAHREN
S   M 23.1          // SCHRITT 1
M002:  NOP 0;
NETWORK
TITLE = SCHRITT 2 WERKZEUG AUSDRÜCKEN
U   M 23.1          // SCHRITT 1
UN  M 23.2          // SCHRITT 2
UN  M 27.1          // WERKZEUG AUSGEDRÜCKT
S   M 53.0          // M70 WERKZEUG AUSDRÜCKEN
U   M 23.1          // SCHRITT 1
UN  M 23.2          // SCHRITT 2
U   M 27.1          // WERKZEUG AUSGEDRÜCKT
S   M 23.2          // SCHRITT 2
NETWORK
TITLE = Z-ACHSE WZW-SCHWENKPOSITION KEIN WERKZEUG IN DER SPINDEL
U   M 23.2          // SCHRITT 2
UN  M 23.3          // SCHRITT 3
SPBN M004;
L   MD 78;          // Z-ACHSE 1MM UNTER SW-ENDSCHALTER
U   M 40.0          // WERKZEUG 0 ANGEWÄHLT
SPB  M104;
L   MD 74;          // Z-ACHSE WZW-SCHWENKPOSITION KEIN WERKZEUG IN DER SPINDEL
M104:  NOP 0;
T   MD 28           // Z-ACHSE ZIELPOSITION
CC   FC 60           // Z-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
S   M 23.3          // SCHRITT 3
S   M 20.3          // WERKZEUG ZURÜCKGEBEN FERTIG
R   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
M004:  NOP 0;
END_FUNCTION
FUNCTION FC 57 : VOID
NAME:WERKZEUG 1 AUFNEHMEN
BEGIN
NETWORK
TITLE = SCHRITT 0 WZW-SCHEIBE AUF NEUE A-ACHSPOSITION
UN  M 21.0          // SCHRITT 0
SPBN M001;
L   MD 36           // NEUE A-ACHSPOSITION
T   MD 48           // A-ACHSE ZIELPOSITION
CC   FC 61           // A-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
R   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
S   M 21.0          // SCHRITT 0
M001:  NOP 0;
NETWORK
TITLE = SCHRITT 2 Z-ACHSE AUF T1-T8 AUSDRÜCK UND EINZIEPOSITION FAHREN
U   M 21.0          // SCHRITT 0
UN  M 21.1          // SCHRITT 1
SPBN M002;
S   A 6.1           // WERKZEUGKEGEL ABBLASEN
L   MD 64;           // Z-ACHSE T1-T8 AUSDRÜCK UND EINZIEPOSITION
T   MD 28           // Z-ACHSE ZIELPOSITION
CC   FC 60           // Z-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
S   M 21.1          // SCHRITT 1
R   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
R   A 6.1           // WERKZEUGKEGEL ABBLASEN
M002:  NOP 0;
NETWORK
TITLE = SCHRITT 3 WERKZEUG EINZIEHEN
UN  M 21.2          // SCHRITT 2
U   M 21.1          // SCHRITT 1
UN  M 27.0          // WERKZEUG EINGEZOGEN
S   M 53.1          // M72 WERKZEUG EINZIEHEN
UN  M 21.2          // SCHRITT 2
U   M 21.1          // SCHRITT 1
U   M 27.0          // WERKZEUG EINGEZOGEN
S   M 21.2          // SCHRITT 2
NETWORK
TITLE = SCHRITT 4 Z-ACHSE WZW-GREIFERPOSITION FÜR T1
U   M 21.2          // SCHRITT 2
UN  M 21.3          // SCHRITT 3
SPBN M003;
L   DB10.DBD 84;    // Z-ACHSE WZW-GREIFERPOSITION FÜR T1
T   MD 28           // Z-ACHSE ZIELPOSITION
CC   FC 60           // Z-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
S   M 21.3          // SCHRITT 3
R   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
M003:  NOP 0;
NETWORK
TITLE = SCHRITT 5 WZW-SCHEIBE AUF 0 - POSITION FAHREN
U   M 21.3          // SCHRITT 3
UN  M 21.4          // SCHRITT 4
SPBN M004;
S   M 21.7          // HM WEKZEUGWENDERSCHEIBE MIT 20% EILGANG FAHREN
L   0               // A-ACHSPOSITION 0 GRAD
T   MD 48           // A-ACHSE ZIELPOSITION
CC   FC 61           // A-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
R   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
R   M 21.7          // HM WEKZEUGWENDERSCHEIBE MIT 20% EILGANG FAHREN
S   M 21.4          // SCHRITT 4
U   M 21.4          // SCHRITT 4
UN  M 52.7          // RUNDACHSE AKTIVIERT
S   M 20.4          // WERKZEUG AUFNEHMEN FERTIG
M004:  NOP 0;
NETWORK
TITLE = SCHRITT 6 Z-ACHSE 1MM UNTER SW-ENDSCHALTER FAHREN
U   M 21.4          // SCHRITT 4
UN  M 21.5          // SCHRITT 5
U   M 52.7          // RUNDACHSE AKTIVIERT
SPBN M005;
L   MD 78;          // Z-ACHSE 1MM UNTER SW-ENDSCHALTER
T   MD 28           // Z-ACHSE ZIELPOSITION
CC   FC 60           // Z-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
S   M 21.5          // SCHRITT 5
R   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
M005:  NOP 0;
END_FUNCTION
FUNCTION FC 59 : VOID
NAME:WERKZEUG 2 BIS 8 AUFNEHMEN
BEGIN
NETWORK
TITLE = SCHRITT 0 WZW-SCHEIBE AUF NEUE A-ACHSPOSITION
UN  M 21.0          // SCHRITT 0
SPBN M001;
L   MD 36           // NEUE A-ACHSPOSITION
T   MD 48           // A-ACHSE ZIELPOSITION
CC   FC 61           // A-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
R   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
S   M 21.0          // SCHRITT 0
M001:  NOP 0;
NETWORK
TITLE = SCHRITT 2 Z-ACHSE AUF T1-T8 AUSDRÜCK UND EINZIEPOSITION FAHREN
U   M 21.0          // SCHRITT 0
UN  M 21.1          // SCHRITT 1
SPBN M002;
S   A 6.1           // WERKZEUGKEGEL ABBLASEN
L   MD 64;           // Z-ACHSE T1-T8 AUSDRÜCK UND EINZIEPOSITION
T   MD 28           // Z-ACHSE ZIELPOSITION
CC   FC 60           // Z-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
S   M 21.1          // SCHRITT 1
R   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
R   A 6.1           // WERKZEUGKEGEL ABBLASEN
M002:  NOP 0;
NETWORK
TITLE = SCHRITT 3 WERKZEUG EINZIEHEN
UN  M 21.2          // SCHRITT 2
U   M 21.1          // SCHRITT 1
UN  M 27.0          // WERKZEUG EINGEZOGEN
S   M 53.1          // M72 WERKZEUG EINZIEHEN
UN  M 21.2          // SCHRITT 2
U   M 21.1          // SCHRITT 1
U   M 27.0          // WERKZEUG EINGEZOGEN
S   M 21.2          // SCHRITT 2
NETWORK
TITLE = SCHRITT 4 Z-ACHSE 1MM UNTER SW-ENDSCHALTER FAHREN
U   M 21.2          // SCHRITT 2
UN  M 21.3          // SCHRITT 3
SPBN M003;
L   MD 78;          // Z-ACHSE 1MM UNTER SW-ENDSCHALTER
T   MD 28           // Z-ACHSE ZIELPOSITION
CC   FC 60           // Z-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
S   M 21.3          // SCHRITT 3
R   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
M003:  NOP 0;
NETWORK
TITLE = SCHRITT 5 WZW-SCHEIBE AUF 0 - POSITION FAHREN
U   M 21.3          // SCHRITT 3
UN  M 21.5          // SCHRITT 5
SPBN M004;
L   0               // A-ACHSPOSITION 0 GRAD
T   MD 48           // A-ACHSE ZIELPOSITION
CC   FC 61           // A-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
R   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
S   M 21.5          // SCHRITT 5
U   M 21.5          // SCHRITT 5
UN  M 52.7          // RUNDACHSE AKTIVIERT
S   M 20.4          // WERKZEUG AUFNEHMEN FERTIG
M004:  NOP 0;
END_FUNCTION
FUNCTION FC 55 : VOID
NAME:WERKZEUG 0 ODER KEIN WERKZEUG ZURÜCKGEBEN
BEGIN
NETWORK
TITLE = WERKZEUGSPANNEINRICHTUNG ÖFFNEN ZUM AUFNEHMEN EINES WERKZEUGES
UN  M 23.0          // SCHRITT 0
UN  M 27.1          // WERKZEUG AUSGEDRÜCKT
S   M 53.0          // M70 WERKZEUG AUSDRÜCKEN
UN  M 23.0          // SCHRITT 0
U   M 27.1          // WERKZEUG AUSGEDRÜCKT
S   M 23.0          // SCHRITT 0
NETWORK
TITLE = Z-ACHSE AUF ZIELPOSITION FAHREN
UN  M 23.1          // SCHRITT 1
SPBN M001;
L   MD 78;          // Z-ACHSE 1MM UNTER SW-ENDSCHALTER
;;       L   DB10.DBD 88;    // Z-ACHSE WZW-SCHWENKPOSITION KEIN WERKZEUG IN DER SPINDEL
T   MD 28           // Z-ACHSE ZIELPOSITION
CC   FC 60           // Z-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
S   M 23.1          // SCHRITT 1
R   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
M001:  NOP 0;
U   M 23.0          // SCHRITT 0
U   M 23.1          // SCHRITT 1
S   M 20.3          // WERKZEUG ZURÜCKGEBEN FERTIG
END_FUNCTION
FUNCTION FC 56 : VOID
NAME:WERKZEUG 2 BIS 8 ZURÜCKGEBEN
BEGIN
NETWORK
TITLE = SCHRITT 0 Z-ACHSE 1MM UNTER SW-ENDSCHALTER FAHREN
UN  M 23.0          // SCHRITT 0
SPBN M001;
L   MD 78;          // Z-ACHSE 1MM UNTER SW-ENDSCHALTER
T   MD 28           // Z-ACHSE ZIELPOSITION
CC   FC 60           // Z-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
S   M 23.0          // SCHRITT 0
M001:  NOP 0;
NETWORK
TITLE = SCHRITT 1 WZW-SCHEIBE AUF RÜCKGABEPOSITION FAHREN
U   M 23.0          // SCHRITT 0
UN  M 23.1          // SCHRITT 1
UN  DB3.DBX 7.1;    // MELDUNG 7057 WEKZEUGHALTER BELEGT
SPBN M002;
S   M 20.5          // ÜBERPRÜFEN OB WERKZEUGPLATZ FREI IST
L   MD 32           // A-ACHSPOSITION FÜR EINGESPANNTES WERKZEUG
T   MD 48           // A-ACHSE ZIELPOSITION
CC   FC 61           // A-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
R   M 20.5          // ÜBERPRÜFEN OB WERKZEUGPLATZ FREI IST
R   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
S   M 23.1          // SCHRITT 1
M002:  NOP 0;
NETWORK
TITLE = SCHRITT 2 Z-ACHSE AUF T1-T8 AUSDRÜCK UND EINZIEPOSITION FAHREN
U   M 23.1          // SCHRITT 1
UN  M 23.2          // SCHRITT 2
SPBN M003;
L   MD 64;           // Z-ACHSE T1-T8 AUSDRÜCK UND EINZIEPOSITION
T   MD 28           // Z-ACHSE ZIELPOSITION
CC   FC 60           // Z-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
S   M 23.2          // SCHRITT 2
R   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
M003:  NOP 0;
NETWORK
TITLE = SCHRITT 3 WERKZEUG AUSDRÜCKEN
U   M 23.2          // SCHRITT 2
UN  M 23.3          // SCHRITT 3
UN  M 27.1          // WERKZEUG AUSGEDRÜCKT
S   M 53.0          // M70 WERKZEUG AUSDRÜCKEN
U   M 23.2          // SCHRITT 2
UN  M 23.3          // SCHRITT 3
U   M 27.1          // WERKZEUG AUSGEDRÜCKT
S   M 23.3          // SCHRITT 3
NETWORK
TITLE = Z-ACHSE AUF ZIELPOSITION FAHREN
U   M 23.3          // SCHRITT 3
UN  M 23.4          // SCHRITT 4
SPBN M004;
L   MD 78;          // Z-ACHSE 1MM UNTER SW-ENDSCHALTER
U   M 40.0          // WERKZEUG 0 ANGEWÄHLT
SPB  M104;
L   MD 74;          // Z-ACHSE WZW-SCHWENKPOSITION KEIN WERKZEUG IN DER SPINDEL
M104:  NOP 0;
T   MD 28           // Z-ACHSE ZIELPOSITION
CC   FC 60           // Z-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
S   M 23.4          // SCHRITT 4
S   M 20.3          // WERKZEUG ZURÜCKGEBEN FERTIG
R   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
M004:  NOP 0;
END_FUNCTION
FUNCTION FC 58 : VOID
NAME:WERKZEUG 0 AUFNEHMEN
BEGIN
NETWORK
TITLE = SCHRITT 1 WZW-SCHEIBE AUF POSITION 8 FAHREN
UN  M 21.0          // SCHRITT 0
SPBN M002;
;       L   0               // A-ACHSPOSITION 0 GRAD
L   DB10.DBD 60     // Position für Werkzeug 8 in Grad
T   MD 48           // A-ACHSE ZIELPOSITION
CC   FC 61           // A-ACHSE AUF ZIELPOSITION FAHREN
U   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
R   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
S   M 21.0          // SCHRITT 0
S   M 21.1          // SCHRITT 1
M002:  NOP 0;
NETWORK
TITLE = SCHRITT 2
U   M 21.1          // SCHRITT 1
U   M 40.0          // WERKZEUG 0 ANGEWÄHLT
S   M 20.4          // WERKZEUG AUFNEHMEN FERTIG
S   M 25.0          // WERKZEUG 0 AUFNEHMEN FERTIG
END_FUNCTION
FUNCTION FC 61 : VOID
NAME:A-ACHSE AUF ZIELPOSITION FAHREN
BEGIN
NETWORK
TITLE = SCHRITT 2  WZW-SCHEIBE AUF GÜLTIGE POSITION FAHREN
UN  M 22.0          // SCHWENKEN AKTIV
UN  M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
SPBN M001;
S   M 22.0          // SCHWENKEN AKTIV
=   DB25.DBX 1.4;   // NC-SATZ ABSCHICKEN
=   DB25.DBX 548.5; // GENAUHALTMODUS SETZEN
=   DB25.DBX 549.0; // G1 WIRD AUSGELÖST
=   DB25.DBX 549.4; // VORSCHUB IN GRAD/S WERT IN DBD556
L   DB10.DBD 28;    // VORSCHUBWERT IN GRAD/SEKUNDEN
T   DB25.DBD 556;   // F-WERT IN M/S ODER M/U
S   DB25.DBX 21.4;  // POSITIONSANFORDERUNG FÜR KANALACHSE C
L   MD 48           // A-ACHSE ZIELPOSITION
T   DB25.DBD 70;    // POSITIONSWERT FÜR KANALACHSE C
L   0               // MW 46 LÖSCHEN
T   MW 46           // MW 46 LÖSCHEN
U   E 4.2;          // 12mm-BERO WERKZEUG BESTÜCKT
S   M 46.1          // WERKZEUG 1 VORHANDEN
U   M 21.7          // HM WEKZEUGWENDERSCHEIBE MIT 20% EILGANG FAHREN
SPBN M001;
L   DB10.DBD 24;    // T-1 ÜBERNAHMEVORSCHUBWERT IN GRAD/SEKUNDEN FUER WZW-SCHEIBE
T   DB25.DBD 556;   // F-WERT IN M/S ODER M/U
M001:  NOP 0;
NETWORK
TITLE = ÜBERPRÜFEN OB WERKZEUGPLATZ FREI IST
U   DB25.DBX 332.0; // NC-SATZ FERTIG
U   M 22.0          // SCHWENKEN AKTIV
R   DB25.DBX 332.0; // NC-SATZ FERTIG
S   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
R   M 22.0          // SCHWENKEN AKTIV
U   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
U   M 20.5          // ÜBERPRÜFEN OB WERKZEUGPLATZ FREI IST
SPBN M002;
L   MW 46   // WERKZEUGBESTÜCKUNG
L   MW 42   // EINGESPANNTES WERKZEUG AUS DEN SETTINGDATEN
UW
T   MW 44   // ERGEBNISS VON WERKZEUGHALTERÜBERPRÜFUNG
L   MW 44   // ERGEBNISS VON WERKZEUGHALTERÜBERPRÜFUNG
L   0
<>I
S   DB3.DBX 7.1;     // MELDUNG 7057 WEKZEUGHALTER BELEGT
=   DB1.DBX 1440.0; // NC-RESET AUSLÖSEN
R   M 22.1          // WZW-SCHEIBE GÜLTIGE POSITION ERREICHT
M002:  NOP 0;
NETWORK
TITLE = WERKZEUGE IN SCHEIBE ÜBERPRÜFEN
U   E 4.2;          // 12mm-BERO WERKZEUG BESTÜCKT
FP  M 27.4
U   M 27.4
U   M 22.0          // SCHWENKEN AKTIV
U   M 20.5          // ÜBERPRÜFEN OB WERKZEUGPLATZ FREI IST
SPBN M003;
;Werkzeug 1 Bero filtern
L   DB1.DBD 16
L   +0.70E-02;
<R;
BEB
;Werkzeug 2
L   DB1.DBD 16
L   +2.10E-02;
<R;
S   M 46.2          // WERKZEUG 2 VORHANDEN
BEB
;Werkzeug 3
L   DB1.DBD 16
L   +3.20E-02;
<R;
S   M 46.3          // WERKZEUG 3 VORHANDEN
BEB
;Werkzeug 4
L   DB1.DBD 16
L   +4.90E-02; -
<R;
S   M 46.4          // WERKZEUG 4 VORHANDEN
BEB
;Werkzeug 5
L   DB1.DBD 16
L   +6.30E-02;
<R;
S   M 46.5          // WERKZEUG 5 VORHANDEN
BEB
;Werkzeug 6
L   DB1.DBD 16
L   +7.70E-02;
<R;
S   M 46.6          // WERKZEUG 6 VORHANDEN
BEB
;Werkzeug 7
L   DB1.DBD 16
L   +9.10E-02;
<R;
S   M 46.7          // WERKZEUG 7 VORHANDEN
BEB
;Werkzeug 8
L   DB1.DBD 16
L   +1.05E-01;
<R;
S   M 47.0          // WERKZEUG 8 VORHANDEN
BEB
M003:  NOP 0;
END_FUNCTION
FUNCTION FC 51 : VOID
NAME:WERKZEUGSPANNSYSTEM
BEGIN
UN  E 4.1            // WERKZEUG AUSGEDRÜCKT
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
=   M 94.3;         // SFG WERKZEUG AUSGEDRÜCKT
NETWORK
TITLE = WERKZEUG MIT DEN TASTEN "STRG + 1" ODER "STRG + ^" AUSDRÜCKEN
//  ALT U        CTR ^   DB20.DBX 294.2   FUTTER AUF/ZU
//  ALT K        CTR 1   DB20.DBX 294.5   WZW EINE POSITION WEITERSCHWENKEN
U   DB20.DBX 294.5  // (STRG 1 oder Alt K) Werkzeug ausdrücken/einziehen
FP  M 26.6          // FP-M (STRG 1 oder Alt K) Werkzeug ausdrücken/einziehen
U   DB20.DBX 294.2  //(STRG ^ oder ALT U) FUTTER AUF/ZU
U   M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
O(
U   M 26.6          // FP-M (STRG 1 oder Alt K) Werkzeug ausdrücken/einziehen
UN  M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
)
U   E 2.1;          // S1 TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
U   E 2.5;          // S2 TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
UN  M 52.0;         // WERKZEUGWENDEN AKTIV
U   M 27.2          // FREIGABE WERKZEUG AUSDRÜCKEN
FP  M 27.3
U   M 27.3
SPBN M101
L   0
T   MW 40           // MW 40
T   DB20.DBW 356;   // AKTIVES WERKZEUG IN DB20
M101:  NOP 0
NETWORK
TITLE = FREIGABE WERKZEUG AUSDRÜCKEN
U   M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
=   M 27.2          // FREIGABE WERKZEUG AUSDRÜCKEN
NETWORK
TITLE = WERKZEUG AUSDRÜCKEN
O   M 27.3
O   M 53.0;          // M70 WERKZEUG AUSDRÜCKEN
UN  M 27.1           // WERKZEUG AUSGEDRÜCKT
U   M 27.2           // FREIGABE WERKZEUG AUSDRÜCKEN
S   A 6.0            // WERKZEUGSPANNMOTOR
S   M 27.6           // HM WERKZEUGSPANNMOTOR AUSDRÜCKEN
S   M 94.1;          // SFG WERKZEUG AUSGEDRÜCKT
R   M 27.0           // WERKZEUG EINGEZOGEN
O   M 27.6           // HM WERKZEUGSPANNMOTOR AUSDRÜCKEN
O   DB1.DBX 1370.0;  // 1.SPS SCHLEIFENDURCHLAUF
UN  E 4.1            // WERKZEUG AUSGEDRÜCKT
U   E 4.0            // WERKZEUG EINGEZOGEN
R   A 6.0            // WERKZEUGSPANNMOTOR
R   M 27.6           // HM WERKZEUGSPANNMOTOR AUSDRÜCKEN
R   M 53.0;          // M70 WERKZEUG AUSDRÜCKEN
S   M 27.1           // WERKZEUG AUSGEDRÜCKT
SPBN M102
L   0
T   DB15.DBW 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
U   DB3.DBX 6.7;    // MELDUNG 7055 WEKZEUGSPANNSYSTEM ÖFFNEN
R   M 52.3          // WZW-ABGEBROCHEN
R   DB3.DBX 6.7;    // MELDUNG 7055 WEKZEUGSPANNSYSTEM ÖFFNEN
M102:  NOP 0
NETWORK
TITLE = WERKZEUG EINZIEHEN
O(
U   M 27.3
U   M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
)
O   M 53.1;          // M72 WERKZEUG EINZIEHEN
UN  M 27.0           // WERKZEUG EINGEZOGEN
UN  M 27.6           // HM WERKZEUGSPANNMOTOR AUSDRÜCKEN
U   M 27.2           // FREIGABE WERKZEUG AUSDRÜCKEN
S   A 6.0            // WERKZEUGSPANNMOTOR
S   M 27.7           // HM WERKZEUGSPANNMOTOR EINZIEHEN
O   M 27.7           // HM WERKZEUGSPANNMOTOR EINZIEHEN
O   DB1.DBX 1370.0;  // 1.SPS SCHLEIFENDURCHLAUF
UN  E 4.0            // WERKZEUG EINGEZOGEN
U   E 4.1            // WERKZEUG AUSGEDRÜCKT
R   A 6.0            // WERKZEUGSPANNMOTOR
R   M 27.7           // HM WERKZEUGSPANNMOTOR EINZIEHEN
R   M 53.1;          // M72 WERKZEUG EINZIEHEN
S   M 27.0           // WERKZEUG EINGEZOGEN
R   M 27.1           // WERKZEUG AUSGEDRÜCKT
U   M 27.0           // WERKZEUG EINGEZOGEN
FP  M 53.2;          // HM M72 WERKZEUG EINZIEHEN
U   M 53.2;          // HM M72 WERKZEUG EINZIEHEN
UN  DB1.DBX 1370.0;  // 1.SPS SCHLEIFENDURCHLAUF
SPBN M103
L   MW 40           // MW 40
T   DB15.DBW 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
M103:  NOP 0
U   M 27.1           // WERKZEUG AUSGEDRÜCKT
S   M 94.1;          // SFG WERKZEUG AUSGEDRÜCKT
U   M 27.0           // WERKZEUG EINGEZOGEN
R   M 94.1;          // SFG WERKZEUG AUSGEDRÜCKT
END_FUNCTION
FUNCTION FC 52 : VOID
NAME:WERKZEUGPOSITION AUS DEN SETTINGDATEN SETZEN
BEGIN
NETWORK
TITLE = WERKZEUGPOSITION AUS DEN SETTINGDATEN SETZEN
;         Rückgabeposition für eingespanntes Werkzeug ermitteln
L   0;
T   MW 42
L   DB15.DBW 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
L   0;
==I;
=   M 20.0           // KEINE WERKZEUGNUMMER GESPEICHERT
SPB  M001
L   DB15.DBW 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
L   1
==I
=   M 42.0          // WERKZEUG 0 EINGESPANNT
SPB  M001
L   DB10.DBD 32     // Position für Werkzeug 1 in Grad
T   MD 32           // A-ACHSPOSITION FÜR EINGESPANNTES WERKZEUG
L   DB15.DBW 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
L   2
==I
=   M 42.1          // WERKZEUG 1 EINGESPANNT
SPB  M001
L   DB10.DBD 36     // Position für Werkzeug 2 in Grad
T   MD 32           // A-ACHSPOSITION FÜR EINGESPANNTES WERKZEUG
L   DB15.DBW 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
L   4
==I
=   M 42.2          // WERKZEUG 2 EINGESPANNT
SPB  M001
L   DB10.DBD 40     // Position für Werkzeug 3 in Grad
T   MD 32           // A-ACHSPOSITION FÜR EINGESPANNTES WERKZEUG
L   DB15.DBW 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
L   8
==I
=   M 42.3          // WERKZEUG 3 EINGESPANNT
SPB  M001
L   DB10.DBD 44     // Position für Werkzeug 4 in Grad
T   MD 32           // A-ACHSPOSITION FÜR EINGESPANNTES WERKZEUG
L   DB15.DBW 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
L   16
==I
=   M 42.4          // WERKZEUG 4 EINGESPANNT
SPB  M001
L   DB10.DBD 48     // Position für Werkzeug 5 in Grad
T   MD 32           // A-ACHSPOSITION FÜR EINGESPANNTES WERKZEUG
L   DB15.DBW 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
L   32
==I
=   M 42.5          // WERKZEUG 5 EINGESPANNT
SPB  M001
L   DB10.DBD 52     // Position für Werkzeug 6 in Grad
T   MD 32           // A-ACHSPOSITION FÜR EINGESPANNTES WERKZEUG
L   DB15.DBW 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
L   64
==I
=   M 42.6          // WERKZEUG 6 EINGESPANNT
SPB  M001
L   DB10.DBD 56     // Position für Werkzeug 7 in Grad
T   MD 32           // A-ACHSPOSITION FÜR EINGESPANNTES WERKZEUG
L   DB15.DBW 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
L   128
==I
=   M 42.7          // WERKZEUG 7 EINGESPANNT
SPB  M001
L   DB10.DBD 60     // Position für Werkzeug 8 in Grad
T   MD 32           // A-ACHSPOSITION FÜR EINGESPANNTES WERKZEUG
L   DB15.DBW 25;    // EINGESPANNTES WERKZEUG IN DEN SETTINGDATEN
L   256
==I
=   M 43.0          // WERKZEUG 8 EINGESPANNT
SPB  M001
L   0
T   MD 32           // A-ACHSPOSITION FÜR EINGESPANNTES WERKZEUG
S   M 20.2          // ILLEGALE WERKZEUGNUMMER IN DEN SETTINGDATEN
=   DB3.DBX 7.0;    // MELDUNG 7056 ILLEGALE WERKZEUGNUMMER IN DEN SETTINGDATEN
=   DB1.DBX 1440.0; // NC-RESET AUSLÖSEN
M001:  NOP 0
NETWORK
TITLE = WERKZEUG EINGEZOGEN UND KEINE WERKZEUGNUMMER GESPEICHERT (GROSSES PROBLEM)
U   M 20.0          // KEINE WERKZEUGNUMMER GESPEICHERT
U   M 27.0          // WERKZEUG EINGEZOGEN
SPBN M111
S   DB3.DBX 6.7;    // MELDUNG 7055 WEKZEUGSPANNSYSTEM ÖFFNEN
=   DB1.DBX 1440.0; // NC-RESET AUSLÖSEN
M111:  NOP 0
NETWORK
TITLE = WERKZEUG AUSGEDRÜCKT UND KEINE WERKZEUGNUMMER GESPEICHERT (KEIN PROBLEM)
U   M 20.0           // KEINE WERKZEUGNUMMER GESPEICHERT
U   M 27.1           // WERKZEUG AUSGEDRÜCKT
S   M 20.1           // WERKZEUG AUSGEDRÜCKT UND KEINE WERKZEUGNUMMER GESPEICHERT
END_FUNCTION
FUNCTION FC 53 : VOID
NAME:NEUES T-WORT ÜBERPRÜFEN UND NEUE WZWSCHEIBENPOSITION FESTLEGEN
BEGIN
NETWORK
TITLE = 7000  FALSCHES T-WORT PROGRAMMIERT
L   DB20.DBW 184;   // LADE T-WORT
L   8;              // LADE KONSTANTE DEZ 8
>I;                 // VERGLEICH AUF GRÖSSER
=   M 22.4          // T-WORT GRÖSSER 10
L   DB20.DBW 184;   // LADE T-WORT
L   0;              // LADE KONSTANTE DEZ 0
<I;                 // VERGLEICH AUF KLEINER
=   M 22.5          // T-WORT KLEINER 0
O   M 22.4          // T-WORT GRÖSSER 8
O   M 22.5          // T-WORT KLEINER 0
U   DB20.DBX 182.0  // NEUES T-WORT (WERKZEUG)
SPBN M222
S   DB3.DBX 0.0;    // 7000  FALSCHES T-WORT
=   DB1.DBX 1440.0; // NC-RESET AUSLÖSEN
M222:  NOP 0
NETWORK
TITLE = T-WORT IST GÜLTIG
UN  DB3.DBX 0.0;    // 7000  FALSCHES T-WORT
SPBN M002
L   0               // MW 40 LÖSCHEN
T   MW 40           // MW 40 LÖSCHEN
L   DB20.DBW 184;   // T-WORT
L   0
==I
=   M 40.0          // WERKZEUG 0 ANGEWÄHLT (EINGESPANNTES WERKZEUG ZURÜCKGEBEN
;  ODER WZ-SPANSYSTEM EINZIEHEN )
SPB M002
L   DB10.DBD 32     // Position für Werkzeug 1 in Grad
T   MD 36           // NEUE A-ACHSPOSITION
L   DB20.DBW 184;   // T-WORT
L   1
==I
=   M 40.1
SPB M002
L   DB10.DBD 36     // Position für Werkzeug 2 in Grad
T   MD 36           // NEUE A-ACHSPOSITION
L   DB20.DBW 184;   // T-WORT
L   2
==I
=   M 40.2
SPB M002
L   DB10.DBD 40     // Position für Werkzeug 3 in Grad
T   MD 36           // NEUE A-ACHSPOSITION
L   DB20.DBW 184;   // T-WORT
L   3
==I
=   M 40.3
SPB M002
L   DB10.DBD 44     // Position für Werkzeug 4 in Grad
T   MD 36           // NEUE A-ACHSPOSITION
L   DB20.DBW 184;   // T-WORT
L   4
==I
=   M 40.4
SPB M002
L   DB10.DBD 48     // Position für Werkzeug 5 in Grad
T   MD 36           // NEUE A-ACHSPOSITION
L   DB20.DBW 184;   // T-WORT
L   5
==I
=   M 40.5
SPB M002
L   DB10.DBD 52     // Position für Werkzeug 6 in Grad
T   MD 36           // NEUE A-ACHSPOSITION
L   DB20.DBW 184;   // T-WORT
L   6
==I
=   M 40.6
SPB M002
L   DB10.DBD 56     // Position für Werkzeug 7 in Grad
T   MD 36           // NEUE A-ACHSPOSITION
L   DB20.DBW 184;   // T-WORT
L   7
==I
=   M 40.7
SPB M002
L   DB10.DBD 60     // Position für Werkzeug 8 in Grad
T   MD 36           // NEUE A-ACHSPOSITION
L   DB20.DBW 184;   // T-WORT
L   8
==I
=   M 41.0
M002:  NOP 0
L   MW 40           // NEUES WERKZEUG VON GÜLTIGEM T-WORT
L   MW 42           // EINGESPANNTES WERKZEUG AUS DEN SETTINGDATEN
<>I;                // VERGLEICH AUF UNGLEICH
=   M 22.6          // T-WORT UNGLEICH ISTWERKZEUG
L   MW 40           // NEUES WERKZEUG VON GÜLTIGEM T-WORT
L   MW 42           // EINGESPANNTES WERKZEUG AUS DEN SETTINGDATEN
==I;                // VERGLEICH AUF UNGLEICH
=   M 26.1          // NEUES T-WORT GLEICH ISTWERKZEUG
END_FUNCTION
FUNCTION FC 60 : VOID
NAME:Z-ACHSE AUF ZIELPOSITION FAHREN
BEGIN
UN  M 22.2          // Z-ACHSE AUF ZIELPOSITION FAHREN AKTIV
SPBN M001;
S   M 22.2          // Z-ACHSE AUF ZIELPOSITION FAHREN AKTIV
R   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
;       S   DB25.DBX 340.3; // EINLESEFREIGABE K1
;       S   DB25.DBX 340.4; // ACHSFREIGABE    K1
=   DB25.DBX 1.4;   // NC-SATZ ABSCHICKEN
=   DB25.DBX 548.7  // G0 wird ausgelöst
=   DB25.DBX 20.2;  // POSITIONSANFORDERUNG FÜR Z-ACHSE
L   MD 28           // Z-ACHSE ZIELPOSITION
T   DB25.DBD 30;    // POSITIONSWERT FÜR Z-ACHSE
M001:  NOP 0;
U   DB25.DBX 332.0; // NC-SATZ FERTIG
U   M 22.2          // Z-ACHSE AUF ZIELPOSITION FAHREN AKTIV
R   M 22.2          // Z-ACHSE AUF ZIELPOSITION FAHREN AKTIV
S   M 22.3          // Z-ACHSE IST AUF ZIELPOSITION
R   DB25.DBX 332.0; // NC-SATZ FERTIG
END_FUNCTION
FUNCTION FC 23 : VOID
NAME:SICHERHEITSSCHALTUNG ACC
BEGIN
//  X6:1    E 2.0    = M 15.0          // 1.TürendschalterMASCHINENTÜR ZU (HAUPTMOTORSCHÜTZ EIN)
//                                        Überwachung ob HA-Schütze abgefallen sind (muß 0-Signal
//                                        bei offener Tür oder Räderdeckel oder gedrücktem NOT-AUS sein)
//  X6:2    E 2.1    = M 15.1          // 1.Türendschalter MASCHINENTÜR OFFEN
//                                       (0 Signal bei geschlossener Tür!)
//  X6:5    E 2.5                      // nur MILL55 ACC 2.Türendschalter MASCHINENTÜR ZU 2.Türendschalter
//                                       (0 Signal bei geschlossener Tür!)
//  X6:10   A 3.4                      // nur MILL55 ACC Sicherheitsausgang für Relais (Kategorie 3)
NETWORK
TITLE =  SICHERHEITSSCHALTUNG DEFEKT K1,K2 ODER K3 HÄNGENGEBLIEBEN
UN  A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
;       U   E 2.1;          // S1 TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
;       U   E 2.5;          // S2 TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
U   E 2.0;          // K1,K2,oder K3 EIN (1 SIGNAL BEI GESCHLOSSENER TÜR)
O(
U   A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
UN  E 2.0;          // K1,K2,oder K3 EIN (1 SIGNAL BEI GESCHLOSSENER TÜR)
)
L   S5TIME#1S;
SE  T 6;           // T6 EINSCHALTVERZÖGERT
U   T 6;           // T6
S   DB2.DBX 1.1;    // HW-FEHLER SICHERHEITSSCHALTUNG
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 1.1;    // HW-FEHLER SICHERHEITSSCHALTUNG
NETWORK
TITLE = MASCHINENTÜR GESCHLOSSEN
U(
O   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
O   M 1.0           // HM MASCHINENTÜR OFFEN
)
UN  E 2.1;          // S1 TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
UN  E 2.5;          // S2 TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
UN  E 2.0;          // K1,K2,oder K3 EIN (1 SIGNAL BEI GESCHLOSSENER TÜR)
S   A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
NETWORK
TITLE = MASCHINENTÜR OFFEN
O   E 2.1;          // S1 TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
O   E 2.5;          // S2 TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
R   A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
NETWORK
TITLE = HM BEI OFFENER MASCHINENTÜR SETZEN
UN  A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
U   E 2.1;          // S1 TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
U   E 2.5;          // S2 TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
UN  E 2.0;          // K1,K2,oder K3 EIN (1 SIGNAL BEI GESCHLOSSENER TÜR)
UN  M 1.0           // HM MASCHINENTÜR OFFEN
S   M 1.1           // HM MASCHINENTÜR OFFEN
NETWORK
TITLE = 10S WIEDERANLAUFZEIT FÜR LENZESTELLER
U   M 1.1           // HM MASCHINENTÜR OFFEN
L   S5TIME#10S;     // 10S
SE  T 12;           // EINSCHALTVERZ.
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
U   M 1.1           // HM MASCHINENTÜR OFFEN
O   T 12;           // EINSCHALTVERZ.
S   M 1.0           // HM MASCHINENTÜR OFFEN
R   M 1.1           // HM MASCHINENTÜR OFFEN
NETWORK
TITLE = MELDUNG 7023 WARTEZEIT HAUPTANTRIEB
UN  M 1.0           // HM MASCHINENTÜR OFFEN
UN  E 2.1;          // S1 TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
UN  E 2.5;          // S2 TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
UN  E 2.0;          // K1,K2,oder K3 EIN (1 SIGNAL BEI GESCHLOSSENER TÜR)
UN  A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
=   DB3.DBX 2.7;    // MELDUNG 7023 WARTEZEIT HAUPTANTRIEB
=   M 90.0;         // WARTEZEIT HAUPTANTRIEB AFG/EFG
=   M 96.2;         // WARTEZEIT HAUPTANTRIEB NC_START VERR.
U   A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
R   M 1.0           // HM MASCHINENTÜR OFFEN
U   A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
FP  M 120.5         // FM AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
U   A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
FN  M 120.6         // NEGATIV-FM TÜR OFFEN
UN  A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
=   M 96.3;         // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN NC_START VERR.
END_FUNCTION
FUNCTION FC 34 : VOID
NAME:ZUWEISEN DER EINGÄNGE AC95 - ACC
BEGIN
NETWORK
TITLE = TÜRSCHALTER ZUWEISUNG
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 2.0;          // MASCHINENTÜR ZU (HAUPTMOTORSCHÜTZ EIN)
O
UN  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 1.5;          // MASCHINENTÜR ZU (HAUPTMOTORSCHÜTZ EIN)
=   M 15.0;         // MASCHINENTÜR ZU (HAUPTMOTORSCHÜTZ EIN)
NETWORK
TITLE = TÜRSCHALTER ZUWEISUNG
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
UN  M 2.0           // KENNUNG AC95 UMBAU AUF ACC
UN  A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
O(
U   M 2.0           // KENNUNG AC95 UMBAU AUF ACC
U   E 2.1;          // TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
)
O(
UN  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 1.6;          // TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
)
UN  DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
=   M 15.1;         // MASCHINENTÜR OFFEN
NETWORK
TITLE = NOT AUS ZUWEISUNG
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 2.2;          // NOT-AUS AC 2000
O
UN  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 1.4;          // NOT-AUS AC 95
=   M 15.2;         // NOT AUS
NETWORK
TITLE = RÄDERDECKELENDSCHALTER ZUWEISUNG
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 2.3;          // RÄDERDECKELENDSCHALTER (IN SERIE MIT NOT-AUS!)
O
UN  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 1.7;          // RÄDERDECKELENDSCHALTER (IN SERIE MIT NOT-AUS!)
=   M 15.6;         // RÄDERDECKELENDSCHALTER (IN SERIE MIT NOT-AUS!)
NETWORK
TITLE = SERVO READY ZUWEISUNG
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 0.0;          // SERVO READY ACHSE 0  (X)
O
UN  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN6
U   E 1.0;          // SERVO READY ACHSE 0  (X)
=   M 15.3;         // SERVO READY ACHSE 0  (X)
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 0.1;          // SERVO READY ACHSE 1  (Y)
O
UN  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 1.1;          // SERVO READY ACHSE 1  (Y)
=   M 15.4;         // SERVO READY ACHSE 1  (Y)
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 0.2;          // SERVO READY ACHSE 2  (Z)
O
UN  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 1.2;          // SERVO READY ACHSE 2  (Z)
=   M 15.5;         // SERVO READY ACHSE 2  (Z)
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 0.3;          // SERVO READY HA
O
UN  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 1.3;          // SERVO READY HA
=   M 15.7;         // SERVO READY HA
NETWORK
TITLE = N=0 VON LENZE UMRICHTER ZUWEISUNG
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 2.4;          // n=0 RELAIS VON LENZE-FU
O
UN  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 2.3;          // n=0 RELAIS VON LENZE-FU
=   M 16.0;         // n=0 RELAIS VON LENZE-FU
END_FUNCTION
FUNCTION FC 35 : VOID
NAME:ZUWEISEN DER AUSGÄNGE AC95 - ACC
BEGIN
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
SPBN   M001;
// ACC TASTE TÜR AUF (MSTT) ZUWEISEN
U   DB1.DBX 1403.7; // Concept CAM
U   DB1.DBX 1374.0; // MASCHIENENTASTATUR TÜR AUF
O(
UN  DB1.DBX 1403.7; // Concept CAM
U   DB1.DBX 1374.1; // MASCHIENENTASTATUR TÜR ZU
)
=   M 138.1;        // HM MASCHIENENTASTATUR TÜR AUF
// ACC TASTE TÜR ZU (MSTT) ZUWEISEN
U   DB1.DBX 1403.7; // Concept CAM
U   DB1.DBX 1374.1; // MASCHIENENTASTATUR TÜR ZU
O(
UN  DB1.DBX 1403.7; // Concept CAM
U   DB1.DBX 1374.0; // MASCHIENENTASTATUR TÜR AUF
)
=   M 138.2;        // HM MASCHIENENTASTATUR TÜR ZU
// ACC   TASTE SCHRAUBSTOCK ZU ZUWEISEN ACC
U   DB20.DBX 294.3; // TASTE SCHRAUBSTOCK ZU (PC)
UN  DB20.DBX 294.4; // TASTE SCHRAUBSTOCK AUF (PC)
FP  M 138.7;        // FM TASTE SCHRAUBSTOCK ZU ACC
// ACC   TASTE SCHRAUBSTOCK AUF ZUWEISEN ACC
U   DB20.DBX 294.4; // TASTE SCHRAUBSTOCK AUF (PC)
UN  DB20.DBX 294.3; // TASTE SCHRAUBSTOCK ZU (PC)
FP  M 137.1;        // FM TASTE SCHRAUBSTOCK AUF ACC
// ACC  FREIGABE ACHSEN ZUWEISEN
U   M 17.0;         // AUSGANGSMERKER  FREIGABE ACHSEN
=   A 3.0;          // ENABLE SM MODUL A
UN  M 2.0;          // KENNUNG UMBAU AUF ACC
SPBN   M3501;
U   M 17.0;         // AUSGANGSMERKER  FREIGABE ACHSEN
U   A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
L   S5TIME#1500MS;  // 1,5 S
SE  T 13;           // EINSCHALTVERZ.
U   T 13;           // EINSCHALTVERZ.
=   A 3.1;          // ENABLE SM MODUL B
ON  A 3.1;          // ENABLE SM MODUL B
O   E 0.5;          // Servo Ready Werkzeugwender Antrieb ( pys.Achse 4 )
U   M 52.5          // WERKZEUGWENDER AKTIVIERT
O(
ON  A 3.1;          // ENABLE SM MODUL B
O   E 0.4;          // Servo Ready Rundachs Antrieb       ( pys.Achse 5 )
U   M 52.7          // RUNDACHSE AKTIVIERT
)
=   M 90.3;         // ENABLE SM MODUL B AFG/EFG
=   M 96.4;         // ENABLE SM MODUL B NC_START VERR.
M3501:     NOP 0;
// ACC  REGLERFREIGABE HAUPTANTRIEB ZUWEISEN
U   M 17.1;         // AUSGANGSMERKER  REGLERFREIGABE HAUPTANTRIEB
=   A 0.3;          // REGLERFREIGABE HAUPTANTRIEB
// ACC  KÜHLMITTEL (M8=EIN/M9=AUS) ZUWEISEN
U   M 17.2;         // AUSGANGSMERKER KÜHLMITTEL (M8=EIN/M9=AUS)
=   A 3.3;          // KÜHLMITTEL (M8=EIN/M9=AUS)
// ACC  MINIMALSCHMIERUNG ZUWEISEN
U   M 18.0;         // AUSGANGSMERKER MINIMALSCHMIERUNG
=   A 4.0;          // MINIMALSCHMIERUNG
// ACC  AUSBLASVENTIL ZUWEISEN
U   M 18.2;         // AUSGANGSMERKER AUSBLASVENTIL
=   A 4.2;          // AUSBLASVENTIL
// ACC  TÜR AUF ZUWEISEN
U   M 18.3;         // AUSGANGSMERKER TÜR AUF
=   A 4.3;          // TÜR AUF
// ACC  TÜR ZU ZUWEISEN
U   M 18.4;         // AUSGANGSMERKER TÜR ZU
=   A 4.4;          // TÜR ZU
// ACC  SCHRAUBSTOCK SPANNEN ZUWEISEN
U   M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
=   A 4.5;          // SCHRAUBSTOCK SPANNEN
// ACC  SCHRAUBSTOCK ENTSPANNEN ZUWEISEN
U   M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
=   A 4.6;          // SCHRAUBSTOCK ENTSPANNEN
// ACC  TEILAPPARAT TEILEN ZUWEISEN
U   M 18.7;         // AUSGANGSMERKER TEILAPPARAT TEILEN
=   A 4.7;          // TEILAPPARAT TEILEN
// ACC  ROBOTIK PROGRAMM STOP (M30,M0,M1,M2) ZUWEISEN
U   M 19.0;         // AUSGANGSMERKER ROBOTIK PROGRAMM STOP (M30,M0,M1,M2)
=   A 5.0;          // ROBOTIK PROGRAMM STOP (M30,M0,M1,M2)
// ACC  ROBOTIK ACHSEN STEHEN AUF REF.PKT.
U   M 19.1;         // ROBOTIK ACHSEN STEHEN AUF REF.PKT.
=   A 5.1;          // ROBOTIK ACHSEN STEHEN AUF REF.PKT.
// ACC  ROBOTIK TÜR OFFEN ZUWEISEN
U   M 19.3;         // AUSGANGSMERKER ROBOTIK TÜR OFFEN
=   A 5.3;          // ROBOTIK TÜR OFFEN
// ACC  ROBOTIK TÜR GESCHLOSSEN ZUWEISEN
U   M 19.4;         // AUSGANGSMERKER ROBOTIK TÜR GESCHLOSSEN
=   A 5.4;          // ROBOTIK TÜR GESCHLOSSEN
// ACC  ROBOTIK SCHRAUBSTOCK HINTEN ZUWEISEN
U   M 19.5;         // AUSGANGSMERKER ROBOTIK SCHRAUBSTOCK HINTEN
=   A 5.5;          // ROBOTIK SCHRAUBSTOCK HINTEN
// ACC  ROBOTIK SCHRAUBSTOCK GESPANNT ZUWEISEN
U   M 19.6;         // AUSGANGSMERKER ROBOTIK SCHRAUBSTOCK GESPANNT
=   A 5.6;          // ROBOTIK SCHRAUBSTOCK GESPANNT
// ACC  ROBOTIK ALARMAUSGANG ZUWEISEN
U   M 19.7;         // AUSGANGSMERKER ROBOTIK ALARMAUSGANG
=   A 5.7;          // ROBOTIK ALARMAUSGANG
M001:     NOP 0;
UN  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
SPBN   M002;
// AC95 TASTE TÜR AUF (MSTT) ZUWEISEN
U   DB1.DBX 1374.0; // MASCHIENENTASTATUR TÜR AUF AC95
=   M 138.1;        // HM MASCHIENENTASTATUR TÜR AUF
// AC95 TASTE TÜR ZU (MSTT) ZUWEISEN
U   DB1.DBX 1374.1; // MASCHIENENTASTATUR TÜR ZU  AC95
=   M 138.2;        // HM MASCHIENENTASTATUR TÜR ZU
// AC95  TASTE SCHRAUBSTOCK ZU ZUWEISEN AC95
U   DB20.DBX 294.4; // TASTE SCHRAUBSTOCK ZU (PC)
UN  DB20.DBX 294.3; // TASTE SCHRAUBSTOCK AUF (PC)
FP  M 138.7;        // FM TASTE SCHRAUBSTOCK ZU AC95
// AC95  TASTE SCHRAUBSTOCK AUF ZUWEISEN AC95
U   DB20.DBX 294.3; // TASTE SCHRAUBSTOCK AUF (PC)
UN  DB20.DBX 294.4; // TASTE SCHRAUBSTOCK ZU (PC)
FP  M 137.1;        // FM TASTE SCHRAUBSTOCK AUF AC95
// AC95 FREIGABE ACHSEN ZUWEISEN
U   M 17.0;         // AUSGANGSMERKER  FREIGABE ACHSEN
=   A 0.1;          // FREIGABE ACHSEN
// AC95 REGLERFREIGABE HAUPTANTRIEB ZUWEISEN
U   M 17.1;         // AUSGANGSMERKER  REGLERFREIGABE HAUPTANTRIEB
=   A 0.0;          // REGLERFREIGABE HAUPTANTRIEB
// AC95 KÜHLMITTEL (M8=EIN/M9=AUS) ZUWEISEN
U   M 17.2;         // AUSGANGSMERKER KÜHLMITTEL (M8=EIN/M9=AUS)
=   A 0.4;          // KÜHLMITTEL (M8=EIN/M9=AUS)
// AC95 AUSBLASVENTIL ZUWEISEN
U   M 18.2;         // AUSGANGSMERKER AUSBLASVENTIL
=   A 4.2;          // AUSBLASVENTIL
// AC95 TÜR AUF ZUWEISEN
U   M 18.3;         // AUSGANGSMERKER TÜR AUF
=   A 4.3;          // TÜR AUF
// AC95 TÜR ZU ZUWEISEN
U   M 18.4;         // AUSGANGSMERKER TÜR ZU
=   A 4.4;          // TÜR ZU
// AC95 SCHRAUBSTOCK SPANNEN ZUWEISEN
U   M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
=   A 4.5;          // SCHRAUBSTOCK SPANNEN
// AC95 SCHRAUBSTOCK ENTSPANNEN ZUWEISEN
U   M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
=   A 4.6;          // SCHRAUBSTOCK ENTSPANNEN
// AC95 TEILAPPARAT TEILEN ZUWEISEN
U   M 18.7;         // AUSGANGSMERKER TEILAPPARAT TEILEN
=   A 4.7;          // TEILAPPARAT TEILEN
// AC95 ROBOTIK PROGRAMM STOP (M30,M0,M1,M2) ZUWEISEN
U   M 19.0;         // AUSGANGSMERKER ROBOTIK PROGRAMM STOP (M30,M0,M1,M2)
=   A 5.0;          // ROBOTIK PROGRAMM STOP (M30,M0,M1,M2)
// AC95 ROBOTIK ACHSEN STEHEN AUF REF.PKT.
U   M 19.1;         // ROBOTIK ACHSEN STEHEN AUF REF.PKT.
=   A 5.1;          // ROBOTIK ACHSEN STEHEN AUF REF.PKT.
// AC95 ROBOTIK TÜR OFFEN ZUWEISEN
U   M 19.3;         // AUSGANGSMERKER ROBOTIK TÜR OFFEN
=   A 5.3;          // ROBOTIK TÜR OFFEN
// AC95 ROBOTIK TÜR GESCHLOSSEN ZUWEISEN
U   M 19.4;         // AUSGANGSMERKER ROBOTIK TÜR GESCHLOSSEN
=   A 5.4;          // ROBOTIK TÜR GESCHLOSSEN
// AC95 ROBOTIK SCHRAUBSTOCK HINTEN ZUWEISEN
U   M 19.5;         // AUSGANGSMERKER ROBOTIK SCHRAUBSTOCK HINTEN
=   A 5.5;          // ROBOTIK SCHRAUBSTOCK HINTEN
// AC95 ROBOTIK SCHRAUBSTOCK GESPANNT ZUWEISEN
U   M 19.6;         // AUSGANGSMERKER ROBOTIK SCHRAUBSTOCK GESPANNT
=   A 5.6;          // ROBOTIK SCHRAUBSTOCK GESPANNT
// AC95 ROBOTIK ALARMAUSGANG ZUWEISEN
U   M 19.7;         // AUSGANGSMERKER ROBOTIK ALARMAUSGANG
=   A 5.7;          // ROBOTIK ALARMAUSGANG
M002:     NOP 0;
END_FUNCTION
FUNCTION FC  0 : VOID
NAME: WERKZEUGWENDER
BEGIN
NETWORK  1
UN  DB20.DBX 182.0; // NEUES T-WORT
SPB M001;
L   DB20.DBW 184;   // T-WORT
T   DB20.DBW 356;   // AKTIVES WERKZEUG IN DB20
M001:NOP 0;
NETWORK  2
L   DB20.DBW 184;   // T-WORT
L   W#16#1;         // HEX 1
<I;                 // VERGLEICH AUF KLEINER
U   DB20.DBX 182.0; // NEUES T-WORT
=   M 125.1;        // T-WORT KLEINER 1
NETWORK  3
L   DB20.DBW 184;   // T-WORT
L   W#16#63;        // WERKZEUGNR.99
>I;                 // VERGLEICH AUF GRÖSSER
U   DB20.DBX 182.0; // NEUES T-WORT
=   M 125.2;        // T-WORT GRÖSSER 99
NETWORK  4
O   M 125.1;        // T-WORT KLEINER 1
O   M 125.2;        // T-WORT GRÖSSER 99
S   DB3.DBX 0.0;    // T-WORT UNGÜLTIG
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB3.DBX 0.0;
END_FUNCTION
FUNCTION FC  1 : VOID
NAME: INITIALISIERUNGEN
BEGIN
NETWORK 1
TITLE = M0,M1 AUSLÖSUNG
U   DB20.DBX 224.1; // M1 AKTIV
U   DB20.DBX 324.5; // BEDINGTER HALT AKTIV
O   DB20.DBX 224.0  // M0 AKTIV
=   M 0.0;         // M0/M1 AKTIV
U   DB20.DBX 192.1; // M1 DYNAMISCH
U   DB20.DBX 324.5; // BEDINGTER HALT AKTIV
O   DB20.DBX 192.0; // M0 DYNAMISCH
=   M 0.1;         // HM M0/M1 DYNAMISCH
O   DB20.DBX 192.0; // M0 DYNAMISCH
=   DB20.DBX 256.0; // M0 RÜCKMELDEN
U   DB20.DBX 192.1; // M1 DYNAMISCH
U   DB20.DBX 324.5; // BEDINGTER HALT AKTIV
=   DB20.DBX 256.1; // M1 RÜCKMELDEN
NETWORK 2
TITLE = TÜRRÜCKMELDUNGEN
UN  DB10.DBX 75.0;  // TÜRAUTOMATIK AKTIVIEREN
UN  M 15.1;          // MASCHINENTÜR OFFEN
=   DB1.DBX 1390.1; // SPS>OBERFL. TÜR GESCHLOSSEN
UN  DB10.DBX 75.0;  // TÜRAUTOMATIK AKTIVIEREN
UN  DB1.DBX 1390.1; // SPS>OBERFL. TÜR GESCHLOSSEN
=   DB1.DBX 1390.0; // SPS>OBERFL. TÜR OFFEN
NETWORK 3
TITLE = SPS/OB-->EINZELSTÜCK AKTIV
UN  DB10.DBX 1.0;   // SPS-MSD DAUERLAUF AKTIV
=   DB1.DBX 1390.3; // SPS/RM-->EINZELSTÜCK AKTIV
NETWORK$4
TITLE = VORSCHUBSCHALTER AUF 0%
L   DB20.DBW 358;   // FEED-OVERRIDE-ISTWERT
L   W#16#0000;      // LADE 0
==I;                // VERGLEICH AUF GLEICH
=   M 100.2;        //VORSCHUBSCHALTER AUF 0%
NETWORK 5
TITLE = 6008  FEHLENDER CAN-TEILN.
O   DB10.DBX 75.0;  // TÜRAUTOMATIK AKTIVIEREN
O   DB10.DBX 75.1;  // EL. SCHRAUBSTOCK AKTIVIEREN
O   DB10.DBX 75.3;  // AUSBLASEINRICHTUNG AKTIVIEREN
O   DB10.DBX 75.6;  // SCHÄFER TEILAPPARAT AKTIVIEREN
O   DB10.DBX 75.7;  // ROBOTIK SCHNITTSTELLE AKTIVIEREN
U(;
ON  DB1.DBX 1414.0; // 1.CAN-EINGANGSPORT (CAN-ADRESSE 0)
ON  DB1.DBX 1415.0; // 1.CAN-AUSGANGSPORT (CAN-ADRESSE 7)
);
UN  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
=   DB2.DBX 1.0;    // 6008  FEHLENDER CAN-TEILN.
NETWORK 6
TITLE = AUSWAHL DES FREQUENZUMRICHTERS
L   DB10.DBB 20;    // HARWAREKOMPONENTEN
L   0
==I;                // VERGLEICH AUF GLEICH
=   M 115.0;        // FELDERER FU
L   DB10.DBB 20;    // HARWAREKOMPONENTEN
L   1
==I;                // VERGLEICH AUF GLEICH
=   M 115.1;        // LENZE FU
END_FUNCTION
FUNCTION FC  2 : VOID
NAME: STUECKZAEHLER
BEGIN
NETWORK  1
TITLE = STÜCKZÄHLER
UN  DB20.DBX 192.2;  // M2 dyn
UN  DB20.DBX 195.6;  // M30 dyn
O   DB20.DBX 324.2;  // DRYRUN AKTIV
SPB    M214;
SET;
L   DB15.DBD 4;      // Zähler
L   DW#16#000F_423F; // 999999 STÜCK
<>D   ;
SPB   M211;
SET;
L   DW#16#0000_0000;
T   DB15.DBD 4;
M211:  NOP 0;
L   DB15.DBD 4;
L   DW#16#0000_0001;
+D    ;              // Stückzähler +1 low
T   DB15.DBD 4;
L   DB15.DBD 8;      // CHARGENZÄHLER
L   DW#16#000F_423F; // 999999 STÜCK
<>D   ;
SPB   M213;
SET;
L   DW#16#0000_0000;
T   DB15.DBD 8;
M213:  NOP 0;
L   DB15.DBD 8;
L   DW#16#0000_0001;
+D    ;              // Stückzähler +1 low
T   DB15.DBD 8;
M214:  L   DB15.DBD 0;      // Sollstückz
L   DW#16#0000_0000;
<>D   ;
SPB   M202;
SPA   M201;
M202:  NOP 0;
L   DB15.DBD 4;      // Stückz.
L   DB15.DBD 0;      // Sollstückz.
<D  ;
SPB   M201;
SET;
S   DB3.DBX 5.3;     // 7043  SOLLSTÜCKZAHL ERREICHT
L   DW#16#0;
T   DB15.DBD 4;
M201:  NOP 0;
NETWORK  2
TITLE = NC-STARTVERRIEGELUNG
U   DB3.DBX 5.3;     // Meldung Sollstückzahl erreicht
=   M 96.6;          // Meldung Sollstückzahl erreicht NC_START VERR.
O   DB1.DBX 1370.2;  // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3;  // RESETTASTE GEDRÜCKT
R   DB3.DBX 5.3;     // Meldung Sollstückzahl erreicht
END_FUNCTION
FUNCTION FC  3 : VOID
NAME: HAUPTANTRIEB FELDERER FU
BEGIN
// AUSGÄNGE:
//************************     ACC     *********************************************
U   DB10.DBX 2.0;   // ACC  AKTIVIEREN
SPBN   M001;
NETWORK
TITLE = REGLERFREIGABE FÜR HAUPTANTRIEB
O   DB20.DBX 340.0; // SPINDEL EIN IM UHRZEIGER
O   DB20.DBX 340.1; // SPINDEL EIN GEGEN UHRZEIGER
S   M 145.2;
U   M 145.2;
U   M 104.2;        // SUM  SFG
O
UN  M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
U   M 145.7         // HM REGLERFREIGABE FÜR HAUPTANTRIEB
=   A 3.1           // FREIGABE ACHSEN B = REGLERFREIGABE FÜR HAUPTANTRIEB
S   M 145.7         // HM REGLERFREIGABE FÜR HAUPTANTRIEB
NETWORK
TITLE = AUSGANG SETZEN FÜR PUL UP
UN  A 3.4
S   A 3.4
M001:     NOP 0;
NETWORK  1
TITLE = FELDERER HAUPTANTRIEB BETRIEBSBEREIT
UN  M 15.7;         // HA BETRIEBSBEREIT
=   M 94.2;         // SFG
NETWORK  2
TITLE = FELDERER ÜBERWACHUNG HA SCHARF
U   M 110.0;        // AUX-ON
L   S5TIME#2S;      // 2S
SE  T 2;            // EINSCHALTVERZ.
NETWORK  3
TITLE = FELDERER 6013 HAUPTANTRIEB NICHT BEREIT
UN  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
UN  M 15.7;         // HA SERVO-READY
L   S5TIME#200MS;   // 0.2S
SE  T 3;            // EINSCHALTVERZ
U   T 2;
U   T 3;
S   DB2.DBX 1.5;    // HAUPTENTRIEB NICHT BEREIT 6013
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 1.5;
NETWORK  4
TITLE = FELDERER FM TASTE SPINDEL START (MSST)
O   DB20.DBX 295.4; // TASTE SPINDEL START
UN  M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
FP  M 132.5;        // FM TASTE SPINDEL START
NETWORK  5
TITLE = FELDERER FM TASTE SPINDEL HALT (MSST)
O   DB20.DBX 295.3; // TASTE SPINDEL HALT
UN  M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
FP  M 132.7;        // FM TASTE SPINDEL HALT
NETWORK  6
TITLE = FELDERER  DREHRICHTUNG MIT START-TASTE WECHSELN
U   M 106.7;        // M3/M4 SPINDELSTART FREIGABE
UN  DB20.DBX 324.0; // PROGRAMM LÄUFT
U   M 132.5;        // FM TASTE SPINDEL START
S   M 108.3;        // HM SPINDEL EINGESCHALTET
L   S5TIME#1S;      // 1 SEKUNDE
SV  T 44;           // VERLÄNGERTER IMPULS
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUSGELÖST
R   M 108.3;        // HM SPINDEL EINGESCHALTET
NETWORK  7
TITLE = FELDERER NEGATIV FLANKENMERKER
U   T 44;           // VERLÄNGERTER IMPULS M3/M4
FN  M 108.2;        // FM /TASTE SPINDEL START
NETWORK 8
TITLE =  FELDERER
U   M 104.2;        // SUM SFG
UN  M 131.7;        // ACHSEN IN BEWEGUNG
UN  M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
U   M 132.7;        // FM TASTE SPINDEL HALT
S   M 132.1;        // SPINDEL HALT AKTIV
S   M 95.7;         // SFG
S   M 91.7;         // AFG,EFG
U   M 106.7;        // BETR.MANUAL UND INC1 BIS 10000
R   M 91.7;         // AFG,EFG
U   M 110.0;        // AUX ON
U   M 132.5;        // FM TASTE SPINDEL START
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUSGELÖST
R   M 132.1;        // SPINDEL HALT AKTIV
R   M 95.7;         // SFG
R   M 91.7;         // AFG,EFG
NETWORK 9
TITLE = FELDERER
UN  M 110.0;        // AUX-ON
=   M 95.0;         // SPINDELFREIGABE
NETWORK 10
TITLE = FELDERER SUMME SFG
L   MW 94;          // WORT SFG (SPINDELFREIGABE)
L   W#16#0;         // H 0
==I;                // VERGLEICH AUF GLEICH
=   M 104.2;        // SUM SFG
NETWORK 11
TITLE = FELDERER HAUPTANTRIEB FREIGABE
U   M 104.2;        // SUM SFG
UN  DB1.DBX 1440.0; // RESET AUSGELÖST
=   M 114.0;        // HAUPTANTRIEB FREIGABE
NETWORK 12
TITLE = FELDERER ANWAHL HAUPTANTRIEB   M3
U   M 114.0;        // HA FREIGABE
U(;
UN  DB10.DBX 1.6;   // SPINDEL START NACH M0
U   M 104.3;        // SUM NC-STARTVERR.
U   M 101.0;        // NC-STARTSPEICHER
U   M 100.6;        // M3 SPEICHER
O;
U   DB20.DBX 192.3; // M3 DYN.
O;
U   T 44;           // VERLÄNGERTER IMPULS M3/M4
UN  DB20.DBX 295.4; // TASTE SPINDEL START
);
=   DB20.DBX 256.3; // M3 AUSLÖSEN
S   DB20.DBX 340.0; // SPINDEL EIN IM UHRZEIGER
R   DB20.DBX 340.1; // SPINDEL EIN GEGEN UHRZEIGER
R   M 100.6;        // M3 SPEICHER
NETWORK 13
TITLE = FELDERER ANWAHL HAUPTANTRIEB   M4
U   M 114.0;        // HA FREIGABE
U(;
UN  DB10.DBX 1.6;         // SPINDEL START NACH M0
U   M 104.3;        // SUM NC-STARTVERR.
U   M 101.0;        // NC-STARTSPEICHER
U   M 100.7;        // M4 SPEICHER
O;
U   DB20.DBX 192.4; // M4 DYN.
O;
U   M 108.2;        // FM /T44 M3/M4
U   DB20.DBX 295.4; // TASTE SPINDEL START
);
=   DB20.DBX 256.4; // M4 AUSLÖSEN
S   DB20.DBX 340.1; // SPINDEL EIN GEGEN UHRZEIGER
R   DB20.DBX 340.0; // SPINDEL EIN IM UHRZEIGER
R   M 100.7;        // M4 SPEICHER
NETWORK 14
TITLE = FELDERER ANWAHL HAUPTANTRIEB   M5
U   M 108.3;        // HM SPINDEL EINGESCHALTET
U(;
O   M 132.7;        // FM TASTE SPINDEL HALT
ON  M 106.7;        // M3/M4 SPINDELSTART FREIGABE
);
O   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
O   DB20.DBX 192.5; // M5 DYNAMISCH
O   DB1.DBX 1440.0; // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB20.DBX 324.2; // DRYRUN AKTIV
=   M 114.5;        // M5 HM
NETWORK 15
TITLE = FELDERER M3,M4 SPEICHERN BEI M0,M1
U   M 0.1;         // HM M0/M1 STATISCH
UN  DB10.DBX 1.6;   // SPINDELSTART NACH M0
U   DB20.DBX 224.3; // M3 STATISCH
S   M 100.6;        // M3 SPEICHER
U   M 0.1;         // HM M0/M1 STATISCH
UN  DB10.DBX 1.6;   // SPINDELSTART NACH M0
U   DB20.DBX 224.4; // M4 STATISCH
S   M 100.7;        // M4 SPEICHER
O   DB1.DBX 1440.0; // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   M 100.6;        // M3 SPEICHER
R   M 100.7;        // M4 SPEICHER
NETWORK 16
TITLE = FELDERER M5 VON M0,M1 UND M5 HM
U   M 0.1;         // HM M0/M1 STATISCH
U(;
O   DB20.DBX 224.3; // M3 STATISCH
O   DB20.DBX 224.4; // M4 STATISCH
);
O   M 114.5;        // M5 HM
UN  M 101.0;        // NC-START SPEICHER
O   DB20.DBX 195.6; // M30 DYN.
O   DB20.DBX 192.2; // M2 DYN.
=   DB20.DBX 256.5; // M5 AUSLÖSEN
R   DB20.DBX 340.0; // SPINDEL EIN IM UHRZEIGER
R   DB20.DBX 340.1; // SPINDEL EIN GEGEN UHRZEIGER
R   M 108.3;        // HM SPINDEL EINGESCHALTET
R   M 145.2
NETWORK 17
TITLE = FELDERER SPINDELFREIGABE
U   M 114.0;        // HA FREIGABE
=   DB20.DBX 340.5; // SPINDELFREIGABE
// BAUSTEINENDE
NETWORK 18
TITLE = FELDERER HAUPTANTRIEB STEHT
O   DB20.DBX 224.5; // M5 STATISCH
ON  M 114.0;        // HA FREIGABE
=   M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
END_FUNCTION
FUNCTION FC 33 : VOID
NAME: HAUPTANTRIEB LENZE FU
BEGIN
NETWORK  1
TITLE = LENZE  HAUPTANTRIEB BETRIEBSBEREIT
UN  M 15.7;         // SERVO READY HA
=   M 94.2;         // SFG
=   M 96.7;         // SERVO READY HAUPTANTRIEB NC_START VERR.
NETWORK  2
TITLE = LENZE  ÜBERWACHUNG HA SCHARF
U   M 110.0;        // AUX-ON
L   S5TIME#4S;      // 4S
SE  T 2;            // EINSCHALTVERZ.
NETWORK  3
TITLE = LENZE  6013 HAUPTANTRIEB NICHT BEREIT
;       UN  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
UN  M 15.7;         // SERVO READY HA
L   S5TIME#200MS;   // 0.2S
SE  T 3;            // EINGANGSFILTER
U   T 2;
U   T 3;
S   DB2.DBX 1.5;    // HAUPTENTRIEB NICHT BEREIT 6013
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 1.5;
NETWORK  4
TITLE = LENZE  FM TASTE SPINDEL START (MSST)
O   DB20.DBX 295.4; // TASTE SPINDEL START
UN  M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
FP  M 132.5;        // FM TASTE SPINDEL START
NETWORK  5
TITLE = LENZE  FM TASTE SPINDEL HALT (MSST)
O   DB20.DBX 295.3; // TASTE SPINDEL HALT
UN  M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
FP  M 132.7;        // FM TASTE SPINDEL HALT
NETWORK  6
TITLE = LENZE
U   M 106.7;        // M3/M4 SPINDELSTART FREIGABE
UN  DB20.DBX 324.0; // PROGRAMM LÄUFT
U   M 132.5;        // FM TASTE SPINDEL START
S   M 108.3;        // HM SPINDEL EINGESCHALTET
L   S5TIME#1S;      // 1 SEKUNDE
SV  T 44;           // VERLÄNGERTER IMPULS
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUSGELÖST
R   M 108.3;        // HM SPINDEL EINGESCHALTET
NETWORK  7
TITLE = LENZE  NEGATIV FLANKENMERKER
U   T 44;           // VERLÄNGERTER IMPULS M3/M4
FN  M 108.2;        // FM /TASTE SPINDEL START
NETWORK 8
TITLE = LENZE
U   M 104.2;        // SUM SFG
UN  M 131.7;        // ACHSEN IN BEWEGUNG
UN  M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
U   M 132.7;        // FM TASTE SPINDEL HALT
S   M 132.1;        // SPINDEL HALT AKTIV
S   M 95.7;         // SFG
S   M 91.7;         // AFG,EFG TASTE SPINDEL HALT
U   M 106.7;        // BETR.MANUAL UND INC1 BIS 10000
R   M 91.7;         // AFG,EFG
U   M 110.0;        // AUX ON
U   M 132.5;        // FM TASTE SPINDEL START
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUSGELÖST
R   M 132.1;        // SPINDEL HALT AKTIV
R   M 95.7;         // SFG
R   M 91.7;         // AFG,EFG
NETWORK 9
TITLE = LENZE
UN  M 110.0;        // AUX-ON
=   M 95.0;         // SPINDELFREIGABE
NETWORK 10
TITLE = LENZE  SUMME SFG
;       U   M 95.0;         // AUX-ON
;       U   M 95.1;         // SPN IN BEWEGUNG (SFG)
;       U   M 95.2;         // SFG MELDUNG 7050 KEIN TEIL GESPANNT
;       U   M 94.1;         // SFG WERKZEUG AUSGEDRÜCKT
;       U   M 94.3;         // SFG WERKZEUG AUSGEDRÜCKT
L   MW 94;          // WORT SFG (SPINDELFREIGABE)
L   W#16#0;         // H 0
==I;                // VERGLEICH AUF GLEICH
=   M 104.2;        // SUM SFG
NETWORK 11
TITLE = LENZE  HAUPTANTRIEB FREIGABE
U   M 104.2;        // SUM SFG
UN  DB1.DBX 1440.0; // RESET AUSGELÖST
=   M 114.0;        // HAUPTANTRIEB FREIGABE
NETWORK 12
TITLE = LENZE  ANWAHL HAUPTANTRIEB   M3
U   M 114.0;        // HA FREIGABE
U(;
UN  DB10.DBX 1.6;   // SPINDEL START NACH M0
U   M 104.3;        // SUM NC-STARTVERR.
U   M 101.0;        // NC-STARTSPEICHER
U   M 100.6;        // M3 SPEICHER
O;
U   DB20.DBX 192.3; // M3 DYN.
O;
U   T 44;           // VERLÄNGERTER IMPULS M3/M4
UN  DB20.DBX 295.4; // TASTE SPINDEL START
);
S   M 114.1
U   M 114.1
L   S5T#0S300MS;
SE  T 0;
U   M 114.1
U   T 1;
O   T 0;
=   DB20.DBX 256.3; // M3 AUSLÖSEN
S   DB20.DBX 340.0; // SPINDEL EIN IM UHRZEIGER
R   DB20.DBX 340.1; // SPINDEL EIN GEGEN UHRZEIGER
R   M 100.6;        // M3 SPEICHER
R   M 114.1
NETWORK 13
TITLE = LENZE  ANWAHL HAUPTANTRIEB   M4
U   M 114.0;        // HA FREIGABE
U(;
UN  DB10.DBX 1.6;   // SPINDEL START NACH M0
U   M 104.3;        // SUM NC-STARTVERR.
U   M 101.0;        // NC-STARTSPEICHER
U   M 100.7;        // M4 SPEICHER
O;
U   DB20.DBX 192.4; // M4 DYN.
O;
U   M 108.2;        // FM /T44 M3/M4
U   DB20.DBX 295.4; // TASTE SPINDEL START
);
S   M 114.2
U   M 114.2
L   S5T#0S300MS;
SE  T 1;
U   M 114.2
U   T 0;
O   T 1;
=   DB20.DBX 256.4; // M4 AUSLÖSEN
S   DB20.DBX 340.1; // SPINDEL EIN GEGEN UHRZEIGER
R   DB20.DBX 340.0; // SPINDEL EIN IM UHRZEIGER
R   M 100.7;        // M4 SPEICHER
R   M 114.2
NETWORK 14
TITLE = LENZE  ANWAHL HAUPTANTRIEB  M5
U   M 108.3;        // HM SPINDEL EINGESCHALTET
U(;
O   M 132.7;        // FM TASTE SPINDEL HALT
ON  M 106.7;        // M3/M4 SPINDELSTART FREIGABE
);
O   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
O   DB20.DBX 192.5; // M5 DYNAMISCH
O   DB1.DBX 1440.0; // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB20.DBX 324.2; // DRYRUN AKTIV
=   M 114.5;        // M5 HM
NETWORK 15
TITLE = LENZE  M3,M4 SPEICHERN BEI M0,M1
U   M 0.1;         // HM M0/M1 DYNAMISCH
UN  DB10.DBX 1.6;              // SPINDELSTART NACH M0
U   DB20.DBX 224.3; // M3 STATISCH
S   M 100.6;        // M3 SPEICHER
U   M 0.1;         // HM M0/M1 DYNAMISCH
UN  DB10.DBX 1.6;   // SPINDELSTART NACH M0
U   DB20.DBX 224.4; // M4 STATISCH
S   M 100.7;        // M4 SPEICHER
O   DB1.DBX 1440.0; // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   M 100.6;        // M3 SPEICHER
R   M 100.7;        // M4 SPEICHER
NETWORK 16
TITLE = LENZE  M5 VON M0,M1,M2,M30 UND M5 HM
U   M 0.1;         // HM M0/M1 DYNAMISCH
U(;
O   DB20.DBX 224.3; // M3 STATISCH
O   DB20.DBX 224.4; // M4 STATISCH
);
O   M 114.5;        // M5 HM
UN  M 101.0;        // NC-START SPEICHER
O   DB20.DBX 195.6; // M30 DYN.
O   DB20.DBX 192.2; // M2 DYN.
=   DB20.DBX 256.5; // M5 RÜCKMELDEN
R   DB20.DBX 340.0; // SPINDEL EIN IM UHRZEIGER
R   DB20.DBX 340.1; // SPINDEL EIN GEGEN UHRZEIGER
R   M 108.3;        // HM SPINDEL EINGESCHALTET
R   M 114.1
R   M 114.2
R   M 114.4
NETWORK 17
TITLE = LENZE  SPINDELFREIGABE
U   M 114.0;        // HA FREIGABE
=   DB20.DBX 340.5; // SPINDELFREIGABE
NETWORK 18
TITLE = LENZE  HAUPTANTRIEB STEHT
U   M 16.0;         // n=0 RELAIS VON LENZE-FU
ON  M 15.7;         // HA SERVO-READY
=   M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
R   M 114.6         // HM REGLERFREIGABE FÜR HAUPTANTRIEB
NETWORK 19
TITLE = LENZE  REGLERFREIGABE FÜR HAUPTANTRIEB
O   M 114.1;        // ANFORDERUNG M3
O   M 114.2;        // ANFORDERUNG M4
=   M 90.7;         // AFG UND EFG WEGNEHMEN
S   M 114.4;        // HM
U   M 114.4;        // HM
U   M 104.2;        // SUM  SFG
O
UN  M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
U   M 114.6;        // HM REGLERFREIGABE FÜR HAUPTANTRIEB
=   M 17.1;         // AUSGANGSMERKER  REGLERFREIGABE HAUPTANTRIEB
S   M 114.6;        // HM REGLERFREIGABE FÜR HAUPTANTRIEB
END_FUNCTION
FUNCTION FC  4 : VOID
NAME: BETRIEBSARTEN
BEGIN
NETWORK 1
TITLE = SUM SPINDEL EIN/AUS
UN  DB20.DBX 327.2; // BETRIEBSART REFERENZ
UN  DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
UN  DB20.DBX 326.2; // BETRIEBSART MDI
UN  DB20.DBX 326.1; // BETRIEBSART EDIT
UN  DB20.DBX 326.3; // BETRIEBSART REPOS
UN  DB20.DBX 327.4; // BETRIEBSART PRESET
=   M 106.7;        // M3/M4 SPINDEL FREIGABE IN DER -
// BETRIEBSART JOG UND INC
END_FUNCTION
FUNCTION FC  5 : VOID
NAME: ACHSEN BEREITSCHAFT
BEGIN
NETWORK 0
TITLE = FREIGABE ACHSEN
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
L   S5TIME#100MS;   // 1S
SS  T 4;            // EINSCHALTVERZÖGERUNG (SPEICHERND)
U   M 115.1;        // LENZE FU
U   T 4;            // EINSCHALTVERZÖGERUNG (SPEICHERND)
=   M 17.0;         // AUSGANGSMERKER  FREIGABE ACHSEN
NETWORK  1
TITLE = ACHSEN BETRIEBSBEREIT
UN  M 15.3;         // X-ACHSE SERVO-READY
UN  M 15.4;         // Y-ACHSE SERVO-READY
UN  M 15.5;         // Z-ACHSE SERVO-READY
=   M 111.0;        // ACHSEN  BEREITSCHAFT
NETWORK  2
TITLE = ACHSEN BETRIEBSBEREIT  AFG/EFG
UN  M 111.0;        // ACHSEN BETRIEBSBEREIT
=   M 90.1;         // ACHSEN BETRIEBSBEREIT  AFG/EFG
NETWORK  3  8**********
TITLE = ÜBERWACHUNG ACHSEN SCHARF
U   M 110.0;        // AUX_ON
L   S5TIME#1S500MS; // 15X0,1S
SE  T 5;            // STARTEN T5 ALS EINSCHALTVERZ.
NETWORK  4
TITLE = ALARM ANTRIEB X-ACHSE DEFEKT
U   M 15.3;         // X-ACHSE SERVO-READY
U   T 5;            // ÜBERWACHUNG ACHSEN SCHARF
S   DB2.DBX 1.2;    // ALARM ANTRIEB X-ACHSE DEFEKT
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 1.2;    // ALARM ANTRIEB X-ACHSE DEFEKT
NETWORK  5
TITLE = ALARM ANTRIEB Y-ACHSE DEFEKT
U   M 15.4;         // Y-ACHSE SERVO READY
U   T 5;            // ÜBERWACHUNG ACHSEN SCHARF
S   DB2.DBX 1.3;    // ALARM ANTRIEB Y-ACHSE DEFEKT
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 1.3;    // ALARM ANTRIEB Y-ACHSE DEFEKT
NETWORK  6
TITLE = ALARM ANTRIEB Z-ACHSE DEFEKT
U   M 15.5;         // Z-ACHSE SERVO-READY
U   T 5;            // ÜBERWACHUNG ACHSEN SCHARF
L   S5TIME#500MS;
SE  T 31;
U   T 31;
S   DB2.DBX 1.4;    // ALARM ANTRIEB Z-ACHSE DEFEKT
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 1.4;    // ALARM ANTRIEB Z-ACHSE DEFEKT
NETWORK  6
TITLE = ALARM ANTRIEB WZW-ACHSE DEFEKT  8**************
;       U   M 15.xxx;         // WZW-ACHSE SERVO-READY
;       U   T 5;            // ÜBERWACHUNG ACHSEN SCHARF
;       S   DB2.DBX 1.xxx;    // ALARM ANTRIEB Z-ACHSE DEFEKT
;
;       O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
;       O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
;       R   DB2.DBX 1.4;    // ALARM ANTRIEB Z-ACHSE DEFEKT
END_FUNCTION
FUNCTION FC  6 : VOID
NAME: ACHSEN_JOG
BEGIN
NETWORK  1
TITLE = ACHSEN_JOG FREIGABE
U   DB20.DBX 340.4; // ACHSFREIGABE
=   M 112.0;        // ACHSEN_JOG FREIGABE
NETWORK  2
TITLE = ACHSEN_JOG/STARTVERR.
U   DB20.DBX 1.2;   // FC JOG_ANFORDERUNG
UN  M 112.0;        // ACHSEN_JOG FREIGABE
R   DB20.DBX 1.2;   // FC JOG_ANFORDERUNG
NETWORK
TITLE = JOG A-ACHSE / STARTVERR.
U   DB20.DBX 1.2;   // FC JOG-ACHSEN
U(
O   DB20.DBX 11.2;  // JOG-KANALACHSE A IN POS.RICHTUNG
O   DB20.DBX 13.2;  // JOG-KANALACHSE A IN NEG.RICHTUNG
UN  A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
)
R   DB20.DBX 1.2;   // FC JOG-ACHSEN
NETWORK
TITLE = ACHSEN INC/STARTVERR.
ON  M 112.0;        // ACHSEN_JOG FREIGABE
O   M 100.2;        // VORSCHUBSCHALTER AUF 0%
U   DB20.DBX 1.3;   // FC INC ANFORDERUNG
R   DB20.DBX 1.3;   // FC INC ANFORDERUNG
END_FUNCTION
FUNCTION FC 32 : VOID
NAME: AUTOMATISCH IN BA REF UMSCHALTEN
BEGIN
NETWORK
TITLE = AUTOMATISCH IN BA REF UMSCHALTEN MIT DER AUX-ON TASTE VERHINDERN (ZUM FREIFAHREN!!)
UN  M 121.0
UN  M 121.2
UN  M 52.2          // REFERENZPUNKT X,Y,Z UND WZW C-ACHSE AKTIV
U   DB1.DBX 1374.4  // AUX ON TASTE
UN  M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
S   M 121.1
S   M 121.0
U   DB1.DBX 1374.4  // AUX ON TASTE
UN  M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
UN  M 121.1
=   M 121.2
R   M 121.0
UN  DB1.DBX 1374.4  // AUX ON TASTE
R   M 121.1
NETWORK
TITLE = AUTOMATISCH IN BA REF UMSCHALTEN
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
S   DB100.DBX500.0; // BIT FRÜR BA JOG
L   DB20.DBW 288    // BA ANWAHL
L   0
==I
SPB M0011
L   DB20.DBW 288    // BA ANWAHL
T   DB100.DBW 500
M0011: NOP 0
UN  M 52.3          // WZW-ABGEBROCHEN
UN  DB3.DBX 7.1;    // MELDUNG 7057 WEKZEUGHALTER BELEGT
UN  DB3.DBX 7.2;    // MELDUNG 7058 ACHSEN FREIFAHREN
UN  M 52.2          // REFERENZPUNKT X,Y,Z UND WZW C-ACHSE AKTIV
UN  M 121.0
;       U   M 110.0;        // AUX-ON
SPBN M0012
L   W#16#0400       // BIT BA REF SETZEN
T   DB20.DBW 288    // BA ANWAHL
SPA M0013
M0012: NOP 0
L   DB100.DBW 500
T   DB20.DBW 288    // BA ANWAHL
M0013: NOP 0
L   DB20.DBW 326
L   DB20.DBW 288
<>I
=   DB20.DBX 0.0    // BA WECHSELANFORDERUNG
END_FUNCTION
FUNCTION FC  7 : VOID
NAME: ACHSEN_REFERENZIEREN
BEGIN
NETWORK  1
TITLE = FREIGABE REFERENZIEREN
U   M 104.0;        // SUM AFG
U   M 110.0;        // AUX_ON AUTO
UN  M 100.2;        // VORSCHUBSCHALTER AUF 0%
UN  M 52.3          // WZW-ABGEBROCHEN
UN  DB3.DBX 6.7;    // MELDUNG 7055 WEKZEUGSPANNSYSTEM ÖFFNEN
UN  DB3.DBX 7.0;    // MELDUNG 7056 ILLEGALE WERKZEUGNUMMER IN DEN SETTINGDATEN
UN  DB3.DBX 7.1;    // MELDUNG 7057 WEKZEUGHALTER BELEGT
UN  DB3.DBX 7.2;    // MELDUNG 7058 ACHSEN FREIFAHREN
=   M 120.0;        // FREIGABE REFERENZIEREN
NETWORK  2
TITLE = REFERENZPUNKT ANFAHREN ABBRECHEN (RESET AUSLÖSEN)
U   DB20.DBX 327.2; // BETRIEBSART REFERENZ
U   M 15.1;         // MASCHINENTÜR OFFEN
U   M 131.7;        // ACHSEN IN BEWEGUNG
SPBN   M001;
=   DB1.DBX 1440.0; // NC-RESET AUSLÖSEN
M001:  NOP 0
NETWORK
TITLE = REFPKT.X,Y,Z, VON MST ZUGLEICH ANFAHREN
U   DB20.DBX 830.4; // VON MST ALLE ACHSEN REFERENZIEREN
U   DB20.DBX 327.2; // BETRIEBSART REFERENZ
U   M 120.0;        // FREIGABE REFERENZIEREN
UN  M 121.3;        // REFPKT.X,Z,ZUGLEICH ANFAHREN
FP  M 121.4         // FM REFPKT.X,Z, VON MST ZUGLEICH ANFAHREN
U   M 121.4         // FM REFPKT.X,Z, VON MST ZUGLEICH ANFAHREN
SPBN   M331;
=   DB20.DBX 8.0;   // REFERENZIEREN X-ACHSE
=   DB20.DBX 8.1;   // REFERENZIEREN Y-ACHSE
=   DB20.DBX 8.2;   // REFERENZIEREN Z-ACHSE
=   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
M331:  NOP 0
NETWORK  4
TITLE = REFPKT.X,Y,Z, ZUGLEICH ANFAHREN
U   M 120.0;        // FREIGABE REFERENZIEREN
UN  M 121.3;        // REFPKT.X,Z,ZUGLEICH ANFAHREN
U   DB20.DBX 8.0;   // REFERENZIEREN X-ACHSE
U   DB20.DBX 8.1;   // REFERENZIEREN Y-ACHSE
U   DB20.DBX 8.2;   // REFERENZIEREN Z-ACHSE
S   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
FP  M 121.5;        // FM REFPKT.X,Z,ZUGLEICH ANFAHREN
O   DB1.DBX 1370.3; // RESET TASTE GEDRÜCKT
O   DB1.DBX 1440.0  // NC-RESET AUSLÖSEN
O   M 122.5;        // FM REFERENZPUNKT AKTIV
O   M 52.6          // FM REFERENZPUNKT X,Y,Z UND WZW C-ACHSE AKTIV
R   M 121.3;        // REFPKT.X,Z,ZUGLEICH ANFAHREN
;8(((((       U   M 120.4;        // REFERENZPUNKT X&Y&Z-ACHSE AKTIV
;       U   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
;       R   DB20.DBX 8.0;   // REFERENZIEREN X-ACHSE
;       R   DB20.DBX 8.1;   // REFERENZIEREN Y-ACHSE
;       R   DB20.DBX 8.2;   // REFERENZIEREN Z-ACHSE
;       R   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
NETWORK  5
TITLE = REFERENZPUNKT  DEAKTIVIEREN
U   DB1.DBX 134.0;  // REFERENZPUNKT X-ACHSE AKTIV
U   DB1.DBX 134.1;  // REFERENZPUNKT Y-ACHSE AKTIV
U   DB1.DBX 134.2;  // REFERENZPUNKT Z-ACHSE AKTIV
=   M 120.4;        // REFERENZPUNKT X&Y&Z-ACHSE AKTIV
O   M 120.6         // NEGATIV-FM TÜR OFFEN
O   M 120.5         // FM AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
O   DB3.DBX 7.2;    // MELDUNG 7058 ACHSEN FREIFAHREN
O   DB1.DBX 1370.3; // RESET TASTE GEDRÜCKT
O   DB1.DBX 1440.0  // NC-RESET AUSLÖSEN
U(
ON  M 26.0          // ABSOLUTE ACHSE 4 (WZW) 0 GRAD ERREICHT
O   E 16.4          // REF BERO WZW
)
O(
U   DB20.DBX 9.2;   // REFERENZIEREN VON KANALACHSE A
U   DB10.DBX 100.0  // WERKZEUGWENDER EINSTELLEN AKTIVIERT
O   M 121.5;        // FM REFPKT XYZ ZUGLEICH ANFAHREN
U   M 120.0;        // FREIGABE REFERENZIEREN
U   M 23.6;         // Z-ACHSE IM SICHEREN BEREICH
)
O   DB3.DBX 7.1;    // MELDUNG 7057 WEKZEUGHALTER BELEGT
U   M 52.5          // WERKZEUGWENDER AKTIVIERT
FP  M 121.6
U   M 121.6
SPBN   M111;
=   DB20.DBX 348.4; // KANAL0 REFERENZPUNKT DER RUNDACHSEN DEAKTIVIEREN
=   DB25.DBX 348.4; // KANAL1 REFERENZPUNKT DER RUNDACHSEN DEAKTIVIEREN
R   M 120.2         // WZW-ACHSE 0 GRAD ERREICHT
R   M 120.3         // HM WZW-ACHSE 0 GRAD ERREICHT
R   M 25.0          // WERKZEUG 0 AUFNEHMEN FERTIG
M111:  NOP 0
U   M 121.5;        // FM REFPKT XYZ ZUGLEICH ANFAHREN
;8(((((       UN  M 52.5          // WERKZEUGWENDER AKTIVIERT
;       UN  M 52.7          // RUNDACHSE AKTIVIERT
O   DB2.DBX 1.2;    // ALARM X-ACHSE DEFEKT
O   DB2.DBX 1.3;    // ALARM Y-ACHSE DEFEKT
O   DB2.DBX 1.4;    // ALARM Z-ACHSE DEFEKT
O   DB2.DBX 1.5;    // ALARM WZW-ACHSE DEFEKT 8************
O   DB2.DBX 0.0;    // ALARM NOT-AUS
FP  M 121.7
U   M 121.7
SPBN   M221;
=   DB20.DBX 348.0; // KANAL0 REFERENZPUNKT DEAKTIVIEREN
=   DB25.DBX 348.0; // KANAL1 REFERENZPUNKT DEAKTIVIEREN
=   DB20.DBX 348.4; // KANAL0 REFERENZPUNKT DER RUNDACHSEN DEAKTIVIEREN
=   DB25.DBX 348.4; // KANAL1 REFERENZPUNKT DER RUNDACHSEN DEAKTIVIEREN
U   M 121.5;        // FM REFPKT XYZ ZUGLEICH ANFAHREN
S   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
R   M 120.4;        // REFERENZPUNKT X&Y&Z-ACHSE AKTIV
=   DB20.DBX 348.4; // KANAL0 REFERENZPUNKT DER RUNDACHSEN DEAKTIVIEREN
=   DB25.DBX 348.4; // KANAL1 REFERENZPUNKT DER RUNDACHSEN DEAKTIVIEREN
R   M 120.2         // WZW-ACHSE 0 GRAD ERREICHT
R   M 120.3         // HM WZW-ACHSE 0 GRAD ERREICHT
R   M 25.0          // WERKZEUG 0 AUFNEHMEN FERTIG
M221:  NOP 0
NETWORK  6
TITLE = REFPKT.ANFAHREN SPERREN
U   DB20.DBX 1.1;   // REFERENZPKT ANFAHREN (UMLENKUNG)
UN  M 120.0;        // FREIGABE REFERENZIEREN
R   DB20.DBX 1.1;   // REFERENZPKT ANFAHREN (UMLENKUNG)
S   DB20.DBX 318.7; // NEG.QUITTUNG DNC
NETWORK  7
TITLE = Z-ACHSE REFERENZIEREN (FC-CODE)
U   M 121.5;        // FM REFPKT.X,Z,ZUGLEICH ANFAHREN
U   DB20.DBX 8.2;   // REFERENZIEREN Z-ACHSE
UN  M 120.4;        // REFERENZPUNKT X&Y&Z-ACHSE AKTIV
O(;
U   M 120.0;        // FREIGABE REFERENZIEREN
UN  M 121.3;        // REFPKT.X,Z,ZUGLEICH ANFAHREN
U   DB20.DBX 8.2;   // REFERENZIEREN Z-ACHSE
);
O(
U   M 121.5;        // FM REFPKT.X,Z,ZUGLEICH ANFAHREN
UN  M 52.5          // WERKZEUGWENDER AKTIVIERT
UN  M 52.7          // RUNDACHSE AKTIVIERT
)
=   DB20.DBX 8.2;   // REFERENZIEREN Z-ACHSE
NETWORK  8
TITLE = FM REFERENZPUNKT Z-ACHSE AKTIV
U   DB1.DBX 134.2;  // REFERENZPUNKT Z-ACHSE AKTIV
FP  M 120.7;        // FM REFERENZPUNKT Z-ACHSE AKTIV
NETWORK  9
TITLE = X-ACHSE REFERENZIEREN (FC-CODE)
U   M 121.3;        // REFPKT.X,Z,ZUGLEICH ANFAHREN
U   M 120.7;        // FM REFERENZPUNKT Z-ACHSE AKTIV
O(;
U   M 120.0;        // FREIGABE REFERENZIEREN
UN  M 121.3;        // REFPKT.X,Z,ZUGLEICH ANFAHREN
U   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
U   DB20.DBX 8.0;   // REFERENZIEREN X-ACHSE
);
=   DB20.DBX 8.0;   // REFERENZIEREN X-ACHSE
R   DB20.DBX 8.2;   // REFERENZIEREN Z-ACHSE
SPBN   M002;
=   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
M002:     NOP 0;
NETWORK 10
TITLE = Y-ACHSE REFERENZIEREN (FC-CODE)
U   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.7;        // FM REFERENZPUNKT Z-ACHSE AKTIV
O(;
U   M 120.0;        // FREIGABE REFERENZIEREN
UN  M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
U   DB20.DBX 8.1;   // REFERENZIEREN Y-ACHSE
);
=   DB20.DBX 8.1;   // REFERENZIEREN Y-ACHSE
NETWORK  8
TITLE = FM X-,Y-,Z-REFERENZPUNKT AKTIV
U   M 120.4;        // REFERENZPUNKT X&Y&Z-ACHSE AKTIV
FP  M 120.1;        // FM REFERENZPUNKT X,Y und Z-ACHSE AKTIV
R   M 121.0
U   DB1.DBX 134.0;  // REFERENZPUNKT X-ACHSE AKTIV
U   DB1.DBX 134.1;  // REFERENZPUNKT Y-ACHSE AKTIV
U   DB1.DBX 134.2;  // REFERENZPUNKT Z-ACHSE AKTIV
U(
O   DB1.DBX 134.4;  // REFERENZPUNKT WZW-ACHSE AKTIV
ON  M 52.5          // WERKZEUGWENDER AKTIVIERT
)
U(
O   DB1.DBX 134.5;  // REFERENZPUNKT A-ACHSE AKTIV
ON  M 52.7          // RUNDACHSE AKTIVIERT
)
=   M 52.2          // REFERENZPUNKT X,Y,Z UND WZW C-ACHSE AKTIV
FP  M 52.6          // FM REFERENZPUNKT X,Y,Z UND WZW C-ACHSE AKTIV
NETWORK  8
TITLE =  WZW REFERENZIEREN
U   M 52.5          // WERKZEUGWENDER AKTIVIERT
SPBN   M003;
NETWORK 10
TITLE = Z-ACHSE IM SICHEREN BEREICH
L   DB1.DBD 8;      // Z-ACHSE ISTPOSITION
L   MD 74;          // Z-ACHSE WZW-SCHWENKPOSITION KEIN WERKZEUG IN DER SPINDEL
>=R
=   M 23.6;         // Z-ACHSE IM SICHEREN BEREICH
NETWORK 10
TITLE = WZW-ACHSE REFERENZIEREN (FC-CODE)
U   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.7;        // FM REFERENZPUNKT Z-ACHSE AKTIV
;8((((       O(
;       U   M 120.4;        // REFERENZPUNKT X&Y&Z-ACHSE AKTIV
;       U   M 121.5;        // FM REFPKT XYZ ZUGLEICH ANFAHREN
;       )
O(
UN  M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
U   DB20.DBX 9.2;   // REFERENZIEREN VON KANALACHSE A
U   DB10.DBX 100.0  // WERKZEUGWENDER EINSTELLEN AKTIVIERT
)
U(
O   DB10.DBX 100.0  // WERKZEUGWENDER EINSTELLEN AKTIVIERT
O   M 23.6;         // Z-ACHSE IM SICHEREN BEREICH
)
U   M 120.0;        // FREIGABE REFERENZIEREN
SPBN   M004;
=   DB25.DBX 9.4;   // REFERENZIEREN VON KANALACHSE C
=   DB25.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
R   M 120.2         // WZW-ACHSE 0 GRAD ERREICHT
R   M 120.3         // HM WZW-ACHSE 0 GRAD ERREICHT
M004:     NOP 0;
NETWORK 10
TITLE = WZW-ACHSE REFERENZIEREN VON ACHSTASTE (DIE TASTE IST NUR IM EISTELLMODUS WIRKSAM!!)
U   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
U   DB20.DBX 9.2;   // REFERENZIEREN VON KANALACHSE A
U   DB10.DBX 100.0  // WERKZEUGWENDER EINSTELLEN AKTIVIERT
R   DB20.DBX 9.2;   // REFERENZIEREN VON KANALACHSE A
R   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
NETWORK
TITLE = WZW-ACHSE AUF 0° FAHREN
U   DB1.DBX 134.4;  // REFERENZPUNKT WZW-ACHSE AKTIV
FP  M 122.0         // FM REFERENZPUNKT WZW-ACHSE AKTIV
UN  DB10.DBX 100.0  // WZW MILL55 EINSTELLEN AKTIVIERT
O(
U   DB10.DBX 100.0  // WZW MILL55 EINSTELLEN AKTIVIERT
U   M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
)
U   M 122.0         // FM REFERENZPUNKT WZW-ACHSE AKTIV
UN  M 120.2         // WZW-ACHSE 0 GRAD ERREICHT
UN  M 120.3         // HM WZW-ACHSE 0 GRAD ERREICHT
U   M 120.0;        // FREIGABE REFERENZIEREN
U(
O   DB10.DBX 100.0  // WERKZEUGWENDER EINSTELLEN AKTIVIERT
O   M 23.6;         // Z-ACHSE IM SICHEREN BEREICH
)
SPBN M006;
S   M 120.3         // HM WZW-ACHSE 0 GRAD ERREICHT
=   DB25.DBX 1.4;   // NC-SATZ ABSCHICKEN
=   DB25.DBX 548.5; // GENAUHALTMODUS SETZEN
=   DB25.DBX 549.0; // G1 WIRD AUSGELÖST
=   DB25.DBX 549.4; // VORSCHUB IN GRAD/S WERT IN DBD556
L   DB10.DBD 28;    // VORSCHUBWERT IN GRAD/SEKUNDEN
T   DB25.DBD 556;   // F-WERT IN M/S ODER M/U
S   DB25.DBX 21.4;  // POSITIONSANFORDERUNG FÜR KANALACHSE C
L   +0.00E0;
T   DB25.DBD 70;    // POSITIONSWERT FÜR KANALACHSE C
L   0
T   MW 46
M006:     NOP 0;
U   M 120.3         // HM WZW-ACHSE 0 GRAD ERREICHT
U   DB25.DBX 332.0; // NC-SATZ FERTIG
SPBN M007;
S   M 120.2         // WZW-ACHSE 0 GRAD ERREICHT
R   M 120.3         // HM WZW-ACHSE 0 GRAD ERREICHT
R   DB25.DBX 332.0; // NC-SATZ FERTIG
U   E 4.2;          // 12mm-BERO WERKZEUG BESTÜCKT
S   M 46.1          // WERKZEUG 1 VORHANDEN
M007:     NOP 0;
M003:     NOP 0;
U   M 52.7          // RUNDACHSE AKTIVIERT
SPBN   M008;
NETWORK 10
TITLE = RUNDACHSE A REFERENZIEREN (FC-CODE)
U   M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   M 120.7;        // FM REFERENZPUNKT Z-ACHSE AKTIV
O(;
UN  M 121.3;        // REFPKT.X,Y,Z,ZUGLEICH ANFAHREN
U   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
U   DB20.DBX 9.2;   // REFERENZIEREN VON KANALACHSE A
);
O(
U   M 120.4;        // REFERENZPUNKT X&Y&Z-ACHSE AKTIV
U   M 121.5;        // FM REFPKT XYZ ZUGLEICH ANFAHREN
)
;       O(
;       U   M 120.5         // FM AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
;       U   DB1.DBX 134.2;  // REFERENZPUNKT Z-ACHSE AKTIV
;       U   M 120.0;        // FREIGABE REFERENZIEREN
;       )
U   M 120.0;        // FREIGABE REFERENZIEREN
SPBN   M005;
=   DB20.DBX 9.2;   // REFERENZIEREN VON KANALACHSE A
=   DB20.DBX 1.1;   // REFERENZPKT.ANFAHREN (UMLENKUNG)
M005:     NOP 0;
M008:     NOP 0;
NETWORK 11
TITLE = FM REFERENZPUNKT AKTIV
U   M 52.2          // REFERENZPUNKT X,Y,Z UND WZW C-ACHSE AKTIV
FP  M 122.5;        // FM REFERENZPUNKT AKTIV
UN  M 52.2          // REFERENZPUNKT X,Y,Z UND WZW C-ACHSE AKTIV
=   M 96.5;         // REFERENZPUNKT NICHT AKTIV NC_START VERR.
NETWORK 13
TITLE = MELDUNG 7017 REFERENZP. ANFAHREN
UN  DB3.DBX 6.2;    // MELDUNG KEIN TEIL GESPANNT
UN  DB3.DBX 5.0;    // MELDUNG MASCHINENTÜR OFFEN
UN  DB3.DBX 6.7;    // MELDUNG 7055 WEKZEUGSPANNSYSTEM ÖFFNEN
UN  DB3.DBX 7.1;    // MELDUNG 7057 WEKZEUGHALTER BELEGT
UN  DB3.DBX 7.2;    // MELDUNG 7058 ACHSEN FREIFAHREN
UN  M 52.2          // REFERENZPUNKT X,Y,Z UND WZW C-ACHSE AKTIV
UN  DB3.DBX 2.7;    // MELDUNG 7023 WARTEZEIT HAUPTANTRIEB
=   DB3.DBX 2.1;    // 7017  REFERENZPUNKT ANFAHREN
END_FUNCTION
FUNCTION FC  8 : VOID
NAME: AUX_ON AC95
BEGIN
NETWORK  1
TITLE = ALARM NOT_AUS AC95
UN  M 15.2;         // NOT AUS
S   DB2.DBX 0.0;    // ALARM NOT-AUS
=   DB1.DBX 1390.5; // SPS>OBERFL. NOT AUS
O   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 0.0;    // ALARM NOT-AUS
NETWORK  2
TITLE = MELDUNG MASCHINENTÜR OFFEN AC95
U   M 15.1;          // MASCHINENTÜR OFFEN
UN  DB3.DBX 6.2;    // KEIN TEIL GESPANNT (M7050)
UN  DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
UN  DB3.DBX 2.7;    // MELDUNG 7023 WARTEZEIT HAUPTANTRIEB
UN  DB3.DBX 0.2;    // 7002  Werkzeug wechseln
=   DB3.DBX 5.0;    // MASCHINENTÜR OFFEN (7040)
NETWORK  3
TITLE = 6024 ALARM TÜR OFFEN AC95
O   M 131.7;        // ACHSEN IN BEWEGUNG
O   M 92.0;         // RUNDACHSE IN BEWEGUNG
U   DB20.DBX 326.4; // BETRIEBSART AUTO
=   M 102.0;        // HM
U   M 131.7;        // ACHSEN IN BEWEGUNG
U   DB20.DBX 327.2; // BETRIEBSART REFERENZ
=   M 102.1;        // HM
U   DB20.DBX 324.0; // PROGRAMM LÄUFT
UN  DB20.DBX 324.1; // STOP-ZUSTAND
U   DB20.DBX 326.4; // BETRIEBSART AUTO
=   M 102.2;        // HM
UN  M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
=   M 102.3;        // HM
O   M 102.0;        // HM
O   M 102.1;        // HM
O   M 102.2;        // HM
O   M 102.3;        // HM
=   M 110.2;        // HM TÜRALARM
U   M 110.2;        // HM TÜRALARM
U   M 15.1;         // MASCHINENTÜR OFFEN
S   DB2.DBX 3.0;    // 6024 ALARM TÜR OFFEN
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 3.0;    // 6024 ALARM TÜR OFFEN
NETWORK  4
TITLE = SUM ALARME 1 AC95
L   DB2.DBW 0;      // WORT ALARME 1
L   W#16#0;
<>I;
=   M 105.0;        // SUM ALARME1
NETWORK  5
TITLE = SUM ALARME 2 AC95
L   DB2.DBW 2;      // WORT ALARME 2
L   W#16#0;
<>I;
=   M 105.1;        // SUM ALARME 2
NETWORK  6
TITLE = SUM THERM.ALARME AC95
L   DB2.DBW 4;      // BYTE/THERM. ALARME
L   W#16#0;
<>I;
=   M 105.2;        // SUM THERM. ALARME
NETWORK  7
TITLE = ANWAHL AUX_ON AC95
U   DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
S   M 110.1;        // AUX_ON EIN
U   M 110.1;        // AUX_ON EIN
=   DB1.DBX 1390.2; // SPS>OBERFLÄCHE AUX-ON
S   M 110.0;        // AUX_ON AUTO
S   M 110.3;        // AUX-ON MANUAL
NETWORK  8
TITLE = ABWAHL AUX_ON AC95
O   M 105.0;        // SUM ALARME 1
O   M 105.1;        // SUM ALARME 2
O   M 105.2;        // SUM THERM. ALARME
O   DB1.DBX 1366.3; // ALARM AKTIV
R   M 110.3;        // AUX-ON MANUAL
UN  M 110.3;        // AUX-ON MANUAL
O   M 15.1;         // MASCHINENTÜR OFFEN
O   M 18.3;         // AUSGANGSMERKER TÜR AUF
O   DB1.DBX 1366.3; // ALARM AKTIV
R   M 110.0;        // AUX-ON AUTO
NETWORK  9
TITLE = HW-ÜBERPRÜFUNG TÜR ÖFFNEN AC95 (ALARM 6009)
U   M 15.1;          // MASCHINENTÜR OFFEN
L   S5TIME#1S;      // 10X0.1S
SE  T 6;            // T6 EINSCHALTVERZÖGERT
U   T 6;            // T6
U   M 15.0;         // MASCHINENTÜR ZU (HAUPTMOTORSCHÜTZ EIN)
S   DB2.DBX 1.1;    // HW-FEHLER SICHERHEITSSCHALTUNG
NETWORK 10
TITLE = HW-ÜBERPRÜFUNG TÜR SCHLIESZEN AC95
UN  DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
UN  M 15.1;          // MASCHINENTÜR OFFEN
U   M 15.2;         // NOT AUS
U   M 15.6;         // RÄDERDECKEL GESCHLOSSEN
L   S5TIME#1S;      // 10X0.1S
SE  T 11;           // T11 EINSCHALTV.
U   T 11;           // T11
UN  M 15.0;         // MASCHINENTÜR ZU (HAUPTMOTORSCHÜTZ EIN)
S   DB2.DBX 1.1;    // HW-FEHLER SICHERHEITSSCHALTUNG
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 1.1;    // HW-FEHLER SICHERHEITSSCHALTUNG
END_FUNCTION
FUNCTION FC  9 : VOID
NAME: AUX_ON ACC
BEGIN
NETWORK  1
TITLE = FREIGABE DIFFERENZIALLEITUNGSTREIBER AUF ACC-MOTHERBOARD
U   M 110.3;        // AUX-ON MANUAL
S   A 3.7;          // FREIGABE DIFFERENZIALLEITUNGSTREIBER TAKTE FÜR SM
UN  M 110.3;        // AUX-ON MANUAL
L   S5TIME#1S;      // 10X0.1S
SE  T 10;
U   T 10;
R   A 3.7;          // FREIGABE DIFFERENZIALLEITUNGSTREIBER TAKTE FÜR SM
NETWORK  1
TITLE = ALARM NOT_AUS ACC
UN  M 15.2;         // NOT AUS
S   DB2.DBX 0.0;    // ALARM NOT-AUS
=   DB1.DBX 1390.5; // SPS>OBERFL. NOT AUS
O   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 0.0;    // ALARM NOT-AUS
NETWORK  2
TITLE = MELDUNG MASCHINENTÜR OFFEN ACC
U   M 15.1;         // MASCHINENTÜR OFFEN
UN  DB3.DBX 6.2;    // KEIN TEIL GESPANNT (M7050)
UN  DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
UN  DB3.DBX 6.7;    // MELDUNG 7055 WEKZEUGSPANNSYSTEM ÖFFNEN
UN  DB3.DBX 7.0;    // MELDUNG 7056 ILLEGALE WERKZEUGNUMMER IN DEN SETTINGDATEN
UN  DB3.DBX 7.1;    // MELDUNG 7057 WEKZEUGHALTER BELEGT
UN  DB3.DBX 7.2;    // MELDUNG 7058 ACHSEN FREIFAHREN
UN  DB3.DBX 2.7;    // MELDUNG 7023 WARTEZEIT HAUPTANTRIEB
UN  DB3.DBX 0.2;    // 7002  Werkzeug wechseln
=   DB3.DBX 5.0;    // MASCHINENTÜR OFFEN (7040)
NETWORK
TITLE = ACHSEN IN BEWEGUNG
L   DB1.DBB 1358;   // ACHSEN IN BEWEGUNG
L   B#16#0;         // LADE KONSTANTE HEX 0
<>I;                // VERGLEICH AUF UNGLEICH
=   M 131.7;        // ACHSEN IN BEWEGUNG
NETWORK  3
TITLE = 6024 ALARM TÜR OFFEN ACC
O   M 131.7;        // ACHSEN IN BEWEGUNG
O   M 92.0;         // RUNDACHSE IN BEWEGUNG
U   DB20.DBX 326.4; // BETRIEBSART AUTO
=   M 102.0;        // HM
U   M 131.7;        // ACHSEN IN BEWEGUNG
U   DB20.DBX 327.2; // BETRIEBSART REFERENZ
=   M 102.1;        // HM
U   DB20.DBX 324.0; // PROGRAMM LÄUFT
UN  DB20.DBX 324.1; // STOP-ZUSTAND
U(
O   DB20.DBX 326.4; // BETRIEBSART AUTO
O   DB20.DBX 326.2; // BETRIEBSART MDI
)
=   M 102.2;        // HM
UN  M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
=   M 102.3;        // HM
O   M 102.0;        // HM
O   M 102.1;        // HM
O   M 102.2;        // HM
O   M 102.3;        // HM
=   M 110.2;        // HM TÜRALARM
U   M 110.2;        // HM TÜRALARM
U   M 15.1;         // MASCHINENTÜR OFFEN
S   DB2.DBX 3.0;    // 6024 ALARM TÜR OFFEN
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 3.0;    // 6024 ALARM TÜR OFFEN
NETWORK  4
TITLE = SUM ALARME 1 ACC
L   DB2.DBW 0;      // WORT ALARME 1
L   W#16#0;
<>I;
=   M 105.0;        // SUM ALARME1
NETWORK  5
TITLE = SUM ALARME 2 ACC
L   DB2.DBW 2;      // WORT ALARME 2
L   W#16#0;
<>I;
=   M 105.1;        // SUM ALARME 2
NETWORK  5
TITLE = SUM ALARME 3 ACC
L   DB2.DBW 3;      // WORT ALARME 3
L   W#16#0;
<>I;
=   M 105.3;        // SUM ALARME 3
NETWORK  6
TITLE = SUM THERM.ALARME ACC
L   DB2.DBW 4;      // BYTE/THERM. ALARME
L   W#16#0;
<>I;
=   M 105.2;        // SUM THERM. ALARME
NETWORK  7
TITLE = ANWAHL AUX_ON ACC
U   DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
S   M 110.1;        // AUX_ON EIN
U   M 110.1;        // AUX_ON EIN
=   DB1.DBX 1390.2; // SPS>OBERFLÄCHE AUX-ON
S   M 110.0;        // AUX_ON AUTO
S   M 110.3;        // AUX-ON MANUAL
NETWORK  8
TITLE = ABWAHL AUX_ON ACC
O   M 105.0;        // SUM ALARME 1
O   M 105.1;        // SUM ALARME 2
O   M 105.3;        // SUM ALARME 3
O   M 105.2;        // SUM THERM. ALARME
O   DB1.DBX 1366.3; // ALARM AKTIV
R   M 110.3;        // AUX-ON MANUAL
UN  M 110.3;        // AUX-ON MANUAL
ON  A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
O   M 18.3;         // AUSGANGSMERKER TÜR AUF
O   DB1.DBX 1366.3; // ALARM AKTIV
R   M 110.0;        // AUX-ON AUTO
END_FUNCTION
FUNCTION FC 13 : VOID
NAME: NC-STARTVERR. UND BEDIENTASTEN
BEGIN
NETWORK
TITLE = FM CYCLE-START TASTE VON BEDIENPULT UND RAFITASTE
O   DB20.DBX 831.5  // CYCLE-START TASTE VON BEDIENPULT
FP  M 105.5;        // FM CYCLE-START TASTE VON BEDIENPULT
U   M 105.5;        // FM CYCLE-START TASTE VON BEDIENPULT
=   DB20.DBX 3.6;   // NC-START ANFORDERUNG
NETWORK
TITLE = FM NC-STOPP TASTE VON BEDIENPULT oder HM M0/M1 DYNAMISCH
O   DB20.DBX 831.4  // NC-STOPP TASTE VON BEDIENPULT
FP  M 105.6;        // FM NC-STOPP TASTE VON BEDIENPULT
O   M 105.6;        // FM NC-STOPP TASTE VON BEDIENPULT
O   M 0.1;          // HM M0/M1 DYNAMISCH
O   DB20.DBX 0.1;   // NC-STOPP ANFORDERUNG
=   DB20.DBX 0.1;   // NC-STOPP ANFORDERUNG
NETWORK
TITLE = FM RESET TASTE VON BEDIENPULT
O   DB20.DBX 828.6  // RESET TASTE VON BEDIENPULT
FP  M 105.7;        // FM RESET TASTE VON BEDIENPULT
O   M 105.7;        // FM RESET TASTE VON BEDIENPULT
O   DB20.DBX 4.0;   // RESET ANFORDERUNG
=   DB20.DBX 4.0;   // RESET ANFORDERUNG
NETWORK  1
TITLE = AUX_ON/WORT NC_START VERR.
UN  M 96.2;         // WARTEZEIT HAUPTANTRIEB NC_START VERR.
UN  M 96.3;         // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN NC_START VERR.
UN  M 96.4;         // ENABLE SM MODUL B  NC_START VERR.
UN  M 96.6;         // Meldung Sollstückzahl erreicht NC_START VERR.
UN  M 96.7;         // SERVO READY HAUPTANTRIEB NC_START VERR.
UN  M 97.2;         // MELDUNG 7050 KEIN TEIL GESPANNT NC_START VERR.
UN  M 97.3;         // SPN IN BEWEGUNG NC_START VERR.
UN  M 97.4;         // TEIL.IN BEWEGUNG NC_START VERR.
=   M 104.4;        // SUM NC_START VERR.OHNE REFERENZPUNKT NICHT AKTIV
UN  M 96.5;         // REFERENZPUNKT NICHT AKTIV NC_START VERR.
UN  M 96.0;         // ABSOLUTE ACHSE 4 (WZW) 0 GRAD ERREICHT NC_START VERR.
UN  M 96.1;         // WZW FREIGABE NC_START VERR.
U   M 104.4;        // SUM NC_START VERR.OHNE REFERENZPUNKT NICHT AKTIV
=   M 104.3;        // SUM NC_START VERR.
NETWORK  3
O   M 100.6;        // M3 SPEICHER
O   M 100.7;        // M4 SPEICHER
UN  DB10.DBX 1.6;   // SPINDEL START NACH M0
U   DB20.DBX 0.2;   // FC NC-START
U   M 104.3;        // SUM NC_STARTVERR.
S   M 101.0;        // NC-START SPEICHER
NETWORK
TITLE = BETRIEBSART AUTOMATIK UND MDI
;       UN  DB20.DBX 326.2; // BETRIEBSART MDI
;       UN  DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
UN  DB20.DBX 326.0; // BETRIEBSART JOG
UN  DB20.DBX 326.1; // BETRIEBSART EDIT
UN  DB20.DBX 326.3; // BETRIEBSART REPOS
UN  DB20.DBX 326.5; // BETRIEBSART INC 1
UN  DB20.DBX 326.6; // BETRIEBSART INC 10
UN  DB20.DBX 326.7; // BETRIEBSART INC 100
UN  DB20.DBX 327.0; // BETRIEBSART INC 1000
UN  DB20.DBX 327.1; // BETRIEBSART INC 10000
UN  DB20.DBX 327.2; // BETRIEBSART REFERENZ
UN  DB20.DBX 327.3; // BETRIEBSART INC VAR
UN  DB20.DBX 327.4; // BETRIEBSART PRESET
O   DB1.DBX 1402.5; // HEIDENHAIN TNC355 AKTIV
=   M 103.0;        // BETRIEBSART AUTOMATIK UND MDI
NETWORK  4
TITLE = NC_START/STARTVERR.
ON  M 103.0;        // BETRIEBSART AUTOMATIK UND MDI
ON  M 104.3;        // SUM NC-START VERR.
O   M 101.0;        // NC-START SPEICHER
O   DB1.DBX 1366.3; // ALARM AKTIV
U   DB20.DBX 0.2;   // FC NC_START
R   DB20.DBX 0.2;   // FC NC_START
=   DB20.DBX 319.0; // NEG.QUITTUNG DNC
END_FUNCTION
FUNCTION FC 10 : VOID
NAME: BEI_PROGRAMM_ENDE_RESET_ODER_NEUSTART
BEGIN
NETWORK 1
TITLE = PROGRAMM ENDE AN NC
O   M 10.1;         // HM
UN  DB10.DBX 1.0;   // SPS-MSD (DAUERLAUF)
R   M 10.1;         // HM
=   DB20.DBX 348.1; // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT
NETWORK 2
TITLE = PROGRAMM ENDE UND NEUSTART (DAUERLAUF) AN NC
O   M 10.1;         // HM
U   DB10.DBX 1.0;   // SPS-MSD (DAUERLAUF)
R   M 10.1;         // HM
=   DB20.DBX 348.2; // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT UND GESTARTET
NETWORK 3
TITLE = ISTDREHZAHL KLEINER 20U/MIN
O   DB20.DBX 192.2; // M2 DYN.
O   DB20.DBX 195.6; // M30 DYN.
S   M 10.0;         // HM
U   M 10.0;         // HM
U   M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
S   M 10.1;         // HM
R   M 10.0;         // HM
NETWORK 3
TITLE = M2 und M30 RÜCKMELDEN VON M2,M30 UND RESET
O   DB20.DBX 348.1; // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT
O   DB20.DBX 348.2; // PROGRAMM LÄUFT WIRD ZURÜCKGESETZT UND GESTARTET
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUSGELÖST
R   M 10.0;         // HM
R   M 10.1;         // HM
=   DB20.DBX 259.6; // M30 RÜCKMELDEN
=   DB20.DBX 256.2; // M2 RÜCKMELDEN
END_FUNCTION
FUNCTION FC 11 : VOID
NAME: NC_START_UND_NC_STOP_VON_M0_ODER_M1
BEGIN
NETWORK  2
TITLE = FC NC-START
UN  DB10.DBX 1.6;   // Spindelstart nach M0
U   M 101.0;        // NC-Start Speicher
U(;
O   DB20.DBX 224.3; // M3 Statisch
O   DB20.DBX 224.4; // M4 Statisch
);
;       O   E 2.4           // Extern CYCLE Start 8**** Fußschalter für Andi's PCM50
O   M 150.1;        // FM ROBOTIK INTERFACE NC-START
S   DB20.DBX 0.2;   // FC NC-START
U   DB20.DBX 0.2;   // FC NC-START
R   M 101.0;        // NC-Start Speicher
END_FUNCTION
FUNCTION FC 12 : VOID
NAME: AFG_EFG
BEGIN
NETWORK  1
TITLE =  FM TASTE VORSCHUB START
U   DB20.DBX 295.2; // TASTE VORSCHUB START
UN  DB20.DBX 295.1; // TASTE VORSCHUB HALT
UN  M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
FP  M 133.5;        // FM TASTE VORSCHUB START
NETWORK  2
TITLE =  FM TASTE VORSCHUB HALT
U   DB20.DBX 295.1; // TASTE VORSCHUB HALT
UN  DB20.DBX 295.2; // TASTE VORSCHUB START
UN  M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
FP  M 133.7;        // FM TASTE VORSCHUB HALT
NETWORK
TITLE = VORSCHUB HALT/START FREIGABE
U   M 104.0;        // SUM  AFG
=   M 133.0;        // VORSCHUB HALT/START FREIGABE
NETWORK  3
TITLE = VORSCHUB HALT AKTIV
O   DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
O   DB20.DBX 326.2; // BETRIEBSART MDI
O   DB20.DBX 327.2; // BETRIEBSART REFERENZ
U(
U   M 133.0;        // VORSCHUB HALT/START FREIGABE
U   M 133.7;        // FM TASTE VORSCHUB HALT
UN  M 15.1;          // MASCHINENTÜR OFFEN
U   M 131.7;        // ACHSEN IN BEWEGUNG
)
S   M 91.6;         // AFG,EFG VORSCHUB HALT AKTIV
U   M 133.5;        // FM TASTE VORSCHUB START
UN  M 132.1;        // SPINDEL HALT AKTIV
UN  M 15.1;          // MASCHINENTÜR OFFEN
O   DB1.DBX 1440.0; // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   M 91.6;         // AFG,EFG
NETWORK  4
TITLE = SUM MELDUNGEN  AFG/EFG
L   DB3.DBW 0;      // WORT/MELDUNGEN  AFG/EFG
L   W#16#0;         // LADE KONSTANTE HEX 0
<>I;                // VERGLEICH AUF UNGLEICH
=   M 91.0;         // SUM MELDUNGEN  AFG/EFG
NETWORK  5
TITLE = SUM  AFG K0
UN  M 90.1;         // ACHSEN BETRIEBSBEREIT  AFG/EFG
UN  M 90.4;         // SPM IN BEWEGUNG
UN  M 90.5;         // AFG / EFG T-WORT IST GÜLTIG
UN  M 90.7;         // AFG UND EFG WEGNEHMEN
UN  M 91.0;         // SUM MELDUNGEN  AFG/EFG
UN  M 91.2;         // AFG MELDUNG 7050 KEIN TEIL GESPANNT
UN  M 91.5;         // SCHRAUBSTOCKBEW. AKTIV  AFG/EFG
UN  M 91.6;         // AFG,EFG VORSCHUB HALT AKTIV
UN  M 91.7;         // AFG,EFG TASTE SPINDEL HALT
=   M 104.0;        // SUM  AFG
NETWORK  5
TITLE = SUM  EFG K0
UN  M 90.1;         // ACHSEN BETRIEBSBEREIT  AFG/EFG
UN  M 90.2;         // ABSOLUTE ACHSE 4 (WZW) 0 GRAD ERREICHT AFG/EFG
UN  M 90.4;         // SPM IN BEWEGUNG
UN  M 90.5;         // AFG / EFG T-WORT IST GÜLTIG
UN  M 90.7;         // AFG UND EFG WEGNEHMEN
UN  M 91.0;         // SUM MELDUNGEN  AFG/EFG
UN  M 91.2;         // AFG MELDUNG 7050 KEIN TEIL GESPANNT
UN  M 91.5;         // SCHRAUBSTOCKBEW. AKTIV  AFG/EFG
UN  M 91.6;         // AFG,EFG VORSCHUB HALT AKTIV
UN  M 91.7;         // AFG,EFG TASTE SPINDEL HALT
=   M 104.1;        // SUM  EFG
NETWORK  6
TITLE = AFG/EFG
UN  DB20.DBX 327.2; // BETRIEBSART REFERENZ
UN  DB20.DBX 326.4; // BETRIEBSART AUTOMATIK
UN  DB20.DBX 326.2; // BETRIEBSART MDI
UN  DB20.DBX 326.1; // BETRIEBSART EDIT
UN  DB20.DBX 326.3; // BETRIEBSART REPOS
UN  DB20.DBX 327.4; // BETRIEBSART PRESET
U   M 110.3;        // AUX-ON MANUAL
O;
UN  DB20.DBX 326.0; // BETRIEBSART JOG
UN  DB20.DBX 326.1; // BETRIEBSART EDIT
UN  DB20.DBX 326.3; // BETRIEBSART REPOS
UN  DB20.DBX 327.4; // BETRIEBSART PRESET
U   M 110.0;        // AUX-ON AUTO
=   M 110.7;        // HM BETRIEBSARTEN
U   M 110.7;        // HM BETRIEBSARTEN
U   M 104.0;        // SUM  AFG
=   DB20.DBX 340.4; // ACHSFREIGABE    K0
U   M 110.7;        // HM BETRIEBSARTEN
U   M 104.1;        // SUM  EFG
=   DB20.DBX 340.3; // EINLESEFREIGABE K0
NETWORK  5
TITLE = AFG K1
UN  M 90.1;         // ACHSEN BETRIEBSBEREIT  AFG/EFG
UN  M 90.3;         // ENABLE SM MODUL B AFG/EFG
UN  M 90.4;         // SPM IN BEWEGUNG
UN  M 90.7;         // AFG UND EFG WEGNEHMEN
UN  M 91.0;         // SUM MELDUNGEN  AFG/EFG
UN  M 91.2;         // AFG MELDUNG 7050 KEIN TEIL GESPANNT
UN  M 91.5;         // SCHRAUBSTOCKBEW. AKTIV  AFG/EFG
UN  M 91.6;         // AFG,EFG VORSCHUB HALT AKTIV
UN  M 91.7;         // AFG,EFG TASTE SPINDEL HALT
U   M 110.7;        // HM BETRIEBSARTEN
=   DB25.DBX 340.4; // ACHSFREIGABE    K1
NETWORK  6
TITLE = EFG K1
UN  M 90.1;         // ACHSEN BETRIEBSBEREIT  AFG/EFG
UN  M 90.3;         // ENABLE SM MODUL B AFG/EFG
UN  M 90.4;         // SPM IN BEWEGUNG
UN  M 90.7;         // AFG UND EFG WEGNEHMEN
UN  M 91.0;         // SUM MELDUNGEN  AFG/EFG
UN  M 91.2;         // AFG MELDUNG 7050 KEIN TEIL GESPANNT
UN  M 91.5;         // SCHRAUBSTOCKBEW. AKTIV  AFG/EFG
UN  M 91.6;         // AFG,EFG VORSCHUB HALT AKTIV
UN  M 91.7;         // AFG,EFG TASTE SPINDEL HALT
U   M 110.7;        // HM BETRIEBSARTEN
=   DB25.DBX 340.3; // EINLESEFREIGABE K1
NETWORK
TITLE = Freigaben für Betriebsart Repos
U   DB20.DBX 324.0; // PROGRAMM LÄUFT
U   DB20.DBX 324.1; // PROGRAMM ANGEHALTEN
U   DB20.DBX 326.3; // BETRIEBSART REPOS
SPBN     M1302;
=   DB25.DBX 340.3; // EINLESEFREIGABE K1
=   DB25.DBX 340.4; // ACHSFREIGABE    K1
M1302: NOP   0;
END_FUNCTION
FUNCTION FC 14 : VOID
NAME: ALARMSTATUS
BEGIN
NETWORK  1
TITLE =
U   DB1.DBX 1366.3; // ALARM AKTIV
=   M 19.7;         // AUSGANGSMERKER ROBOTIK ALARMAUSGANG
END_FUNCTION
FUNCTION FC 16 : VOID
NAME: SPS_OBERFLAECHEN_SIGNALE
BEGIN
NETWORK  1
U   DB10.DBX 75.0;  // TÜRAUTOMATIK AKTIVIEREN
=   DB1.DBX 1418.0; // AUT.TÜR
O   DB10.DBX 75.4;  // Pneum.Schraubstock aktivieren
O   DB10.DBX 75.1;  // Elektr.Schraubstock aktivieren
=   DB20.DBX 382.1; // Spannmittel vorhanden
U   DB10.DBX 75.3;
=   DB20.DBX 382.4; // AUSBLASVORRICHTUNG
U   DB10.DBX 75.6;
=   DB20.DBX 382.5; // TEILAPPARAT
END_FUNCTION
FUNCTION FC 17 : VOID
NAME: TUERAUTOMATIK
BEGIN
NETWORK  1
TITLE = FREIGABE TÜR ÖFFNEN
;       UN  M 110.2;        // HM TÜRALARM
NETWORK  1
TITLE = FM PC-TASTE TÜR
U   DB1.DBX 1374.2; // TASTE TÜR
U   M 139.6         // HM TÜR OFFEN VON AUSGANGSMERKER TÜR AUF
UN  M 139.5;        // HM TÜR AUF
UN  M 110.2;        // HM TÜRALARM
S   M 139.2;        // HM TÜR SCHLIESSEN VON PC-TASTE
R   M 139.6         // HM TÜR OFFEN VON AUSGANGSMERKER TÜR AUF
UN  DB1.DBX 1374.2; // TASTE TÜR
R   M 139.2;        // HM TÜR SCHLIESSEN VON PC-TASTE
NETWORK  4
TITLE = ANFORDERUNG TÜR AUF
U   DB1.DBX 1374.2; // TASTE TÜR
UN  M 139.2;        // HM TÜR SCHLIESSEN VON PC-TASTE
O;
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
UN  M 18.3;         // AUSGANGSMERKER TÜR AUF
UN  M 18.4;         // AUSGANGSMERKER TÜR ZU
U   M 15.1;         // MASCHINENTÜR OFFEN
U   E 4.4;          // TÜR OFFEN TÜRAUTOMATIK
;       O   DB1.DBX 1382.0; // DNC-ANFORDERUNG TÜR AUF
O   M 138.1;        // HM MASCHIENENTASTATUR TÜR AUF
O   M 149.1;        // HM ROBOTER-INTERFACE
=   M 139.5;        // HM TÜR AUF
O   M 0.1;          // HM M0/M1 DYNAMISCH
O   DB20.DBX 195.6; // M30 DYNAMISCH
O   DB20.DBX 192.2; // M2  DYNAMISCH
O   DB1.DBX 1382.0; // DNC-ANFORDERUNG TÜR AUF
UN  DB10.DBX 1.0;   // SPS-MSD DAUERLAUF AKTIV
S   M 139.3;        // MERKER TÜR ÖFFNEN
NETWORK  5
TITLE = ANFORDERUNG TÜR ZU
U   DB1.DBX 1382.1; // DNC-ANFORDERUNG TÜR ZU
S   M 136.1         // HM DNC-ANFORDERUNG TÜR ZU
UN  M 15.1;         // MASCHINENTÜR OFFEN
O   DB1.DBX 1382.0; // DNC-ANFORDERUNG TÜR AUF
O   DB1.DBX 1382.2; // DNC-ANFORDERUNG TÜR STOPP
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUSGELÖST
R   M 136.1         // HM DNC-ANFORDERUNG TÜR ZU
U   M 138.2;        // HM MASCHIENENTASTATUR TÜR ZU
U   M 139.6         // HM TÜR OFFEN VON AUSGANGSMERKER TÜR AUF
S   M 139.7
R   M 139.6         // HM TÜR OFFEN VON AUSGANGSMERKER TÜR AUF
UN  M 138.2;        // HM MASCHIENENTASTATUR TÜR ZU
R   M 139.7
O   M 139.2;        // HM TÜR SCHLIESSEN VON PC-TASTE
O   M 136.1         // HM DNC-ANFORDERUNG TÜR ZU
O   M 139.7
O   M 149.0;        // HM ROBOTER-INTERFACE
O(;
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
UN  M 18.3;         // AUSGANGSMERKER TÜR AUF
UN  M 18.4;         // AUSGANGSMERKER TÜR ZU
UN  M 15.1;         // MASCHINENTÜR OFFEN
;       U   M 15.0;         // MASCHINENTÜR ZU (HAUPTMOTORSCHÜTZ EIN)
UN  E 4.4;          // TÜR OFFEN TÜRAUTOMATIK
);
=   M 139.4;        // MERKER TÜR SCHLIESSEN
NETWORK  6
TITLE = ABBRUCHBEDINGUNG
O(
U   M 139.6         // HM TÜR OFFEN VON AUSGANGSMERKER TÜR AUF
UN  E 4.4;          // TÜR OFFEN TÜRAUTOMATIK
)
O(
UN  M 15.1;         // MASCHINENTÜR OFFEN
U   E 4.4;          // TÜR OFFEN TÜRAUTOMATIK
)
L   S5TIME#1S;      // 1 S
SE  T 27;           // EINSCHALTVERZÖGERUNG
U   T 27;
S   DB2.DBX 3.3;    // TÜRENDSCHALTER DEF.(ALARM 6027)
;       R   M 139.6         // HM TÜR OFFEN VON AUSGANGSMERKER TÜR AUF
R   M 110.1;        // AUX-ON EIN
R   M 110.0;        // AUX-ON AUTO
R   M 110.3;        // AUX-ON MANUAL
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 3.3;    // TÜRENDSCHALTER DEF.
O   DB1.DBX 1382.2; // DNC-ANFORDERUNG TÜR STOP
O   DB2.DBX 3.4;    // TÜRÜBERW.ZEIT ABGEL.
ON  M 110.1;        // AUX-ON EIN
ON  M 15.2;         // NOT AUS
R   M 136.1         // HM DNC-ANFORDERUNG TÜR ZU
R   M 18.3;         // AUSGANGSMERKER TÜR AUF
R   M 18.4;         // AUSGANGSMERKER TÜR ZU
R   M 139.2;        // HM TÜR SCHLIESSEN VON PC-TASTE
R   M 139.3;        // MERKER TÜR ÖFFNEN
NETWORK  7
TITLE = TÜR ÖFFNEN
O   M 139.3;        // MERKER TÜR ÖFFNEN
O   M 139.5;        // MERKER TÜR ÖFFNEN
O   M 400.0         // MERKER TÜR ÖFFNEN
UN  M 110.2;        // HM TÜRALARM
O(
ON  E 2.1;          // S1 TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
ON  E 2.5;          // S2 TÜRENDSCHALTER (0 SIGNAL BEI GESCHLOSSENER TÜR)
U   M 18.3;         // AUSGANGSMERKER TÜR AUF
)
S   M 18.3;         // AUSGANGSMERKER TÜR AUF
R   M 18.4;         // AUSGANGSMERKER TÜR ZU
R   M 139.1;        // AUSGANGSMERKER TÜR ZU
O   M 18.3;         // AUSGANGSMERKER TÜR AUF
O   DB1.DBX 1370.0;  // 1.SPS SCHLEIFENDURCHLAUF
U   E 4.4;          // TÜR OFFEN TÜRAUTOMATIK
S   M 139.6         // HM TÜR OFFEN VON AUSGANGSMERKER TÜR AUF
U   M 139.6         // HM TÜR OFFEN VON AUSGANGSMERKER TÜR AUF
S   M 18.3;         // AUSGANGSMERKER TÜR AUF
R   M 139.3;        // MERKER TÜR ÖFFNEN
U   M 18.3;         // AUSGANGSMERKER TÜR AUF
UN  E 4.4;          // TÜR OFFEN TÜRAUTOMATIK
=   M 139.0;        // TÜR IN BEWEGUNG
L   S5TIME#10S;     // 10 SEKUNDEN
SE  T 20;           // STARTEN T20 EINSCHALTVERZ.
NETWORK  8
TITLE = TÜR SCHLIESSEN
U   M 139.4;        // MERKER TÜR SCHLIESSEN
UN  M 90.4;         // SPM IN BEWEGUNG
O   M 400.1         // MERKER TÜR SCHLIESSEN
=   M 18.4;         // AUSGANGSMERKER TÜR ZU
R   M 18.3;         // AUSGANGSMERKER TÜR AUF
R   M 139.4;        // MERKER TÜR SCHLIESSEN
R   M 139.6         // HM TÜR OFFEN VON AUSGANGSMERKER TÜR AUF
U   M 18.4;         // AUSGANGSMERKER TÜR ZU
U   M 15.1;         // MASCHINENTÜR OFFEN
=   M 139.0;        // TÜR IN BEWEGUNG
L   S5TIME#10S;     // 10 SEKUNDEN
SE  T 21;           // STARTEN T21 EINSCHALTVERZ.
U   M 18.4;         // AUSGANGSMERKER TÜR ZU
UN  M 15.1;         // MASCHINENTÜR OFFEN
O   M 120.5         // FM AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
S   M 139.1;        // AUSGANGSMERKER TÜR ZU
U   M 139.1;        // AUSGANGSMERKER TÜR ZU
S   M 18.4;         // AUSGANGSMERKER TÜR ZU
NETWORK  9
O   T 20;           // T20
O   T 21;           // T21
UN  DB2.DBX 3.3;    // TÜRENDSCHALTER DEF.(ALARM 6027)
S   DB2.DBX 3.4;    // TÜR ZEIT ABGEL. (ALARM 6028)
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 3.4;
NETWORK 10
TITLE = DNC-STATUSRÜCKMELDUNG TÜR
UN  M 15.1;          // MASCHINENTÜR OFFEN
UN  E 4.4;          // TÜR OFFEN TÜRAUTOMATIK
U   M 104.4;        // SUM NC_START VERR.OHNE REFERENZPUNKT NICHT AKTIV
=   M 148.0;        // HM ROBOTER-INTERFACE
=   DB1.DBX 1390.1; // SPS>OBERF. TÜR GESCHLOSSEN
U   M 15.1;          // MASCHINENTÜR OFFEN
U   E 4.4;          // TÜR OFFEN TÜRAUTOMATIK
=   M 148.1;        // HM ROBOTER-INTERFACE
=   DB1.DBX 1390.0; // SPS>OBERFL. TÜR OFFEN
U   DB1.DBX 1382.0; // DNC-TÜR AUF
UN  M 18.3;         // AUSGANGSMERKER TÜR AUF
S   DB1.DBX 1398.0; // NEG.QUITTUNG DNC
U   DB1.DBX 1382.1; // DNC-TÜR ZU
UN  M 18.4;         // AUSGANGSMERKER TÜR ZU
S   DB1.DBX 1398.0; // NEG.QUITTUNG DNC
END_FUNCTION
FUNCTION FC 18 : VOID
NAME: EL_SCHRAUBSTOCK
BEGIN
NETWORK  1
TITLE = SPN SPANNEN
U(;
ON  DB20.DBX 324.0; // PROGRAMM LÄUFT
O   DB20.DBX 324.1; // ZUSTAND STOP
);
U   DB20.DBX 294.4; // PC-TASTE PINOLE VOR
O   DB20.DBX 195.2; // M26 SPM SPANNEN(DYN.)
O   M 149.3;        // ROBOTIK/FM SCHRAUBSTOCK ZU
O   DB20.DBX 302.2; // DNC-INTRFACE SPM SPANNEN
U   M 140.2;        // SPN FREIGABE
S   M 140.0;        // SPN SPANNEN
=   DB20.DBX 259.2; // M26 AUSLÖSEN
R   M 140.1;        // SPN ENTSPANNEN
NETWORK  2
TITLE = SPN ENTSPANNEN
U(;
ON  DB20.DBX 324.0; // PROGRAMM LÄUFT
O   DB20.DBX 324.1; // ZUSTAND STOP
);
U   DB20.DBX 294.3; // PC-TASTE PINOLE ZURÜCK
O   DB20.DBX 195.1; // M25 SPM ENTSPANNEN (DYN.)
O   M 149.2;        // ROBOTIK/FM SCHRAUBSTOCK AUF
O   DB20.DBX 302.1; // DNC INTERFACE SPM ENTSPANNEN
U   M 140.2;        // SPN FREIGABE
S   M 140.1;        // SPN ENTSPANNEN
=   DB20.DBX 259.1; // M25 AUSLÖSEN
R   M 140.0;        // SPN SPANNEN
NETWORK  3
TITLE = FREIGABE SPN
U   M 110.3;        // AUX-ON MANUAL
U   M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
UN  M 131.7;        // ACHSEN IN BEWEGUNG
UN  M 106.6;        // BETRIEBSART EDIT-MODE
=   M 140.2;        // SPN FREIGABE
NETWORK  4
TITLE = SPN IN BEWEGUNG
U   M 140.0;        // SPN SPANNEN
UN  E 4.5;          // SPN GESPANNT
O;
U   M 140.1;        // SPN ENTSPANNEN
UN  E 4.1;          // SPN HINTEN
=   M 90.4;         // SPN IN BEWEGUNG (AFG,EFG)
=   M 95.1;         // SPN IN BEWEGUNG (SFG)
=   M 97.3;         // SPN IN BEWEGUNG (NC-STARTVERR.)
L   S5TIME#30S;     // 30S
SE  T 24;
UN  DB2.DBX 3.6;    // KEIN TEIL GESPANNT (ALARM 6030)
U   T 24;
S   DB2.DBX 2.3;    // SPN ZEITÜBERW. (ALARM 6019)
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 2.3;    // SPN ZEITÜBERW. (ALARM 6019)
NETWORK  5
TITLE = MELDUNG 7050 KEIN TEIL GESPANNT
UN  DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
UN  E 4.5;          // SPN GESPANNT
UN  E 4.1;          // SPN HINTEN
O   E 4.0;          // BERO VORNE
=   M 91.2;         // AFG MELDUNG 7050 KEIN TEIL GESPANNT
=   M 95.2;         // SFG MELDUNG 7050 KEIN TEIL GESPANNT
=   M 97.2;         // NC-STARTVERRIEGELUNG MELDUNG 7050 KEIN TEIL GESPANNT
=   DB3.DBX 6.2;    // KEIN TEIL GESPANNT (7050)
U   E 4.0;          // BERO VORNE
U   M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
S   DB2.DBX 3.6;    // KEIN TEIL GESPANNT (ALARM 6030)
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 3.6;    // KEIN TEIL GESPANNT (ALARM 6030)
NETWORK  6
TITLE = SPN SPANNEN
U   M 140.0;        // SPN SPANNEN
L   S5TIME#700MS;   // 0,1 S
SE  T 22;
R   M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
U   M 140.0;        // SPN SPANNEN
U   T 22;
S   M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
NETWORK  7
TITLE = SPN ENTSPANNEN
U   M 140.1;        // SPN ENTSPANNEN
L   S5TIME#700MS;   // 0,1 S
SE  T 23;           // T23
R   M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
U   M 140.1;        // SPN ENTSPANNEN
U   T 23;           // T23
S   M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
NETWORK  8
TITLE = ABBRUCHBEDINGUNG
U   M 90.4;         // SPN IN BEWEGUNG
U(;
ON  M 15.2;         // NOT AUS
O   DB1.DBX 1440.0; // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
);
O   DB2.DBX 2.4;    // SCHRAUBSTOCK AUSGEFALLEN (A6020)
O   DB2.DBX 2.6;    // SPN-PLATINE DEFEKT (ALARM 6022)
O   DB2.DBX 2.3;    // SCHRAUBSTOCK ZEITÜBERS.(A6019)
O   DB2.DBX 3.6;    // KEIN TEIL GESPANNT (A6030)
R   M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
R   M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
R   M 140.0;        // SPN SPANNEN
R   M 140.1;        // SPN ENTSPANNEN
O   E 4.1;          // SPN HINTEN
R   M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
R   M 140.1;        // SPN ENTSPANNEN
NETWORK  9
TITLE = ÜBERWACHUNG SPN-PLATINE
U   E 4.5;          // SPN GESPANNT
UN  M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
UN  M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
L   S5TIME#2S;      // 200 * 0,01 = 2 SEKUNDEN
SE  T 30;           // EINSCHALTVERZÖGERUNG
U   T 30;
S   DB2.DBX 2.6;    // SPN-PLATINE DEFEKT (ALARM 6022)
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 2.6;    // SPN-PLATINE DEFEKT (ALARM 6022)
NETWORK 10
TITLE = ÜBERWACHUNG SPM
U   M 140.4;        // SPM GESPANNT
S   M 140.7;        // HM SPM
UN  M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
R   M 140.7;        // HM SPM
U   M 140.7;        // HM SPM
UN  E 4.5;          // SPM GESPANNT
S   DB2.DBX 2.4;    // SPM AUSGEFALLEN (ALARM 6020)
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 2.4;    // SPM AUSGEFALLEN (ALARM 6020)
NETWORK 11
TITLE = ROBOTIC INTERFACE
U   M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
U   E 4.5;          // SPM GESPANNT
=   M 140.4;        // SPM GESPANNT
=   M 138.5;        // SCHRAUBSTOCK GESPANNT
=   DB20.DBX 310.2; // SPS>OBERFL.SCHRAUBSTOCK GESPANNT
U   E 4.1;          // SPM HINTEN
=   M 138.4;        // SCHRAUBSTOCK ENTSPANNT
=   DB20.DBX 310.1; // SPS>OBERFL.SCHRAUBSTOCK HINTEN
NETWORK 12
TITLE = NEG.QUITTUNG DNC
U   DB20.DBX 302.2; // DNC SCHRAUBSTOCK SPANNEN
UN  M 140.0;        // SPM SPANNEN
S   DB20.DBX 318.1; // NEG.QUITTUNG DNC
U   DB20.DBX 302.1; // DNC SCHRAUBSTOCK ENTSPANNEN
UN  M 140.1;        // SPM ENSPANNEN
S   DB20.DBX 318.1; // NEG.QUITTUNG DNC
END_FUNCTION
*****************************
FUNCTION FC 30 : VOID
NAME: PNEUM_SCHRAUBSTOCK
BEGIN
NETWORK
TITLE = FREIGABE SCHRAUBSTOCK ZU/AUF
U   M 110.3;        // AUX-ON MANUAL
U   M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
UN  M 131.7;        // ACHSEN IN BEWEGUNG
UN  M 106.6;        // BETRIEBSART EDIT-MODE
O   DB1.DBX 1370.0; // SPS-1.SCHLEIFENDURCHLAUF
=   M 138.0;        // FREIGABE SCHRAUBSTOCK ZU/AUF
NETWORK
TITLE = HM SCHRAUBSTOCK ZU (M26)
U   DB20.DBX 324.0; // PROGRAMM LÄUFT
U   DB20.DBX 195.2; // M26 DYN.
O(
U   DB15.DBX 20.2   // SCHRAUBSTOCK GESPANNTIN DEN SETTINGDATEN SPEICHERN
U   DB1.DBX 1370.0; // SPS-1.SCHLEIFENDURCHLAUF
)
S   M 137.5;        // HM SCHRAUBSTOCK ZU (M26)
NETWORK
TITLE = LEISTUNG SCHRAUBSTOCK ZU
U   M 138.0;        // FREIGABE SCHRAUBSTOCK ZU/AUF
U(;
U   M 138.7;        // FM TASTE SCHRAUBSTOCK ZU
UN  M 137.1;        // FM TASTE SCHRAUBSTOCK AUF
O   DB20.DBX 195.2; // M26 DYN.
O(;
O   M 149.3;        // ROBOTIK/FM SCHRAUBSTOCK ZU
O   DB20.DBX 302.2; // DNC/SCHRAUBSTOCK ZU
);
U(;
ON  DB20.DBX 324.0; // PROGRAMM LÄUFT
O   DB20.DBX 324.1; // KERN/STOP
);
O(;
U   M 137.5;        // HM SCHRAUBSTOCK ZU (M26)
UN  M 131.7;        // ACHSEN IN BEWEGUNG
);
);
S   M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
R   M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
=   DB20.DBX 259.2; // M26 AUSLÖSEN
S   M 137.7;        // HM M26 STATISCH
R   M 137.6;        // HM M25 STATISCH
NETWORK
TITLE = RÜCKS. HM SCHRAUBSTOCK ZU (M26)
U   M 137.5;        // HM SCHRAUBSTOCK ZU (M26)
UN  M 131.7;        // ACHSEN IN BEWEGUNG
R   M 137.5;        // HM SCHRAUBSTOCK ZU (M26)
NETWORK
TITLE = HM SCHRAUBSTOCK AUF (M25)
U   DB20.DBX 324.0; // PROGRAMM LÄUFT
U   DB20.DBX 195.1; // M25 DYN.
O(
U   DB15.DBX 20.1   // SCHRAUBSTOCK ENTSPANNT IN DEN SETTINGDATEN SPEICHERN
U   DB1.DBX 1370.0; // SPS-1.SCHLEIFENDURCHLAUF
)
S   M 137.4;        // HM SCHRAUBSTOCK AUF (M26)
NETWORK
TITLE = LEISTUNG SCHRAUBSTOCK AUF
U   M 137.1;        // FM TASTE SCHRAUBSTOCK AUF
UN  M 138.7;        // FM TASTE SCHRAUBSTOCK ZU
O   DB20.DBX 195.1; // M25 DYN.
O(;
O   M 149.2;        // ROBOTIK/FM SCHRAUBSTOCK AUF
O   DB20.DBX 302.1; // DNC/SCHRAUBSTOCK AUF
);
U(;
ON  DB20.DBX 324.0; // PROGRAMM LÄUFT
O   DB20.DBX 324.1; // KERN/STOP
);
O(;
U   M 137.4;        // HM SCHRAUBSTOCK AUF (M25)
UN  M 131.7;        // ACHSEN IN BEWEGUNG
);
U   M 138.0;        // FREIGABE SCHRAUBSTOCK ZU/AUF
S   M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
R   M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
=   DB20.DBX 259.1; // M25 AUSLÖSEN
S   M 137.6;        // HM M25 STATISCH
R   M 137.7;        // HM M26 STATISCH
NETWORK
TITLE = RÜCK. HM SCHRAUBSTOCK AUF (M26)
U   M 137.4;        // HM SCHRAUBSTOCK AUF (M26)
UN  M 131.7;        // ACHSEN IN BEWEGUNG
R   M 137.4;        // HM SCHRAUBSTOCK AUF (M26)
NETWORK
TITLE = SCHRAUBSTOCK GESPANNT
U   M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
U   E 4.5;          // DRUCKSCHALTER SCHRAUBSTOCK
=   M 138.5;        // SCHRAUBSTOCK GESPANNT
FP  M 26.3
NETWORK
TITLE = SCHRAUBSTOCK ENTSPANNT
U   M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
UN  E 4.5;          // DRUCKSCHALTER SCHRAUBSTOCK
=   M 138.4;        // SCHRAUBSTOCK ENTSPANNT
FP  M 26.4
NETWORK
TITLE = SCHRAUBSTOCK IN DEN SETTINGDATEN SICHERN
O   M 26.3
O   M 26.4
SPBN   M001;
U   M 138.4;        // SCHRAUBSTOCK ENTSPANNT
=   DB15.DBX 20.1   // SCHRAUBSTOCK ENTSPANNT IN DEN SETTINGDATEN SPEICHERN
U   M 138.5;        // SCHRAUBSTOCK GESPANNT
=   DB15.DBX 20.2   // SCHRAUBSTOCK GESPANNTIN DEN SETTINGDATEN SPEICHERN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
M001:     NOP 0;
NETWORK
TITLE = SCHRAUBSTOCKBEW. AKTIV  AFG/EFG
U   M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
UN  E 4.5;          // DRUCKSCHALTER SCHRAUBSTOCK
O;
U   M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
U   E 4.5;          // DRUCKSCHALTER SCHRAUBSTOCK
O   M 137.5;        // HM SCHRAUBSTOCK ZU (M26)
O   M 137.4;        // HM SCHRAUBSTOCK AUF (M25)
=   M 91.5;         // SCHRAUBSTOCKBEW. AKTIV  AFG/EFG
NETWORK
TITLE = 6072  SCHRAUBSTOCK NICHT BEREIT
U   M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
L   S5TIME#1S;      // 10X0,1S
SE  T 32;           // STARTEN T32 ALS EINSCHALTVERZ.
UN  E 4.5;          // DRUCKSCHALTER SCHRAUBSTOCK
L   S5TIME#200MS;   // 2X0,1S
SE  T 33;           // STARTEN T33 ALS EINSCHALTVERZ.
U   M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
L   S5TIME#1S;      // 10X0,1S
SE  T 34;           // STARTEN T34 ALS EINSCHALTVERZ.
U   E 4.5;          // DRUCKSCHALTER SCHRAUBSTOCK
L   S5TIME#200MS;   // 2X0,1S
SE  T 35;           // STARTEN T35 ALS EINSCHALTVERZ.
U   T 32;           // SCHRAUBST. ZU/ÜBERW. SCHARF
U   T 33;           // DRUCKSCHALTER SCHRAUBST. (0,2S)
O;
U   T 34;           // SCHRAUBST. AUF/ÜBERW. SCHARF
U   T 35;           // DRUCKSCHALTER SCHRAUBST. (0,2S)
O;
UN  M 138.5;        // SCHRAUBSTOCK GESPANNT
U(;
O   DB20.DBX 224.3; // M3 STATISCH
O   DB20.DBX 224.4; // M4 STATISCH
);
S   DB2.DBX 9.0;    // 6072  SCHRAUBSTOCK NICHT BEREIT
U   DB2.DBX 9.0;    // 6072  SCHRAUBSTOCK NICHT BEREIT
U   M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
R   M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
R   M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
UN  M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
UN  M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
U(
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
)
R   DB2.DBX 9.0;    // 6072  SCHRAUBSTOCK NICHT BEREIT
NETWORK
TITLE = 6050  M25/M26 BEI LAUFENDEM HA
UN  M 114.3;        // ISTDREHZAHL KLEINER 20U/MIN
U   DB20.DBX 195.1; // M25 DYN.
S   DB2.DBX 6.2;    // 6050  M25 BEI LAUFENDEM HA
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 6.2;    // 6050  M25 BEI LAUFENDEM HA
NETWORK
TITLE = 7054  SCHRAUBSTOCK SCHLIESSEN
UN  M 138.5;        // SCHRAUBSTOCK GESPANNT
UN  DB3.DBX 5.2;    // 7042  MASCHINENTÜR INIT.
UN  DB3.DBX 2.4;    // 7020  SONDERBETRIEB AKTIV
UN  DB3.DBX 5.0;    // 7040  MASCHINENTÜR OFFEN
UN  DB3.DBX 2.0;    // 7016  HILFSANTRIEBE EINSCHALTEN
UN  DB3.DBX 2.1;    // 7017  REFERENZPUNKT ANFAHREN
UN  DB3.DBX 2.6;    // 7022  WERKZEUGWENDER INITIALISIEREN
=   DB3.DBX 6.6;    // 7054  SCHRAUBSTOCK SCHLIESSEN
NETWORK
TITLE = SPS/OB-->SCHRAUBSTOCK GESPANNT
U   M 138.5;        // SCHRAUBSTOCK GESPANNT
=   DB20.DBX 310.2; // SPS/OB-->SCHRAUBSTOCK GESPANNT
NETWORK
TITLE = SPS/OB-->SCHRAUBSTOCK ENTSPANNT
U   M 138.4;        // SCHRAUBSTOCK ENTSPANNT
=   DB20.DBX 310.1; // SPS/OB-->SCHRAUBSTOCK ENTSPANNT
NETWORK
TITLE = SCHRAUBSTOCK/FC DNC-QUITTUNG
U   DB1.DBX 1366.0; // DNC-BETRIEBN AKTIV
U(;
U   DB20.DBX 302.2; // DNC/SCHRAUBSTOCK ZU
UN  M 137.7;        // HM M26 STATISCH
O;
U   DB20.DBX 302.1; // DNC/SCHRAUBSTOCK AUF
UN  M 137.6;        // HM M25 STATISCH
);
S   DB20.DBX 318.1; // SCHRAUBSTOCK/FC DNC-QUITTUNG
END_FUNCTION
FUNCTION FC 21 : VOID
NAME: TEILAPPARAT
BEGIN
NETWORK  1
TITLE = TEILAPPARAT EINSCHALTBEDINGUNG
UN  E 4.7;          // TEILEN FERTIG
UN  DB1.DBX 1370.0; // SPS-1.SCHLEIFENDURCHLAUF
UN  M 144.0;        // TEILAPPARAT TEILEN AKTIV
=   DB3.DBX 6.3;    // TEIAPPARAT N. VERRIEGELT (M7051)
U   E 4.7;          // TEILEN FERTIG
U   DB1.DBX 1370.0; // SPS-1.SCHLEIFENDURCHLAUF
S   M 144.4;        // TEILEN FERTIG UND VERRIEGELT
NETWORK  2
TITLE = TEILAPPARAT TEILEN
O   DB20.DBX 294.1; // TEILAPPARAT TEILEN TASTATUR
O   DB20.DBX 303.3; // DNC-INTRF.TEILAPPARAT TEILEN
UN  DB20.DBX 324.0; // PROGRAMM LÄUFT
O   DB20.DBX 195.3; // M27 TEILAPPARAT TEILEN (DYN)
U   M 144.7;        // TEILAPPARAT FREIGABE
UN  M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
=   M 143.0;        // TEILAPPARAT TEILEN
=   DB20.DBX 259.3; // M27 AUSLÖSEN
NETWORK  3
TITLE = FREIGABE TEILAPPARAT
U   M 110.3;        // AUX-ON MANUAL
UN  M 106.6;        // BETRIEBSART EDIT-MODE
=   M 144.7;        // FREIGABE TEILAPPARAT TEILEN
NETWORK  4
TITLE = START-FLANKENMERKER
U   M 143.0;        // TEILAPPARAT TEILEN
UN  M 144.5;        // HM START
=   M 144.6;        // FM START
U   M 143.0;        // TEILAPPARAT TEILEN
=   M 144.5;        // HM START
NETWORK  5
TITLE = TEILAPPARAT SCHRITT 1
U   M 144.6;        // FM START
UN  M 144.0;        // TEILAPPARAT TEILEN AKTIV
S   M 144.0;        // TEILAPPARAT TEILEN AKTIV
S   M 144.1;        // TEILAPPARAT SCHRITT1
S   M 18.7;         // AUSGANGSMERKER TEILAPPARAT TEILEN
R   M 144.4;        // TEILEN FERTIG UND VERRIEGELT
L   DB10.DBW 4;     // LADE ZEIT AUS SPS-MSD
ITB;                // UMWANDELN EINER GANZZAHL IN EINEN BCD-WERT
T   MW 160;         // TRANSFERIERE IN MW160
SET;
=   M 161.4;        // SETZE ZEITRASTER 0,1S
U   M 144.0;        // TEILAPPARAT TEILEN AKTIV
L   MW 160;
SE  T 7;            // ÜBERWACHUNGSZEIT TEILEN
U   T 7;            // ÜBERWACHUNGSZEIT TEILEN
S   DB2.DBX 6.0;    // TEILUNGSZEIT ABGELAUFEN (A 6048)
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 6.0;
NETWORK  6
TITLE = TEILAPPARAT SCHRITT 2
U   M 144.1;        // TEILAPPARAT SCHRITT 1
UN  E 4.7;          // TEILAPPARAT TEILEN FERTIG
S   M 144.2;        // TEILAPPARAT SCHRITT 2
R   M 144.1;        // TEILAPPARAT SCHRITT 1
L   DB10.DBW 6;     // TEILUNGSZEIT AUS SPS-MSD
ITB;                // UMWANDELN EINER GANZZAHL IN EINEN BCD-WERT
T   MW 162;
SET;
=   M 163.4;        // SETZE ZEITRASTER 0,1S
U   M 144.2;        // TEILAPPARAT SCHRITT 2
L   MW 162;
SE  T 8;            // ZEIT FÜR TEILUNGSVORGANG
NETWORK  7
TITLE = TEILAPPARAT SCHRITT 3
U   T 8;
U   M 144.2;        // TEILAPPARAT SCHRITT 2
R   M 18.7;         // AUSGANGSMERKER TEILAPPARAT TEILEN
S   M 144.3;        // TEILAPPARAT SCHRITT 3
R   M 144.2;        // TEILAPPARAT SCHRITT 2
L   DB10.DBW 8;     // VERRIEGELUNGSZEIT AUS SPS-MSD
ITB;                // UMWANDELN EINER GANZZAHL IN EINEN BCD-WERT
T   MW 164;
SET;
=   M 165.4;        // SETZE ZEITRASTER 0,1S
U   M 144.3;        // TEILAPPARAT SCHRITT 3
L   MW 164;
SE  T 9;
U   T 9;
S   DB2.DBX 6.1;    // VERRIEGELUNGSZEIT ABGEL.(A 6049)
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 6.1;    // VERRIEGELUNGSZEIT ABGEL.(A 6049)
NETWORK  8
TITLE = TEILAPPARAT TEILEN FERTIG
U   M 144.3;        // TEILAPPARAT SCHRITT 3
U   E 4.7;          // TEILEN FERTIG
S   M 144.4;        // TEILEN FERTIG UND VERRIEGELT
R   M 144.3;        // TEILAPPARAT SCHRITT 3
R   M 144.0;        // TEILAPPARATTEILEN AKTIV
NETWORK  9
TITLE = TEILAPPARAT IN BEWEGUNG
O   M 144.0;        // TEILAPPARAT TEILEN AKTIV
ON  E 4.7;          // TEILEN FERTIG
=   M 92.0;         // TEILAPPARAT IN BEWEGUNG (EFG)
=   M 97.4;         // TEIL.IN BEWEGUNG (NC-STARTVERR.)
NETWORK 10
TITLE = ABBRUCHBEDINGUNG
O   DB2.DBX 6.0;    // TEILUNGSZEIT ABGELAUFEN (A 6048)
O   DB2.DBX 6.1;    // VERRIEGELUNGSZEIT ABGEL.(A 6049)
O   DB2.DBX 6.2;    // TEILAPPARAT AUSGEFALLEN (A 6050)
R   M 18.7;         // AUSGANGSMERKER TEILAPPARAT TEILEN
R   M 144.0;        // TEILAPPARAT TEILEN AKTIV
R   M 144.1;        // TEILAPPARAT SCHRITT 1
R   M 144.2;        // TEILAPPARAT SCHRITT 2
R   M 144.3;        // TEILAPPARAT SCHRITT 3
R   M 144.4;        // TEILEN FERTIG UND VERRIEGELT
NETWORK 11
TITLE = ÜBERWACHUNG TEILAPPARAT
U   M 144.4;        // TEILEN FERTIG UND VERRIEGELT
UN  E 4.7;          // TEILEN FERTIG
S   DB2.DBX 6.2;    // TEILAPPARAT AUSGEFALLEN (A 6050)
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
R   DB2.DBX 6.2;    // TEILAPPARAT AUSGEFALLEN (A 6050)
NETWORK 12
TITLE = NEG.QUITTUNG DNC
U   DB20.DBX 303.3; // DNC-INTRF.TEILAPPARAT TEILEN
UN  M 144.0;        // TEILAPPARAT TEILEN AKTIV
S   DB20.DBX 318.5; // NEG.QUITTUNG DNC
U   M 144.4;        // TEILEN FERTIG UND VERRIEGELT
=   DB20.DBX 311.0; // SPS-OBERFL.TEILEN FERTIG U.VERRI
END_FUNCTION
FUNCTION FC 22 : VOID
NAME: AUSBLASEN
BEGIN
NETWORK  1
TITLE = FM TASTE AUSBLASEN
O   DB20.DBX 294.6; // PC-TASTE AUSBLASEN EIN
UN  M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
FP  M 142.7;        // FM TASTE AUSBLASEN
NETWORK  2
TITLE = AUSBLASEN EIN
U   M 142.7;        // FM AUSBLASEN
UN  M 142.2;        // AUSBLASEN EIN
=   M 142.1;        // HM
S   M 142.2;        // AUSBLASEN EIN
NETWORK  3
TITLE = AUSBLASEN AUS
U   M 142.7;        // FM AUSBLASEN
U   M 142.2;        // AUSBLASEN EIN
UN  M 142.1;        // HM
R   M 142.2;        // AUSBLASEN EIN
NETWORK  4
TITLE = AUSBLASVENTIL EIN
U(;
U   M 142.7;        // FM AUSBLASEN
U   M 142.2;        // AUSBLASEN EIN
O   DB20.DBX 200.7; // M71 AUSBLASEN EIN
O   DB20.DBX 303.0; // DNC AUSBLASEN EIN
);
U   M 110.3;        // AUX ON MANUAL
S   M 18.2;         // AUSGANGSMERKER AUSBLASVENTIL
S   M 142.2;        // AUSBLASEN EIN
=   DB20.DBX 264.7; // M71 AUSLÖSEN
S   DB20.DBX 310.7; // SPS>OBERFLÄCHE AUSBLASEN AKTIV
NETWORK  5
TITLE = AUSBLASVENTIL AUS
U   M 142.7;        // FM AUSBLASEN
UN  M 142.2;        // AUSBLASEN EIN
O   DB20.DBX 201.0; // M72 AUSBLASEN AUS
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB1.DBX 1440.0; // RESET AUSGELÖST
O   DB20.DBX 302.7; // DNC AUSBLASEN AUS
ON  M 110.3;        // AUX ON MANUAL
R   M 18.2;         // AUSGANGSMERKER AUSBLASVENTIL
R   M 142.2;        // AUSBLASEN EIN
R   DB20.DBX 310.7; // SPS>OBERFLÄCHE AUSBLASEN AKTIV
=   DB20.DBX 265.0; // M72 AUSLÖSEN
NETWORK  6
TITLE = DNC-BEFEHLE UND RÜCKMELDUNG
U   DB20.DBX 303.0; // DNC AUSBLASEN EIN
UN  DB20.DBX 264.7; // M71 AUSLÖSEN
S   DB20.DBX 318.4; // NEG.QUITTUNG DNC
U   DB20.DBX 302.7; // DNC AUSBLASEN AUS
UN  DB20.DBX 265.0; // M72 AUSLÖSEN
S   DB20.DBX 318.4; // NEG.QUITTUNG DNC
END_FUNCTION
FUNCTION FC 26 : VOID
NAME: KÜHLMITTEL
BEGIN
NETWORK
TITLE = FM TASTE KÜHLMITTEL (MSTT/PC)
UN  DB10.DBX 75.3;  // AUSBLASEINRICHTUNG AKTIVIERT
UN  M 15.1;          // MASCHINENTÜR OFFEN
U   DB20.DBX 294.6; // TASTE KÜHLMITTEL (PC)
UN  M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
FP  M 107.7;        // FM TASTE KÜHLMITTEL
NETWORK
TITLE = ANWAHL KÜHLMITTEL EIN
UN  M 107.1;        // KÜHLMITTEL EIN
U   M 107.7;        // FM TASTE KÜHLMITTEL
O;
U   DB20.DBX 225.1; // M9 STATISCH
U   DB20.DBX 193.0; // M8 DYN.
UN  DB20.DBX 192.7; // M7 DYN.
O   DB20.DBX 302.6; // DNC/KÜHLMITTEL EIN
S   M 107.1;        // KÜHLMITTEL EIN
S   M 117.1;        // M7 MINIMALSCHMIERUNG EIN
=   M 107.2;        // HM KÜHLMITTEL EIN
=   DB20.DBX 257.0; // M8 AUSLÖSEN
NETWORK
TITLE = ANWAHL MINIMALSCHMIERUNG EIN
U   DB20.DBX 225.1; // M9 STATISCH
U   DB20.DBX 192.7; // M7 DYN.
UN  DB20.DBX 193.0; // M8 DYN.
S   M 117.1;        // M7 MINIMALSCHMIERUNG EIN
=   DB20.DBX 256.7; // M7 AUSLÖSEN
NETWORK
TITLE = KÜHLMITTEL PROGRAMMIERT
U   DB20.DBX 326.4; // KERN/BA AUTO
U   DB20.DBX 257.0; // M8 AUSLÖSEN
U   DB20.DBX 193.0; // M8 DYN.
S   M 107.3;        // KÜHLMITTEL PROGRAMMIERT
NETWORK
TITLE = ABWAHL KÜHLMITTEL EIN
U   M 107.1;        // KÜHLMITTEL EIN
UN  M 107.2;        // HM KÜHLMITTEL EIN
U   M 107.7;        // FM TASTE KÜHLMITTEL
UN  M 107.3;        // KÜHLMITTEL PROGRAMMIERT
O;
U   DB20.DBX 193.1; // M9 DYN.
O   DB20.DBX 302.5; // DNC/KÜHLMITTEL AUS
O;
U   M 107.4;        // KÜHLMITTEL UNTERBRECHUNG
UN  DB20.DBX 326.4; // KERN/BA AUTO
O   DB1.DBX 1370.0; // 1.SPS-SCHLEIFENDURCHLAUF
O   DB1.DBX 1440.0; // RESET AUSGELÖST
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
ON  M 110.3;        // AUX-ON MANUAL
R   M 107.1;        // KÜHLMITTEL EIN
R   M 117.1;        // M7 MINIMALSCHMIERUNG EIN
R   M 107.3;        // KÜHLMITTEL PROGRAMMIERT
=   DB20.DBX 257.1; // M9 AUSLÖSEN
NETWORK
TITLE = KÜHLMITTEL MIT TASTE UNTERBRECHEN
U   M 107.3;        // KÜHLMITTEL PROGRAMMIERT
UN  M 107.4;        // KÜHLMITTEL UNTERBRECHUNG
U   M 107.7;        // FM TASTE KÜHLMITTEL
S   M 107.4;        // KÜHLMITTEL UNTERBRECHUNG
=   M 107.5;        // HM KÜHLMITTEL UNTERBRECHUNG
U   M 107.4;        // KÜHLMITTEL UNTERBRECHUNG
UN  M 107.5;        // HM KÜHLMITTEL UNTERBRECHUNG
U   M 107.7;        // FM TASTE KÜHLMITTEL
ON  M 107.1;        // KÜHLMITTEL EIN
R   M 107.4;        // KÜHLMITTEL UNTERBRECHUNG
NETWORK
TITLE = KÜHLMITTEL FREIGABE
U(;
ON  M 15.1;          // MASCHINENTÜR OFFEN
O   DB20.DBX 294.6; // TASTE KÜHLMITTEL (PC)
);
UN  M 107.4;        // KÜHLMITTEL UNTERBROCHEN
UN  M 0.0;         // M0/M1 AKTIV
UN  DB20.DBX 324.2; // DRYRUN
UN  M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
UN  M 52.0;         // WERKZEUGWENDEN AKTIV
=   M 107.0;        // KÜHLMITTEL FREIGABE
NETWORK
TITLE = KÜHLMITTEL LEISTUNG
U   M 107.1;        // KÜHLMITTEL EIN
U   M 107.0;        // KÜHLMITTEL FREIGABE
=   M 17.2;         // AUSGANGSMERKER KÜHLMITTEL (M8=EIN/M9=AUS)
NETWORK
TITLE = MINIMALSCHMIERUNG LEISTUNG
U   M 117.1;        // M7 MINIMALSCHMIERUNG EIN
U   M 107.0;        // KÜHLMITTEL FREIGABE
=   M 18.0;         // AUSGANGSMERKER MINIMALSCHMIERUNG
NETWORK
TITLE = SPS/OB-->KÜHLMITTEL EIN
U   M 107.1;        // KÜHLMITTEL EIN
=   DB20.DBX 310.5; // SPS/OB-->KÜHLMITTEL EIN
NETWORK
TITLE = SPS/OB-->KÜHLMITTEL SPRITZT
U   M 17.2;         // AUSGANGSMERKER KÜHLMITTEL (M8=EIN/M9=AUS)
=   DB20.DBX 310.6; // SPS/OB-->KÜHLMITTEL SPRITZT
NETWORK
TITLE = KÜHLMITTEL/FC DNC-QUITTUNG
U   DB1.DBX 1366.0; // DNC-BETRIEB AKTIV
U(;
U   DB20.DBX 302.6; // DNC/KÜHLMITTEL EIN
UN  DB20.DBX 225.0; // M8 STATISCH
O;
U   DB20.DBX 302.5; // DNC/KÜHLMITTEL AUS
UN  DB20.DBX 225.1; // M9 STATISCH
);
S   DB20.DBX 318.3; // KÜHLMITTEL/FC DNC-QUITTUNG
END_FUNCTION
FUNCTION FC 40 : VOID
NAME: ROBOTIK_SCHNITTSTELLE
BEGIN
NETWORK
TITLE = Robotik Achsen stehen auf Referenzpunkt
L   DB1.DBD 70;     // REF-POSITION X
L   -0.5E-03;       // 0,5MM
+R  ;
L   DB1.DBD 0;      // IST POSITION X
<R  ;
=   M 168.0
L   DB1.DBD 74;     // REF-POSITION Y
L   -0.5E-03;       // 0,5MM
+R  ;
L   DB1.DBD 4;      // IST POSITION Y
<R  ;
=   M 168.1
L   DB1.DBD 78;     // REF-POSITION Z
L   -0.5E-03;       // 0,5MM
+R  ;
L   DB1.DBD 8;      // IST POSITION Z
<R  ;
=   M 168.2
U   M 168.0
U   M 168.1
U   M 168.2
U   DB1.DBX 134.0;  // REFERENZPUNKT X-ACHSE AKTIV
U   DB1.DBX 134.1;  // REFERENZPUNKT Y-ACHSE AKTIV
U   DB1.DBX 134.2;  // REFERENZPUNKT Z-ACHSE AKTIV
=   M 19.1;         // ROBOTIK ACHSEN STEHEN AUF REF.PKT.
NETWORK  1
TITLE = TÜR AUF/ZU
;       U   E 5.0;          // BEFEHL TÜR ZU
;       FP  M 152.0;        // FM TÜR ZU
;       U   E 5.1;          // BEFEHL TÜR AUF
;       FP  M 152.1;        // FM TÜR AUF
;       U   M 152.0;        // FM TÜR ZU
U   E 5.0;          // BEFEHL TÜR ZU
UN  E 5.1;          // TÜR AUF
=   M 149.0;        // ÜBERGABE MERKER TÜR ZU
;       U   M 152.1;        // FM TÜR AUF
U   E 5.1;          // BEFEHL TÜR AUF
UN  E 5.0;          // TÜR ZU
=   M 149.1;        // ÜBERGABEMERKER TÜR AUF
NETWORK  2
TITLE = SPM    VOR/ZURÜCK
U   E 5.2;          // BEFEHL SPM    ZURÜCK
FP  M 152.2;        // FM SPM    ZURÜCK
U   E 5.3;          // BEFEHL SPM    VOR
FP  M 152.3;        // FM SPM    VOR
U   M 152.2;        // FM SPM    ZURÜCK
UN  E 5.3;          // SPM    VOR
=   M 149.2;        // ROBOTIK/FM SCHRAUBSTOCK AUF
U   M 152.3;        // FM SPM    VOR
UN  E 5.2;          // SPM    ZURÜCK
=   M 149.3;        // ROBOTIK/FM SCHRAUBSTOCK ZU
NETWORK  3
TITLE = CYCLE START
U   E 5.6;          // BEFEHL CYCLE START
FP  M 150.1;        // FM CYCLE START
NETWORK  4
TITLE = FEEDHOLD ROBOTIK SCHNITSTELLE
U   E 5.7;          // BEFEHL FEEDHOLD
=   DB3.DBX 0.7;    // AFG,EFG FEEDHOLD (MELDUNG 7007)
NETWORK  5
TITLE = AUSGÄNGE ROBOTIK SCHNITTSTELLE
U   M 148.1;
=   M 19.3;         // AUSGANGSMERKER ROBOTIK TÜR OFFEN
U   M 148.0;
=   M 19.4;         // AUSGANGSMERKER ROBOTIK TÜR GESCHLOSSEN
U   M 138.4;        // SCHRAUBSTOCK ENTSPANNT
=   M 19.5;         // AUSGANGSMERKER ROBOTIK SCHRAUBSTOCK HINTEN
U   M 138.5;        // SCHRAUBSTOCK GESPANNT
=   M 19.6;         // AUSGANGSMERKER ROBOTIK SCHRAUBSTOCK GESPANNT
NETWORK  6
TITLE = PROGRAMM STEHT
O   M 0.0;         // M0/M1 AKTIV
O   DB20.DBX 224.2; // M2 STATISCH
O   DB20.DBX 227.6; // M30 STATISCH
=   M 19.0;         // AUSGANGSMERKER ROBOTIK PROGRAMM STOP (M30,M0,M1,M2)
END_FUNCTION
FUNCTION FC 63: VOID
NAME: WARNLEUCHTE
BEGIN
NETWORK
TITLE = BLINKLICHT IMPULSDAUER
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
SPBN   M001
L   DB10.DBW 10;    // LADE ZEIT AUS SPS-MSD
ITB;                // UMWANDELN EINER GANZZAHL IN EINEN BCD-WERT
T   MW 70;          // TRANSFERIERE IN MW30
SET;
R   M 71.4;         // ZEITRASTER 0,01S EINSTELLEN
R   M 71.5;         // ZEITRASTER 0,01S EINSTELLEN
M001:  NOP 0
UN  T 48;           // PAUSENDAUER
L   MW 70;          // LADE MW30
SV  T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
NETWORK
TITLE = BLINKLICHT PAUSENDAUER
U   DB1.DBX 1370.0; // 1.SPS SCHLEIFENDURCHLAUF
SPBN   M002
L   DB10.DBW 12;    // LADE ZEIT AUS SPS-MSD
ITB;                // UMWANDELN EINER GANZZAHL IN EINEN BCD-WERT
T   MW 72;          // TRANSFERIERE IN MW32 (ZEITRASTER IST 0,01S)
SET;
R   M 73.4;         // ZEITRASTER 0,01S EINSTELLEN
R   M 73.5;         // ZEITRASTER 0,01S EINSTELLEN
M002:  NOP 0
UN  T 47;           // IMPULSDAUER
L   MW 72;          // LADE MW30
SV  T 48;           // STARTEN ALS VERLÄNGERTER IMPULS
END_FUNCTION
FUNCTION FC 62: VOID
NAME: LED ANSTEUERUNG
BEGIN
NETWORK
TITLE = LAMPENTEST
;       U   DB20.DBX 294.7  // 8*************
;       S   DB20.DBX 846.0  // SKIP
;       S   DB20.DBX 846.1  // DRYRUN
;       S   DB20.DBX 847.0  // TÜR SCHLIESSEN
;       S   DB20.DBX 847.1  // TÜR TÜR ÖFFNEN
;       S   DB20.DBX 847.3  // 1X TASTE
;       S   DB20.DBX 847.4  // OPT.STOP (M1)
;       S   DB20.DBX 848.4  // FUTTER
;       S   DB20.DBX 848.6  // RESET
;       S   DB20.DBX 848.7  // EINZELSATZ
;       S   DB20.DBX 849.6  // PINOLE ZURÜCK
;       S   DB20.DBX 849.7  // PINOLE VOR
;       S   DB20.DBX 850.0  // WERKZEUGWENDEN
;       S   DB20.DBX 850.4  // REFERENZPUNKT ANFAHREN
;       S   DB20.DBX 851.1  // KÜHLMITTEL
;       S   DB20.DBX 851.4  // PROGRAMM HALT
;       S   DB20.DBX 851.5  // PROGRAMM START
;       S   DB20.DBX 851.6  // VORSCHUB HALT
;       S   DB20.DBX 852.0  // VORSCHUB START
;       S   DB20.DBX 852.1  // SPINDEL STOPP
;       S   DB20.DBX 852.3  // SPINDEL START
;       S   DB20.DBX 852.4  // AUX OFF
;       S   DB20.DBX 852.6  // AUX ON
;ROBOTIK TEST
;
;       R   DB20.DBX 846.0  // SKIP
;       R   DB20.DBX 846.1  // DRYRUN
;       R   DB20.DBX 847.0  // TÜR SCHLIESSEN
;       R   DB20.DBX 847.1  // TÜR ÖFFNEN
;       R   DB20.DBX 847.3  // 1X TASTE
;       R   DB20.DBX 847.4  // OPT.STOP (M1)
;       R   DB20.DBX 848.4  // FUTTER
;       R   DB20.DBX 848.6  // RESET
;       R   DB20.DBX 848.7  // EINZELSATZ
;       R   DB20.DBX 849.6  // PINOLE ZURÜCK
;       R   DB20.DBX 849.7  // PINOLE VOR
;       R   DB20.DBX 850.0  // WERKZEUGWENDEN
;       R   DB20.DBX 850.4  // REFERENZPUNKT ANFAHREN
;       R   DB20.DBX 851.1  // KÜHLMITTEL
;       R   DB20.DBX 851.4  // PROGRAMM HALT
;       R   DB20.DBX 851.5  // PROGRAMM START
;       R   DB20.DBX 851.6  // VORSCHUB HALT
;       R   DB20.DBX 852.0  // VORSCHUB START
;       R   DB20.DBX 852.1  // SPINDEL STOPP
;       R   DB20.DBX 852.3  // SPINDEL START
;       R   DB20.DBX 852.4  // AUX OFF
;       R   DB20.DBX 852.6  // AUX ON
;       U   T 47;           // BLINKIMPULS
;       =   DB20.DBX 851.0
;
;
;       U   T 48;           // STARTEN ALS VERLÄNGERTER IMPULS
;       =   DB20.DBX 850.6
NETWORK
TITLE =
;
;
NETWORK
TITLE = KÜHLMITTEL
U   M 18.0;         // AUSGANGSMERKER MINIMALSCHMIERUNG
=   DB20.DBX 851.1  // LED KÜHLMITTEL
NETWORK
TITLE = DRYRUN AKTIV
U   DB20.DBX 324.2  // DRYRUN aktiv
;       U   M 94.x;         // DRYRUN  SFG
=   DB20.DBX 846.1  // LED DRYRUN
NETWORK
TITLE = AUSBLENDSATZ SKIP
U   DB20.DBX 324.6  // Ausblendsatz (SKIP) aktiv
=   DB20.DBX 846.0  // LED SKIP
NETWORK
TITLE = Optionaler Halt M1 aktiv
U   DB20.DBX 324.5  // Optionaler Halt M1 aktiv
=   DB20.DBX 847.4  // LED OPT.STOP (M1)
NETWORK
TITLE =  EINZELSATZ
U   DB20.DBX 324.7  // Einzelsatz (SINGLE) aktiv
=   DB20.DBX 848.7  // LED EINZELSATZ
NETWORK
TITLE =  AUX ON LEUCHTEN, AUX OFF BLINKEN
;       U   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
O   M 110.0;        // AUX-ON
=   DB20.DBX 852.6  // LED AUX ON
NETWORK
TITLE =  REFERENZPUNKT ANFAHREN
U   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
U   M 110.0;        // AUX-ON
O   M 121.3;        // REFPKT.X,Z,ZUGLEICH ANFAHREN
UN  M 52.2          // REFERENZPUNKT X,Y,Z UND WZW C-ACHSE AKTIV
;       UN  DB20.DBX 324.4; // REFERENZPUNKT AKTIV
;       UN  DB3.DBX 2.6;    // 7022  WERKZEUGWENDER INITIALISIEREN
=   DB20.DBX 850.4  // LED REFERENZPUNKT ANFAHREN
;  NETWORK
;  TITLE =  WERKZEUGWENDEN AKTIV
;
;       U   DB3.DBX 2.6;    // 7022  WERKZEUGWENDER INITIALISIEREN
;       U   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
;       O   M 152.0;        // WERKZEUGWENDEN AKTIV
;       =   DB20.DBX 850.0  // LED WERKZEUGWENDEN
NETWORK
TITLE =  SPINDEL HALT AKTIV
U   M 132.1;        // SPINDEL HALT AKTIV
U   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
=   DB20.DBX 852.3  // LED SPINDEL START
NETWORK
TITLE =  VORSCHUB HALT AKTIV
;      U   M 133.1;        // VORSCHUB HALT AKTIV
U   M 91.6;         // AFG,EFG VORSCHUB HALT AKTIV
U   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
=   DB20.DBX 852.0  // LED VORSCHUB START
NETWORK
TITLE = LED PROGRAMM HALT AKTIV
U   DB20.DBX 324.0; // Programm läuft
U   DB20.DBX 324.1; // STOP-ZUSTAND
U   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
=   DB20.DBX 851.5  // PROGRAMM START
NETWORK
TITLE = SCHRAUBSTOCK GESPANNT
O   M 138.5;        // SCHRAUBSTOCK GESPANNT
O   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
U   DB10.DBX 75.4;  // PNEUM.SPANNMITTEL AKTIVIEREN
=   DB20.DBX 849.6  // LED SCHRAUBSTOCK GESPANNT
NETWORK
TITLE = TÜR GESCHLOSSEN TÜRAUTOMATIK
O   T 47;           // STARTEN ALS VERLÄNGERTER IMPULS
O   A 3.4;          // AUSGANG FÜR HILFSRELAIS TÜR GESCHLOSSEN
U   DB10.DBX 75.0;  // TÜRAUTOMATIK AKTIVIEREN
=   DB20.DBX 847.0  // TÜR SCHLIESSEN
NETWORK
TITLE =  PROGRAMM BEENDEN UND LED LÖSCHEN
;       U   DB1.DBX 1440.1; // PROGRAMM BEENDEN
;       U   M 110.0;        // AUX-ON
;       R   DB1.DBX 1440.1; // PROGRAMM BEENDEN
U   DB1.DBX 1440.1; // PROGRAMM BEENDEN
UN  M 110.0;        // AUX-ON
SPBN   M001
L   W#16#0;         // LADE KONSTANTE HEX 0
T   DB20.DBW 846    // LED IM BEDIENPULT LÖSCHEN
T   DB20.DBW 848    // LED IM BEDIENPULT LÖSCHEN
T   DB20.DBW 850    // LED IM BEDIENPULT LÖSCHEN
T   DB20.DBW 852    // LED IM BEDIENPULT LÖSCHEN
M001:  NOP 0
END_FUNCTION
FUNCTION FC 75 : VOID
NAME:
BEGIN
U   A 3.4;          // MASCHINENTÜR GESCHLOSSEN
L   S5TIME#5S;       // 2S
SE  T 14;           // EINSCHALTVERZ.
U   T 14;           // EINSCHALTVERZ.
S   M 400.0         // MERKER TÜR ÖFFNEN
R   M 400.1         // MERKER TÜR SCHLIESSEN
FP  M 400.2
U   M 400.2
SPBN   M001;
L   MD 410
L   1.
+R
T   MD 410
M001:     NOP 0;
U   E 4.4;          // TÜR OFFEN TÜRAUTOMATIK
S   M 400.1         // MERKER TÜR SCHLIESSEN
R   M 400.0         // MERKER TÜR ÖFFNEN
END_FUNCTION
ORGANIZATION_BLOCK OB  1
NAME: ORGANISATIONSBAUSTEIN
BEGIN
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   E 3.0           // EINGANG KENNUNG AC95 UMBAU AUF ACC
=   M 2.0           // KENNUNG AC95 UMBAU AUF ACC
O   DB10.DBX 100.1  // STANDARTMASCHINE AKTIVIERT
O   DB10.DBX 100.2  // STANDARTMASCHINE MIT RUNDACHSE AKTIVIERT
O   M 2.0           // KENNUNG AC95 UMBAU AUF ACC
ON  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
U   DB1.DBX 1403.7  // CAM Concept Oberfläche
CC    FC 64           // M0 AUSLÖSEN BEI TXX UND CAMCONCEPT OBERFLÄCHE
CALL  FC 34;          // ZUWEISEN DER EINGÄNGE AC 95 - ACC
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
UN  M 2.0           // KENNUNG AC95 UMBAU AUF ACC
CC    FC 23           // SICHERHEITSSCHALTUNG ACC
CALL  FC  1;          // INITIALISIERUNGEN
O   DB10.DBX 100.1  // STANDARTMASCHINE AKTIVIERT
O   DB10.DBX 100.2  // STANDARTMASCHINE MIT RUNDACHSE AKTIVIERT
UN  DB10.DBX 100.0  // WERKZEUGWENDER EINSTELLEN AKTIVIERT
UN  DB10.DBX 100.3  // WERKZEUGWENDER AKTIVIERT
UN  DB10.DBX 100.4  // WERKZEUGWENDER UND RUNDACHSE AKTIVIERT
ON  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
O   M 2.0           // KENNUNG AC95 UMBAU AUF ACC
CC    FC  0;          // WERKZEUGWENDER SIMULIEREN
U   DB1.DBX 1426.1; // ROBOTIK SCHNITTSTELLE AKTIVIERT MIT DISKETTE
U   DB10.DBX 75.7;  // ROBOTIK SCHNITTSTELLE AKTIVIERT MIT WINCONFIG
CC    FC 40;          // ROBOTIK SCHNITTSTELLE
CALL  FC 10;          // BEI PROGRAMM ENDE RESET ODER NEUSTART
CALL  FC 11;          // NC-START UND NC-STOP VON M0 ODER M1
CALL  FC  5;          // ACHSEN BEREITSCHAFT
O   M 115.0;        // FELDERER FU
CC    FC  3;          // HAUPTANTRIEB FELDERER FU
O   M 115.1;        // LENZE FU
CC    FC 33;          // HAUPTANTRIEB LENZE FU
CALL  FC  4;          // BETRIEBSARTEN
UN  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
CC    FC  8;          // AUX_ON AC95
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
UN  M 2.0           // KENNUNG AC95 UMBAU AUF ACC
CC    FC  9;          // AUX_ON ACC
U   M 2.0           // KENNUNG AC95 UMBAU AUF ACC
CC   FC 88           // AUX_ON AC95 UMBAU AUF ACC
CALL  FC  6;          // ACHSEN_JOG
CALL  FC 32;          // AUTOMATISCH IN BA REF UMSCHALTEN
CALL  FC  7;          // ACHSEN REFERENZIEREN
CALL  FC 16;          // SPS->OBEFL.SIGNALE
CALL  FC 14;          // ALARMSTATUS
U   DB10.DBX 75.0;  // TÜRAUTOMATIK AKTIVIEREN
CC    FC 17;          // TÜRAUTOMATIK
U   DB10.DBX 75.1;  // EL. SCHRAUBSTOCK AKTIVIEREN
UN  DB10.DBX 75.4;  // PNEUM.SPANNMITTEL AKTIVIEREN
U(
ON  DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
O   M 2.0           // KENNUNG AC95 UMBAU AUF ACC
)
CC    FC 18;          // EL. SCHRAUBSTOCK
U   DB10.DBX 75.4;  // PNEUM.SPANNMITTEL AKTIVIEREN
UN  DB10.DBX 75.1;  // EL.SCHRAUBSTOCK AKTIVIEREN
CC    FC 30;          // PNEUM.SPANNMITTEL
UN  DB10.DBX 75.4;  // PNEUM.SPANNMITTEL AKTIVIEREN
UN  DB10.DBX 75.1;  // EL.SCHRAUBSTOCK AKTIVIEREN
S   M 18.6;         // AUSGANGSMERKER SCHRAUBSTOCK ENTSPANNEN
R   M 18.5;         // AUSGANGSMERKER SCHRAUBSTOCK SPANNEN
FP  M 26.5
U   M 26.5
SPBN   M001;
R   DB15.DBX 20.2   // SCHRAUBSTOCK GESPANNT IN DEN SETTINGDATEN SPEICHERN
=   DB15.DBX 20.1   // SCHRAUBSTOCK ENTSPANNT IN DEN SETTINGDATEN SPEICHERN
=   DB20.DBX 348.3; // ANFORDERUNG ZUM SOFORTIGEN SETTINGDATENSICHERN
M001:     NOP 0;
U   DB10.DBX 75.6;  // SCHÄFER TEILAPPARAT AKTIVIEREN
CC    FC 21;          // TEILAPPARAT
U   DB10.DBX 75.3;  // AUSBLASEINRICHTUNG AKTIVIEREN
CC    FC 22;          // AUSBLASEN
CALL  FC 26;          // KÜHLMITTEL (M8/M9)
CALL  FC  2;          // STÜCKZÄHLER
O   DB10.DBX 100.4  // WERKZEUGWENDER UND RUNDACHSE AKTIVIERT
O   DB10.DBX 100.2  // STANDARTMASCHINE MIT RUNDACHSE AKTIVIERT
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
UN  M 2.0           // KENNUNG AC95 UMBAU AUF ACC
=   M 52.7          // RUNDACHSE AKTIVIERT
O   DB10.DBX 100.0  // WERKZEUGWENDER EINSTELLEN AKTIVIERT
O   DB10.DBX 100.3  // WERKZEUGWENDER AKTIVIERT
O   DB10.DBX 100.4  // WERKZEUGWENDER UND RUNDACHSE AKTIVIERT
UN  DB10.DBX 100.1  // STANDARTMASCHINE AKTIVIERT
UN  DB10.DBX 100.2  // STANDARTMASCHINE MIT RUNDACHSE AKTIVIERT
U   DB10.DBX 2.0;   // AC 2000 AKTIVIEREN
UN  M 2.0           // KENNUNG AC95 UMBAU AUF ACC
=   M 52.5          // WERKZEUGWENDER AKTIVIERT
U   M 52.5          // WERKZEUGWENDER AKTIVIERT
CC    FC 51;          // WERKZEUGSPANNSYSTEM
U   M 52.5          // WERKZEUGWENDER AKTIVIERT
CC    FC 50;          // WERKZEUGWENDER
U   M 200.0         // WERKZEUGPOSITIONEN EINSTELLEN
U   DB10.DBX 100.0  // WERKZEUGWENDER EINSTELLEN AKTIVIERT
CC    FC 49           // WERKZEUGPOSITIONEN INS MILL55_ACC.MSD EINTRAGEN
CALL  FC 12;          // AFG/EFG
CALL  FC 13;          // NC-STARTVERR. UND BEDIENTASTEN
U   M 300.0         //
CC  FC 75;            //
O   DB1.DBX 1370.2; // QUITTIERTASTE GEDRÜCKT
O   DB1.DBX 1370.3; // RESETTASTE GEDRÜCKT
O   DB2.DBX 1.1;    // HW-FEHLER SICHERHEITSSCHALTUNG
R   M 300.0         //
R   M 400.0         // MERKER TÜR ÖFFNEN
R   M 400.1         // MERKER TÜR SCHLIESSEN
CALL  FC 62;          // LED ANSTEUERUNG
CALL  FC 63;          // WARNLEUCHTE
CALL  FC 41;          // RENISHAW MESSTASTER
U   DB10.DBX 2.0;  // ACC AKTIVIEREN
CC   FC 65 ;        // HANDRAD
CALL  FC 35;          // ZUWEISEN DER AUSGÄNGE AC95 - ACC
;DB10.DBX B100= ;Bit 0  1  (MILL55_.MSD) WERKZEUGWENDER EINSTELLEN AKTIVIERT
;DB10.DBX B100= ;Bit 1  2  (MILL55A.MSD) STANDARTMASCHINE AKTIVIERT
;DB10.DBX B100= ;Bit 2  4  (MILL55B.MSD) STANDARTMASCHINE MIT RUNDACHSE AKTIVIERT
;DB10.DBX B100= ;Bit 3  8  (MILL55C.MSD) WERKZEUGWENDER AKTIVIERT
;DB10.DBX B100= ;Bit 4 16  (MILL55D.MSD) WERKZEUGWENDER UND RUNDACHSE AKTIVIERT
;       U   DB10.DBX 100.0  // WERKZEUGWENDER EINSTELLEN AKTIVIERT
;       U   DB10.DBX 100.1  // STANDARTMASCHINE AKTIVIERT
;       U   DB10.DBX 100.2  // STANDARTMASCHINE MIT RUNDACHSE AKTIVIERT
;       U   DB10.DBX 100.3  // WERKZEUGWENDER AKTIVIERT
;       U   DB10.DBX 100.4  // WERKZEUGWENDER UND RUNDACHSE AKTIVIERT
U   DB25.DBX 332.0; // NC-SATZ FERTIG
R   DB25.DBX 332.0; // NC-SATZ FERTIG
END_ORGANIZATION_BLOCK
